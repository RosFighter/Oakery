




label start:

    call intro from _call_intro

    $ i = 0
    while i < 27:
        $ items.update({
                    "test"+str(i+1): Item("Тестовый предмет №"+str(i+1), _("Предмет, созданный для тестирования интерфейса инвентаря"), "interface items item", _("Книги"), 100, True, True),
                    })
        $ i += 1

    jump breakfast




    return

label intro:
    scene location house terrace morning-a
    menu:
        Max_00 "Меня зовут Макс. Я обычный парень из обычной семьи. Ну как обычной... нормальной. Хотя, кого я обманываю. Нормального в моей семье мало. Но обо всём по порядку."
        "далее":
            Max_01 "Недавно меня выперли из школы. Официально - за низкую успеваемость. Хотя оценки были так себе, но настоящая причина в математичке. Говорят, я приставал... Да она сама глазки строила! Директору, её мужу, это почему-то не понравилось..."
            Max_14 "В общем, сейчас я с неоконченным средним образованием. Без работы и конкретных планов на будущее... Но всё должно измениться, обязательно!"
            scene BG intro ann
            Max_04 "Это Анна, моя мама. Сама воспитывает нас с двумя сёстрами уже несколько лет. Работает в офисе какой-то компании. Хотя, зарплата у неё вполне приличная, но почти всё уходит на оплату съёмного жилья, еду и одежду."
            Max_00 "Ну а это наша квартира. Тот ещё тараканник! Мама пытается заработать на первый взнос, чтобы взять ипотеку, но пока что-то не получается. Все мы мечтаем отсюда съехать, но куда?"
            scene BG intro alice
            Max_08 "Алиса, моя старшая сестра. Недавно закончила школу и, так же, как и я, ищет свой путь. Целыми днями сидит в ноутбуке и занимается каким-то своим блогом. Как это часто бывает с братьями и старшими сёстрами, мы не очень ладим..."
            scene BG intro lisa
            Max_00 "Лиза, младшая сестрёнка, ещё учится в школе. Не знаю как учится, но если не выгнали как меня, значит всё в порядке. С Лизой мы общаемся на одной волне, хотя изредка ссоримся. Но если что-то случается, защиты ищет именно у меня."
            Max_14 "Отца я плохо помню. Он ушёл, когда мне было лет пять. Мама не хочет рассказывать, что тогда случилось... Ходили слухи, что дела у него пошли в гору. С тех пор мы о нём ничего не слышали. До прошлого месяца, когда пришла та девушка..."

            menu:
                Max_04 "Мама устроилась на новую работу. Лиза пошла в местную школу. Алиса так и сидит в своём ноуте, а я... На этом мой рассказ заканчивается и начинается что-то новое и интересное!"
                "начать игру":
                    return
                "Повторить историю":
                    jump intro
        "пропустить":
            return


label Sleep:
    menu:
        Max_19 "Как же я хочу спать..."

        "Спать до утра":
            call Waiting(360, 1, True, "08:00") from _call_Waiting # спим 360 минут или до наступления 8 утра

    return


label StartDialog:

    if len(current_room.cur_char) == 1:
        if current_room.cur_char[0] == "lisa":
            jump LisaTalkStart
        elif current_room.cur_char[0] == "alice":
            jump AliceTalkStart
        elif current_room.cur_char[0] == "ann":
            jump AnnTalkStart

    jump AfterWaiting



label Wearied:
    # прождали все доступное время - спим до восьми
    menu:
        Max_19 "Как же я хочу спать..."

        "Спать до утра":
            $ current_room = house[0]
            call Waiting(270, 1, True, "08:00") from _call_Waiting_1

    return

label Nap:
    if max_profile.energy > 40.0:
        $ txt = _("Я сейчас не очень хочу спать, но если я хочу сохранить силы...")
    else:
        $ txt = _("Что-то я сегодня устал, надо бы вздремнуть...")

    menu:
        Max_19 "[txt!t]"

        "Подремать пару часов":
            $ t = 2 * 60
        "Подремать 3 часа" if tm <= "16:00":
            $ t = 3 * 60
        "Подремать 4 часа" if tm <= "15:00":
            $ t = 4 * 60
        "Подремать 5 часов" if tm <= "14:00":
            $ t = 5 * 60
        "Не-а, может позже...":
            jump AfterWaiting

    call Waiting(t, 1, True) from _call_Waiting_5
    return

label Alarm:

    menu:
        Max_00 "В каком часу я должен встать?"

        "В 5 утра":
            $ t = TimeDifference(tm, "05:00")
            call Waiting(t, 1, True, "05:00") from _call_Waiting_6
        "В 6 утра":
            $ t = TimeDifference(tm, "06:00")
            call Waiting(t, 1, True, "06:00") from _call_Waiting_7
        "В 7 утра":
            $ t = TimeDifference(tm, "07:00")
            call Waiting(t, 1, True, "07:00") from _call_Waiting_8
        "Не-а, может позже...":
            jump AfterWaiting
    return
