

label kira_swim:
    scene image 'Kira swim '+pose3_4+kira.dress
    $ persone_button1 = 'Kira swim '+pose3_4+kira.dress+'b'
    return


label kira_sun:
    scene image 'BG char Alice sun'
    $ renpy.show('Kira sun '+pose3_4+kira.dress)
    $ persone_button1 = 'Kira sun '+pose3_4+kira.dress+'b'
    return


label kira_bath:
    scene location house bathroom door-evening
    if peeping['kira_bath'] != 0:
        return

    $ peeping['kira_bath'] = 1
    menu:
        Max_01 "Только один человек может в это время не спать и плескаться в ванне. И человек этот - Кира!"
        "{i}постучаться{/i}" if talk_var['kira.bath.mass']==0:
            Kira "{b}Кира:{/b} Кто там? Я принимаю ванну и абсолютно голая... Так что не входите, дайте отдохнуть!"
            if flags['ladder'] < 1:
                Max_09 "Нужно найти способ как-то подглядеть за девчонками в ванной. Может, попробовать вечером со двора?!"   #если Макс ни разу не пытался подглядеть вечером за девчонками в ванной
            elif flags['ladder'] < 2:
                Max_09 "Нужно найти способ как-то подглядеть за девчонками в ванной. Может, стоит осмотреться изнутри?!"    # если Макс подглядывал со двора, но сам не мылся
            elif flags['ladder'] < 3:
                Max_09 "Нужно уже наконец купить стремянку, чтобы подглядывать за тем, что происходит в ванной..."   #если всё ещё не куплена стремянка, но она уже есть в магазине
            else:
                Max_09 "В следующий раз лучше заглянуть со стремянки..."     # стремянка уже установлена
            jump .end

        "{i}войти{/i}" if talk_var['kira.bath.mass'] > 0:
            # уже состоялся массаж ног в ванной
            jump .mass_bath

        "{i}установить стремянку{/i}" if items['ladder'].have:
            scene BG char Max bathroom-window-evening-00
            $ renpy.show('Max bathroom-window-evening 01'+mgg.dress)
            Max_01 "Надеюсь, что ни у кого не возникнет вопроса, а что же здесь делает стремянка... Как, что? Конечно стоит, мало ли что! А теперь начинается самое интересное..."
            $ flags['ladder'] = 3
            $ items['ladder'].have = False
            $ items['ladder'].InShop = False
            jump .ladder

        "{i}воспользоваться стремянкой{/i}" if flags['ladder'] > 2 and talk_var['kira.bath.mass']==0:
            jump .ladder

        "{i}уйти{/i}":
            jump .end

    label .ladder:
        $ renpy.scene()
        $ renpy.show('Max bathroom-window-evening 02'+mgg.dress)
        Max_04 "Посмотрим, что у нас тут..."

        scene BG bath-00
        show Kira bath-window 01
        show FG bath-00
        $ notify_list.append(_("Скрытность Макса капельку повысилась"))
        $ mgg.stealth += 0.03

        Max_02 "Ох, чёрт! Какая аппетитная попка у неё... Это я удачно решил дождаться её возвращения! Давай, тётя Кира, этой попке пора стать мокренькой..."
        if all([talk_var['kira.porn']>0, talk_var['lisa.footmass']>0]):
            # совместный просмотр порно уже был, Макс делал девчонкам массаж ног
            show Kira bath-window 02
            Max_05 "Ого! Да она сперва решила поиграть со своей киской... Вот это классно! Только почему бы не делать это лёжа в ванной? Такое ощущение, что она это специально..."   #если был совместный просмотр порно с Кирой
            show Kira bath-window 03
            Max_10 "Блин! Она меня видела! Вот это уже не круто... Хотя, это же тётя Кира... она же не станет меня сдавать маме? Или нет?!"
            show Kira bath-window 04
            menu:
                Max_08 "Что? Она зовёт меня к себе?! Да ладно! Как-то мне это не очень нравится..."
                "{i}идти к Кире{/i}":
                    $ talk_var['kira.bath.mass'] == 1
                    jump .kira_mass_bath_first

        else:
            $ __r1 = renpy.random.choice(['05', '06'])
            $ renpy.show('Kira bath-window '+__r1)
            Max_04 "Да-а-а, у неё просто обалденная грудь! Сосочки прямо там, где надо... Ох, я бы с ней ванну попринимал! Надеюсь, мне это приснится. Пора идти спать..."
            jump .end_sleep

    label .kira_mass_bath_first:
        scene BG char Kira bath-talk-00
        $ __r1 = renpy.random.choice(['01', '02'])
        $ renpy.show('Kira bath-talk '+__r1)
        Kira_04 "А мне было интересно, осмелишься ли ты зайти... Значит, Макс, любишь подглядывать?"
        Max_07 "Ну, немного..."
        Kira_05 "Немного? Это сколько? Раз в неделю? Или раз в день?"
        Max_09 "Ты сдашь меня маме?"
        Kira_02 "Ох, конечно нет, Макс! Я и сама большая любительница за чем-нибудь... интересным понаблюдать."
        Max_01 "Фух... хорошо."
        Kira_07 "Хотя, всё будет зависеть от тебя!"
        Max_08 "В каком это смысле?"

        scene BG char Kira bath-talk-02
        $ renpy.show('Kira bath-talk 2-'+__r1)
        show Max bath-talk 2-01
        Kira_01 "Я слышала, ты неплохо так массируешь ножки то одной сестре, то другой..."
        Max_01 "Ну да, бывает такое."
        Kira_02 "Я сегодня очень устала... И если бы нашёлся тот, кто помог бы снять усталость с моих ног, то может быть, в качестве благодарности, я бы и не стала рассказывать своей сестре, какой у неё испорченый сын."
        Max_07 "Э-э-э... Что, прямо здесь?"
        Kira_05 "Ой, а я разве тебя смущаю, Макс?! Ты разве не за этим подглядывал? Чтобы как можно больше всего увидеть безнаказанно..."
        Max_08 "Да, но... прямо здесь... в ванне?!"
        Kira_08 "Что ты, нет! Ты сядешь здесь, рядом с ванной, а я свешу тебе свои ноги... Ха! Конечно, в ванне! Я же вижу с каким трудом ты прикрываешь свой... энтузиазм... Расслабься уже и залезай в ванну. Или, может, хочешь уйти?"
        Max_02 "Конечно, нет! Это я мигом..."

        scene BG char Kira bath-talk-03
        show Kira bath-talk 3-01
        Kira_07 "Ну и как, Макс, доволен видом? Удобно устроился?"
        Max_05 "Ага, у меня просто невероятная тётя!"
        menu:
            Kira_02 "Да, я такая... Понимаю, ты был бы не прочь так всю нось просидеть, но не пора ли сделать своей любимой тёте приятно?"
            "{i}начать массаж{/i}":
                pass
        $ __r1 = renpy.random.choice(['01', '02'])
        $ renpy.show('Kira bath-mass 1-'+__r1)
        Max_04 "{i}Я и мечтать о таком не мог! Что буду сидеть с совершенно голой тётей в одной ванне и массировать её ножки... А как изящно она прикрывает ими свою киску!{/i}"
        Kira_04 "Да, Макс, это очень приятно! Ты молодец... Кто тебя научил этому?"
        Max_01 "Курсы на компьютере... Ну и на Алисе с Лизой тренировался."
        Kira_05 "Думаю, тебе нужно это развивать... Уверенна, твои нежные, но сильные пальчики могут принести невероятное наслаждение!"

        scene BG char Kira bath-mass-02
        $ renpy.show('Kira bath-mass 2-'+__r1)
        Max_07 "И как мне это развивать?"
        Kira_01 "У меня здесь есть одна бывшая однокласница и как я помню, она с мужем как раз занимается массажем и йогой. Я попробую с ней встретиться и договориться, чтобы они тебя поднатаскали в этом... Если, конечно, хочешь?"
        Max_03 "Конечно, да! Я сейчас понял, насколько это классно, делать массаж голой женщине!"
        Kira_09 "Ты только не останавливайся, от твоих прикосновений мне становится так жарко..."

        $ renpy.show('Kira bath-mass 3-'+__r1)
        Max_07 "Ухх, тётя Кира, ты..."
        Kira_07 "Ой! А во что это там такое твёрдое и большое упёрлась моя нога?"
        Max_02 "А то ты не знаешь?!"
        menu:
            Kira_09 "Ну конечно знаю... Видимо, мне стало уже слишком хорошо, чтобы просто лежать без дела. Так что давай-ка закругляться, Макс. Уже так поздно..."
            "{i}закончить массаж{/i}":
                pass

        scene BG char Kira bath-talk-00
        $ __r1 = renpy.random.choice(['01', '02'])
        $ renpy.show('Kira bath-talk '+__r1)
        Max_04 "Ладно, тётя Кира. Я тогда пошёл. Спокойной ночи. Было очень классно побыть с тобой!"
        Kira_04 "Приятных снов, Макс. И да, пожалуй, в следующий раз, ты можешь просто заходить без стука. Если ещё не будешь спать..."
        Max_05 "С радостью."
        jump .end_sleep

    label .mass_bath:
        scene BG char Kira bath-talk-00
        $ __r1 = renpy.random.choice(['01', '02', '03'])
        $ renpy.show('Kira bath-talk '+__r1)
        Kira_01 "А, Макс, это ты! Всё полуночничаешь... А я вот хотела немного поваляться в ванне, перед тем, как идти спать. Присоединишься?"
        menu:
            Max_02 "Конечно, тётя Кира! Тебя я и дожидался."
            "{i}снять шорты{/i}":
                pass
        scene BG char Kira bath-talk-02
        $ renpy.show('Kira bath-talk 2-'+__r1)
        show Max bath-talk 2-02
        menu:
            Kira_02 "О да, я вижу, как ты меня ждал! Залезай скорее..."
            "{i}присоединиться к тёте{/i}":
                pass
        scene BG char Kira bath-talk-03
        show Kira bath-talk 3-01
        Kira_07 "Ну и как, Макс, доволен видом? Удобно устроился?"
        Max_05 "Ага, у меня самая красивая тётя на свете!"
        menu:
            Kira_02 "Да, я такая... Понимаю, ты был бы не прочь так всю нось просидеть, но не пора ли сделать своей любимой тёте приятно?"
            "{i}начать массаж{/i}":
                pass
        $ __r1 = renpy.random.choice(['01', '02'])
        $ renpy.show('Kira bath-mass 1-'+__r1)
        Max_04 "{i}А ведь мечты сбываются, если очень захотеть! Сидим с тётей голые в ванне и я массирую её гладкие и стройные ножки. А как изящно она прикрывает ими свою киску!{/i}"
        Kira_04 "Как хорошо... Это то, что мне так сейчас нужно! Хочется, чтобы это продолжалось как можно дольше..."
        scene BG char Kira bath-mass-02
        $ renpy.show('Kira bath-mass 2-'+__r1)
        Max_03 "{i}Она так прекрасна... а мокренькая так вообще огонь! Её шикарная грудь так классно вздымается, когда она начинает глубоко дышать от моего массажа...{/i}"
        if talk_var['kira.porn']<2:
            Kira_07 "Ты только не останавливайся, от твоих прикосновений мне становится так жарко! Напряжения, как не бывало..."
            menu:
                Max_01 "Это потому что я уже скоро закончу."
                "{i}закончить массаж{/i}":
                    pass
            scene BG char Kira bath-talk-03
            show Kira bath-talk 3-01
            Kira_08 "Ты молодец, Макс. Мне было очень приятно, а как легко моим ножкам стало! Спасибо тебе и давай-ка закругляться. Уже так поздно..."
            Max_04 "Ладно, тётя Кира. Я тогда пошёл. Спокойной ночи. Было очень классно побыть с тобой!"
            Kira_04 "Приятных снов, Макс."
            jump .end_sleep
        else:
            $ _ch1 = GetChance(mgg.social, 2, 900)
            menu:
                Kira_07 "Ты только не останавливайся, от твоих прикосновений мне становится так жарко! Напряжения, как не бывало..."
                "А моё напряжение только растёт, тётя Кира! Может поможешь? \n{color=[_ch1.col]}(Убеждение. Шанс: [_ch1.vis]){/color}" if talk_var['kira.tv.touch']<1:
                    if not RandomChance(_ch1.ch):
                        Kira_09 "Макс, уже так поздно... и меня настолько расслабила ванна и твой массаж, что я засыпаю. Так что давай закругляться."
                        scene BG char Kira bath-talk-03
                        show Kira bath-talk 3-01
                        Max_04 "Ладно, тётя Кира. Я тогда пошёл. Спокойной ночи. Было очень классно побыть с тобой!"
                        Kira_04 "Приятных снов, Макс."
                        jump .end_sleep
                "А моё напряжение только растёт, тётя Кира! Может поможешь?":
                    pass
            $ renpy.show('Kira bath-mass 3-'+__r1)
            Kira_05 "О, ты прав, Макс! Думаю, очень непросто высидеть со мной в одной ванне и ограничиться лишь массажем..."
            Max_05 "Ухх... от твоих прикосновений мне становится уже немного лучше!"
            scene BG char Kira bath-talk-03
            show Kira bath-mass fj1-01
            Kira_02 "Раз уж ты снял усталость с моих ножек, то может они, в качестве благодарности, смогут помочь и тебе..."
            Max_03 "{i}Ох... Определённо смогут! Не зря я так хорошо размял её пальчики, они начинают такое вытворять... Ого! Тётя так возбудилась, что раздвинула ножки и начала ласкать свою киску...{/i}"
            Kira_07 "Ну как, Макс? Тебе нравится то, что твоя тётя умеет вытворять ногами?"
            Max_20 "Да-а-а... Это очень приятно! Только не прекращай, тётя Кира!"
            $ __r1 = renpy.random.randint(1,2)
            if __r1== 1:
                show Kira bath-mass fj1-02
            else:
                scene BG char Kira bath-cun-01
                show Kira bath-mass fj2-01
            $ _ch1 = GetChance(mgg.sex+10, 4, 900)
            menu:
                Kira_09 "Ох, Макс, как же меня заводит твой огромный член! Ты так жадно смотришь, как я ласкаю себя, свою грудь... Ах, моя киска вся горит, не отводи от неё глаз..."
                "{i}пытаться не кончить{/i} \n{color=[_ch1.col]}(Сексуальный опыт. Шанс: [_ch1.vis]){/color}":
                    pass
            if not RandomChance(_ch1.ch):
                scene BG char Kira bath-fj-03
                show Kira bath-mass fj3-01
                Kira_06 "[norestrain!t]Ухх, Макс! Ты кончил раньше, чем мне хотелось..."
            else:
                scene BG char Kira bath-cun-01
                show Kira bath-mass fj2-02
                menu:
                    Kira_10 "[restrain!t]Тебе нравится, как я ввожу пальчики в свою киску? Это так приятно и сладко, что я уже вот-вот кончу! Ох... давай, не сдерживайся..."
                    "{i}кончить{/i}":
                        pass
                scene BG char Kira bath-fj-03
                show Kira bath-mass fj3-01
                Kira_08 "Фух, Макс, ну мы и выдали с тобой..."
            Max_02 "Да, тётя Кира, это было что-то нереальное!"
            Kira_01 "Давай уже быстренько разбегаться, а то вдруг наши с тобой стоны кто-то услышал."
            Max_01 "Ага. Спокойной ночи, тётя Кира. С тобой очень здорово!"
            Kira_04 "Приятных снов, Макс."
            jump .end_sleep

    label .end_sleep:
        $ spent_time += 30
        $ current_room = house[0]
        jump Sleep

    label .end:
        $ spent_time += 10
        jump Waiting


label kira_night_swim:
    $ renpy.scene()
    $ renpy.show('Kira night-swim '+renpy.random.choice(['01', '02', '03']))

    Max_03 "Ого... Купаешься голой, тётя Кира?! Классно смотришься!"
    Kira_01 "А, Макс... Я думала, что все уже спят. Хотела немного поплавать... А ты чего не спишь?"
    Max_01 "Да, что-то не спится... К тебе можно?"
    Kira_02 "Ну, бассейн большой. К тому же, это ваш дом. Кому как не тебе решать? Но, давай не сегодня. Я очень устала и собиралась уже идти спать..."
    Max_04 "Тогда и я пойду спать... Спокойной ночи, тётя Кира."
    Kira_05 "Ага. Приятных снов, Макс."
    $ current_room = house[0]
    jump Sleep


label kira_sleep_night:
    # не доступна пользователю
    return


label kira_sleep_morning:

    scene BG char Kira lounge-morning-01
    $ renpy.show('Kira sleep '+pose3_4+kira.dress)
    if peeping['kira_sleep'] != 0:
        return

    $ peeping['kira_sleep'] = 1
    menu:
        Max_04 "Ага! Тётя Кира ещё спит. Хорошо, что её ночнушка слегка просвечивает... Ухх, как она хороша..."
        "{i}подойти поближе{/i}":
            scene BG char Kira lounge-morning-02
            $ renpy.show('Kira sleep-closer '+pose3_4+kira.dress)
            if pose3_4=='01':
                Max_02 "Класс! Какая сладкая попка у моей тёти... Хочется любоваться бесконечно. И не только любоваться..."
            elif pose3_4=='02':
                Max_03 "О, да! К этой шикарной попке я бы с удовольствием прижался... Как и к этим соблазнительным сисечкам. Ммм, очаровательна, как ни крути..."
            else:
                Max_05 "Чёрт, от вида этих раздвинутых и стройных ножек в шортах становится слишком тесно... Ещё бы, такая горячая красотка!"
        "{i}уйти{/i}":
            pass

    return


label return_for_club:
    return


label kira_night_tv:
    $ renpy.block_rollback()
    scene BG tv-watch-01
    $ __list = []
    python:
        for i in range(1, 6):
            for j in range(3, 4):
                __list.append('porn-0'+str(i)+' 0'+str(j))
        for j in range(1, 7):
            __list.append('serial 0'+str(j))
    $ __film = renpy.random.choice(__list)
    $ renpy.show('tv '+__film, at_list=[tv_screen,])
    $ renpy.show('Kira tv-watch 01'+kira.dress)

    if 'serial' in __film:
        # кира смотрит сериал
        menu:
            Max_01 "Тётя Кира ещё не легла спать... смотрит сериалы. Может, стоит задержаться?"
            "{i}подойти ближе{/i}":
                pass
        scene BG lounge-tv-00
        $ renpy.show('Kira tv '+pose3_4+kira.dress)
        menu:
            Max_04 "Да-а-а, кто-то залипает на сериалы, а я вот залипаю на свою тётю..."
            "Тётя Кира?":
                scene BG lounge-tv-01
                $ renpy.show('Kira tv-closer '+pose3_4+kira.dress)
                show Max tv 00c

                menu:
                    Kira_01 "А? Макс? Как ты подкрался незаметно... Я думала, уже все спят давно... А меня тут один сериальчик зацепил, решила досмотреть..."
                    "Мне что-то не спится. Можно тоже телек с тобой посмотреть?":
                        menu:
                            Kira_04 "Да без проблем, садись рядом. Будем досматривать сериал или найдём какой-нибудь боевик, фантастику?"
                            "Может быть, эротику?" if talk_var['kira.porn']==0:
                                Kira_08 "Макс, а ты не слишком юн для этого? Я не думаю, что это хорошая идея."
                                Max_09 "Ты прямо как моя мама! Я уже взрослый, а в этом доме не то что посмотреть эротику не выйдет, так даже просто произнести это нельзя."
                                Kira_14 "Вообще, я думаю, что тебе уже пора спать. Три часа ночи... Давай в другой раз поговорим об этом..."
                                Max_08 "Вот так всегда..."
                                jump .end
                            "Давай смотреть порно?!" if talk_var['kira.porn']>0:
                                jump .porn
                            "Я насчёт уроков поцелуев, если момент подходящий..." if 'kira' in talk_var['ask.teachkiss']:
                                jump .teachkiss
                    "Я просто хотел пожелать спокойной ночи!":
                        Kira_00 "Хорошо, Макс. Спокойной ночи! Я тоже уже скоро ложусь спать, только серию досмотрю..."
                        Max_00 "Ага, приятных снов..."
                        jump .end
            "{i}посмотреть, что будет дальше{/i}":
                scene BG lounge-tv-00
                $ renpy.show('Kira tv m-'+pose3_4+kira.dress)
                menu:
                    Max_05 "Ухх... Она начала ласкать свою киску... Вот это уже горячо!"
                    "Тётя Кира?":
                        scene BG lounge-tv-01
                        $ renpy.show('Kira tv-closer m-'+pose3_4+kira.dress)
                        show Max tv 00c

                        menu:
                            Kira_07 "А? Макс? Как ты подкрался незаметно... Я думала, уже все спят давно... А я тут... отдыхаю, как видишь. Я тебя не смущаю таким своим видом?"
                            "Ни капли!":
                                $ renpy.show('Kira tv-closer '+pose3_4+kira.dress)
                                Kira_04 "Если твоя мама узнает, то нам обоим влетит. Так что... Пусть этот инцидент останется тайной..."
                                Max_03 "Конечно, тётя Кира. Мне что-то не спится. Можно тоже телек с тобой посмотреть?"
                                jump .porn
                            "Да ты продолжай, я посижу, посмотрю...":
                                $ renpy.show('Kira tv-closer '+pose3_4+kira.dress)
                                Kira_04 "Ну что ты, Макс. Я просто не ожидала, что кто-то увидит. Так неловко... Мы ведь сохраним этот инцидент в тайне?"
                                Max_03 "Конечно, тётя Кира. Мне что-то не спится. Можно тоже телек с тобой посмотреть?"
                                jump .porn
                            "Я насчёт уроков поцелуев, если момент подходящий..." if 'kira' in talk_var['ask.teachkiss']:
                                $ renpy.show('Kira tv-closer '+pose3_4+kira.dress)
                                jump .teachkiss
                            "Я просто хотел пожелать спокойной ночи!":
                                $ renpy.show('Kira tv-closer '+pose3_4+kira.dress)
                                jump .good_night
                    "{i}уже поздно, пора спать...{/i}":
                        jump .end


            "{i}уже поздно, пора спать...{/i}":
                jump .end
    else:
        # Кира смотрит порнушку
        pass

    menu:
        Max_01 "Тётя Кира ещё не легла спать... смотрит порнушку. Может, стоит задержаться?"
        "{i}подойти ближе{/i}":
            pass
    scene BG lounge-tv-00
    $ renpy.show('Kira tv m-'+pose3_4+kira.dress)
    menu:
        Max_05 "Ухх... Она начала ласкать свою киску... Вот это уже горячо! От такой компании я не откажусь..."
        "Тётя Кира?":
            scene BG lounge-tv-01
            $ renpy.show('Kira tv-closer m-'+pose3_4+kira.dress)
            show Max tv 00c

            if talk_var['kira.porn']>0:
                menu:
                    Kira_07 "А? Макс? Как ты подкрался незаметно... Я думала, уже все спят давно... А я тут... отдыхаю, как видишь. Присоединишься?"
                    "Конечно, да!":
                        jump .porn
                    "Я насчёт уроков поцелуев, если момент подходящий..." if 'kira' in talk_var['ask.teachkiss']:
                        jump .teachkiss
                    "Я просто хотел пожелать спокойной ночи!":
                        jump .good_night
            else:
                menu:
                    Kira_07 "А? Макс? Как ты подкрался незаметно... Я думала, уже все спят давно... А я тут... отдыхаю, как видишь. Я тебя не смущаю таким своим видом?"
                    "Ни капли!":
                        $ renpy.show('Kira tv-closer '+pose3_4+kira.dress)
                        Kira_04 "Если твоя мама узнает, то нам обоим влетит. Так что... Пусть этот инцидент останется тайной..."
                        Max_03 "Конечно, тётя Кира. А мне можно тоже... посмотреть этот фильм?"
                        jump .porn
                    "Да ты продолжай, я посижу, посмотрю...":
                        $ renpy.show('Kira tv-closer '+pose3_4+kira.dress)
                        Kira_04 "Ну что ты, Макс. Я просто не ожидала, что кто-то увидит. Так неловко... Мы ведь сохраним этот инцидент в тайне?"
                        Max_03 "Конечно, тётя Кира. А мне можно тоже... посмотреть этот фильм?"
                        jump .porn
                    "Я насчёт уроков поцелуев, если момент подходящий..." if 'kira' in talk_var['ask.teachkiss']:
                        $ renpy.show('Kira tv-closer '+pose3_4+kira.dress)
                        jump .teachkiss
                    "Я просто хотел пожелать спокойной ночи!":
                        $ renpy.show('Kira tv-closer '+pose3_4+kira.dress)
                        jump .good_night
        "{i}уже поздно, пора спать...{/i}":
            jump .end

    label .porn:
        if talk_var['kira.porn']>0:
            jump .porn_view
        Kira_08 "Макс, а не рано тебе такое смотреть? Если эротику, то ещё куда ни шло... но это самое настоящее порно! Хотя, и эротику тебе не стоит смотреть. Во всяком случае, со мной..."
        Max_07 "Как будто я порно не видел... Мама с Эриком всё время тут порно смотрят, прежде чем уходят к себе наверх."
        if online_cources[0].current > 2:
            $ _ch1 = GetChance(mgg.social, 2, 900)
        menu:
            Kira_07 "Ага. И ты не придумал ничего лучше, чем попробовать посмотреть со своей тётей порно среди ночи?"
            "Вообще-то, с лучшей тётей на свете! \n{color=[_ch1.col]}(Убеждение. Шанс: [_ch1.vis]){/color}" if online_cources[0].current > 2: # второй курс убеждения пройден
                if RandomChance(_ch1.ch):
                    Kira_02 "[succes!t]Очень приятно это слышать, Макс. Порно, значит... Ну давай, присаживайся. Только руки не распускать. И не смущайся - я иногда... к себе прикасаюсь, так ощущения от фильма более... интересные, если ты меня понимаешь..."
                    Max_04 "Да без проблем! Давай уже смотреть..."
                else:
                    jump .failed
            "Вообще-то, с лучшей тётей на свете!" if online_cources[0].current < 3: # курс не пройден, убедить нельзя
                jump .failed

    label .porn_view:
        # совместный просмотр порно
        scene BG tv-watch-01
        $ talk_var['kira.porn']+=1
        if talk_var['kira.porn']==1:
            show tv kira-porn-00 01 at tv_screen
            $ renpy.show('Kira tv-watch 01'+kira.dress)
            show Max tv-watch 01c

            Kira_04 "Давай. Хорошо, что я привезла с собой такие фильмы, а то с запретами вашей мамы здесь легко с ума сойти можно..."
            Max_10 "И не говори, тётя Кира. Сам изо дня в день мучаюсь... Столько девчонок красивых в доме, а если заметят стояк, так сразу извращенцем и ненормальным называют."
            show tv kira-porn-00 02 at tv_screen
            Kira_05 "Так нужно донести до каждой из них, что это нормальная реакция! Хоть это и твоя мама с сёстрами, но они так же ещё и девочки."
            Max_09 "Да я пытаюсь, нужно время..."
            show tv kira-porn-00 03 at tv_screen
            Kira_03 "Так, мы порно собирались смотреть или что? Всё, тихо... всё внимание на экран."
            Max_02 "{i}О да, так классно смотреть такое со своей тётей... Она ведь та ещё проказница! А в шортах-то как тесно...{/i}"

            scene BG char Alice tv-mass-01
            show Kira tv-closer max-01

            Max_03 "{i}Ого, она трогает свою грудь! Блин, мне повезло с тётей. Она такая классная!{/i}"
            Kira_07 "Макс, тебе тесно в твоих шортах? Я же ведь уже всё видела. Стесняешься меня? Мы с тобой сидим, смотрим порно, а ты прикрываешься?"

            show Kira tv-closer max-02

            Kira_05 "В общем, если хочешь, можешь себя трогать. Как я уже говорила, фильм от этого становится интереснее..."
            Max_04 "Да для меня ты уже интереснее фильма..."
            menu:
                Kira_02 "Спасибо, Макс... Это приятно слышать... Но ты так всё пропустишь самое интересное на экране..."
                "{i}начать дрочить{/i}":
                    pass

            scene BG char Alice tv-mass-05
            show Kira tv-closer max-03

            Max_05 "{i}С ума сойти... Я дрочу на глазах у своей тёти перед огромным экраном, на котором показывают порно... А она продолжает трогать свою грудь... и даже раздвинула ножки... И не стесняясь ласкает свою киску через трусики!{/i}"
            Kira_10 "Ох, он у тебя такой большой, Макс... Это всё так возбуждает... Ммм..."
            menu:
                Max_20 "{i}Да, Кира... Твоя помощь бы мне не помешала... Хотя... похоже, я сейчас кончу!{/i}"
                "{i}кончить{/i}":
                    pass

            scene BG char Kira tv-kiss-03
            show Kira tv-closer max-04
            Kira_09 "Ох, ты уже? Так быстро... Ну, иди приведи себя в порядок и ложись спать. Тебе уже пора. Я тоже скоро... заканчиваю..."
            Max_04 "Хорошо. Спокойной ночи, тётя Кира."
            jump .end
        else:
            $ renpy.show('tv kira-porn-00 01', at_list=[tv_screen,])
            $ renpy.show('Kira tv-watch 01'+kira.dress)
            show Max tv-watch 01c

            Kira_04 "Давай. Благо, у меня большая коллекция таких фильмов..."
            Max_03 "Супер! Хотя я больше рад тому, с кем я это смотрю."

            scene BG char Alice tv-mass-01
            show Kira tv-closer max-02

            Kira_07 "Не стесняйся, Макс... Вижу, тебе уже очень тесно в шортах, доставай свой член! На экране уже давно это сделали... Ты отстаёшь..."
            Max_04 "Меня уговаривать не надо..."

            show Kira tv-closer max-03
            $ __suf = ''
            if talk_var['kira.bath.mass']:
                Kira_05 "Если хочешь, можешь даже снять шорты, чтобы резинка ничего не передавливала..."
                Max_02 "Только если ты тоже разденешься..."
                menu:
                    Kira_02 "Как же без этого, Макс. Но только немножко... чтобы ты не кончил уж очень быстро..."
                    "{i}снять шорты{/i}":
                        $ __suf = 'a'
                        show Kira tv-closer max-03a
            Kira_05 "Да, Макс, так уже лучше... С тобой просмотр порно становится куда интереснее. Похоже, тебе совершенно не стыдно дрочить прямо перед своей тётей?"
            Max_01 "Ни капли не стыдно! Особенно, если ей самой это нравится..."
            Kira_09 "О да, мне это очень нравится! От этого я становлюсь такой мокренькой... Ой, какая же я грязная девчонка..."
            Max_05 "{i}Да, тётя Кира, ты очень плохая девочка... Мы смотрим порно, а ты раздвинула ножки и с таким жаром ласкаешь свою киску, пока я дрочу...{/i}"
            Kira_10 "Ох, он у тебя такой большой, Макс... Это всё так возбуждает... Как хорошо... Делай это быстрее..."
            menu:
                Max_20 "{i}Да, Кира... Твоя помощь бы мне не помешала... Хотя... похоже, я сейчас кончу и без этого!{/i}"
                "{i}кончить{/i}":
                    $ renpy.show('Kira tv-closer max-04'+__suf)

            Kira_09 "Ох, ты всё? Ну, тогда иди приведи себя в порядок и ложись спать. Тебе уже пора. Я тоже уже близка к тому... чтобы закончить..."
            Max_04 "Хорошо. Спокойной ночи, тётя Кира."
            jump .end

    menu .teachkiss:
        Kira_00 "Да, да... Момент вполне подходящий... Ну что же, давай приступим."
        "С чего начнём?" if talk_var['teachkiss'] == 1:
            # первый урок
            $ renpy.block_rollback()
            scene BG char Alice tv-mass-01
            show Kira tv-closer 01a
            show Max tv-closer 04c
            Kira_01 "Очень важная часть поцелуя - это прелюдия. Если ты накинешься на девушку в первый раз и начнёшь целовать, пусть даже и хорошо, то скорее всего, получишь отказ..."
            Max_00 "И что делать?"
            Kira_02 "Ну, с прелюдиями я тебе не помогу, тут всё индивидуально. Просто, поймай нужный момент и попробуй меня поцеловать как умеешь, для начала..."
            Max_02 "Да это легко!"
            scene BG char Kira tv-kiss-01
            show Kira tv-kiss 1-01
            Kira_04 "А вот и нет. Излишняя самоуверенность на многих действует, но скромных девушек может отпугнуть. Ты должен чувствовать ту, с которой планируешь... что ты там планируешь..."
            Max_04 "Тётя Кира, хватит меня дразнить, я всё это и так знаю!"
            menu:
                Kira_05 "Ну хорошо. Давай приступим..."
                "{i}поцеловать её{/i}":
                    pass
            $ __r1 = renpy.random.choice(['02', '03'])
            $ renpy.show('Kira tv-kiss 1-'+__r1)
            menu:
                Max_03 "{i}Ого... Какой горячий ротик... И она так ловко орудует язычком... Так... И что мне, как... Эх, учиться мне ещё и учиться... А что, я очень даже не против такому учиться!{/i}"
                "{i}продолжить целоваться{/i}":
                    $ renpy.block_rollback()
                    $ __r1 = {'02':'03', '03':'02'}[__r1]
                    $ renpy.show('Kira tv-kiss 1-'+__r1)
                    Max_05 "{i}Ухх, она такая классная, что даже не хочется это прекращать! Так бы и целовал её вечно...{/i}"
                    show Kira tv-kiss 1-01
                    Kira_01 "Ну всё, на сегодня хватит. Конечно, получается у тебя пока не очень, но это нормально. И ты знаешь, где меня найти, если захочешь потренироваться ещё... А теперь марш спать!"
                    $ mgg.kissing = 1.1
                "{i}прикоснуться к её груди{/i}":
                    $ renpy.block_rollback()
                    show Kira tv-kiss 1-04
                    Kira_03 "Так, Макс, а вот это уже лишнее! Разве я позволяла тебе распускать руки?"
                    Max_07 "Нет... Рука просто сама потянулась."
                    show Kira tv-kiss 1-01
                    Kira_04 "Что я тебе говорила про излишнюю самоуверенность? Спешка хороша для другого дела... и то, только по настроению. Понял меня?"
                    Max_03 "Ага... Я учту это."
                    Kira_01 "Тогда, на сегодня мы закончили. Конечно, получается у тебя пока не очень, но это нормально. И ты знаешь, где меня найти, если захочешь потренироваться ещё... А теперь марш спать!"
                    $ mgg.kissing = 0.9
            Max_01 "Конечно! Спасибо, тётя Кира!"
            $ poss['seduction'].OpenStage(7)
            jump .end

        "Как в прошлый раз?" if talk_var['teachkiss'] == 2:
            # второй урок
            $ renpy.block_rollback()
            scene BG char Kira tv-kiss-01
            show Kira tv-kiss 1-01
            Kira_01 "В прошлый раз я тебе рассказала про прелюдии, но с ними мы разобрались. Так что, давай просто будем повышать твою технику опытным путём..."
            menu:
                Max_02 "Да я только за!"
                "{i}поцеловать её{/i}":
                    pass
            $ __r1 = renpy.random.choice(['02', '03'])
            $ renpy.show('Kira tv-kiss 1-'+__r1)
            Max_04 "{i}Как же это приятно... Она такая... и такое делает язычком... Ух... Это просто невероятно...{/i}"
            show Kira tv-kiss 1-01
            Kira_02 "Ну, уже лучше... Возвращаясь к прелюдии, кроме самих поцелуев важную роль играют так же и обоюдные ласки, которыми обмениваются партнёры во время поцелуев."
            Max_07 "Ласки, это в смысле..."
            Kira_07 "Прикосновения, Макс. Как и во время поцелуя, важно поймать нужный момент, чтобы не отпугнуть свою девушку твоим натиском, а наоборот - разжечь в ней страсть! Нужно чувствовать её и чего она хочет, понимаешь?"
            Max_01 "Не особо. Надо пробовать, тётя Кира."
            menu:
                Kira_05 "Ты прав. Когда вы оба уже во всю будете увлечены поцелуем, начни с лёгких прикосновений к талии, ножкам... Поверь мне, ты почувствуешь, нравится ли ей это... Ну что, попробуем?"
                "{i}поцеловать её{/i}":
                    pass
            $ __r1 = renpy.random.choice(['02', '03'])
            $ renpy.show('Kira tv-kiss 1-'+__r1)
            menu:
                Max_03 "{i}О да! Кажется, мне никогда не надоест такая практика... Она так увлечённо целуется... Что она там говорила, про нужный момент? Может быть, это как раз он?{/i}"
                "{i}прикоснуться к её груди{/i}":
                    $ renpy.block_rollback()
                    show Kira tv-kiss 1-04
                    Kira_03 "Макс, это уже слишком! Зачем же так сразу? Как мне кажется, ты так ничего и не понял..."
                    Max_07 "Просто решил начать с этого."
                    show Kira tv-kiss 1-01
                    Kira_04 "Что я тебе говорила про излишнюю самоуверенность? Спешка хороша для другого дела... и то, только по настроению. Понял меня?"
                    Max_03 "Ага... Я учту это."
                    Kira_01 "Тогда, на сегодня мы закончили. Конечно, получается у тебя пока не очень, но это нормально. И ты знаешь, где меня найти, если захочешь потренироваться ещё... А теперь марш спать!"
                    Max_01 "Конечно! Спасибо, тётя Кира!"
                    $ mgg.kissing += 0.4

                "{i}прикоснуться к её попке{/i}":
                    $ renpy.block_rollback()
                    show Kira tv-kiss 1-01
                    Kira_03 "Макс, ты немного спешишь! Зачем же так сразу? Как мне кажется, ты так ничего и не понял..."
                    Max_07 "Просто решил начать с этого."
                    Kira_04 "Что я тебе говорила про излишнюю самоуверенность? Спешка хороша для другого дела... и то, только по настроению. Понял меня?"
                    Max_03 "Ага... Я учту это."
                    Kira_01 "Тогда, на сегодня мы закончили. Конечно, получается у тебя пока не очень, но это нормально. И ты знаешь, где меня найти, если захочешь потренироваться ещё... А теперь марш спать!"
                    Max_01 "Конечно! Спасибо, тётя Кира!"
                    $ mgg.kissing += 0.4

                "{i}подтянуть её ближе к себе{/i}":
                    $ renpy.block_rollback()
                    scene BG char Alice tv-mass-05
                    show Kira tv-kiss 2-01
                    menu:
                        Max_02 "{i}Ничего себе! Кира меня не оттолкнула! Похоже, я всё делаю верно... и, наверно, можно двигаться дальше? Боже мой, какая же у неё гладкая кожа, просто шёлк!{/i}"
                        "{i}прикоснуться к её груди{/i}":
                            pass
                    scene BG char Kira tv-kiss-03
                    show Kira tv-kiss 3-01
                    Max_04 "{i}Отлично! Кажется, ей действительно нравится, что я делаю... Она целует меня с такой жадностью, пока я мну её обалденную грудь... Она прикасается всё ниже и ниже, класс!{/i}"
                    if talk_var['kira.bath.mass']==0:
                        #если массажа ног Киры в ванне ещё не было
                        Kira_07 "Ммм... Макс... думаю... нам надо заканчивать. Уже ведь так поздно."
                        scene BG char Kira tv-kiss-01
                        show Kira tv-kiss 1-04
                        Kira_04 "Уже значительно лучше. С сегодняшним уроком ты справился очень хорошо, но хорошего помаленьку. А то у тебя будет мозоль на языке... А теперь бегом спать! Я тут планирую ещё немного телек посмотреть..."
                        $ talk_var['kira.tv.touch'] = 1 # теперь в ванной Макс сможет получить FJ (с убеждением)
                        $ mgg.kissing += 0.6
                    else:
                        #если был массаж ног Киры в ванне
                        $ _ch2 = GetChance(mgg.social, 2, 900)
                        menu:
                            Kira_07 "Ммм... Макс... думаю... нам надо заканчивать. Уже ведь так поздно."
                            "Давай ещё немного, тётя Кира... У меня ведь неплохо получается? \n{color=[_ch2.col]}(Убеждение. Шанс: [_ch2.vis]){/color}":
                                pass
                        if not RandomChance(_ch2.ch): ###Убеждение не удалось
                            Kira_04 "[failed!t]Уже значительно лучше. С сегодняшним уроком ты справился очень хорошо, но хорошего помаленьку. А то у тебя будет мозоль на языке... А теперь бегом спать! Я тут планирую ещё немного телек посмотреть..."
                            $ mgg.social += 0.05
                            $ mgg.kissing += 0.6
                        else:
                            $ mgg.social += 0.1
                            $ mgg.kissing += 0.8
                            menu:
                                Kira_09 "[succes!t]О да... уже значительно лучше... Ты быстро схватываешь... Ммм..."
                                "{i}развязать её ночнушку{/i}":
                                    scene BG char Kira tv-kiss-03
                                    show Kira tv-kiss 3-02
                                    Max_05 "{i}Да! Я сделал это! Наконец-то я могу насладиться нежностью её обнажённой груди... Обалдеть можно, какая же она классная! Похоже, Кире тоже это нравится, я уже чувствую, как набухли её сосочки. Ухх, чёрт... она она начала поглаживать мой член...{/i}"
                                    Kira_05 "О боже, Макс... Какой же ты развратник, забрался мне под ночнушку... Охх... Это очень мило, что я вызываю такое влечение у своего племянника, но нам надо остановиться..."
                                    Max_07 "Почему?"
                                    scene BG char Kira tv-kiss-01
                                    show Kira tv-kiss 1-04a
                                    Kira_04 "Потому что с сегодняшним уроком ты справился очень хорошо, но хорошего помаленьку. А то у тебя будет мозоль на языке... А теперь бегом спать! А мне ещё нужно придти немного в себя после этого..."
                    Max_01 "Конечно! Спасибо, тётя Кира! Спокойной ночи!"

            jump .end
        "Отлично!" if talk_var['teachkiss'] > 2:
            # третий и последующие уроки поцелуев
            jump .end


    label .good_night:
        Kira_02 "Хорошо, Макс. Спокойной ночи! Я тоже уже ложусь спать... скоро."
        Max_01 "Ага, приятных снов..."
        jump .end

    label .failed:
        Kira_14 "[failed!t]Приятно слышать, Макс, но тебе пора спать! Три часа ночи... Мне уже и самой пора ложиться спать."
        Max_08 "Вот так всегда..."

    label .end:
        $ spent_time += 30
        $ current_room = house[0]
        jump Sleep


label kira_shower:
    return


label kira_lisa_shower:
    return


label kira_alice_shower:
    return
