
label Eric_talk_afterdinner:
    $ renpy.block_rollback()
    if day > 4:
        jump .second_talk
    $ spent_time = 20
    $ current_room = house[6]

    scene BG char Max talk-terrace-00
    show Eric meet 01a
    show Max talk-terrace 01a
    Eric_00 "Макс, пока твоя мама переодевается, я бы хотел с тобой поговорить. С глазу на глаз, так сказать..."
    if talk_var['empathic'] > 1:
        Max_01 "Конечно..."
        $ talk_var['empathic'] = 7
        menu:
            Eric_05 "Я заметил, что ты настроен вполне дружелюбно. Для меня важно подружиться с твоей семьёй, чтобы твоя мама не испытывала дискомфорт на этой почве, если ты меня понимаешь..."
            "Понимаю, хорошо...":
                jump .good
            "И зачем мне это?":
                jump .bad
    elif talk_var['empathic'] > 0:
        Max_00 "Ну, давай..."
        $ talk_var['empathic'] = 5
        menu:
            Eric_09 "Я знаю, что мы начали знакомство не идеально, но мне показалось, что мы можем найти общий язык. Для меня важно, чтобы твоя мама не испытывала какой-либо дискомфорт из-за этого..."
            "Понимаю, хорошо...":
                jump .good
            "И зачем мне это?":
                jump .bad
    else:
        Max_09 "У меня есть выбор?"
        $ talk_var['empathic'] = 3
        menu:
            Eric_13 "Ты знаешь, мы начали знакомство как-то совсем неудачно. Предлагаю как-то уладить этот конфликт. Я бы очень не хотел, чтобы твоя мама испытывала дискомфорт по этому поводу..."
            "Понимаю, хорошо...":
                jump .good
            "И зачем мне это?":
                jump .bad

    label .good:
        $ talk_var['empathic'] += 1
        show Eric meet 01a
        show Max talk-terrace 01a
        menu:
            Eric_01 "Отлично. Если мы подружимся, ты не пожалеешь, Макс. Но, чтобы убедиться, давай вернёмся к этому разговору через неделю. Если мы найдём общий язык, то всё будет отлично. Ну а если нет..."
            "А если нет, то что?":
                jump .what
            "Думаю, подружимся...":
                $ talk_var['empathic'] += 2
                jump .friend

    label .bad:
        show Max talk-terrace 04a
        show Eric meet 01b
        $ talk_var['empathic'] -= 1
        menu:
            Eric_01 "Я бы на твоём месте не искал врага там, где его нет. Предлагаю вернуться к этому разговору через неделю. Если мы подружимся, ты не пожалеешь. А вот если решишь со мной воевать, то ты точно проиграешь..."
            "Это ещё почему?":
                jump.what
            "Да я не собираюсь воевать...":
                $ talk_var['empathic'] += 1
                jump .friend

    menu .what:
        Eric_05 "Если решишь испытать судьбу, то сам всё скоро узнаешь. У меня есть влияние, деньги, харизма. А главное - я умею убеждать и подчинять других людей. А что есть у тебя?"
        "Да верю, верю. Я просто спросил и не хочу ссориться...":
            $ talk_var['empathic'] += 1
            jump .friend
        "Меня все любят и мне поверят, что ты мне угрожал!":
            $ talk_var['empathic'] -= 2
            show Max talk-terrace 04a
            show Eric meet 01b
            menu:
                Eric_02 "Правда? А я вот уже слышал, что Алиса относится к тебе как к маленькому извращенцу, Лиза рядом с тобой просто потому, что других защитников в доме не было, а мать смотрит на тебя как на неудачника..."
                "Не верю! Всё не так!":
                    jump .bullshit
                "Она так сказала?":
                    jump .shesaid
        "У меня есть мозги и я что-нибудь придумаю!":
            $ talk_var['empathic'] -= 2
            show Max talk-terrace 04a
            show Eric meet 01b
            menu:
                Eric_02 "Серьёзно? И как ты с этими мозгами довёл ситуацию до того, что Алиса в тебе видит маленького извращенца, подглядывающего из-за угла. Лиза рядом только потому, что других защитников не было, а мать... смотрит на тебя как на неудачника!"
                "Не верю! Всё не так!":
                    jump .bullshit
                "Она так сказала?":
                    jump .shesaid

    label .bullshit:
        menu:
            Eric_09 "Ну не знаю... Даже если что-то ещё и не так, то с моей помощью всё так и будет, поверь... Но повторюсь, я не хочу с тобой воевать и лучше бы ты был на моей стороне..."
            "Ладно, посмотрим...":
                jump .ok
            "Никогда. Отвали!":
                jump .no

    label .shesaid:
        Eric_09 "Ну не словами, но я же это вижу со стороны. Тебя выгнали из школы за отношения с учителем. Если ты не понял, то это такой зашквар, что вернуть себе репутацию будет непросто..."
        show Eric meet 01a
        menu:
            Eric_00 "Но повторюсь, я не хочу с тобой воевать. Наоборот, я бы хотел с тобой подружиться и если ты будешь на моей стороне, ты только выиграешь..."
            "Ладно, посмотрим... И что я выиграю?":
                jump .ok
            "Никогда. Отвали!":
                jump .no

    label .ok:
        show Max talk-terrace 01a
        show Eric meet 01a
        menu:
            Eric_05 "Смотря как пройдёт наше общение за следующую неделю. Если помиримся, то я буду помогать решать твои проблемы, ну а ты мои, если такие возникнут с твоей семьёй..."
            "Какие проблемы?":
                menu:
                    Eric_00 "Ты слишком забегаешь вперёд, Макс. Давай сначала посмотрим на то, как всё пойдёт. Дай мне хотя бы шанс тебя убедить..."
                    "Ну, ладно, убедил.":
                        jump .friend
                    "Нет, без вариантов...":
                        jump .no
            "Договорились...":
                jump .friend

    label .no:
        show Max talk-terrace 04a
        show Eric meet 01b
        menu:
            Eric_00 "Ну, как хочешь. Надеюсь, в тебе сейчас говорят эмоции, а не здравый смысл. У тебя есть неделя, чтобы передумать. Тогда и поговорим снова и будет ясно, как быть."
            "{i}промолчать{/i}":
                $ talk_var['empathic'] = 0
                $ poss['alpha'].OpenStage(0)
                jump Waiting

    label .friend:
        show Max talk-terrace 01a
        show Eric meet 01a
        Eric_05 "Я рад, правда. Ты не пожалеешь. Ладно, твоя мама уже идёт, мы поехали. Вернёмся к этому разговору через неделю..."
        Max_04 "Ага..."
        $ poss['alpha'].OpenStage(0)
        jump Waiting

    label .second_talk:
        $ spent_time = 20
        scene BG char Max talk-terrace-00
        if talk_var['empathic'] > 5:
            show Eric meet 01a
            $ renpy.show('Max talk-terrace 01'+mgg.dress)
        else:
            show Eric meet 01b
            $ renpy.show('Max talk-terrace 04'+mgg.dress)
        Eric_00 "Ну что, Макс, готов пообщаться снова, пока твоя мама собирается?"
        if talk_var['empathic']>7:
            Max_01 "Конечно!"
            menu:
                Eric_05 "Отлично! Ну что, вроде бы ты и правда настроен на дружбу. Я прав?"
                "Да, я только за.":
                    jump .goodgood
                "Тебе показалось. Я тебя ненавижу!":
                    jump .badbad
        elif 3<talk_var['empathic']<8:
            Max_00 "Ну, можно..."
            menu:
                Eric_01 "Правда, я не до конца понял твою позицию. Ты настроен на дружбу или хочешь попытаться повоевать?"
                "Думаю, воевать смысла нет...":
                    jump .goodgood
                "Я тебя ненавижу!":
                    jump .badbad
        else:
            Max_09 "Ты ещё здесь?"
            menu:
                Eric_09 "Значит, дружить ты не намерен. Я правильно понимаю?"
                "Почему? Я подумал как следует. Воевать точно не хочу...":
                    jump .goodgood
                "Правильно. Я тебя ненавижу!":
                    jump .badbad

    label .badbad:
        show Eric meet 01b
        $ renpy.show('Max talk-terrace 04'+mgg.dress)
        Eric_02 "Всё ясно с тобой. Ну, это твой выбор. У тебя была неделя, чтобы всё обдумать. Ты решил начать войну, ну что же, не вини меня за то, как я её закончу..."
        $ notify_list.append(_("{color=[orange]}{i}{b}Внимание:{/b} Ваши отношения значительно ухудшились!{/i}{/color}"))
        $ AttitudeChange('eric', -3)
        $ poss['alpha'].SetStage(3)
        jump Waiting

    label .goodgood:
        show Eric meet 01a
        $ renpy.show('Max talk-terrace 01'+mgg.dress)
        Eric_01 "Очень хорошо. Я рад. Ну, ты не пожалеешь, это правильный выбор..."
        Max_09 "Но ты так и не сказал, почему не пожалею..."
        Eric_03 "Ну давай начнём с самого простого. Чего ты хочешь?"
        Max_07 "В каком смысле?"
        menu:
            Eric_05 "Я не слепой и вижу как ты смотришь на своих сестёр и даже маму. Не отрицай, иногда даже твои штаны тебя выдают. Но я тебя не виню, ещё бы, такие цыпочки кругом!"
            "Что? Ты ошибаешься!":
                pass
            "Они никакие не цыпочки!":
                pass
            "Я вдруг понял... Да пошёл ты!":
                jump .badbad
        menu:
            Eric_01 "Да ладно, здесь все свои! Любой мужик бы думал только о том, как поиметь их всех по очереди или даже разом. Это нормально! Я тебя понимаю!"
            "Думаешь?":
                menu:
                    Eric_05 "Уверен в этом. И если честно, надеюсь, у меня всё получится. А ты мне в этом поможешь!"
                    "А если я расскажу про это всем?":
                        jump .tell
                    "Только, если я что-то получу за это...":
                        jump .get
                    "Ты подонок! Я передумал. Убирайся!":
                        jump .badbad
            "Постой, и ты этого хочешь?":
                menu:
                    Eric_05 "Конечно! Я же нормальный мужик... Кстати, ты же никому не расскажешь, верно? Мы же типа друзья?"
                    "А если я расскажу про это всем?":
                        jump .tell
                    "Только, если я что-то получу за это...":
                        jump .get
                    "Ты подонок! Я передумал. Убирайся!":
                        jump .badbad
    menu .tell:
        Eric_03 "Всем? Это кому же? Сёстрам? Ну да, поверят они тебе... Расскажешь матери? Попытайся. Заодно, узнаешь на что способны мои, так сказать, \"чары\"..."
        "Да я не собираюсь рассказывать, просто спросил...":
            pass
        "Вот и проверим...":
            menu:
                Eric_09 "Не ожидал я от тебя такого... Ну ладно, дело твоё... Но учти, на этой войне у тебя нет ни единого шанса!"
                "Да я пошутил! Конечно, не расскажу никому...":
                    pass
                "Вот и посмотрим...":
                    jump .badbad
        "Ты - псих ненормальный. Убирайся!":
            jump .badbad
    Eric_01 "Так ты меня разводишь? А я тебе почти поверил! Молодец..."
    Max_09 "Ладно, а что я с этого получу?"

    label .get:
        Eric_03 "Отличный вопрос, Макс! Пришло время выяснить, чего же именно ты хочешь?"
        Max_07 "Я хочу деньги, подглядывать и кое-что ещё..."
        Eric_05 "Ого. Вот это запросы! Ну, мы договоримся, Макс. Деньги для меня не проблема, но в разумных пределах, конечно..."
        Max_01 "Сколько?"
        Eric_04 "Посмотрим на твоё поведение. Ещё рано торговаться. Если будешь в своё время закрывать глаза, то денег будет больше. Ну а если будешь мешать, то..."
        Max_07 "Ладно. А что насчёт подглядывать? Это можно?"
        Eric_01 "Ну лично я не возражаю. Думаю, даже твою маму подготовлю, чтобы если заметила, то не наказывала. Если ты помнишь, со следующей недели шлёпать вас будут голыми, а это очень унизительно... Что ещё ты там просил?"
        Max_08 "Я бы тоже хотел кое-кого..."
        menu:
            Eric_02 "Если ты про свою семью, то хотя я тебя и понимаю, но торговаться на этот счёт не намерен. Я тебе буду платить за твоё невмешательство. Более того, даже разрешу подглядывать без последствий, а вот все женщины в семье - мои."
            "Договорились!":
                pass
            "Нет, на это я не согласен.":
                menu:
                    Eric_09 "Ну, тогда мы с тобой перед диллемой. Ты знаешь слишком много о моих планах, я знаю, что ты тот ещё извращенец... Как быть?"
                    "Я не извращенец!":
                        menu:
                            Eric_03 "Ну, это как посмотреть. Ты хотел потребовать \"доступа\" к женщинам в своей семье... Если бы твоя мать узнала, сомневаюсь, что это бы хорошо для тебя закончилось, правда?"
                            "А есть только два варианта?":
                                pass
                            "Тогда... Остаётся только послать тебя куда подальше. Такая дружба мне не нужна!":
                                jump .badbad
                    "А есть только два варианта?":
                        pass
                    "Ты знаешь... Я тут подумал. Да пошёл ты! Не надо мне ничего от тебя. Я от тебя избавлюсь!":
                        jump .badbad
                menu:
                    Eric_02 "Боюсь, что да. Либо ты со мной, либо против меня. Напомню - я тебе буду платить, ты сможешь смотреть, но нельзя трогать... Ну или война и ты точно проиграешь. Очень быстро..."
                    "Если так, то я согласен на твои условия, конечно!":
                        pass
                    "Знаешь, что... Эрик? А иди-ка ты в жопу! Без тебя справимся!":
                        jump .badbad
        Eric_05 "Вот это разговор! Ну, тогда мы точно поладим. Если я увижу, что ты держишь свои обещания, то даже дам тебе кое-что, от чего ни один мужик не откажется, поверь!"
        Max_03 "О чём речь?"
        Eric_06 "Любопытно? Ну я намекну... Сможешь не только подглядывать, но и принимать участие..."
        Max_06 "Да ладно?"
        Eric_03 "Ага. Так что, в твоих интересах мне не только не мешать, но и помогать, если возникнет такая необходимость. Ну там, слово замолвить или сделать что-то..."
        Max_05 "Договорились!"
        Eric_01 "Ну всё, Макс. Я рад, что мы разобрались и, так сказать, поделили территорию. Теперь всё в твоих руках. Ну и в моих тоже... О, твоя мама идёт. Ну всё, пока!"
        $ notify_list.append(_("{color=[lime]}{i}{b}Внимание:{/b} Ваши отношения значительно улучшились{/i}{/color}"))
        $ poss['alpha'].SetStage(2)
        $ AttitudeChange('eric', 4)
        jump Waiting


label eric_resting:
    scene BG char Ann relax-evening-01
    $ renpy.show('Eric relax '+pose3_1+eric.dress)
    $ persone_button1 = 'Eric relax '+pose3_1+eric.dress+'b'
    return


label eric_ann_tv:
    $ renpy.block_rollback()
    scene BG tv-watch-01
    $ film = ae_tv_order[0]

    if tv_scene == '' or not peeping['ann_eric_tv']:
        ### Дальний план, нейтральная поза
        $ renpy.show('porn_'+film+' 01_02', at_list=[tv_screen,])
        $ renpy.show('Eric tv-watch 01'+eric.dress)
    elif peeping['ann_eric_tv'] > 1:
        $ renpy.show('tv serial '+renpy.random.choice(['01','02','03','04','05','06','07']), at_list=[tv_screen,])
        $ renpy.show('Eric tv-watch 01'+eric.dress)
    else:
        ### Дальний план, поза, на которой остановилось подглядывание
        if pose2_3 == '01':
            $ renpy.show('porn_'+film+' 05_06', at_list=[tv_screen,])
        else:
            $ renpy.show('porn_'+film+' 07_08', at_list=[tv_screen,])
        $ renpy.show('Eric tv-watch '+tv_scene+pose2_3+eric.dress)

    if peeping['ann_eric_tv']:
        return
    ### Дальний план, нейтральная поза

    $ peeping['ann_eric_tv'] = 1
    if flags['ae.tv.hj'] > 0:
        $ txt = _("Кажется, мама с Эриком смотрят какой-то фильм. Наверняка, снова порно...")
        $ txt2 = _("Макс, мы тут с Эриком фильм смотрим. Я бы тебя пригласила, но он не для детей. Ты не мог бы погулять где-то? И, пожалуйста, не подглядывай. Хорошо?")
    else:
        $ txt = _("Кажется, мама с Эриком смотрят какой-то фильм. Интересно, какой...")
        $ txt2 = _("Ой, Макс... Мы тут с Эриком фильм смотрим. Я бы тебя пригласила, но он не для детей. Ты не мог бы погулять где-то? И, пожалуйста, не подглядывай. Хорошо?")
    menu:
        Max_07 "[txt!t]"
        "{i}наблюдать за ними{/i}":
            pass
        "{i}подойти к ним{/i}":
            scene BG lounge-tv-00
            $ renpy.show('Eric tv '+renpy.random.choice(['01', '02', '03'])+eric.dress)
            menu:
                Ann_12 "[txt2!t]"
                "Конечно, мам! {i}(уйти){/i}":
                    jump .end
                "Конечно, мам! {i}(спрятаться){/i}":
                    scene BG tv-watch-01
        "{i}уйти{/i}":
            $ spent_time += 10
            $ current_room = house[0]
            jump Waiting

    $ tv_scene = renpy.random.choice(['bj', 'hj']) if flags['ae.tv.hj'] > 0 else 'hj'

    $ spent_time += 10
    $ pose2_3 = '01'
    $ renpy.show('porn_'+film+' 03_04', at_list=[tv_screen,])
    $ renpy.show('Eric tv-watch '+tv_scene+pose2_3+eric.dress)
    $ _alt = False

    if tv_scene == 'bj':
        ### Дальний план, минет
        if flags['ae.tv.bj'] > 0:
            $ txt = _("Ого! На экране стало интереснее! А мама, видимо, снова потянулась отсасывать Эрику...")
            $ txt2 = _("На экране уже во всю идёт самое интересное и мама скинула полотенце! Вот бы посмотреть на эту голую попку с другого ракурса... Может быть, подойти ближе?")
        else:
            $ txt = _("Ого! На экране стало интереснее! А куда это мама так наклонилась?")
            $ txt2 = _("Ничего себе! Эрик стянул с мамы полотенце и я вижу её голую попку! Может быть, подойти ближе?")
        menu:
            Max_08 "[txt!t]"
            "{i}продолжать смотреть{/i}":
                ### Дальний план, минет без полотенца
                $ spent_time += 10
                $ pose2_3 = '02'
                $ ann.dress_inf = '00'
                $ renpy.show('porn_'+film+' 05_06', at_list=[tv_screen,])
                $ renpy.show('Eric tv-watch '+tv_scene+pose2_3+eric.dress)
                menu:
                    Max_07 "[txt2!t]"
                    "{i}Что за вопрос? Конечно!{/i}":
                        jump .closer1
                    "{i}взглянуть со стороны{/i}" if flags['ae.tv.hj'] != 0 and mgg.stealth >= 11.0:
                        $ _alt = True
                        jump .closer1
                    "{i}продолжать смотреть{/i}":
                        $ spent_time += 10
            "{i}подойти ближе{/i}":
                ### Ближний план, минет в полотенце
                jump .closer1
            "{i}взглянуть со стороны{/i}" if flags['ae.tv.hj'] != 0 and mgg.stealth >= 11.0:
                $ _alt = True
                jump .closer1
            "{i}тихо уйти{/i}":
                jump .end
    else:
        ### Дальний план, дрочка
        if flags['ae.tv.bj'] > 0:
            $ txt = _("Ого! На экране стало интереснее! А мама, вроде, снова начала дрочить Эрику...")
            $ txt2 = _("На экране уже во всю идёт самое интересное и мама скинула полотенце! Жаль, что её грудь не очень хорошо видно... Может быть, подойти ближе?")
        else:
            $ txt = _("Ого! На экране стало интереснее! А что это мама там делает?")
            $ txt2 = _("Ничего себе! Эрик стянул с мамы полотенце и я вижу её голую грудь! Может быть, подойти ближе?")
        menu:
            Max_08 "[txt!t]"
            "{i}продолжать смотреть{/i}":
                ### Дальний план, дрочка без полотенца
                $ spent_time += 10
                $ pose2_3 = '02'
                $ ann.dress_inf = '00'
                $ renpy.show('porn_'+film+' 05_06', at_list=[tv_screen,])
                $ renpy.show('Eric tv-watch '+tv_scene+pose2_3+eric.dress)
                menu:
                    Max_07 "[txt2!t]"
                    "{i}Что за вопрос? Конечно!{/i}":
                        jump .closer1
                    "{i}взглянуть со стороны{/i}" if flags['ae.tv.hj'] != 0 and mgg.stealth >= 11.0:
                        $ _alt = True
                        jump .closer1
                    "{i}продолжать смотреть{/i}":
                        $ spent_time += 10
            "{i}подойти ближе{/i}":
                ### Ближний план, дрочка в полотенце
                jump .closer1
            "{i}взглянуть со стороны{/i}" if flags['ae.tv.hj'] != 0:
                $ _alt = True
                jump .closer1
            "{i}тихо уйти{/i}":
                jump .end

    ## откровенная порнуха на экране
    $ renpy.show('porn_'+film+' 07_08', at_list=[tv_screen,])
    if flags['ae.tv.hj'] > 0:
        menu:
            Max_10 "Если меня заметят, пока я смотрю порно на большом экране и подглядываю за тем, что происходит здесь же, наяву, меня точно накажут"
            "{i}продолжать смотреть{/i}":
                $ renpy.show('porn_'+film+' 09_10', at_list=[tv_screen,])
                Max_09 "Похоже, порно подходит к концу, а вот Эрик не кончает! Наверно, они просто решили {i}разогреться{/i} перед тем, что будет в маминой спальне... А это значит, мне пора уходить."
                $ spent_time = 50 - int(tm[-2:])  # ещё10 минут добави концовка
                jump .end
            "{i}тихо сматываться{/i}":
                jump .end
    else:
        menu:
            Max_10 "Ого! Если меня заметят, пока я подглядываю за ТАКИМ, меня точно накажут. Нужно срочно уходить!"
            "{i}подойти ближе{/i}":
                jump .closer1
            "{i}взглянуть со стороны{/i}" if flags['ae.tv.hj'] != 0 and mgg.stealth >= 11.0:
                $ _alt = True
                jump .closer1
            "{i}тихо уйти{/i}":
                jump .end

    label .closer1:
        ### ближний план отрисовываем согласно установленным переменным bj/hj + поза в полотенце/без полотенца
        $ spent_time += 10
        if _alt:
            scene BG char Alice tv-mass-05
            $ renpy.show('Max tv 02'+mgg.dress)
            $ renpy.show('Eric tv '+tv_scene+pose2_3+eric.dress+'-alt')
        else:
            scene BG lounge-tv-00
            $ renpy.show('Eric tv '+tv_scene+pose2_3+eric.dress)

        if flags['ae.tv.hj'] == 0:
            if pose2_3 == '01':
                Max_15 "Она что, дрочит ему?! Прямо здесь... Нужно это срочно прекратить! Только, как это лучше сделать?" nointeract
            else:
                Max_16 "Она что, совершенно голая дрочит ему?! Прямо здесь... Нужно это срочно прекратить! Только, как это лучше сделать?" nointeract

            $ __dial = [("Вы охренели!", 2), ("{i}тихо уйти{/i}", 3)]
            if False:
                $ __dial.insert(0, ("Ма-а-ам?", 1))
            $ __r1 = renpy.display_menu(__dial)

            if __r1 == 1:
                Eric_09 "Макс! Чего хотел? Не видишь, мы заняты? Если хочешь смотреть, делай это тихо и нам не мешай. А ты, Ань, не останавливайся, продолжай..."
                Max_12 "Охренеть!"
                jump .end
            elif __r1 == 2:
                pass
            else:
                ##"{i}тихо уйти{/i}":
                jump .end
        else:
            if tv_scene == 'hj':   # дрочка
                if pose2_3 == '01':  # Анна в полотенце
                    Max_11 "Да, она ему дрочит! Прямо здесь... Они настолько увлечены друг другом, что ничего вокруг не замечают! Ну, кроме того, что на экране происходит..." nointeract
                else:
                    Max_07 "Ох... мама такая голая и мокрая... У неё такое соблазнительное тело, а какая попка! Ммм... Хорошо, что они настолько увлечены друг другом, что ничего вокруг не замечают! Ну, кроме того, что на экране происходит..." nointeract
            elif flags['ae.tv.bj']:  # не первый минет
                if pose2_3 == '01':  # Анна в полотенце
                    Max_11 "Да, она отсасывает ему! Прямо здесь... Они настолько увлечены друг другом, что ничего вокруг не замечают! Ну, кроме того, что на экране происходит..." nointeract
                else:
                    Max_07 "Ох... мама такая голая и мокрая... У неё такое соблазнительное тело, а какая попка! Ммм... Хорошо, что они настолько увлечены друг другом, что ничего вокруг не замечают! Ну, кроме того, что на экране происходит..." nointeract
            else:  # первый минет
                Max_15 "Она что, отсасывает ему?! Прямо здесь... Вот же развратница, а если кто-то зайдёт и увидит!" nointeract

            $ __r1 = renpy.display_menu([("{i}тихо уйти{/i}", 3),])
            if __r1:
                jump .end

    ### Макс выдал себя
    $ renpy.show('Eric tv hjbj'+pose2_3+eric.dress)
    $ peeping['ann_eric_tv'] = 2
    menu:
        Ann_13 "Что? Кто здесь? Макс?! Выйди немедленно, не видишь..."
        "Как раз всё вижу. Не стыдно?":
            pass
        "Хорошо. А вы тут продолжайте... Не стесняйтесь.":
            pass
        "Извини, мам. Я просто не ожидал ТАКОЕ увидеть...":
            Ann_04 "Я рада, Макс, что ты всё понимаешь. Сделаем вид, что ничего не было и ты ничего не видел, хорошо?"
            Max_01 "Конечно, мам!"
            jump .end

    Ann_14 "Так, Макс, нам надо поговорить! Отойдем на кухню..."
    Max_08 "Ну ладно."
    #### Разговор с Анной в сторонке
    scene lounge-tv-talk-00
    $ renpy.show('Ann tv-talk '+pose2_3+'a')
    menu:
        Ann_12 "Послушай, сынок... Понимаешь, взрослым иногда нужно уединяться. Мы думали, что все наверху и что мы тут, всем и так понятно... В общем..."
        "Хотите потрахаться - идите в свою комнату. Нечего этим на виду у всех заниматься!":
            Ann_15 "Макс! Что за слова? И как ты со мной разговариваешь? А ну-ка марш в свою комнату. Чтобы не видела тебя тут, пока мы смотрим фильмы!"
            Max_11 "Ладно, ладно. Так бы сразу и сказала..."
        "Да всё в порядке. Это ты извини... Я просто от неожиданности крикнул.":
            Ann_04 "Макс! Ты у меня такой умница. Хорошо, что ты всё понимаешь. Ладно, иди в свою комнату, мы больше не будем... Тебя смущать..."
            Max_01 "Хорошо, мам..."
        "А вы не против, если я посмотрю?":
            Ann_13 "Посмотришь? На что? На нас? Или фильм? В любом случае нет! Ты ещё маленький. И, вообще, почему я перед тобой отчитываюсь. Дай взрослым отдохнуть. А ты иди в свою комнату!"
            Max_10 "Ладно, уже ухожу..."

    label .end:
        $ flags['ae.tv.'+tv_scene] += 1
        $ spent_time += 10
        $ current_room = house[0]
        jump Waiting


label eric_ann_fucking:
    scene location house annroom door-night
    if peeping['ann_eric_sex1'] > 0:
        return

    $ peeping['ann_eric_sex1'] = 1

    $ _ch1 = GetChance(mgg.stealth, 3, 900)
    menu:
        Max_00 "Судя по звукам, мама с Эриком чем-то занимаются. Открыть дверь точно не стоит, влетит..."
        "{i}заглянуть в окно\n{color=[_ch1.col]}(Скрытность. Шанс: [_ch1.vis]){/color}{/i}":
            pass
        "{i}уйти{/i}":
            $ current_room = house[1]
            jump AfterWaiting

    $ spent_time += 10
    $ fuck_scene = renpy.random.choice([6,1,2,3,4,5,6,1,2,3,4,5,6,1,2,3,4,5,6])
    if fuck_scene in [3, 6]:
        scene BG char Eric bed-02
    else:
        scene BG char Eric bed-01
    if fuck_scene == 6:
        show AnimAnnEric1
    else:
        $ renpy.show('Eric fuck 0'+str(fuck_scene))
    if fuck_scene in [3, 6]:
        $ renpy.show('FG ann&eric-voyeur-02')
    else:
        $ renpy.show('FG ann&eric-voyeur-01')

    if RandomChance(_ch1.ch):
        $ Skill('hide', 0.1)
        $ ann.dress_inf = '00'
        if fuck_scene == 1:
            Max_10 "[undetect!t]Боже мой, что моя мама творит?! Неужели ей действительно нравится отсасывать этому придурку?!" nointeract
        elif fuck_scene == 2:
            Max_07 "[undetect!t]Вот это да! Прямо как в крутом порнофильме! Я даже представить себе не мог, что моя строгая мама способна на такое. Да и Эрик от неё не отстаёт... Кажется, ей это очень нравится!" nointeract
        elif fuck_scene == 3:
            Max_10 "[undetect!t]Что?! Моя мама сосёт этому уроду? Эрик, гад, он же... трахает её в рот, как какую-то дешёвую уличную шлюху! Почему она ему это позволяет?!" nointeract
        elif fuck_scene == 4 or fuck_scene == 6:
            Max_08 "[undetect!t]Ну вот, Эрик трахает маму сзади, да так активно... Кажется, у неё просто нет сил противиться этому, хотя, может быть, ей это даже нравится!" nointeract
        elif fuck_scene == 5:
            Max_07 "[undetect!t]Ничего себе! Вот это страсть! Моя мама скачет на Эрике как сумасшедшая! Я даже представить себе не мог, что она способна на такое! Кажется, они так увлечены друг другом, что не заметят, если я выйду из-за угла..." nointeract
    else:
        if fuck_scene == 6:
            scene BG char Eric bed-02
            $ renpy.show('Eric fuck 06b')
            $ renpy.show('FG ann&eric-voyeur-02')
        else:
            $ renpy.show('Eric fuck 0'+str(fuck_scene)+'b')
        Ann_15 "[spotted!t]Макс?! Какого чёрта? Ты за нами подглядываешь?! Завтра ты будешь наказан! Немедленно убирайся!"
        $ Skill('hide', 0.05)
        $ punreason[3] = 1
        $ current_room = house[0]
        jump Waiting

    $ Skill('hide', 0.1)

    $ rez = renpy.display_menu([(_("{i}продолжить смотреть{/i}"), 0), (_("{i}уйти{/i}"), 1)])
    if rez > 0:
        $ current_room = house[0]
        jump Waiting

    if fuck_scene == 6:
        scene BG char Eric bed-02
        $ renpy.show('Eric fuck 06a')
        $ renpy.show('FG ann&eric-voyeur-02')
    else:
        $ renpy.show('Eric fuck 0'+str(fuck_scene)+'a')
    if fuck_scene == 1:
        Max_09 "Чёрт, этот удачливый ублюдок кончил ей прямо в рот, причём, судя по довольному лицу мамы, ей это понравилось! Ну почему таким уродам всегда везёт?! Ладно, надо уходить, а то они сейчас меня заметят..."
    elif fuck_scene == 2:
        Max_08 "Ого! Похоже, мама кончила и... Эрик тоже... Хорошо, что хоть не маме в рот... А она у нас та ещё проказница! Пора сматываться, пока меня не заметили!"
    elif fuck_scene == 3:
        Max_11 "Чёрт, эта сволочь кончила ей прямо в рот и... похоже мама не в восторге от всего этого. Бедная моя мама... это нельзя так просто оставлять! А пока лучше скорее уходить, не хватало ещё, чтобы меня увидели..."
    elif fuck_scene == 4 or fuck_scene == 6:
        Max_10 "Ох, чёрт... наконец-то Эрик кончил и... хорошо, что не в маму... Вот же счастливый сукин сын... залил ей своей спермой всю спину... Пожалуй, не стоит здесь задерживаться, они могут меня увидеть."
    elif fuck_scene == 5:
        Max_10 "Чёрт возьми... он не сдержался и уже кончил... Хотя, это не удивительно, после таких-то скачек! Вот же повезло этой сволочи Эрику! И надо уже уходить, пока меня не заметили!"

    $ spent_time += 20
    $ Skill('hide', 0.1)
    $ current_room = house[0]
    jump Waiting


label eric_ann_sleep:
    scene location house annroom door-night
    if peeping['ann_sleep'] == 0:
        $ peeping['ann_sleep'] = 1
        menu:
            Max_00 "Кажется, все спят..."
            "{i}заглянуть в окно{/i}":
                scene BG char Ann bed-night-01
                $ renpy.show('Eric sleep-night '+pose3_1)
                $ renpy.show('FG ann-voyeur-night-00'+mgg.dress)
                if pose3_1 == '01':
                    Max_01 "Похоже, они крепко спят... Совершенно голые! Чёрт, жаль только мама лежит за Эриком и её почти не видно... Почему он такой здоровый?" nointeract
                elif pose3_1 == '02':
                    Max_07 "О, да! Этой ночью мама предстала во всей красе... Полностью голенькая... такая соблазнительная. Только вот обезьяна здесь лишняя повисла!" nointeract
                else:
                    Max_04 "Класс! У моей мамы лучшая попка в мире... а голая она просто сводит с ума! Ещё бы эта гора мышц около неё не лежала..." nointeract
                $ rez = renpy.display_menu([(_("{i}прокрасться в комнату{/i}"), 'sneak'), (_("{i}уйти{/i}"), 'exit')])
                if rez != 'exit':
                    $ spent_time += 10
                    scene BG char Ann bed-night-02
                    $ renpy.show('Eric sleep-night-closer '+pose3_1)
                    if pose3_1 == '01':
                        Max_03 "Они действительно крепко спят... Может самого интересного и не видно, но мама так элегантно, по-женски, закинула на него свою ножку... Хорошо, что такая жара и дома нет кондиционеров... Так, пора уходить." nointeract
                    elif pose3_1 == '02':
                        Max_02 "Просто с ума сойти можно! Она лежит всего в метре от меня... совсем голая... и мне видно её киску... такая красивая! А этот ублюдок, Эрик, так по-хозяйски облапал её... Врезать бы ему, гаду... Ладно, пора бы мне уже уходить, а то они ещё проснутся." nointeract
                    else:
                        Max_05 "Мама такая красивая... а её кругленькая оттопыренная попка просто чудо! Так завораживает! Как бы мне хотелось потрогать её... Ох, мечты... Только бы они сейчас не проснулись..." nointeract
                    $ rez = renpy.display_menu([(_("{i}уйти{/i}"), 'exit')])
            "{i}уйти{/i}":
                pass
        $ spent_time = 10
        jump Waiting
    return


label eric_ann_shower:
    scene location house bathroom door-morning
    if peeping['ann_shower'] != 0:
        return

    $ peeping['ann_shower'] = 1
    $ spent_time += 10
    menu:
        Max_00 "Похоже, мама вместе с Эриком принимают душ... Или что они там ещё могут делать?"
        "{i}заглянуть со двора{/i}":
            jump .start_peeping
        "{i}воспользоваться стремянкой{/i}" if flags['ladder'] > 2:
            jump .ladder
        "{i}уйти{/i}":
            return

    label .ladder:
        $ talk_var['ae.ladd'] += 1
        $ spent_time += 20
        $ renpy.scene()
        $ renpy.show('Max bathroom-window-morning 01'+mgg.dress)
        Max_04 "Посмотрим, что у нас тут..."
        $ fuck_scene = renpy.random.choice([6,1,2,3,4,5,6,1,2,3,4,5,6,1,2,3,4,5,6])

        scene BG bathroom-morning-00
        if fuck_scene == 4:
            show Eric bath-window-morning 02a
        else:
            $ renpy.show('Eric bath-window-morning 0'+str(fuck_scene)+'a')
        show FG bathroom-morning-00
        $ Skill('hide', 0.05)
        $ spent_time += 30
        Max_07 "Охх... Боже мой, какие нежности. Похоже, сейчас что-то начнётся..."

        if fuck_scene == 1:
            show Eric bath-window-morning 01b
            Max_10 "Моя мама снова отсасывает этому... Эрику! Да с такой страстью! Ей что, действительно так нравится это делать или она его настолько любит? Хотя о втором мне даже думать не хочется..."
            show Eric bath-window-morning 01c
            Max_09 "Вот чёрт! Эрик кончает маме прямо на лицо, как в каком-то порно! Причём, ей это настолько нравится, что она улыбается и ловит его сперму своим ртом! Неужели она настолько развратна?!"
        elif fuck_scene == 2:
            show Eric bath-window-morning 02b
            Max_09 "Да уж, устроился Эрик хорошо... Мама отсасывает ему с таким наслаждением, аж оторваться не может! Неужели ей действительно нравится сосать этот его огрызок?!"
            show Eric bath-window-morning 02c
            jump .fin2
        elif fuck_scene == 3:
            show Eric bath-window-morning 03b
            Max_09 "Вау! С какой же страстью мама отсасывает Эрику... А ему, похоже, этого даже мало и он пытается засадить свой член поглубже ей в рот... Почему она ему это позволяет, ей что, нравится подчиняться?"
            show Eric bath-window-morning 03c
            jump .fin1
        elif fuck_scene == 4:
            show Eric bath-window-morning 04b
            Max_08 "О Боже! Как бы я мечтал оказаться на месте это счастливого ублюдка! И всё равно, что она - моя мама... Когда её мокрая попка так красиво скачет на члене, голова начинает идти кругом!"
            show Eric bath-window-morning 02c
            jump .fin2
        elif fuck_scene == 5:
            show Eric bath-window-morning 05b
            Max_10 "Ничего себе! Вот это они вытворяют! Эрик трахает маму, разложив её у зеркала как какую-то шлюшку, а она ещё при этом ласкает свою и без того влажную киску... Да уж, только бы со стремянки не упасть от такого зрелища!"
            show Eric bath-window-morning 03c
            jump .fin1
        else:
            scene BG bathroom-morning-00
            show AnimAnnEric2
            show FG bathroom-morning-00
            Max_10 "Ого! Эрик долбит маму сзади с такой силой, что через стекло даже слышны шлепки о её попку! Похоже, она еле сдерживается, чтобы не кричать слишком громко..."
            hide AnimAnnEric2
            show Eric bath-window-morning 03c
            jump .fin1
        jump .end

    label .fin1:
        Max_08 "Чёрт возьми! Она приняла всю его сперму себе в рот и на лицо, и теперь с такой жадностью и удовольствием слизывает её с его члена... Охх... мама, а ведь ты та ещё развратница!"
        jump .end

    label .fin2:
        Max_10 "А вот и финал не заставил себя ждать! Эрик обкончал маме всё лицо и грудь, и она, похоже, очень довольна... улыбается... Охх... какая же она горячая и развратная!"
        jump .end

    label .end:
        Max_00 "Хоть и не хочется, но пока меня не заметили, лучше уходить..."
        if talk_var['ae.ladd'] > 1 and house[3].cams and not flags['cam2bath']:
            $ flags['cam2bath'] = True
            $ house[3].max_cam = 2
            Max_09 "Кстати, они здесь во всю развлекаются и совершенно не попадают под ракурс моей камеры в ванной! Похоже, мне стоит установить ещё одну камеру, чтобы мои зрители видели всю происходящую здесь картину..."

        $ current_room = house[6]
        jump Waiting

    label .start_peeping:
        $ Skill('hide', 0.03)
        $ __r1 = renpy.random.choice(['01', '02', '03'])
        $ _ch1 = GetChance(mgg.stealth, 3, 900)
        $ _ch2 = GetChance(mgg.stealth, 2, 900)
        $ renpy.scene()
        $ renpy.show('Eric shower '+ __r1)
        $ renpy.show('FG shower 00'+mgg.dress)
        menu:
            Max_07 "Вот это да... Похоже намечается что-то большее, чем просто принять душ! Боюсь даже представить, что будет, если меня поймают, пока я подглядываю... за этим..."
            "{i}продолжить смотреть\n{color=[_ch1.col]}(Скрытность. Шанс: [_ch1.vis]){/color}{/i}":
                jump .closer_peepeng
            "{i}взглянуть со стороны\n{color=[_ch2.col]}(Скрытность. Шанс: [_ch2.vis]){/color}{/i}":
                jump .alt_peepeng
            "{i}уйти{/i}":
                jump Waiting
    label .alt_peepeng:
        if not RandomChance(_ch2.ch):
            jump .not_luck
        $ spent_time += 10
        if __r1 == '01':
            $ __r2 = renpy.random.choice(['01', '02', '03'])
        elif __r1 == '02':
            $ __r2 = renpy.random.choice(['05', '06'])
        else:
            $ __r2 = renpy.random.choice(['04', '07'])
        if not RandomChance(_ch1.ch):
            jump .not_luck
        $ Skill('hide', 0.2)
        $ ann.dress_inf = '00a'
        scene BG shower-alt
        $ renpy.show('Max shower-alt 01'+mgg.dress)
        $ renpy.show('Eric shower-alt '+str(__r2))
        show FG shower-water
        if __r2 == '01':   # минет 1
            Max_10 "[undetect!t]Моя мама снова отсасывает этому... Эрику! Да с такой страстью! Ей что, действительно так нравится это делать или она его настолько любит? Хотя о втором мне даже думать не хочется..." nointeract
        elif __r2 == '02':   # минет 2
            Max_09 "[undetect!t]Да уж, устроился Эрик хорошо... Мама отсасывает ему с таким наслаждением, аж оторваться не может! Неужели ей действительно нравится сосать этот его огрызок?!" nointeract
        elif __r2 in ['03', '04']:   # дрочка
            Max_04 "[undetect!t]Охх... Вот же Эрику повезло... Ведь у мамы такие нежные и ласковые руки! Уже только от одного вида её совершенно голого и мокрого тела можно кончить..." nointeract
        elif __r2 == '05':   # секс лицом к лицу
            Max_06 "[undetect!t]Охренеть! Вот это страсть! Кажется, они так увлечены друг другом, что им всё равно, увидит их кто-то или нет... И похоже, маме это очень нравится!" nointeract
        else:    # секс сзади
            Max_05 "[undetect!t]Ого! Эрик трахает маму сзади, да так активно... И... кажется, ей это очень нравится, она даже двигается ему навстречу... и изнывает от страсти!" nointeract
        $ rez = renpy.display_menu([(_("{i}смотреть до конца{/i}"), 'sneak'), (_("{i}уйти{/i}"), 'exit')])
        if rez == 'exit':
            $ current_room = house[6]
            jump Waiting

        $ spent_time += 10
        $ renpy.show('Eric shower-alt '+str(__r2)+'a')
        if __r2 in ['01', '02']:   # минет
            Max_09 "Вот чёрт! Эрик кончает маме прямо на лицо, как в каком-то порно! Причём, ей это настолько нравится, что она улыбается и ловит его сперму своим ртом! Неужели она настолько развратна?!" nointeract
        elif __r2 in ['03', '04']:   # дрочка
            Max_01 "Ну да! Кто бы сомневался, что Эрик не продержится слишком долго. Мама своё дело знает! Ладно, надо сматываться, пока они меня не заметили!" nointeract
        elif __r2 == '05':   # секс лицом к лицу
            Max_07 "Ох, чёрт... Эрик уже кончил... Хорошо, что не в маму... Счастливый сукин сын... И она ещё улыбается?! Пора бы мне уходить, а то ещё заметят..." nointeract
        else:    # секс сзади
            Max_08 "Чёрт возьми... он уже кончил... Счастливый ублюдок... забрызгал маме всю спину с попкой своей спермой! Нужно уходить, а то они вот-вот меня заметят..." nointeract
        $ rez = renpy.display_menu([(_("{i}уйти{/i}"), 'exit')])
        $ current_room = house[6]
        jump Waiting

    label .closer_peepeng:
        $ spent_time += 10
        if __r1 == '01':
            $ __r2 = renpy.random.choice(['01', '02', '03'])
        elif __r1 == '02':
            $ __r2 = renpy.random.choice(['04', '05'])
        else:
            $ __r2 = renpy.random.choice(['06', '07'])
        if not RandomChance(_ch1.ch):
            jump .not_luck

        $ Skill('hide', 0.2)
        $ ann.dress_inf = '00a'
        scene BG shower-closer
        $ renpy.show('Eric shower-closer '+__r2)
        show FG shower-closer
        if __r1 == '01':
            Max_04 "[undetect!t]Охх... Вот же Эрику повезло... Ведь у мамы такие нежные и ласковые руки! Уже только от одного вида её совершенно голого и мокрого тела можно кончить..." nointeract
        elif __r1 == '02':
            Max_06 "[undetect!t]Охренеть! Вот это страсть! Кажется, они так увлечены друг другом, что им всё равно, увидит их кто-то или нет... И похоже, маме это очень нравится!" nointeract
        else:
            Max_05 "[undetect!t]Ого! Эрик трахает маму сзади, да так активно... И... кажется, ей это очень нравится, она даже двигается ему навстречу... и изнывает от страсти!" nointeract
        $ rez = renpy.display_menu([(_("{i}смотреть до конца{/i}"), 'sneak'), (_("{i}уйти{/i}"), 'exit')])
        if rez == 'exit':
            $ current_room = house[6]
            jump Waiting

        $ spent_time += 10
        $ renpy.show('Eric shower-closer '+__r2+'a')
        if __r1 == '01':
            Max_01 "Ну да! Кто бы сомневался, что Эрик не продержится слишком долго. Мама своё дело знает! Ладно, надо сматываться, пока они меня не заметили!" nointeract
        elif __r1 == '02':
            Max_07 "Ох, чёрт... Эрик уже кончил... Хорошо, что не в маму... Счастливый сукин сын... И она ещё улыбается?! Пора бы мне уходить, а то ещё заметят..." nointeract
        else:
            Max_08 "Чёрт возьми... он уже кончил... Счастливый ублюдок... забрызгал маме всю спину с попкой своей спермой! Нужно уходить, а то они вот-вот меня заметят..." nointeract
        $ rez = renpy.display_menu([(_("{i}уйти{/i}"), 'exit')])
        $ current_room = house[6]
        jump Waiting

    label .not_luck:
        scene BG shower-closer
        if __r1 == '01':
            show Eric shower-closer seen01
        else:
            show Eric shower-closer seen02
        show FG shower-closer
        $ Skill('hide', 0.01)
        Ann_15 "[spotted!t]Макс?! Ты какого чёрта здесь делаешь? Подглядывал за нами?! Сегодня будешь наказан! А ну быстро убирайся!"
        $ punreason[3] = 1 # временно не разбиваем душ и спальню в качестве причины наказания
        $ current_room = house[6]
        jump Waiting
