
label shoping:
    $ renpy.block_rollback()
    $ current_room = house[6]
    scene BG shopping-go-00
    show other shopping-go-01
    show FG shopping-go-00
    with fade4

    menu:
        Ann_05 "Макс! Мы ушли на шоппинг. Не скучай тут без нас, хорошо? Вернёмся часа через 3..."
        "Удачи":
            pass
    # $ spent_time = 10

    # авто-запуск третьей фотосессии:
    if 'kira' in chars and all([kira.dcv.photo.done, kira.dcv.feature.stage==10, kira.dcv.photo.stage==2]):
        jump kira_photoset3

    jump Waiting


label back_shoping:
    $ renpy.block_rollback()

    $ flags.back_shop += 1
    $ current_room = house[6]
    scene BG incoming-00
    show other shopping-go-02
    with fade4

    if flags.back_shop == 1:
        ## --- Девчонки возвращаются с первого шоппинга
        Lisa_02 "Привет, Макс! Мы вернулись..."
        Max_04 "Ну как, удачно сходили?"
        Alice_13 "Ну, так... Платье мне, конечно, не купили. Даже то очень длинное, которое чуть выше колен..."
        Max_07 "А почему не купили?"
        Alice_01 "А вот у мамы спроси..."
        Max_00 "Мам?"
        menu:
            Ann_01 "Алиса, мы это уже обсуждали. Я против платьев, где у тебя всё видно. То, что оно длинное, не значит, что твою задницу не видно через разрез до ушей..."
            "Да, жаль, что не купили его...":
                pass
            "Я бы с удовольствием на это посмотрел...":
                pass
        Alice_02 "Ну, теперь остаётся только мечтать. Спасибо тебе мама за чудесный шоппинг... Лизе тоже понравилось, видимо..."
        Max_01 "А что с Лизой? Купальник купили?"
        menu:
            Lisa_09 "Нет. Мама сказала, что тот который я хотела, слишком дорогой, а остальные слишком открытые..."
            "Слишком дорогой это сколько?":
                Ann_01 "Купальник не может стоить таких денег, а она выбрала дизайнерский какой-то. Как будто мы тут миллионеры все..."
                Max_05 "Ясно. А насколько открытыми были остальные?"
            "Насколько открытые?":
                pass
        Ann_05 "Ну тебе бы точно понравилось, как и всем мужикам, кто её бы увидел, но Лиза ещё маленькая, чтобы в таком виде появляться на публике. Так что..."
        Max_03 "Всё ясно..."
        Alice_13 "В общем, бесполезно потратили время. Но Эрик обещал, что на следующий шоппинг купит нам всё, что мы захотим, да, мам?"
        Max_10 "Что?!"
        menu:
            Ann_01 "Алиса, хватит мечтать. Это всё выглядит так, как будто вы хотите из него выкачать как можно больше денег. Успокойтесь и будьте скромнее..."
            "Верно, сами разберёмся без Эрика":
                Ann_05 "Вот, Макс понимает, что это некрасиво клянчить деньги у других..."
                Max_09 "Кстати, где он?"
            "Кстати, где он?":
                pass
        Ann_00 "Эрик подъедет к ужину. Сейчас у него какие-то дела. Не переживай, сегодня ты его увидишь."
        Max_08 "Да я и не переживаю..."
        Ann_05 "Вот и отлично. Ладно, поболтаем позже..."
        Max_00 "Ага..."
        $ poss['Swimsuit'].open(2)

    elif flags.back_shop == 2:
        ## --- Девчонки возвращаются со второго шоппинга

        Ann_05 "Привет, Макс! Мы вернулись..."
        Max_04 "Рассказывайте, что купили?"
        if poss['Swimsuit'].st() != 5:
            if items['bikini'].have:
                $ poss['Swimsuit'].open(6)
            else:
                $ poss['Swimsuit'].open(4)
            $ lisa.gifts.append('bikini')
            $ setting_clothes_by_conditions()
            $ infl[lisa].add_e(40, True)
            Lisa_03 "А мне Эрик подарил купальник! Именно такой, как я и хотела! Красный, представляешь?!"
            Max_11 "Ясно..."
            Lisa_02 "Что? Ты за меня не рад? Я же теперь смогу загорать в нормальном виде!"
            Max_09 "Конечно, рад... Что-то ещё хорошее купили?"
        else:
            Lisa_02 "Да так, вскую ерунду... Для нас, девочек. Тебе это не интересно..."
            Max_00 "Понятно..."
        if poss['nightclub'].st() < 4:
            Alice_07 "Ты забыла про самое главное, Лиза. Мне Эрик купил платье! Представляешь?!"
            Max_10 "Да, это чудесно..."
            Alice_04 "Вот именно! Теперь я смогу ходить по клубам, а не сидеть дома вечерами! Эрик такой молодец. Я в восторге!"
            Max_11 "Поздравляю..."
            if poss['nightclub'].used(1):
                $ poss['nightclub'].open(2)
            else:
                $ poss['nightclub'].open(3)
            $ infl[alice].add_e(40, True)
            $ alice.gifts.append('dress')
            Ann_07 "Макс, ты какой-то грустный. Что-то случилось? Или мне показалось?"
            Max_00 "Показалось, мам..."
        Alice_02 "Не переживай. Ты всегда можешь заработать и купить себе сам всё, что хочешь..."
        Max_09 "Спасибо за совет..."
    $ spent_time = 10
    jump Waiting


label MeetingEric:
    # устанавливаем новое расписание для Анны и Эрика
    call AddEric from _call_AddEric

    $ renpy.block_rollback()
    $ spent_time = 10
    scene BG char Max meet-eric-villa-00
    show Ann meet-Eric 01a
    show Eric meet 01a
    with fade4
    Ann_00 "Макс, подойди пожалуйста. К нам приехал Эрик. Знакомься..."
    show Max meet-Eric 01a
    menu:
        Ann_05 "Знакомься, это - Эрик. Эрик, это Макс, мой сын..."
        "Очень приятно...":
            hide Eric
            show Max meet-Eric 01c
            menu:
                Eric_01 "Мне тоже. Как жизнь, Макс, чем занимаешься?"
                "Всё отлично. Ищу себя, так сказать...":
                    Eric_06 "Отличные слова, Макс. Ну, надеюсь, найдёшь то, что ищешь..."
                    Max_03 "Ага, спасибо..."
                    jump .good
                "Да то тем, то этим...":
                    show Ann meet-Eric 01a
                    show Max meet-Eric 01a
                    show Eric meet 01a
                    Eric_00 "Понятно. Воздух пинаешь... Ну ничего, в своё время всё изменится..."
                    Max_08 "Я не пинаю воздух..."
                    jump .middle
                "Отвечаю вот на дурацкие вопросы...":
                    show Ann meet-Eric 01b
                    show Max meet-Eric 01b
                    show Eric meet 01b
                    Eric_09 "Ого, какие мы дружелюбные, ну что же, не буду тебя утомлять своими вопросами..."
                    Max_09 "Ну и ладно..."
                    jump .bad
        "{i}промолчать{/i}":
            menu:
                Eric_09 "Очень приятно, Макс. А ты у нас не очень разговорчивый, да?"
                "Стараюсь больше слушать...":
                    Eric_06 "Вот это да! Молодец, Макс. Вот это правильный подход."
                    Max_01 "Спасибо..."
                    jump .good
                "Типа того...":
                    Eric_02 "Всё ясно с тобой. У нас тут скромный мальчик. Ну, не буду тебя утомлять больше..."
                    Max_00 "Отлично..."
                    jump .middle
                "Да было бы о чём говорить...":
                    show Ann meet-Eric 01b
                    show Max meet-Eric 01b
                    show Eric meet 01b
                    Eric_02 "Ань, а ты не говорила, что у тебя сын такой агрессивный..."
                    Max_09 "Ты и не спрашивал..."
                    jump .bad
        "Я думал он выше...":
            show Ann meet-Eric 01b
            show Max meet-Eric 01b
            show Eric meet 01b
            menu:
                Eric_02 "Извини, Макс, что не оправдал твои ожидания. В любом случае, приятно с тобой познакомиться..."
                "Да, мне тоже. Извините...":
                    show Ann meet-Eric 01a
                    show Max meet-Eric 01a
                    show Eric meet 01a
                    Eric_05 "Макс, давай сразу на ты. Никаких формальностей. Это же семейный ужин. Так что, давайте будем проще..."
                    Max_01 "Как скажешь..."
                    jump .good
                "Ага...":
                    show Ann meet-Eric 01a
                    show Max meet-Eric 01a
                    show Eric meet 01a
                    Eric_01 "Понятно. Видимо, Макс слишком скромный. Ну ничего страшного, я в его возрасте тоже смущался..."
                    Max_08 "И ничего я не смущаюсь..."
                    jump .middle
                "А мне вот не очень...":
                    menu:
                        Eric_09 "И что со мной не так? Вот так с первого взгляда делаешь выводы о человеке? Не очень умно... Но для твоего возраста это нормально..."
                        "Да мне всё равно...":
                            pass
                        "{i}промолчать{/i}":
                            pass
                    jump .bad
    label .good:
        show Ann meet-Eric 01a
        show Max meet-Eric 01a
        show Eric meet 01a
        menu:
            Ann_07 "Ну, я рада, что вы познакомились и, вроде бы, нашли общий язык. Прошу, Эрик, к столу. Мы завтракаем и ужинаем на свежем воздухе, на веранде, пойдём покажу."
            "{i}идти к столу{/i}":
                $ eric.flags.crush = 2
                jump Waiting

    label .middle:
        show Ann meet-Eric 01b
        show Max meet-Eric 01a
        show Eric meet 01a
        menu:
            Ann_17 "Макс, мы с тобой потом поговорим. Ну, Эрик, проходи к столу. Мы обычно ужинаем, да и завтракаем на открытом воздухе, тут у нас веранда..."
            "{i}идти к столу{/i}":
                $ eric.flags.crush = 1
                jump Waiting

    label .bad:
        show Ann meet-Eric 01b
        show Max meet-Eric 01b
        show Eric meet 01b
        menu:
            Ann_20 "Макс! Ты почему себя так ведёшь? Это не только тебя не красит, но и меня позорит, что я тебя так воспитала!"
            "Извини, Эрик, я просто не в духе...":
                show Ann meet-Eric 01a
                show Max meet-Eric 01a
                show Eric meet 01a
                Ann_01 "Вот, так уже лучше. Ладно, надеюсь, это недоразумение из-за голодного желудка. Пойдёмте все к столу, пора ужинать..."

            "Нормально себя веду...":
                Ann_01 "Нет, Макс, это точно не нормально. Спишем на то, что ты голодный... Пойдём, Эрик, покажу где мы ужинаем, да и завтракаем. Макс, ты тоже иди ужинать."

            "{i}промолчать{/i}":
                Ann_01 "Молчишь? Нечего сказать или стыдно? Будем считать, что ты просто голодный, а не специально хотел нахамить Эрику. Ладно, пойдёмте ужинать..."

        $ eric.flags.crush = 0
        jump Waiting


label Kira_arrival:
    # устанавливаем новое расписание для Киры и девчонок

    $ renpy.block_rollback()
    scene BG delivery-01
    $ renpy.show("Kira arrival 01"+ann.dress)
    with fade4
    Ann_05 "Ну что, дети, дождались? Встречайте, к нам приехала тётя Кира!"
    Max_05 "Супер!"
    menu:
        Kira_01 "Это кто тут у нас? Макс? Я тебя не узнала! Почти настоящий мужчина!"
        "Здравствуй, тётя Кира!":
            pass
        "Что значит почти? Я настоящий мужчина!":
            Kira_06 "Да я шучу, Макс. Конечно, ты уже мужчина. Вижу, всё у тебя в порядке. И как ты только держишься среди таких девчонок? Кстати, где Лиза и Алиса?"
            Max_04 "Уже бегут..."
    scene BG delivery-00
    $ renpy.show("Kira arrival 02"+lisa.dress)
    $ renpy.show("Ann Kira-arrival 01"+ann.dress)
    Kira_07 "Лиза? Алиса? Неужели, это вы? Все такие большие, с меня ростом!"
    Lisa_03 "Тётя Кира! Ура! Я так соскучилась!"
    Max_02 "Хорошую подушку ты нашла..."
    Alice_07 "Я так рада, что ты приехала! Ты же надолго, да?"
    Kira_04 "Что, уже выгоняете?!"
    menu:
        Ann_04 "Что ты, Кира, оставайся хоть навсегда! Я тоже очень рада тебя видеть, сестрёнка!"
        "А мне можно тоже обнять свою тётю?":
            Lisa_05 "Нет! Она моя! Никому не отдам!"
            Max_00 "Ну и ладно. Я тогда... потом."
        "{i}далее{/i}":
            pass
    Ann_01 "Кира, ты меня удивляешь! Не в мини-юбке... да ещё и такая нарядная! Неужели, прямо в таком виде летела в самолёте?"
    Max_07 "Да, кстати..."
    menu:
        Kira_05 "Нет, что ты. Я прилетела пару дней назад и остановилась в гостинице. Просто... нужно было разобраться кое с чем. Но с делами покончено и специально ради вас я наряжалась всё утро!"
        "В гостинице?":
            pass
        "Дела?":
            Kira_02 "Ага. Не буду утомлять вас подробностями, это никому не интересно. Скажу лишь, что всё улажено и теперь я свободна для своей семьи!"
            jump .end_yard
        "Так ты два дня здесь?":
            pass
    Kira_03 "Да, я прошу прощения, что не предупредила, но не могла завалиться к вам, чтобы тут же убежать. Но я со всем разобралась, всё в порядке, теперь я тут!"
    jump .end_yard

    label .end_yard:
        Max_07 "А мне любопытно, что за дела?"
        Ann_02 "Макс, ну хватит пытать свою тётю. Она сама расскажет, если захочет, верно? А теперь пойдёмте все в дом, скоро будем завтракать."

    ## в гостиной
    scene BG char Kira arrival-03
    show Kira arrival 03
    $ renpy.show("Ann Kira-arrival 03"+ann.dress)
    $ renpy.show("Lisa Kira-arrival 03"+lisa.dress)
    show Alice Kira-arrival 03
    with fade4

    Kira_01 "Так как у меня было время подготовиться к встрече с вами, я приехала не с пустыми руками и привезла вам кое-что..."
    Max_03 "Ух-ты!"
    Kira_07 "Я уже в курсе вашей истории с потерянным вещами. Поэтому, я уточнила у вашей мамы ваши размеры и купила лёгкую домашнюю одежду. И, кажется, правильно сделала. В такую жару, дома, лучше носить что-нибудь лёгкое!"
    Lisa_02 "Ой! Я обожаю новые вещички. А что ты мне привезла, тётя Кира, а?"
    Max_01 "Я думал у нас Алиса шмотки любит..."
    Lisa_01 "Макс, какой ты глупый, кошмар! Шмотки любят все девушки! Ну так что там?"
    menu:
        Kira_05 "Так, вы все давайте держите новую одежду, переодевайтесь и встречаемся за столом, где вы завтракаете. Посмотрим, всем ли всё подошло. А тебе, Макс, я ничего не купила. Подумала, что ты можешь и в шортах ходить. Но подарок тебе сделаю, позже решим какой..."
        ## какой подарок будет сделан Максу?
        "Да не стоит...":
            pass
        "Хорошо, тётя Кира!":
            pass

    scene BG punish-morning 00
    with fade4
    Max_11 "{m}Ну где они все?! Если я начну завтракать без них, то потом все только и будут говорить о моей невоспитанности. Давайте уже быстрее!{/m}"

    ## Примерка Лизы
    show Kira arrival 04-lisa-01
    menu:
        Lisa_02 "Ну как вам? Такой классный топ и юбочка и мой любимый цвет, представляете!"
        "Выглядит отлично!":
            Ann_01 "Да, вот только меня немного смущает длина юбки. Видимо, Кира, ты на свой вкус выбирала. Лиза, покрутись, пожалуйста..."
        "Ну-ка, повернись...":
            pass
    show Kira arrival 04-lisa-01a
    Lisa_03 "Мне кажется, что не слишком коротко. В самый раз! И теперь так свободно и легко... Спасибо тебе, тётя Кира!"
    Max_03 "Да, длина в самый раз..."
    ## Примерка Алисы
    show Kira arrival 04-alice-01
    Alice_02 "Ну всё, теперь моя очередь! Как вам? Лично мне очень нравится! Давно надо было раздеться."
    Max_04 "Именно!"
    menu:
        Ann_02 "Я не поняла. А трусы то на тебе хоть есть? Ты же не планируешь в таком виде ходить дома, Алиса?"
        "А что такого? Пусть ходит, я не против...":
            Alice_03 "Спасибо, Макс! Мам, а что такого? Ты не представляешь как жарко было в джинсах, а теперь хоть тело дышит. Да и жара такая на улице и дома..."
            Ann_01 "Ладно, а что там сзади? Ну-ка покрутись..."
        "{i}промолчать{/i}":
            Alice_05 "Мам, да всё в порядке. На мне шорты, ничего не видно. Вот, смотри..."
    show Kira arrival 04-alice-01a
    Ann_12 "Алиса, у тебя же всё видно, аж грудь вываливается! Смотри, как Макс таращится. Кажется, он даже завис..."
    Max_07 "Э... Ну... да..."
    Alice_02 "Мам, я дома могу одеваться так, как мне нравится. Ну и что, что Макс. Я из-за него не буду целыми днями ходить в парандже! Мне удобно, выглядит отлично! Ну а если и увидит что-то, пусть радуется, мне всё равно..."
    Max_02 "Отличная речь, сестрёнка!"
    ## Примерка Анны
    show Kira arrival 04-ann-01
    menu:
        Ann_04 "Ну, теперь и моя очередь. Как вам? Не слишком откровенно?"
        "Нет, мам, всё отлично!":
            pass
        "А можешь немного повернуться?":
            pass
    show Kira arrival 04-ann-01a
    menu:
        Ann_07 "Мне кажется, шортики слишком короткие... Я в них еле влезла, если честно. Надо худеть..."
        "Ты знаешь, длина оптимальная!":
            pass
            $ __mood = 0
        "Мне нравится, мам!":
            $ __mood = 30
    Kira_03 "Ань, всё сидит на тебе просто отлично. И очень идёт этот цвет, кстати... Ну а теперь моя очередь. Сейчас покажу, что себе прикупила..."
    Max_01 "Интересно..."
    ## Примерка Киры
    show Kira arrival 04-kira-01
    menu:
        Kira_05 "Ну, как я вам? Хороша? И Ань, ни слова больше. Тебе всё кажется слишком открытое или откровенное."
        "Мне нравится. Просто и со вкусом!":
            pass
        "Отличные... вещички!":
            pass
        "И почти ничего не видно...":
            pass
    Kira_07 "Вот, слышала, Ань, детям нравится. Естественно, если в семье есть мужчина, это накладывает свои ограничения, но нельзя же быть настолько зажатыми..."
    Max_04 "Верно!"
    Ann_04 "Ладно, посмотрим. Кира, мне кажется, что не стоило тратиться на подарки. Мы рады, что ты сама смогла к нам приехать... Но всё равно, большое спасибо!"
    Max_07 "Давайте уже завтракать, а то остыло почти всё..."

    $ spent_time = 20
    call AddKira from _call_AddKira

    $ AddRelMood('ann', 0, __mood)
    jump Waiting


label follow_cameras_eric:
    # спящая Лиза
    scene Lisa_sleep
    with fade4
    pause 1.5
    # веранда
    scene location house terrace night-b
    with fade4
    pause .5
    # экран ноутбука
    scene BG char Max laptop-night-01t
    show interface laptop start page at laptop_screen

    show video1_movie:
        xpos 221 ypos 93
    pause .2
    if tm<'02:20':
        # камера Алисиной комнаты
        # Эрик ещё в комнате Алисы
        scene BG char Max laptop-night-01t
        $ renpy.show('BG-cam house aliceroom-0 night', at_list=[laptop_screen,])
        $ renpy.show('Alice cams sleep night '+cam_poses_manager(alice, ['01', '02', '03']), at_list=[laptop_screen])
        show Eric cams Alice-room 02 at laptop_screen
        show FG cam-shum-act at laptop_screen
        with diss3
        menu:
            Max_09 "{m}Неутомимый Эрик! Вали уже обратно к маме в комнату и засыпай. А то у меня тоже есть дела у Алисы в комнате...{/m}"
            "{i}подождать{/i}":
                pass

    # камера Алисиной комнаты
    # комната Алисы без Эрика
    scene BG char Max laptop-night-01t
    $ renpy.show('BG-cam house aliceroom-0 night', at_list=[laptop_screen,])
    $ renpy.show('Alice cams sleep night '+cam_poses_manager(alice, ['01', '02', '03']), at_list=[laptop_screen])
    show FG cam-shum-act at laptop_screen
    with diss3
    pause 1
    # комната Анны с Эриком
    scene BG char Max laptop-night-01t
    $ renpy.show('BG-cam house annroom-0 night', at_list=[laptop_screen,])
    $ renpy.show('Eric cams sleep2 '+cam_poses_manager(eric, ['01', '02', '03']), at_list=[laptop_screen])
    show FG cam-shum-act at laptop_screen
    with diss3
    # Эрик покинул комнату Алисы
    menu:
        Max_01 "{m}Отлично! Пора...{/m}"
        "{i}идти к комнате Алисы{/i}":
            jump frame_eric


label frame_eric:
    scene BG char Alice bed-night-01
    $ renpy.show('Alice sleep-night '+pose3_2)
    $ renpy.show('FG alice-voyeur-night-00'+mgg.dress)
    with fade4

    if pose3_2 == '01':
        Max_07 "{m}О, да! Моя старшая сестрёнка выглядит потрясающе... На изгибы её совершенно обнажённого тела хочется смотреть вечно!{/m}" nointeract
    elif pose3_2 == '02':
        Max_04 "{m}Ого! Мне повезло, что Алиса спит спиной к окну... И не подозревает, что демонстрирует свою голенькую попку для меня во всей красе.{/m}" nointeract
    else:
        Max_01 "{m}Обалденно! Сестрёнка спит выгнув спину, отчего её голая грудь торчит, как два холмика... Соблазнительное зрелище...{/m}" nointeract

    menu:
        "{i}подставить Эрика{/i}":
            pass

    # aliceroom-bedalice-night-02 + alice-sleep-night-(04/05/06)
    scene BG char Alice bed-night-02
    $ renpy.show('Alice sleep-night-closer '+pose3_2)
    menu:
        Max_07 "{m}Уж прости, Алиса, ничего личного. Мне позарез нужно показать всем вам, а особенно маме, что Эрик собирается тут с вами всеми делать...{/m}"
        "{i}осторожно дрочить на Алису{/i}":
            pass

    #aliceroom-bedalice-night-02 + alice-sleep-night-(04/05/06) + max-night-(04b/04c)
    $ renpy.show('Max frame-eric 04'+mgg.dress)
    menu:
        Max_20 "{m}Только бы Алиса не проснулась! Иначе я отправлюсь не в военный лагерь, а сразу на кладбище... Но сейчас лучше думать не об этом, а о том, как сладко я бы проникал своим членом во все её самые интересные дырочки... Д-а-а... Это было бы очень приятно...{/m}"
        "{i}кончить Алисе на ноги{/i}":
            pass

    # aliceroom-bedalice-night-02 + alice-sleep-night-(04/05/06) + max-night-(04b/04c) + alice-sleep-night-(04cum/05cum/06cum)
    $ renpy.show('FG Max frame-eric '+pose3_2)
    menu:
        Max_08 "{m}Фух! Вроде, не проснулась... Надеюсь, этого хватит, чтобы утром Алиса забила тревогу. Наверняка, она сразу подумает, что это я её чем-то испачкал. И вот тогда-то я и покажу снимки с Эриком, а уже всю картину воедино она сложит сама. Если я, конечно, смогу уйти незамеченным в свою комнату...{/m}"
        "{i}осторожно идти спать{/i}":
            $ flags.eric_wallet = 3
            $ SetCamsGrow(house[1], 200)
            jump Sleep


label showdown_with_eric:
    # Алиса утром будит Макса

    scene BG black with fade4
    $ alice.dress = alice.clothes.sleep.GetCur().suf

    # morning-oops-01 + morning-oops-01-lisa-02a + morning-oops-01-alice-(01a/01b)
    scene BG char Lisa morning-oops 01
    show Lisa wake up 01
    $ renpy.show('Alice wake up 01' + alice.dress)

    Alice_17 "Макс! Ну-ка быстро просыпайся, а не то я тебе всыплю! Признавайся, чем ты меня ночью измазал?"
    Max_07 "Измазал?! А почему сразу я?!"
    Alice_16 "А кто ещё это может быть?! Здесь вообще без вариантов и гадалка не нужна! Так что говори, чем измазал и пошли к маме..."
    Lisa_10 "Алиса, хватит уже кричать! Я же сплю..."
    Max_00 "Да, Алиса, ты как-то слишком возбуждена. Тебя может неслабо так шокировать то, что я тебе скажу. И даже покажу."
    Alice_06 "Так я и знала, что это гадость какая-то! Ну так что это?"
    Max_01 "Это Эрик с тобой кое-чем... поделился..."

    scene BG black with diss3 # Fade(0.2, 0, 0.2)
    # morning-oops-04-05 + morning-oops-04-alice-(01a/01b) + morning-oops-04-max-01
    scene BG char Lisa morning-oops 04-05
    $ renpy.show('Alice wake up 04' + alice.dress)
    show Max wake up 04

    Alice_12 "Макс, хватит уже... И что мне этот снимок должен сказать? Хотя... не поняла... Он дрочит что ли?! А где это он?"
    Max_07 "А ты присмотрись и отгадаешь где."
    Alice_14 "Ну... Это получается... Эй! Это он что, у моего окна стоит?"
    Max_04 "Не только..."

    # morning-oops-06 + morning-oops-06-alice-(01a/01b) + morning-oops-06-lisa-01a + morning-oops-06-max-01
    scene BG char Lisa morning-oops 06
    $ renpy.show('Alice wake up 06-1' + alice.dress)
    show Lisa wake up 06-1
    show Max wake up 06-1

    Lisa_13 "Вы что тут с утра пораньше разглядываете? Ой! Эрик! Прямо у тебя в комнате, Алиса..."
    Alice_15 "Он что, правда в моей комнате и... дрочит... на меня?!"
    Max_02 "Ну да. Видишь, не такой уж я и извращенец, правда?"
    Alice_06 "То есть, то... что у меня было на ноге, это..."
    Max_10 "Да, Алиса. Оно самое."

    # morning-oops-06 + morning-oops-06-alice-(02a/02b) + morning-oops-06-lisa-02a + morning-oops-06-max-02
    $ renpy.show('Alice wake up 06-2' + alice.dress)
    show Lisa wake up 06-2
    show Max wake up 06-2

    Alice_17 "Вот же подонок озабоченный! Подожди... А ты какого хрена это всё фотографировал?!"
    Max_09 "Видишь ли, Эрик мне много раз угрожал проблемами и ссылкой в военный лагерь, если я буду ему мешать в подобных... делах. Вот и пришлось подстраховаться."
    Alice_16 "И часто этот онанист чёртов ко мне в комнату ходит по ночам?"
    Max_07 "Я бы сказал, что с завидным постоянством!"
    Alice_12 "Ублюдок какой! А ты чего молчал всё это время?!"
    Max_15 "Так ведь, если говорит Эрик, то это святая истина, а если я, то это чепуха какая-то."
    menu:
        Alice_17 "Вот я Эрику с мамой сейчас устрою! Макс, пошли за мной!"
        "{i}идти за Алисой{/i}":
            scene BG black with fade4

    # annroom-morning-01 + eric-dresses-morning-01 + ann-dresses-morning-05c
    # scene BG char Ann mde-01
    # show Eric dressed 01
    # $ renpy.show('Ann dressed 07b'+random_choice(['1', '2']))
    $ var_pose, var_dress = '07', 'b'+random_choice(['1', '2'])
    scene Ann_dressing eric

    Ann_13 "Алиса! Ты зачем так врываешься?! Стучаться надо..."
    Alice_16 "Не волнуйся, мам, я обязательно сейчас по кое-кому постучу! Знаешь, что твой мужчина вытворяет по ночам?"
    Ann_17 "Это что за тон, Алиса! Конечно, знаю. Ой, вы все здесь! Что происходит?"
    Max_07 "У Алисы есть претензии к Эрику..."

    # hugging-annroom-00 + sud-01-ann-01-eric-01 + sud-01-alice-(01a/01b) + sud-01-max-01
    scene BG char Ann hugging-annroom-00
    show Eric showdown annroom 00
    $ renpy.show('Alice showdown annroom 00' + alice.dress)
    show Max showdown annroom 00

    Alice_17 "И ещё какие! Эрик дрочит на меня по ночам! Это как вообще называется? Я думала у нас Макс извращенец, но ты, Эрик..."
    Ann_16 "Ты что такое вообще несёшь, Алиса?! Обвинять в таком Эрика, это..."
    Alice_18 "Мам! У Макса есть фотографии! А это животное лохматое ещё и обкончало меня этой ночью..."
    Eric_09 "Так, Алиса, давай успокаивайся!"

    # annroom-sud-01 + annroom-sud-01-eric-01 + annroom-sud-01-ann-01 + annroom-sud-01-alice-(01a/01b) + annroom-sud-01-max-01 + annroom-sud-01-lisa-01
    scene BG char Ann mde-02
    show Eric showdown annroom 01
    show Ann showdown annroom 01
    $ renpy.show('Alice showdown annroom 01' + alice.dress)
    show Max showdown annroom 01
    show Lisa showdown annroom 01

    Alice_17 "Ну-ка заткнулся! Меня лучше не злить! Сейчас Макс покажет маме снимки и мы послушаем, как ты это объяснишь..."
    Ann_14 "И что вы хотите мне тут показать? Я не понимаю... Как такое вообще может быть?! Эрик, будь добр, объясни это..."
    Eric_13 "Ань, не знаю, что там за снимки, но могу уверить тебя - это фотошоп!"
    Max_09 "В таком случае, я за то, чтобы провести экспертизу их подлинности!"
    Ann_15 "Эрик! У меня в голове не укладывается то, что я вижу... Господи! Ты же дрочишь на мою дочь! Как ты мог такое делать, мерзавец... Лиза, а с тобой всё в порядке?! Эрик ничего с тобой не делал?"
    Lisa_10 "Нет... Правда, он предлагал мне закрепить на практике те уроки сексуального воспитания, что вы для меня проводили."

    # annroom-sud-01 + annroom-sud-01-eric-02 + annroom-sud-01-ann-02 + annroom-sud-01-alice-(02a/02b) + annroom-sud-01-max&lisa-02
    show Eric showdown annroom 02
    show Ann showdown annroom 02
    $ renpy.show('Alice showdown annroom 02' + alice.dress)
    show Max showdown annroom 02
    hide Lisa

    Alice_18 "Ах так! Значит, ты свои ручищи к нашей маленькой Лизе уже тянешь? Вот... получай, скотина..."
    Ann_20 "Эрик, ты совсем что ли больной?! Как ты мог такое делать с моими девочками! Живо выметайся из нашего дома, пока я полицию не вызвала!"
    show Eric showdown annroom 01
    $ renpy.show('Alice showdown annroom 01' + alice.dress)
    Eric_10 "Какую ещё полицию?! Я ничего не делал. Это всё Макс!"
    Lisa_13 "Не надо всё на Макса сваливать! Он хороший..."
    Alice_16 "Я раньше от души могла навалять Максу, но сейчас я в такой ярости, что запросто наваляю такому здоровому извращенцу, как ты!"

    # annroom-sud-01 + annroom-sud-01-ann-03 + annroom-sud-01-max&lisa-03 + annroom-sud-01-alice-(03a/03b) + annroom-sud-01-eric-03
    scene BG char Ann mde-02
    show Ann showdown annroom 03
    show Max showdown annroom 03
    $ renpy.show('Alice showdown annroom 03' + alice.dress)
    show Eric showdown annroom 03

    Ann_19 "Эрик, проваливай! Я не хочу тебя больше видеть!"
    Eric_09 "Вижу, вы все на взводе, так что я поговорю с вами, когда успокоитесь... А сейчас я оденусь и отправлюсь на работу."
    Ann_20 "Я сказала, {b}ПОШЁЛ ВОН НЕМЕДЛЕННО!!!{/b}"
    Alice_17 "И полотенце можешь оставить себе, грязное животное! Убирайся!"

    scene BG black with diss3 # Fade(0.2, 0, 0.2)
    # sexed-hj + annroom-sud-01-alice-(04a/04b) + annroom-sud-01-ann-04 + annroom-sud-01-lisa-04 + annroom-sud-01-max-04
    scene BG char Eric sexed-hj
    $ renpy.show('Alice showdown annroom 04' + alice.dress)
    show Ann showdown annroom 04
    show Lisa showdown annroom 04
    show Max showdown annroom 04

    Max_00 "Мам, ты только не расстраивайся так из-за него. Всякое бывает..."
    Ann_14 "Всё никак в голове не укладывается, что Эрик тут с вами творил... И что я за мать после этого? Я в шоке..."
    Alice_13 "Уж лучше узнать поздно, чем никогда. А у нас всё ещё лучше... Мы узнали, пока ещё не стало поздно. Так что не переживай, мам, мы все думали, что Эрик... уж точно не такой..."
    Max_09 "Хорошо, что я своим мужским взглядом сразу понял, что он мутный тип!"
    Lisa_09 "Так это получается, что? Про бумажник Эрик наврал?"
    Max_08 "Конечно! Он это придумал, чтобы вы меня в военный лагерь сослали. А то я стал слишком мешаться."
    Ann_13 "Ты прости меня, сынок, я должна была прислушиваться к тому, что ты говорил. Что же теперь делать?"
    Max_01 "Жить, как жили до Эрика! Ничего сложного..."
    Ann_12 "Нет, Макс, я о другом... Он же мой начальник! Как мне теперь быть? Ох, не знаю..."
    Max_07 "Д-а-а... Это проблемка. Ты главное не слушай его оправдания, а то он умеет убеждать, как ты сама убедилась прекрасно. А теперь давайте, все вместе, поддержим маму!"

    # annroom-morning-01 + annroom-sud-01-ann&max&lisa-alice-(05a/05b)
    # scene BG char Ann mde-01
    scene Ann_dressing empty
    $ renpy.show('Alice showdown annroom 05' + alice.dress)

    Ann_13 "Ой, дорогие мои, вы меня так раздавите! Но вы всё равно самые лучшие, кто у меня есть... Простите ещё раз за то, что я привела в наш прекрасный дом этого человека и всё испортила."
    Lisa_01 "Не бери в голову, мам. Не вини себя, всё ведь хорошо. Мы с тобой."
    Alice_05 "И если он начнёт устраивать тебе проблемы на работе, то мы все так же дружно, как сейчас, соберёмся и отпинаем его!"
    Max_09 "Думаю, Эрик не настолько туп, чтобы строить маме козни. Особенно, если она будет с ним жёстче..."
    Ann_17 "Ох, ребята, я постараюсь... Хотя, мне даже думать о нём сейчас противно, не то что видеть. Надеюсь, я не потеряю работу."
    Max_08 "Может, удастся перевестись в какой-нибудь другой отдел?"
    Ann_02 "Надеюсь... Ну всё, мне уже дышать нечем. Пора делами заниматься. Я вас очень люблю..."
    Lisa_05 "И мы тебя!"

    $ spent_time = 40
    $ flags.eric_banished = 1       # Эрик изгнан
    $ eric.dcv.battle.stage = 1     # подстава с фотографиями
    $ poss['alpha'].open(5)         # Макс подставил Эрика через Алису и того прогоняют
    $ poss['discrediting'].open(8)
    $ poss['boss'].open(0)
    $ current_room = house[2]
    $ SetCamsGrow(house[2], 150)

    call restore_relations from _call_restore_relations

    jump Waiting


label dictaphone_showdown_with_eric:
    # разборки с Эриком используя диктофон
    scene BG black with diss3

    # lounge-tv-00 + lounge-tv-ann&eric-hjbj(01/01a)
    scene BG lounge-tv-00
    $ renpy.show('Eric tv 01' + eric.dress)
    # with fade4
    Ann_13 "Макс! Лиза! Алиса! Ну зачем так неожиданно влетать?! Так и заикой стать не долго..."
    Lisa_13 "Извини, мама... Но сейчас кто-то действительно станет заикой! Ты хоть знаешь, какие планы строит Эрик?"
    Ann_15 "Это что за тон, Лиза?! И что это за вопрос? Что происходит?!"
    Alice_13 "Мам, я понятия не имею... Это всё они."
    Max_09 "Лиза тут кое-что узнала... И теперь у неё есть претензии к Эрику..."

    # tv-mass-05 + lounge-sud-01-alice-01 + lounge-sud-01-ann-01 + lounge-sud-01-eric-(01/01a) + lounge-sud-01-max-(01a/01b) + lounge-sud-01-lisa-(01a/01b)
    scene BG tv-mass-05
    show Alice showdown lounge 01
    show Ann showdown lounge 01
    $ renpy.show('Eric showdown lounge 01' + eric.dress)
    $ renpy.show('Max showdown lounge 01' + mgg.dress)
    $ renpy.show('Lisa showdown lounge 01' + lisa.dress)

    Lisa_12 "И ещё какие! Мам, помнишь наши совместные уроки в твоей комнате? Так вот Эрик решил расширить программу моего сексуального образования до практических занятий с ним наедине!"
    Ann_17 "Я ни о чём таком не знаю. В каком смысле, практических?"
    Lisa_10 "В таком, чтобы я повторяла всё то, что делала ты. Это как вообще называется? Я думала у нас Макс извращенец, но ты, Эрик..."
    Ann_16 "Ты что такое вообще несёшь, Лиза?! Обвинять в таком Эрика, это... Я уверена, ты просто что-то не так поняла. Верно, Эрик?"
    Eric_13 "Однозначно!"
    Lisa_09 "Мам, с каких пор ты мне не веришь? Макс специально ходил разговаривать с Эриком и Эрик таких мерзостей ему наговорил про всех... Особенно, про меня!"
    Ann_19 "Что?! Это уже явный перебор, Лиза! Мне или нужны доказательства, или тебя придётся хорошенько наказать!"
    Eric_09 "Так, Лиза, давай успокаивайся! Ань, я сейчас поговорю с ней наедине. Макс видимо совсем страх потерял, своей младшей сестрёнке каких-то сказок понарассказывал."

    # tv-mass-05 + lounge-sud-01-alice-01 + lounge-sud-01-ann-01 + lounge-sud-01-eric-(01/01a) + lounge-sud-01-max-(01a/01b) + lounge-sud-01-lisa-(02a/02b)
    $ renpy.show('Lisa showdown lounge 02' + lisa.dress)

    Lisa_13 "Не собираюсь я с тобой наедине больше оставаться! Макс всё записал на диктофон в телефоне. Сейчас послушаем..."
    "{color=[orange]}(все слушают запись...){/color}"
    Ann_14 "Это же... Неужели это действительно правда?! Эрик, будь добр, объясни это..."
    Eric_13 "Ань, я не знаю, что и где это записано, но могу тебя уверить - это неправда! Уверен на все сто, что эта запись подделка."
    Max_09 "В таком случае, я за то, чтобы провести экспертизу её подлинности!"

    # tv-max&kira-sex01-01-f + lounge-sud-01-alice-03-ann-03 + lounge-sud-01-eric-(03/03a)
    scene BG char Kira tv-sex01-01-f
    show Alice showdown lounge 03
    $ renpy.show('Eric showdown lounge 03' + eric.dress)

    Alice_18 "Ах вот так, значит! Ты свои ручищи к нашей маленькой Лизе уже тянешь? Вот... получай, скотина..."
    Ann_15 "Эрик! У меня просто в голове не укладывается... Как ты мог на такое решиться, мерзавец... Лиза, неужели это действительно правда?"
    Max_07 "Разве это не очевидно, мам? Или ты думаешь, что мы тебя так жестоко разыграть решили?"
    Ann_20 "Эрик, ты совсем что ли больной?! Как ты мог, строя отношения со мной, ещё и планировать развращать моих детей! Живо выметайся из нашего дома, пока я полицию не вызвала!"
    Eric_10 "Какую ещё полицию?! Я ничего не делал. Это всё Макс!"
    Lisa_13 "Не надо всё на Макса сваливать! Он хороший..."

    # after-club-s03-f + lounge-sud-01-eric-(04/04a) + lounge-sud-01-ann-04 + lounge-sud-01-alice-04 + lounge-sud-01-max-(04a/04b) + lounge-sud-01-lisa-(04a/04b)
    scene BG char Kira after-club-s03-f
    $ renpy.show('Eric showdown lounge 04' + eric.dress)
    show Ann showdown lounge 04
    show Alice showdown lounge 04
    $ renpy.show('Max showdown lounge 04' + mgg.dress)
    $ renpy.show('Lisa showdown lounge 04' + lisa.dress)

    Alice_16 "Совсем недавно я могла от души навалять Максу, но сейчас я в такой ярости, что запросто наваляю такому здоровому извращенцу, как ты!"
    Ann_19 "Эрик, проваливай! Я не хочу тебя больше видеть!"
    Eric_09 "Вижу, вы все на взводе, так что я поговорю с вами, когда успокоитесь... А сейчас я, пожалуй, отправлюсь к себе домой."
    Ann_20 "Я сказала, {b}ПОШЁЛ ВОН НЕМЕДЛЕННО!!!{/b}"
    Alice_17 "Убирайся из нашей жизни, грязное животное!"

    # tv-max&kira-sex03-01-f + lounge-sud-01-alice-05-ann-05 + lounge-sud-01-lisa-(05a/05b) + lounge-sud-01-max-(05a/05b)
    scene BG tv-sex03-01
    show Alice showdown lounge 05
    $ renpy.show('Lisa showdown lounge 05' + lisa.dress)
    $ renpy.show('Max showdown lounge 05' + mgg.dress)
    with diss3

    Max_00 "Мам, ты только не расстраивайся так из-за него. Всякое бывает..."
    Ann_14 "Всё никак в голове не укладывается, что Эрик тут с вами собирался творить... И что я за мать после этого? Я в шоке..."
    Alice_13 "Уж лучше узнать поздно, чем никогда. А у нас всё ещё лучше... Мы узнали, пока ещё не стало поздно. Так что не переживай, мам, мы все думали, что Эрик... уж точно не такой..."
    Max_09 "Хорошо, что я своим мужским взглядом сразу понял, что он мутный тип!"
    Lisa_09 "И между прочим, про бумажник Эрик наврал..."
    Max_08 "Кстати, да! Он это придумал, чтобы вы меня в военный лагерь сослали. А то я стал слишком мешаться."
    Ann_13 "Ты прости меня, сынок, я должна была прислушиваться к тому, что ты говорил. Что же теперь делать?"
    Max_01 "Жить, как жили до Эрика! Ничего сложного..."
    Ann_12 "Нет, Макс, я о другом... Он же мой начальник! Как мне теперь быть? Ох, не знаю..."
    Max_07 "Д-а-а... Это проблемка. Ты главное не слушай его оправдания, а то он умеет убеждать, как ты сама прекрасно убедилась. А теперь давайте, все вместе, поддержим маму!"

    # tv-ero-04 + lounge-sud-01-lisa-(06a/06b) + lounge-sud-01-alice-06-ann-06-max-(06a/06b)
    scene BG tv-ero-04
    $ renpy.show('Lisa showdown lounge 06' + lisa.dress)
    $ renpy.show('Max showdown lounge 06' + mgg.dress)
    with diss3

    Ann_13 "Ой, дорогие мои, вы меня так раздавите! Но вы всё равно самые лучшие, кто у меня есть... Простите ещё раз за то, что я привела в наш прекрасный дом этого человека и всё испортила."
    Lisa_01 "Не бери в голову, мам. Не вини себя, всё ведь хорошо. Мы с тобой."
    Alice_05 "И если он начнёт устраивать тебе проблемы на работе, то мы все так же дружно, как сейчас, соберёмся и отпинаем его!"
    Max_09 "Думаю, Эрик не настолько туп, чтобы строить маме козни. Особенно, если она будет с ним жёстче..."
    Ann_17 "Ох, ребята, я постараюсь... Хотя, мне даже думать о нём сейчас противно, не то что видеть. Надеюсь, я не потеряю работу."
    Max_08 "Может, удастся перевестись в какой-нибудь другой отдел?"
    Ann_02 "Надеюсь... Ну всё, мне уже дышать нечем. Пора делами заниматься. Я вас очень люблю..."
    Lisa_05 "И мы тебя!"

    scene BG black with diss3
    $ spent_time = 0
    $ tm = '22:00'
    $ flags.asked_phone = 4
    $ flags.eric_banished = 2       # Эрик изгнан
    $ eric.dcv.battle.stage = 2     # подстава через звукозапись
    $ poss['alpha'].open(6)         # Макс подставил Эрика через Лизу и того прогоняют
    $ poss['boss'].open(1)
    $ poss['discrediting'].open(9)
    $ current_room = house[4]
    $ SetCamsGrow(house[4], 150)

    call restore_relations from _call_restore_relations_1

    jump Waiting


label restore_relations:
    $ flags.eric_wallet = 4

    # восстанавливаем отношения/настроение/влияние Макса.
    $ ann.relmax    = rmi['m_r']
    $ ann.mood      = rmi['m_m']
    $ alice.relmax  = rmi['a_r']
    $ alice.mood    = rmi['a_m']
    $ lisa.relmax   = rmi['l_r']
    $ lisa.mood     = rmi['l_m']

    $ infl[ann].add_m(rmi['m_i'])
    $ infl[alice].add_m(rmi['a_i'])
    $ infl[lisa].add_m(rmi['l_i'])

    $ rmi.clear()

    # снимаем блокировку разговоров
    $ ann.flags.talkblock   = 0
    $ alice.flags.talkblock = 0
    $ lisa.flags.talkblock  = 0
    $ kira.flags.talkblock  = 0

    # Обнуляем отношения и влияние Эрика
    $ ann.releric   = 0
    $ alice.releric = 0
    $ lisa.releric  = 0

    $ infl[ann].sub_e(infl[ann].e[0])
    $ infl[alice].sub_e(infl[alice].e[0])
    $ infl[lisa].sub_e(infl[lisa].e[0])

    # в дальнейшем влияние Эрика никогда не поднимется выше 50%
    $ infl[ann].lim_e = Influence.LIM // 2
    $ infl[alice].lim_e = Influence.LIM // 2
    $ infl[lisa].lim_e = Influence.LIM // 2

    # обнуляем текущие "долги" и договорённости с сёстрами
    $ alice.sorry.owe = False           # вкусняшки
    $ lisa.sorry.owe = False            # вкусняшки
    $ alice.dcv.mistress.disable()      # доминирование
    $ lisa.dcv.special.enabled = False  # ужастики
    $ alice.req.reset()                 # договорённости за сигареты

    $ flags.block_peeping = 0           # можно снова подглядывать в душе

    $ eric.dcv.battle.set_lost(61)      # откат Эрика 2 месяца

    if wcv.catch_Kira.stage > 0:
        $ wcv.catch_Kira.stage = 3      # Если Эрик "палил" Макса с Лизой

    $ notify_list.append(__("{g}Отношения Макса с семьёй значительно улучшились.{/g}"))

    return


label failed_dinner:
    # terrace-punish-evening-00 + after-dinner-eric-(01/01a) + after-dinner-ann-(01/01a/01b)
    scene BG punish-evening 00
    $ renpy.show('Eric talk-terrace 03'+eric.dress)
    $ renpy.show('Ann talk-terrace 03'+ann.dress)
    Eric_00 "Итак, настал момент истины. Я дал тебе, Макс, достаточно времени, чтобы ты вернул мне мои деньги при всех. Ну что, ты готов покаяться и исправить свою ошибку?"

    if mgg.money < 25000:
        # у Макса нет суммы в 25000$
        Max_15 "Да пошёл ты... Нет у меня твоих денег, и ты это прекрасно знаешь!"
    else:
        # у Макса есть сумма в 25000$
        Max_15 "Да пошёл ты... Я ни за что не стану возвращать тебе то, чего не брал! И признавать себя вором тоже не стану!"

    Eric_09 "Что ж, значит продолжаем отпираться... Ладно... Как ты видишь, Ань, я сделал всё, что было в моих силах, но это твой сын и тебе принимать решение."
    Ann_14 "Да-да... Прости, Макс, но ты очень меня разочаровал и... нам не остаётся другого выбора, кроме как отправить тебя в военный лагерь. Надеюсь, там из тебя сделают ответственного человека."
    Max_13 "Что?! Кому это, \"нам\"? Вам с Эриком? Это же не ты сейчас говоришь, а он! Как ты можешь выгонять из дома родного сына?"
    Ann_17 "Прекрати, Макс! Я устала слушать твои оправдания. Ты даже не представляешь, до какой степени мне стыдно, что мой единственный сын - вор!"
    Max_14 "Но мам, это всё неправда! Не говори так! Это же Эрик всё подстроил!"
    Eric_13 "Да, стыдно за тебя, Макс, ты разбиваешь сердце собственной матери... Какой позор..."
    Max_16 "Заткнись, Эрик! Мам, неужели ты не видишь, что он тобой манипулирует?!"
    Ann_18 "Не хочу ничего слышать про это. Всё. Собираешь вещи и едешь. Давно надо было это сделать, но я тебя всё жалела. В результате докатились, воруешь в собственном доме! А что будет дальше?"
    Max_11 "Но мам..."
    Ann_16 "Всё, Макс, разговор окончен!"
    menu:
        Eric_01 "Не переживай, Макс, я позабочусь обо всех, пока ты будешь тянуть лямку в армии..."
        "{i}собираться в военный лагерь{/i}":
            scene BG black with diss3
            # villa-delivery-01 + max-badend-01
            scene BG delivery-01
            show Max bad-end 01a
            with diss3
            Max_14 "{m}Вот так и закончилась моя история. Больше мне не верили и не хотели меня видеть. Конечно, если бы не Эрик, всё было бы хорошо, но этот гад... Теперь ему ничто не помешает сделать с моей семьёй всё, что он задумал... Вот если бы я мог вернуться в прошлое... Но есть ли в этом смысл?{/m}" nointeract
        "{i}накинуться на Эрика{/i}":
            scene BG black with diss3
            # villa-delivery-e-01 + max-badend-01a
            scene BG delivery-01
            show Max bad-end 01b
            with diss3
            Max_14 "{m}Вот так и закончилась моя история. От всей этой несправедливости я немного слетел с катушек и почти размазал Эрика по балкону... Почти... Теперь ему ничто не помешает сделать с моей семьёй всё, что он задумал... Вот если бы я мог вернуться в прошлое... Но есть ли в этом смысл?{/m}" nointeract

    menu:
        "{i}загрузить игру{/i}":
            $ ShowMenu("load")()

        "{i}вернуться в главное меню{/i}":
            pass
        "{i}выйти из игры{/i}":
            $ renpy.quit()

    $ renpy.full_restart()


label left_cream(vr=0):

    if vr == 0:
        Max_10 "{m}Ну вот, крем закончился. Надо ещё купить.{/m}"
        if kol_cream == 0:
            $ items['solar'].use()
            $ items['solar'].unblock()

    elif vr == 1:
        Max_08 "{m}Осталось мало крема, в следующий раз может не хватить, лучше купить заранее.{/m}"
        $ items['solar'].unblock()

    return
