
label LisaTalkStart:

    $ dial = TalkMenuItems()
    if len(dial) == 0:
        jump AfterWaiting

    $ __CurShedRec = GetPlan(plan_lisa, day, tm)
    if __CurShedRec.talklabel is not None:
        call expression __CurShedRec.talklabel from _call_expression_5

    $ dial.append((_("{i}уйти{/i}"), "exit"))

    Lisa_00 "Макс, решил поболтать?" nointeract

    $ rez =  renpy.display_menu(dial)

    if rez != "exit":
        $ renpy.block_rollback()
        $ __mood = GetMood("lisa")[0]
        if rez in gifts['lisa']:
            if renpy.has_label(rez.label):
                call expression rez.label from _call_expression_7
        elif __mood < talks[rez].mood:
            if __mood < -2: # Настроение -4... -3, т.е. всё ну совсем плохо
                jump Lisa_badbadmood
            elif __mood < 0: # Настроение -2... -1, т.е. всё ещё всё очень плохо
                jump Lisa_badmood
            else: # Настроение хорошее, но ещё недостаточное для разговора
                jump Lisa_normalmood
        elif talks[rez].kd_id != "" and talks[rez].kd_id in cooldown and not ItsTime(cooldown[talks[rez].kd_id]):
            jump Lisa_cooldown
        elif renpy.has_label(talks[rez].label): # если такая метка сушествует, запускаем ее
            call expression talks[rez].label from _call_expression_6
        $ dial = TalkMenuItems()
        if len(dial) > 0:
            jump LisaTalkStart       # а затем возвращаемся в начало диалога, если в разговоре не указан переход на ожидание

    jump Waiting            # если же выбрано "уйти", уходим в ожидание


label Lisa_badbadmood:
    menu:
        Lisa_09 "Макс, уйди. Не хочу тебя видеть!"
        "Ок...":
            jump Waiting
        "Я хотел извиниться":
            jump Lisa_asksorry


label Lisa_badmood:
    menu:
        Lisa_09 "Макс, уйди пожалуйста. Не хочу сейчас общаться."
        "Ок...":
            jump Waiting
        "Я хотел извиниться":
            jump Lisa_asksorry


label Lisa_asksorry:
    menu:
        Lisa_09 "Извиниться? Как?"
        "Кажется, я передумал...":
            jump Waiting


label Lisa_normalmood:
    menu:
        Lisa_09 "Не сейчас, Макс..."
        "Ок...":
            jump Waiting


label Lisa_cooldown:
    Lisa_09 "Макс, давай поговорим об этом в другой раз."
    Max_00 "Хорошо, Лиза..."
    jump Waiting


label MorningWood:

    scene BG char Lisa morning-oops-01
    show Lisa morning-oops 01
    menu:
        Lisa_11 "Макс! Это что такое?! Я сейчас маму позову!"
        "Что? Ты о чём?":
            pass
        "Что случилось?":
            pass

    Lisa_12 "Ты чем таким занимался при мне?!"
    Max_12 "Да ты о чём, вообще?"
    scene BG char Lisa morning-oops-02
    show Lisa morning-oops 02
    menu:
        Lisa_13 "Ма-ам! Иди сюда, скорее!"
        "Да это не то, о чём ты думаешь...":
            menu:
                Lisa_12 "Не то, о чём думаю? Это явно то, о чём я думаю! Ма-ам!"
                "Да не ори ты так!":
                    pass
                "Ничего же не случилось...":
                    pass
        "Просто, так бывает утром иногда...":
            menu:
                Lisa_12 "Так я тебе и поверила! Ма-ам!"
                "Ну что же ты орёшь так...":
                    pass
                "Давай всё мирно уладим...":
                    pass
    scene BG char Lisa morning-oops-03
    show Ann morning-oops 03
    Ann_15 "Лиза? Что случилось?!"
    scene BG char Lisa morning-oops-04-05
    show Lisa morning-oops 04
    Lisa_12 "Мам! Смотри, что у него!"
    menu:
        Ann_19 "Макс! Объяснишь, чем ты занимался при сестре?!"
        "Да я просто спал, это само...":
            Ann_09 "Ну, если ничего не делал и само... Тогда, Лиза, Макс ни в чём не виноват. Так бывает у мальчиков..."

        "Так бывает по утрам, я не специально...":
            Ann_09 "Ну, да, Лиза. Так у мальчиков иногда бывает по утрам. Не нужно паниковать..."
    Max_11 "Я так сразу и сказал..."
    Lisa_12 "Это не правда! Так не может быть... само. Он что-то делал, наверняка..."
    Max_10 "Да ничего я не делал!"
    show Lisa morning-oops 05
    Ann_08 "Ты сама видела, чтобы Макс чем-то {i}таким{/i} занимался при тебе?"
    Lisa_09 "Нет, но мам... Я просто испугалась."
    scene BG char Lisa morning-oops-06
    show Ann morning-oops 06
    show Lisa morning-oops 06
    Ann_04 "Лиза. Ты, похоже, совсем ничего не знаешь о мальчиках. С одной стороны, я рада, что ты у меня такая. А с другой..."
    Lisa_10 "А что с другой? Я хочу всё знать!"
    menu:
        Ann_09 "Нет, Лиза, тебе ещё рано таким интересоваться. Вот подрастёшь, тогда и поговорим. На этом всё. Сделаем вид, что ничего не было. Ясно?"

        "Ясно...":
            pass

    $ flags["morning_erect"] = 1
    $ SetPossStage("seduction", 0)
    $ spent_time += 30
    jump Waiting


label AfterSchoolFD:
    $ current_room = house[6]
    scene BG incoming-00
    show Lisa incoming-01
    Lisa_01 "Привет, Макс! Я вернулась."
    Max_03 "Супер! Как первый день?"
    Lisa_02 "Да ничего так. Но потом поболтаем. Сейчас переоденусь и прыгну в бассейн. Только об этом и мечтала целый день!"
    Max_04 "Хорошо, здесь и поговорим"

    $ spent_time += 10
    jump Waiting


label about_school:
    $ talk_var["lisa_fd"] += 1
    menu:
        Lisa_00 "Неужели, я дома! До сих пор не могу поверить, что ЭТО наш дом!"
        "Да я сам в шоке!":
            menu:
                Lisa_03 "Вот-вот! Целый день думала о нашем бассейне. Неужели, здесь всегда такая погода?"
                "Да, мы почти в раю":
                    menu:
                        Lisa_02 "Хотелось бы так думать..."
                        "Ну, как дела в школе?":
                            jump .school
                        "Нашла дорогу домой от школы?":
                            jump .way
                "Может быть, тут ураганы бывают?":
                    menu:
                        Lisa_02 "Ага! И сезон дождей 11 месяцев в году!"
                        "Ну, как дела в школе?":
                            jump .school
                        "Нашла дорогу домой от школы?":
                            jump .way
        "Так как дела в школе?":
            jump .school
        "Не заблудилась по дороге?":
            jump .way

    menu .way:
        Lisa_02 "Ты знаешь, тут школа совсем рядом! Пять минут и я на месте. К тому же, топографическим кретинизмом не страдаю... Ты же поэтому из дома не выходишь, да?"
        "Очень смешно!":
            menu:
                Lisa_01 "Ну а почему же ты тогда всё время дома сидишь? Что в старой квартире, что тут. Ты же сегодня даже из дома не выходил?"
                "А меня всё устраивает":
                    Lisa_10 "Бедняжка. Ну, ничего. И тебя вылечат!"
                    Max_04 "Так что там в школе?"
                    jump .school
                "Здесь я никого не знаю":
                    menu:
                        Lisa_09 "Понимаю. Я тоже никого не знаю... почти."
                        "Почти? С кем-то познакомилась?":
                            menu:
                                Lisa_02 "Может быть... А что, любопытно? Или ревнуешь?"
                                "Так кто он?":
                                    jump .who
                                "И ничего я не ревную":
                                    menu:
                                        Lisa_03 "Ну и правильно! Ты же мой брат и останешься самым любимым мужчиной в моей жизни!"
                                        "Вот это признание!":
                                            pass
                                        "Я тоже тебя люблю, сестрёнка":
                                            pass
                                    Lisa_02 "Но ты слишком не зазнавайся. И никому не рассказывай!"
                                    Max_04 "Договорились!"
                                    jump .ok
                                "Ну и не рассказывай, если не хочешь...":
                                    jump .want
                        "Так что там в школе?":
                            jump .school
                "Просто... не люблю людей":
                    menu:
                        Lisa_03 "Вот это признание! А нас ты просто терпишь, да?"
                        "Ну, приходится...":
                            Lisa_10 "Бедняжка. Ну, ничего. И тебя вылечат!"
                            Max_04 "Так что там в школе?"
                            jump .school
                        "Вы - исключение":
                            Lisa_02 "Ну спасибо и на том. Если что, то ты тоже ничего..."
                            Max_04 "Так что там в школе?"
                            jump .school
                        "Да я пошутил":
                            Lisa_02 "Я так и поняла! Вообще, шутки у тебя... как всегда!"
                            Max_04 "Так что там в школе?"
                            jump .school
        "Хм... Ты слишком счастливая. Что-то случилось в школе?":
            jump .school

    label .school:
        Lisa_00 "Ты знаешь, школа отличная! Меня хорошо приняли, все такие доброжелательные... А некоторые особенно..."
        Max_05 "Некоторые? Ты о ком?"
        Lisa_02 "Да так. Познакомилась кое-с-кем..."
        Max_02 "О, рассказывай! Кто он?"
        jump .who

    menu .who:
        Lisa_03 "А почему сразу он? Может быть, я завела подругу?"
        "Так кто он?":
            jump .boy
        "Что за подружка?":
            menu:
                Lisa_02 "Обычная такая подружка... Какие подружки бывают обычно, вот у меня такая!"
                "Что-то ты мне голову морочишь...":
                    menu:
                        Lisa_03 "А что здесь такого? Это же весело!"
                        "Что-то я совсем запутался":
                            jump .ok
                        "Ну и не рассказывай, если не хочешь...":
                            jump .want
                "А имя у твоей подружки не мужское, случайно?":
                    Lisa_00 "Эх... Раскусил ты меня. Да, это не подружка..."
                    Max_00 "Так кто он?"
                    jump .boy

    menu .ok:
        Lisa_00 "Как у тебя день прошёл?"
        "Да как обычно...":
            menu:
                Lisa_03 "Обычно? Это ты свой первый день в таком месте и таком доме называешь обычным?!"
                "Ну, теперь то это будет обычно":
                    menu:
                        Lisa_02 "Это если нам повезёт и нас отсюда не выгонят. За неуплату чего-то..."
                        "Ты же у нас оптимист обычно?":
                            Lisa_00 "Обычно да, но до сих пор не верится, что наша жизнь так изменилась!"
                            Max_00 "Ладно, не будем о грустном"
                            jump .sad
                        "Ага. Или адвокат не заметит ошибку в документах какую-нибудь...":
                            Lisa_00 "Ладно, Макс, не будем о грустном. Может быть, всё будет хорошо!"
                            Max_00 "Да, ты права"
                            jump .sad
                "Ты права...":
                    jump .sad
        "Привыкаю ко всему":
            menu:
                Lisa_03 "Да уж, задача сложная. Невыносимые условия жизни и всё такое, да?"
                "Ну, я постараюсь":
                    jump .sad
                "Человек как таракан, ко всему привыкает":
                    jump .sad

    menu .sad:
        Lisa_02 "Так что планируешь делать, чем займёшься?"
        "Ещё не решил. Что посоветуешь?":
            Lisa_03 "Я бы посоветовала тебе искупаться, для начала. Собственно, я этим и планирую заняться!"
            Max_04 "Хорошая идея..."
            jump .goodplan
        "Придумаю ещё. А у тебя какие планы?":
            Lisa_03 "Лично я буду купаться и загорать. Загорать и купаться. Пока не надоест. А потом повторю!"
            Max_04 "Отличный план!"
            jump .goodplan
        "Буду думать как заработать":
            Lisa_00 "Что, прямо с первого дня? Отдохни, искупайся для начала. Может быть, мысли появятся..."
            Max_00 "Может, ты и права..."
            jump .goodplan

    menu .want:
        Lisa_00 "Может быть, в другой раз..."
        "А если я настаиваю?":
            menu:
                Lisa_02 "Что, ты правда такой любопытный? Не знала!"
                "Короче, рассказывай!":
                    jump .boy
                "Ой, всё...":
                    jump .ok
        "Договорились":
            jump .ok

    label .boy:
        $ talk_var["boy"] += 1
        $ cooldown["lisa_boy"] = CooldownTime("01:30")
        menu:
            Lisa_10 "Ну... его зовут Алекс. Мы учимся в одном классе. Даже сидим рядом..."
            "А подробнее?":
                Lisa_02 "А какие тут могут быть подробности? Мы учимся вместе, сидим в одном классе за одной партой... Ну и он со мной заговорил..."
                Max_00 "И что он сказал?"
                jump .say
            "И как вы познакомились?":
                Lisa_02 "Макс. Ну как ещё я могла познакомиться в первый день в школе с парнем? К тому же, если ещё и сидим рядом... Просто, он со мной заговорил..."
                Max_00 "И что он сказал?"
                jump .say
            "Расскажи о нём":
                menu:
                    Lisa_02 "Ну, парень как парень. Вот как ты, только не такой зануда и красивый."
                    "Я не зануда!":
                        Lisa_03 "Ещё какой зануда! Достаёшь меня допросами о моём парне..."
                        Max_06 "Ого, так он уже твой парень?!"
                        menu:
                            Lisa_10 "Нет, ну... я не так выразилась. Мы только познакомились и, конечно, он не мой парень!"
                            "А хотела бы, чтобы он им стал?":
                                menu:
                                    Lisa_01 "Думаю, что да. У меня ещё никогда не было парня. А этот..."
                                    "Значит, он тебе нравится?":
                                        jump .like
                                    "И что теперь?":
                                        jump .whatnow
                            "Я, между прочим, тоже ничего...":
                                menu:
                                    Lisa_03 "Ты мой братишка! Не говори ерунду. Мы и так с тобой спим вместе. Ну, в одной комнате!"
                                    "А ты своему парню скажешь, что спишь со своим братом?":
                                        pass
                                    "Спим вместе!":
                                        pass
                                menu:
                                    Lisa_00 "Дурак! Я же в шутку это сказала. И, вообще, прекращай меня доставать!"
                                    "Ладно, ладно. Ну а что он говорил?":
                                        jump .say
                                    "И что теперь?":
                                        jump .whatnow
                    "Значит, нравится мой типаж?":
                        menu:
                            Lisa_03 "А какой у тебя типаж? Неудачника, который достаёт младшую сестрёнку?"
                            "Эй, я не неудачник!":
                                jump .hey
                            "Я разве достаю? Просто интересуюсь твоей жизнью":
                                jump .hey
                    "А я тебе нравлюсь?":
                        menu:
                            Lisa_03 "Ну, когда не зануда, вполне!"
                            "А кто тебе нравится больше? Я или он?":
                                menu:
                                    Lisa_12 "Вот теперь ты зануда и нравишься мне с каждой секундой всё меньше и меньше..."
                                    "Ладно, ладно, извини...":
                                        menu:
                                            Lisa_03 "То-то же! А то ведёшь себя как придурок..."
                                            "А вот теперь я могу обидеться...":
                                                jump .hey
                                            "Я же в шутку. И, кстати, придурок - это обидно!":
                                                jump .hey
                                    "А он, значит, теперь нравится больше?":
                                        jump .like
                                    "И всё-таки?":
                                        Lisa_10 "Всё, Макс, достал меня. Дай отдохнуть, поплавать, позагорать..."
                                        Max_00 "Эх..."
                                        jump .end
                            "Значит, он тебе нравится?":
                                jump .like
                            "И что теперь?":
                                jump .whatnow

    menu .hey:
        Lisa_00 "Извини. Я не подумала. Кстати, спасибо, что со мной болтаешь. Я обычно о таком Алисе рассказывала, но она стала слишком взрослой и мы как-то отдалились..."
        "Ну, со мной ты всегда можешь поболтать":
            pass
        "Можешь на меня рассчитывать":
            pass
        "Только не превращай меня в свою подушку!":
            menu:
                Lisa_09 "Ну вот, только я хотела тебе поплакаться о разбитой любви, а ты..."
                "Что? Он уже тебя бросил?!":
                    menu:
                        Lisa_03 "Ага, два раза! Он ещё даже не знает, что мне нравится..."
                        "Значит, он тебе нравится?":
                            jump .like
                        "И что теперь?":
                            jump .whatnow
                "А я не убежал и всё ещё здесь!":
                    menu:
                        Lisa_03 "Ну, тогда терпи меня!"
                        "Значит, он тебе нравится?":
                            jump .like
                        "И что теперь?":
                            jump .whatnow
    menu:
        Lisa_02 "Ладно, буду иметь в виду!"
        "Значит, он тебе нравится?":
            jump .like
        "И что теперь?":
            jump .whatnow

    menu .say:
        Lisa_03 "А вот это уже тебя не касается. Что сказал, то сказал. Мне понравилось. Так и познакомились..."
        "Он тебе нравится?":
            jump .like
        "И что теперь?":
            jump .whatnow
        "Думаю, у вас всё получится":
            jump .willbeok

    menu .whatnow:
        Lisa_00 "Я не знаю. Это же только первый день. Посмотрим, как всё пойдёт. Может быть, он уже с кем-то встречается, а тут пришла я такая красивая..."
        "Всё будет хорошо":
            jump .willbeok
        "Если что, я всегда помогу советом":
            jump .help
        "А не рано тебе с кем-то встречаться?":
            menu:
                Lisa_12 "Ты в моём возрасте уже тусил с этой своей, как её... Правда, она тебя бросила..."
                "Это я её бросил":
                    menu:
                        Lisa_02 "Ну да, потом её караулил под окнами, чтобы сказать, как ты её не любишь..."
                        "Всё было не так!":
                            jump .no
                        "В любом случае, у меня опыта больше":
                            jump .xp
                        "Это всё в прошлом...":
                            jump .no
                "Так что, у меня есть опыт":
                    jump .xp

    label .no:
        Lisa_03 "Так или иначе, я не слышала, чтобы у тебя было много побед на любовном фронте..."
        Max_00 "Я же не обо всём рассказывал..."
        jump .yes

    menu .xp:
        Lisa_03 "Да уж, ты у нас опытный любовник!"
        "Я же не обо всём рассказывал...":
            jump .yes
        "Именно поэтому за советами обращайся ко мне":
            jump .so

    label .yes:
        menu:
            Lisa_02 "Любопытно... И о чём же ты умолчал?"
            "У меня было много девушек":
                Lisa_11 "Куча девушек? Надувных? Где же ты их всех прятал?"
                Max_14 "Парни о таком не рассказывают"
            "А вот это - секрет!":
                pass
            "Тебе лучше не знать...":
                pass
        menu:
            Lisa_12 "Так я тебе и поверила..."
            "В школе по мне все девушки с ума сходили":
                menu:
                    Lisa_02 "Ладно, ладно. Если ты так говоришь, пусть так."
                    "В любом случае, нужна будет помощь или совет - подходи":
                        jump .help
                    "Так или иначе, о парнях я знаю больше тебя...":
                        jump .so
            "Не хочешь - не верь. Но опыт у меня есть":
                jump .help

    label .so:
        Lisa_02 "Я подумаю над твоим предложением. А пока, мы что-то заболтались. Я бы хотела искупаться и позагорать..."
        Max_00 "Хорошо..."
        jump .end

    menu .like:
        Lisa_03 "Ну конечно! Что за глупые вопросы. Вот только я не знаю как правильно себя вести, что говорить..."
        "Я знаю как мыслят парни. Так что, могу помочь советом...":
            jump.help
        "Если будет нужна помощь, обращайся":
            jump.help

    label .help:
        Lisa_00 "Хорошо, Макс, буду иметь в виду. И спасибо, что поболтал со мной. А пока ты не против, если я немного отдохну?"
        Max_00 "Конечно! А я пойду..."
        jump .end

    label .willbeok:
        Lisa_02 "Спасибо, Макс. Приятно слышать, что кому-то не всё равно..."
        Max_04 "Если будет нужна помощь, обращайся"
        jump .help

    label .goodplan:
        Lisa_00 "В общем, если что, я тут."
        Max_00 "Хорошо..."
        jump .end

    label .end:
        $ spent_time += 20

    jump Waiting


label talk_swim:
    $ SetPossStage("Swimsuit", 0)
    menu:
        Lisa_10 "Как... так?"
        "Ну, чего ты в закрытом купальнике лежишь?":
            pass
        "Тебе не жарко в таком купальнике?":
            pass
    menu:
        Lisa_09 "Ну, у меня другого и нет. Ты же знаешь, что наши вещи пропали. Вот там и все мои купальники были..."
        "Очень жаль... Я могу чем-то помочь?":
            menu:
                Lisa_02 "Спасибо. Да, а чем ты можешь помочь, Макс?"
                "Могу как-то решить твою проблему...":
                    jump .findout
                "Я что-нибудь придумаю!":
                    jump .findout

        "Так загорай голая!":
            menu:
                Lisa_01 "Очень смешно, Макс. Что же ты сам так не загораешь?"
                "А давай разденемся вместе!":
                    pass
                "Да легко!":
                    pass
                "Боюсь тебя шокировать..." if flags["morning_erect"] == 0:
                    Lisa_00 "Не поняла. В каком смысле?"
                    Max_00 "Извини, шутки у меня дурацкие... А какой купальник ты хочешь?"
                    jump .want
                "Боюсь тебя снова шокировать..." if flags["morning_erect"] > 0:
                    menu:
                        Lisa_12 "Фу, Макс! Опять твои шуточки... Не надо мне такого счастья. Держи свою штуку там, в штанах..."
                        "Ну, ты сама спросила почему я не раздеваюсь...":
                            menu:
                                Lisa_02 "Извини, не подумала... В общем, другого купальника у меня нет."
                                "А какой ты хочешь?":
                                    jump .want
                                "Я что-нибудь придумаю!":
                                    jump .findout

                        "Точно? А то мне есть что тебе показать...":
                            Lisa_13 "Да, Макс, точно! Я точно уверена, что не хочу это ещё раз видеть. И, вообще, отвали!"
                            Max_00 "Ну, как скажешь..."
                            $ AddRelMood("lisa", 0, -50)
                            jump .end
            menu:
                Lisa_02 "Очень смешно, Макс!"
                "А я и не шучу...":
                    menu:
                        Lisa_00 "И это тоже не смешно. В общем, я тут поваляюсь, если ты не против..."
                        "Извини, шутки у меня дурацкие... А какой купальник ты хочешь?":
                            jump .want
                        "Конечно, одыхай...":
                            $ AddRelMood("lisa", 0, -60)
                            jump .end
                "Ладно. А какой купальник ты хочешь?":
                    jump .want

    label .want:
        Lisa_10 "Ну такой... Максимально открытый. Но не совсем чтобы полностью. И цвет такой красивый, но не чтобы слишком тёмный или холодный... И лямочки такие..."
        Max_09 "Ясно... {i}(или нет){/i}"
        Lisa_02 "В общем, на твоё усмотрение. Только чтобы не хуже, чем этот. Хотя, куда уж хуже..."
        Max_00 "Хорошо, я тебя понял!"
        jump .end

    label .findout:
        Lisa_00 "Например? У тебя же нет денег, ты нигде не работаешь и я что-то сомневаюсь, что ты умеешь шить купальники..."
        Max_04 "Вот увидишь!"
        menu:
            Lisa_02 "Ну, ладно. Посмотрим... Если у меня будет новый купальник, я тебя даже... в щёчку поцелую!"
            "И всё?":
                Lisa_00 "Ну, скажу спасибо..."
                Max_01 "Ладно, идёт!"
            "Договорились!":
                pass
    label .end:
        $ spent_time += 30
    return


label about_boy:

    $ talk_var["boy"] = 2

    menu:
        Lisa_00 "Моего парня? Какого? У меня нет никого..."
        "Я про Алекса, ты же сама рассказывала...":
            jump .so
        "Ты уверена? Его вроде Алекс зовут...":
            jump .so
        "И правильно. Рано ещё тебе парней заводить":
            menu:
                Lisa_12 "А ты кто, моя мамочка, чтобы такие советы давать?"
                "Думаю, она со мной согласилась бы":
                    pass
                "Я знаю, что говорю":
                    pass
                "Значит, он твой парень?":
                    jump .so
            menu:
                Lisa_02 "Думаешь, ты такой пришёл, высказал своё мнение и я побежала его бросила?"
                "Ага, так он твой парень?!":
                    jump .so
                "Ну, да...":
                    pass
                "Это было бы правильное решение...":
                    pass
            menu:
                Lisa_03 "Ага, щаз. Даже если бы он и был моим парнем, только потому, что вы все попросите одновременно, я никого не брошу!"
                "Так у нас тут бунтарка!":
                    jump .reason
                "А вот это мне в тебе нравится!":
                    jump .reason
                "Ну и зря. Иногда большинство бывает право...":
                    Lisa_00 "Угу, миллионы мух не могут ошибаться... Ну ты и зануда, Макс! Не хочу больше об этом разговаривать..."
                    Max_00 "Ну и ладно!"
                    jump .end
    menu .so:
        Lisa_02 "Да никакой он мне не парень. Я же говорю, только познакомилась. Просто... он мне понравился."
        "Да расслабься...":
            jump .reason
        "Ладно, ладно, верю!":
            jump .reason

    menu .reason:
        Lisa_02 "Так ты что-то сказать хотел или зачем эту тему поднял?"
        "Я подумал, что тебе нужна моя помощь":
            menu:
                Lisa_00 "Помощь? Мне? В чём? Если мне будет нужна помощь, я попрошу. Нет проблемы, нечего и решать..."
                "Как скажешь...":
                    jump .end
                "Если что, ты знаешь где меня искать":
                    jump .end
        "Хотел дать тебе один совет...":
            menu:
                Lisa_00 "Знаешь что, Макс. Оставь свои советы при себе. Я что-то не в духе. Да и не о чем сейчас разговаривать. Он мне не парень и у меня нет проблем, чтобы их решать!"
                "Хорошо, хорошо...":
                    jump .end
                "Ну, если что, я тут":
                    jump .end
        "Да, забей...":
            jump .end

    label .end:
        $ spent_time += 30

    return


label wash_dishes_lisa:
    $ talk_var["lisa_dw"] = 1
    menu:
        Lisa_09 "А что насчёт посуды?"
        "Хочешь, я помогу тебе домыть остальное?":
            menu:
                Lisa_01 "А вот знаешь... хочу! Да, спасибо, Макс!"
                "{i}мыть посуду{/i}":
                    scene BG crockery-evening-00
                    $ renpy.show("Max crockery-evening 01"+mgg.dress)
                    if GetRelMax("lisa")[0] < 3:
                        $ AddRelMood("lisa", 10, 60)
                    else:
                        $ AddRelMood("lisa", 0, 60)
                    $ dishes_washed = True
                    $ spent_time = max((60 - int(tm[-2:])), 30)
                    $ cur_ratio = 2
                    Max_00 "И чего я этим занимаюсь? Делать нечего, что ли..."
                    menu:
                        Max_00 "А с другой строны - неплохой способ улучшить отношения с Лизой..."
                        "{i}закончить{/i}":
                            jump Waiting
        "Нет, ничего...":
            menu:
                Lisa_12 "Ну раз ничего, то нечего меня отвлекать. Иди делом займись!"
                "Хорошо, пойду займусь":
                    $ spent_time = 10
                    jump Waiting

    return


label about_boy2: # разговор с Лизой после того, как на ужине вся семья узнала про Алекса
    Lisa_00 "Макс, давай сменим тему..."
    Max_11 "Ну Лиза..."
    if talk_var["boy"] == 3: # bad
        menu:
            Lisa_09 "Макс, я тебе рассказала по секрету, а ты меня спалил маме. Не хочу ничего рассказывать больше..."
            "Извини, я забыл, что обещал":
                Lisa_00 "Вот и как тебе что-то рассказывать по секрету, если ты можешь забыть, что это секрет? А?"
            "Извини, но было же весело?":
                Lisa_00 "Нет, Макс, мне не было весело. Нарушать своё слово это совсем совсем не весело!"
            "Извини, я не подумал...":
                Lisa_00 "Не подумал. Я же просила, а ты... Как тебе рассказывать что-то, если ты не думаешь?"
        Max_08 "Извини ещё раз, я не хотел"
    elif talk_var["boy"] == 5: # good
        Lisa_02 "Ну ладно, ладно. Кстати, спасибо, что меня не выдал. Мне нравится, что я могу на тебя положиться."
        Max_04 "Всегда пожалуйста!"
    else: # normal
        menu:
            Lisa_01 "Макс, ты чего такой любопытный? Я же не выпытываю с кем ты встречаешься..."
            "Я ни с кем не встречаюсь. В моей жизни есть только ты!":
                Lisa_12 "Не подлизывайся, Макс! Сознавайся, чего задумал?"
                Max_00 "Да ничего, просто любопытно..."
            "Если бы и был кто-то, ты бы точно свой нос совала!":
                Lisa_03 "Ха! Ты меня хорошо знаешь. Но ты как-то необычайно настойчиво интересуешься..."
                Max_00 "Ну, мне просто любопытно..."

    Lisa_02 "Ну ладно, Макс. Так чего ты хотел узнать?"
    Max_00 "Да всё! Что тебе удалось выяснить?"
    menu:
        Lisa_00 "Об Алексе? Ну, у него есть подружка, оказывается. Самая красивая девочка в школе, говорят..."
        "Они всё врут. Ты - самая красивая!":
            Lisa_01 "Макс, ты такой сегодня... Спасибо за комплимент, но все мальчики без ума от подружки Алекса..."
        "Ты уверена, что она самая красивая?":
            Lisa_10 "Ну, так говорят все мальчики..."
    Max_00 "А ты её сама видела?"
    menu:
        Lisa_02 "Конечно! Мы же в одном классе учимся. Зовут Оливия."
        "Наверное, стерва?":
            menu:
                Lisa_00 "Нет, Макс. Наоборот, она такая хорошая, тихая, скромная, но почему-то всем нравится... Хотя, и правда симпатичная очень..."
                "В твоём вкусе?":
                    Lisa_12 "В смысле? Мне мальчики нравятся, я же говорила уже. Но да, мне она кажется симпатичной..."
                "Странно...":
                    Lisa_02 "Почему странно? Думаешь, мальчикам нравятся только всякие оторвы? Таких полный класс. Именно этим она и выделяется, что не похожа на других"
        "И какая она?":
            menu:
                Lisa_00 "Ну, она такая хорошая, тихая, скромная. И очень симпатичная..."
                "В твоём вкусе?":
                    Lisa_12 "В смысле? Мне мальчики нравятся, я же говорила уже. Но да, мне она кажется симпатичной..."
                "Странно...":
                    Lisa_02 "Почему странно? Думаешь, мальчикам нравятся только всякие оторвы? Таких полный класс. Именно этим она и выделяется, что не похожа на других"
    Max_00 "Понятно..."
    Lisa_00 "А мне вот не понятно что теперь делать... Кажется, у меня нет никаких шансов..."
    Max_08 "Подожди, а он сильно в неё влюблён?"
    menu:
        Lisa_02 "Ну мы не очень много общались, но она ему нравится, это точно. Хотя, я ему тоже нравлюсь, похоже... Чуть-чуть..."
        "Ну вот видишь, не всё потеряно!":
            Lisa_00 "Ну да, не всё. Но около того. Я даже не представляю что мне делать. Он мне так нравится..."
        "Значит, у тебя есть шанс!":
            Lisa_00 "Шанс? Один из миллиона, может быть... Я не знаю как и что мне делать. Он мне так нравится..."
    $ SetPossStage("Schoolmate", 0)
    Max_00 "Ты знаешь, я что-нибудь придумаю, хорошо?"
    Lisa_01 "Хорошо. Спасибо, Макс, что поговорил. Мне это было нужно..."
    Max_04 "Всегда пожалуйста!"
    $ AddRelMood("lisa", 0, 100)
    $ spent_time += 30
    $ talk_var["boy"] = 6
    $ cooldown["lisa_boy"] = CooldownTime("05:00")
    return


label Lisa_MorningWood: # Разговор с Лизой после утреннего инцидента
    menu:
        Lisa_00 "Макс, я не хочу об этом говорить!"
        "Да ты послушай!":
            Lisa_09 "Макс, я не хочу. Хватит!"
            Max_14 "Ладно, в другой раз..."
            $ cooldown["talkcooldown"] = CooldownTime("03:00")
            $ AddRelMood("lisa", 0, -50)
            $ spent_time = 10
            jump Waiting
        "Я хотел извиниться...":
            pass
    Lisa_02 "Извиниться? Значит, ты и правда что-то там делал?"
    Max_00 "Нет, я хотел извиниться, что не говорил с тобой об этом раньше."
    Lisa_00 "Не поняла. Какое-то странное извинение. О чём ты со мной раньше не говорил?"
    Max_09 "Ты пробовала с мамой о чём-то... взрослом поговорить?"
    Lisa_09 "А причём тут это? Ну да, она всегда говорит, что я ещё маленькая..."
    Max_01 "Вот именно. Я, как старший брат должен был бы тебя подготовить..."
    Lisa_02 "Постой. Во-первых, ты не старший. Хотя старше, да. А во-вторых, подготовить к чему?"
    Max_04 "Ну вот ты что знаешь о мальчиках?"
    Lisa_03 "Знаю, что если все такие же, как ты, то я стану лесбиянкой!"
    Max_08 "Э..."
    Lisa_02 "Да шучу я. Ну я поняла, к чему ты клонишь. Вот только не уверена, что ты тот, кто должен мне что-то объяснять..."
    Max_07 "А ты знаешь много желающих тебе что-то объяснить?"
    Lisa_09 "Тоже верно... Ну ладно. И к чему весь этот разговор?"
    Max_05 "Я могу помочь тебе стать взрослой."
    Lisa_02 "Ты же понимаешь, как это звучит? Лучше уточни, что ты имеешь в виду..."
    Max_03 "Я поделюсь с тобой знаниями, которые тебе помогут..."
    Lisa_01 "Помогут с чем? Мне мама говорит, что ещё рано о таком думать."
    Max_01 "И что, ты не думаешь об этом?"
    Lisa_10 "Думаю... Но я не знаю. О таком вроде не с мальчиками говорят, особенно с братом..."
    Max_07 "У Алисы свои заботы, а мама тебе уже всё сказала..."
    Lisa_00 "Ну, я подумаю. Ты как-то слишком настойчиво помощь предлагаешь. Как будто у тебя свой интерес какой-то..."
    Max_04 "Хочу только помочь младшей сестрёнке."
    Lisa_02 "Ну да, ну да... В общем, я подумаю. Только вот не уверена, что ты всё-таки подходишь на роль такого... наставника, если ты меня понимаешь..."
    Max_09 "Что, моё мнение недостаточно авторитетно для тебя?"
    Lisa_01 "Ну не знаю. Посмотрим..."
    Max_01 "Посмотрим..."

    $ SetPossStage("seduction", 1)
    $ spent_time = 30
    return


label Lisa_sg1:
    menu:
        Lisa_00 "Да, что насчёт успеваемости? Позлорадствовать пришёл или помочь хочешь?"
        "Помочь, конечно!":
            menu:
                Lisa_02 "Ну, я рада, если так. У меня не очень хороши дела с математикой и анатомией. У нас её просто не было. Должна была быть, но учитель всё время болел и я так толком и не разобралась в теме..."
                "Тебе повезло, у меня всё хорошо с этими предметами!":
                    jump .help
                "Может быть, я тебе и смогу помочь...":
                    jump .maybe
        "Для начала хотел узнать, как у тебя дела?":
            menu:
                Lisa_02 "Дела у меня отлично. Если ты об уроках, то всё хорошо кроме математики и анатомии. В прошлой школе не очень повезло с учителем, он всё время болел и я мало чего поняла..."
                "Тебе повезло, у меня всё хорошо с этими предметами!":
                    jump .help
                "Может быть, я тебе и смогу помочь...":
                    jump .maybe

    menu .help:
        Lisa_02 "Это же здорово! Точно... Ты уже неоднократно говорил, что тебя выгнали не за знания. Значит, ты мне поможешь, правда?"
        "Конечно, сестрёнка!":
            menu:
                Lisa_01 "Что? Вот просто так решил помогать и ничего не просить взамен? Правда?"
                "Ну может быть...":
                    jump .maybe
                "Обещаю! Хочу только помогать и ничего взамен!":
                    jump .nothing
        "Может быть...":
            jump .maybe

    label .nothing:
        Lisa_03 "Макс, я тебя обожаю! Спасибо! Сегодня мне помощь не нужна, а вот в следующий раз походи, когда делаю уроки, я найду тебе занятие!"
        Max_01 "Хорошо, Лиза!"
        $ SetPossStage("sg", 2)
        $ flags["lisa_hw"] = True
        $ AddRelMood("lisa", 150, 200)
        $ spent_time += 20
        return

    menu .maybe:
        Lisa_09 "Может быть? В смысле? Ты что-то хочешь за помощь?"
        "Ну сама подумай, зачем мне тратить время просто так?":
            pass
        "Может быть, попрошу что-то...":
            pass
        "Да нет, конечно. Обещаю, что ничего не попрошу взамен!":
            jump .nothing
    menu:
        Lisa_09 "А я думала, что поможешь своей младшей сестрёнке избежать сурового наказания строгой мамы... А ты..."
        "Нет, Лиза. Ты мне - я тебе.":
            pass
        "А я пошутил. Обещаю, что ничего не попрошу взамен!":
            jump .nothing

    Lisa_00 "Ну ладно. И чего тебе нужно? Денег? У меня их нет. Я не зарабатываю... Да у меня ничего нет..."
    Max_02 "Ты не права. У тебя есть кое-что, что мне нравится..."
    Lisa_12 "Ты на что-то намекаешь, Макс? Я не понимаю..."
    Max_01 "Я буду тебе помогать, если ты будешь выполнять мои определённые требования..."
    Lisa_09 "Макс, говори конкретнее. О чём ты? Например?"
    if chars['lisa'].sleeptoples: # если по цепочке "Наставник" продвинулись раньше помощи с уроками
        Max_07 "Например, я попрошу тебя о чём-то, что ты должна будешь сделать..."
        Lisa_02 "Нет, Макс. Кота в мешке мне не надо. Либо ты помогаешь просто так, либо я сама во всём разберусь и справлюсь без тебя..."
        Max_00 "Ну если вопрос так стоит, то конечно буду помогать просто так!"
        jump .nothing

    Max_03 "Например, я попрошу тебя спать без твоих штанов, в трусиках..."
    Lisa_13 "Фу, Макс! Ты извращенец! Зачем тебе это? Я же твоя сестра, забыл?"
    if talk_var['lisa.pun'] > 0: # возможно, следует добавить еще вариант, когда Макс разговаривает Лизой не после первого наказания
        Max_09 "Ну, решай сама. Тебя мама по джинсам отшлёпала, а представь как будет по голой заднице?"
        Lisa_10 "Было больно... Ладно. Всё равно мне жарко в штанах спать. А ты, Макс, гад. Я думала, что мы друзья, а ты... ещё и извращенец..."
        Max_01 "Но я буду тебе помогать с уроками!"
        Lisa_00 "Если не будешь помогать, то я маме всё расскажу и посмотрим, кого накажут. Всё, мы закончили."
        Max_00 "Как скажешь..."
        $ SetPossStage("sg", 3)
        $ flags["lisa_hw"] = True
        $ AddRelMood("lisa", -20, -100)
        $ spent_time += 20
    else:
        Max_07 "Ну, решай сама. Мама если отшлёпает, будет очень очень больно. А так ты ничего не теряешь..."
        Lisa_12 "Нет, Макс, забудь. Мне не нужна твоя помощь!"
        Max_01 "Ну, посмотрим..."
        $ SetPossStage("sg", 1)
        $ flags["lisa_hw"] = True
        $ AddRelMood("lisa", -10, -50)
        $ spent_time += 20

    return


label Lisa_sg2:
    Lisa_09 "Ты о чём, Макс? Каких условиях?"
    Max_01 "Как о чём? О том, каким образом я буду помогать тебе с уроками. Ты мне - я тебе. Ну или мама будет тебя и дальше наказывать..."
    Lisa_10 "Ладно, я согласна. Очень больно было. Так чего ты хочешь?"
    Max_01 "Ну для начала я бы хотел, чтобы ты спала в трусиках, без штанов..."
    menu:
        Lisa_09 "Макс, я говорила, что ты извращенец?"
        "Ага. Ну так как, ты согласна?":
            pass
        "Нет, не говорила...":
            Lisa_12 "Ну так вот, скажу. Ты - извращенец. Я твоя сестра и требовать от меня такого... стыдно должно быть!"
            Max_07 "Допустим, мне стыдно. Так ты согласна?"
    Lisa_10 "Ну у меня не очень большой выбор... Да и жарко в штанах спать... Да, я согласна. Но ты будешь мне помогать с уроками!"
    Max_05 "Как скажешь..."
    $ SetPossStage("sg", 3)
    $ AddRelMood("lisa", -20, -100)
    $ spent_time += 10
    return


label Lisa_HomeWork:
    $ flags["lisa_hw"] = True
    $ punlisa.insert(0, [0, 0, 0, 0, 0])
    $ del punlisa[10:]
    menu:
        Lisa_00 "Ну, если ты не занят и правда знаешь что-то по этой теме..."
        "Ну, давай посмотрим...":
            pass
        "Хотя, в другой раз...":
            jump Waiting
    scene BG char Lisa lessons-help-00
    $ renpy.show("FG lessons-help-"+pose3_1)
    $ renpy.show("Lisa lessons-help "+pose3_1+chars["lisa"].dress)
    $ renpy.show("Max lessons-help "+pose3_1+mgg.dress)

    menu:
        Lisa_02 "Отлично. В общем, мне нужно сделать вот это, это и вот то. Поможешь?"
        "Без проблем!" if possibility["sg"].stn == 2:
            "{i}Спустя какое-то время...{/i}"
            Max_04 "Ну, вот мы и закончили!"
            Lisa_03 "Спасибо, Макс! Ты так меня выручил! Теперь я точно получу пятёрку!"
            Max_01 "Обращайся!"
            $ AddRelMood("lisa", 20, 100)
            $ talk_var['help.hw'] += 1
            $ punlisa[0][0] = 3
            $ spent_time += max((60 - int(tm[-2:])), 40)
            jump Waiting
        "Конечно! {i}(сделать ошибки){/i}" if possibility["sg"].stn == 2:
            $ talk_var['help.hw'] += 1
            jump .make_bag
        "Ты пока отдохни, я сам всё сделаю!" if possibility["sg"].stn == 2:
            $ talk_var['help.hw'] += 1
            jump .self
        "Ага... {i}(сделать ошибки){/i}" if possibility["sg"].stn > 2:
            $ talk_var['help.hw'] += 1
            jump .make_bag
        "Давай я всё сделаю сам! {i}(без ошибок){/i}" if possibility["sg"].stn > 2:
            $ talk_var['help.hw'] += 1
            jump .self
        "Я всё сделаю сам на пятёрку, если ты сделаешь кое-что для меня..." if possibility["sg"].stn > 2  and chars['lisa'].dress > "a" and talk_var['lisa.pun'] > 2: # допилить условие на полотенце
            $ talk_var['help.hw'] += 1
            $ _chance = GetChanceConvince(punlisa, 2)
            $ _chance_color = GetChanceColor(_chance)
            $ ch_vis = str(int(_chance/10)) + "%"
            menu:
                Lisa_09 "Чего ты хочешь, Макс?"
                "Покажи грудь! {color=[_chance_color]}(Убеждение. Шанс: [ch_vis]){/color}":
                    jump .show_breast
        "Ой, тут много... Давай в другой раз.":
            $ AddRelMood("lisa", -5, -50)
            $ spent_time += 10
            jump Waiting

    label .show_breast:
        $ renpy.show("Max lessons-breast "+pose3_1+mgg.dress)
        if RandomChance(_chance):  # убеждение успешно
            $ punlisa[0][0] = 4
            $ mgg.social += 0.2
            menu:
                Lisa_10 "Ну Макс. Ты же мой брат..."
                "Тебе напомнить как мама больно наказывает?":
                    pass
                "Как хочешь. Но мама тебя накажет!":
                    pass
            if GetMood("lisa")[0] < 3:
                $ renpy.show("Lisa lessons-breast "+renpy.random.choice(["01", "02"])+chars["lisa"].dress)
                Lisa_09 "{color=[lime]}{i}Убеждение удалось!{/i}{/color}\nЛадно. Всё равно у меня нет выбора... Смотри, раз ты такой извращенец..."
                Lisa_00 "Ну всё, а теперь сделай работу на отлично!"
            else:
                $ renpy.show("Lisa lessons-breast 03"+chars["lisa"].dress)
                Lisa_05 "{color=[lime]}{i}Убеждение удалось!{/i}{/color}\nЛадно. Тебе повезло, что настроение у меня сейчас очень хорошее... Так что наслаждайся, извращенец..."
                Lisa_01 "Ну все, полюбовался и хватит, а теперь сделай работу на отлично!"
            Max_05 "Вот теперь убедила!"
            $ renpy.show("Lisa lessons-help "+pose3_1+chars["lisa"].dress)
            $ renpy.show("Max lessons-help "+pose3_1+mgg.dress)
            Max_00 "{i}Ну, раз Лиза выполнила мою просьбу, придётся всё сделать как положено. Да и если ей поставят двойку за это снова, она больше ничего уже не покажет мне...{/i}"
            Max_04 "Ну всё, Лиза, я закончил!"
            Lisa_02 "Хорошо, Макс. Теперь мы в рассчёте, да? И надеюсь, что получу только пять, не меньше, верно?"
            Max_01 "Я старался. В этот раз всё будет хорошо."
            $ spent_time = max((60 - int(tm[-2:])), 30)
            jump Waiting
        else:  # убеждение не удалось
            Lisa_12 "{color=[orange]}{i}Убеждение не удалось!{/i}{/color}\nЗнаешь что, Макс? Отвали! Я сама всё сделаю. Пусть и на двойку, но зато без тебя с твоими... извращёнными мыслями!"
            Max_07 "Как скажешь..."
            $ punlisa[0][0] = 2
            $ mgg.social += 0.1
            $ AddRelMood("lisa", -5, -50)
            $ spent_time += 10
            jump Waiting

    label .make_bag:
        $ _chance = GetChance(mgg.social, 4)
        $ _chance_color = GetChanceColor(_chance)
        $ ch_vis = str(int(_chance/10)) + "%"
        menu:
            Lisa_13 "Макс, мне кажется, что здесь должен быть другой ответ. Это же не так решается..."
            "Да, Лиза, ты права...":
                Lisa_03 "Ну, вот видишь! Эх, какой же ты невнимательный, Макс. Но всё равно, спасибо, что помог. Сама я бы долго возилась..."
                Max_01 "Не за что!"
                $ AddRelMood("lisa", 10, 100)
            "Лиза, я знаю о чём говорю! {color=[_chance_color]}(Убеждение. Шанс: [ch_vis]){/color}":
                if RandomChance(_chance):
                    Lisa_02 "{color=[lime]}{i}Убеждение удалось!{/i}{/color}\nДа? Ну ладно. Тебе виднее... Не буду проверять даже. Спасибо за помощь, Макс!"
                    $ punlisa[0][0] = 1
                    $ mgg.social += 0.2
                    Max_01 "Не за что!"
                    $ AddRelMood("lisa", 10, 100)
                else:
                    Lisa_13 "{color=[orange]}{i}Убеждение не удалось!{/i}{/color}\nНет, Макс, ты не прав. Тут надо всё совсем не так решать. Спасибо за помощь, но я уже разобралась..."
                    Max_07 "Как знаешь..."
                    $ punlisa[0][0] = 3
                    $ mgg.social += 0.1
                    $ AddRelMood("lisa", 5, 20)
        $ spent_time += max((60 - int(tm[-2:])), 40)
        jump Waiting

    label .self:
        hide Lisa
        $ renpy.show("Max lessons-help single-01"+mgg.dress)
        $ talk_var["truehelp"] += 1
        Max_10 "Так, что тут у нас... Ох, вроде недавно школу закончил... ну, учился недавно ещё, а уже ничего не помню... Хотя... Вот. Да, всё верно!"
        Max_04 "Ну всё, Лиза, я закончил!"
        hide Max
        $ renpy.show("Lisa lessons-help "+pose3_1+chars["lisa"].dress)
        $ renpy.show("Max lessons-help "+pose3_1+mgg.dress)
        $ punlisa[0][0] = 3
        if talk_var["truehelp"] == 6:
            $ AddRelMood("lisa", 150, 150)
            Lisa_03 "О, супер! Макс! Ты такой молодец! Всё время меня выручаешь... Я бы сама долго возилась и, наверняка, ошибок бы наделала... Спасибо тебе!"
        else:
            $ AddRelMood("lisa", 30, 150)
            Lisa_02 "О, супер! Я сама ещё всё проверю потом, чтобы ошибок не было, но тебе огромнейшее спасибо! Ты меня выручил."
        $ spent_time += max((60 - int(tm[-2:])), 40)
        jump Waiting


label Lisa_sorry:
    Lisa_12 "Значит решил признаться, что бессовестно глазел на меня в душе?!"
    Max_10 "Ты же понимаешь, что я не специально..."
    Lisa_11 "Макс, ты серьёзно думаешь, что я в это поверю? Я же видела, как ты за мной подглядывал!"
    Max_14 "Да я не хотел смотреть, просто мимо шёл..."
    Lisa_13 "Ага, конечно, мимо он шёл... Вот вечером маме расскажу - посмотрим, что она с тобой сделает!"
    Max_10 "Не виноват я! Просто шёл, а ты душ как раз принимала... Ну я и отскочил к стене... где ты меня и заметила... Вот и всё, я даже и не видел ничего такого!"
    Lisa_09 "Ну не знаю, Макс... Как-то это всё выглядит..."
    Max_11 "Неоднозначно?! Согласен..."
    Lisa_10 "Это ещё слабо сказано..."
    Max_07 "Да, тебе не удобно, мне не удобно... Давай мы просто об этом забудем... и ты ничего не будешь говорить маме об этом, а я в свою очередь куплю тебе какую-нибудь вкусняшку?"
    Lisa_00 "Ну, Макс... Я хочу вкусняшку... Но времени у тебя до завтрашнего вечера, понял?! Иначе перед ужином мама всё узнает..."
    Max_01 "Хорошо. Завтра всё будет..."
    $ punreason[0] = 0
    $ peeping['lisa_shower'] = 0
    Lisa_01 "Ну посмотрим."
    $ spent_time += 10
    jump Waiting


label gift_swimsuit:
    Lisa_03 "Да ладно?! Ты купил для меня купальник?"
    Max_01 "Ага. Красный, маленький, красивый..."
    menu:
        Lisa_02 "И ты мне его просто так подаришь?"
        "Конечно. Держи!":
            jump .give
        "Ну, не совсем...":
            menu:
                Lisa_00 "И чего ты хочешь? Говори!"
                "Я тебе его подарю, если разрешишь посмотреть, как ты его примеряешь...":
                    Lisa_09 "Макс! Ну почему ты такой, а? Ты же знаешь, как мне нужен был этот купальник и теперь требуешь от меня что-то..."
                    Max_07 "Знаю, поэтому и могу требовать..."
                    Lisa_00 "Хорошо, Макс. Но когда я совсем разденусь, не смотреть! Хорошо?"
                    Max_04 "Ага..."
                    jump .swimsuit_show
                "Я передумал. Ничего не хочу!":
                    jump .give
    label .give:
        Lisa_03 "Спасибо, Макс! Ты лучший! Я это не забуду!"
        Max_03 "Да, пустяки..."
        $ AddRelMood("lisa", 100, 300)
        jump .end

    label .swimsuit_show:
        scene BG char Lisa newsuit
        show Lisa newsuit 01
        Lisa_12 "Я сейчас разденусь, а ты не смотри! Если замечу, что подглядываешь, всё маме расскажу!"
        Max_02 "Ладно, ладно... Раздевайся уже!"
        show Lisa newsuit 02
        menu:
            Lisa_02 "Макс! Ты подглядываешь. Ты же обещал... Отвернись, быстро!"
            "Угу, уже... {i}(Демонстративно отвернуться){/i}":
                pass
        show Lisa newsuit 03
        Max_04 "{i}Да, закрою глаза, когда тут такое... как же!\nУх! А, сестрёнка хороша... Местами даже очень...{/i}"
        show Lisa newsuit 04
        Max_08 "{i}Вот это вид, я понимаю! Главное, чтобы штаны меня не выдали...{/i}"
        show Lisa newsuit 05
        Lisa_03 "Ну, как тебе? Макс, чего молчишь? Скажи, тебе нравится?"
        Max_07 "О... Очень!"
        show Lisa newsuit 06
        Lisa_02 "Ты чего, заикаешься? Правда всё хорошо?"
        Max_03 "Всё просто отлично! А повернись-ка..."
        show Lisa newsuit 07
        Lisa_03 "Сзади тоже всё хорошо?"
        Max_05 "Сзади всё особенно... хорошо..."
        show Lisa newsuit 08
        Lisa_02 "Я рада, Макс. Спасибо тебе большое! Как ты только нашёл этот купальник? Я именно о таком и мечтала!"
        Max_04 "Да, пустяки..."
        $ AddRelMood("lisa", 100, 200)
        $ spent_time += max((50 - int(tm[-2:])), 30)
        $ current_room = house[0]

    label .end:
        $ items['bikini'].have = False
        $ items['bikini'].InShop = False
        $ items['bathrobe'].InShop = True
        $ chars['lisa'].gifts.append('bikini')
        if chars['lisa'].inferic is not None:
            $ chars['lisa'].inferic = clip(chars['lisa'].inferic-50.0, 0.0, 100.0)
        if chars['lisa'].infmax is not None:
            $ chars['lisa'].infmax = clip(chars['lisa'].infmax+20.0, 0.0, 100.0)
        else:
            $ chars['lisa'].infmax = 20.0

        $ spent_time += 10
        jump Waiting


label gift_bathrobe:
    Lisa_01 "Я люблю подарки. Давай, показывай, что там у тебя?"
    Max_01 "Держи..."
    menu:
        Lisa_03 "Что тут? Халатик?! Прямо как у Алисы, только красный? Какая прелесть! Я тебя обожаю! Спасибо!"
        "Может быть, примеришь его при мне?":
            Lisa_02 "Ага, прямо при тебе сейчас разденусь до гола и примерю, да? Нет уж. Потерпи. После душа буду его надевать, перед тем как сесть делать уроки... А может и вообще всё время буду его на себе носить... Он же такой классный!"
        "Да не за что, сестрёнка!":
            Lisa_03 "Я теперь после душа его надевать буду... перед уроками. Или вообще буду жить в нём, если настроение будет такое же классное, как и сам халатик!"
    Max_00 "Супер! Будет любопытно посмотреть..."
    $ AddRelMood("lisa", 100, 200)
    $ items['bathrobe'].have = False
    $ items['bathrobe'].InShop = False
    $ chars['lisa'].gifts.append('bathrobe')
    if chars['lisa'].inferic is not None:
        $ chars['lisa'].inferic = clip(chars['lisa'].inferic-50.0, 0.0, 100.0)
    if chars['lisa'].infmax is not None:
        $ chars['lisa'].infmax = clip(chars['lisa'].infmax+20.0, 0.0, 100.0)
    else:
        $ chars['lisa'].infmax = 20.0

    $ spent_time += 10
    jump Waiting
