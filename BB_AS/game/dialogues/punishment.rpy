
label StartPunishment:
    $ pun_list.clear()
    $ first = True
    $ defend = False
    # Макс теоретически может получить наказание как утром, так и вечером
    if max(punreason):  # есть причины наказания Макса
        $ pun_list.append("mgg")

    if tm > "18:00":
        # сестры получают наказание толлько вечером
        $ chance = GetLisaPunChance()  # шанс получения Лизой двойки
        if RandomChance(chance):  # получит ли Лиза двойку
            $ punlisa[0][1] = 1
            $ pun_list.append("lisa")

    $ renpy.random.shuffle(pun_list) # перемешаем список последовательности наказания

    if len(pun_list):
        jump punishment
    elif tm > "14:00":
        jump dinner_after_punishment
    else:
        jump breakfast_after_punishment

label punishment:
    $ renpy.block_rollback()
    if tm < "14:00":
        scene BG punish-morning 00
        $ renpy.show("Ann punish-morning 00"+chars["ann"].dress)
    else:
        scene BG punish-evening 00
        $ renpy.show("Ann punish-evening 00"+chars["ann"].dress)

    Ann_20 "Прежде, чем мы начнём, кое-кто заслужил наказание и сейчас все на это посмотрят..."
    $ _i = 0
    while len(pun_list) > _i:
        if pun_list[_i] == "mgg":
            if len(pun_list) > 1:  # за эвент будут наказаны больше одного персонажа
                if first: # Макс наказывается первым
                    $ first = False
                    Ann_20 "Итак, Макс, ты первый..."
                else: # Макса наказывают не первым
                    Ann_00 "Макс, теперь ты..."
            else:  # Макс единственный наказуемый
                Ann_00 "Макс, иди сюда..."
            call punishment_max
        elif pun_list[_i] == "lisa":
            if len(pun_list) > 1:  # за эвент будут наказаны больше одного персонажа
                if first: # Лиза наказывается первой
                    $ first = False
                    Ann_20 "Так, Лиза, начнем с тебя..."
                else: # Лизу наказывают не первой
                    Ann_00 "Теперь Лиза..."
            else:  # наказывают только Лизу
                Ann_00 "Лиза, подойди-ка ко мне."
            call punishment_lisa
        # elif pun_list[i] == "alice":
        #     call punishment_alice
        $ _i += 1

    if tm > "14:00":
        jump dinner_after_punishment
    else:
        jump breakfast_after_punishment


label punishment_max:
    $ renpy.block_rollback()

    if tm < "14:00":
        scene BG punish-morning 01
        $ renpy.show("Ann punish-morning 01"+chars["ann"].dress)
        # показать Макса
    else:
        scene BG punish-evening 01
        $ renpy.show("Ann punish-evening 01"+chars["ann"].dress)
        # показать Макса

    if warning < 2 and newpunishment == 0:
        Ann_00 "Макс! Я вынуждена отчитать тебя перед всеми, так как у нас в семье не должно быть никаких секретов."
        if warning > 0:
            Max_00 "Я снова не виноват!"
            Ann_01 "Не виноват, значит? Снова? Кажется, ты не осознаёшь, что это последнее предупреждение и в следующий раз я тебя выпорю на глазах у сестёр. Ты меня понял? А теперь рассказывай, что натворил, чтобы все были в курсе!"
        else:
            Max_00 "Я не виноват!"
            Ann_01 "Не виноват, значит? А я думаю, что ещё как виноват. В этот раз тебе повезло, это всего лишь первое предупреждение. Надеюсь, второго не потребуется... Кстати, можешь всем рассказать, что ты натворил..."
        menu .pun_reson:
            "Ну, я случайно оказался рядом с душем, когда там была Лиза..." if punreason[0]:
                Lisa_12 "Он видел меня голой, мам! Накажи его! Почему ему предупреждения? Пусть получит что заслужил!"
                $ punreason[0] = 0
                Max_00 "Да ничего я не заслужил!"
            "Ну, я оказался случайно рядом с душем, где мылась Алиса..." if punreason[1]:
                Alice_14 "Случайно? Врёт он всё, мам! Он стоял и пялился на меня!"
                $ punreason[1] = 0
                Max_00 "Да, я мимо проходил!"
            "Ну, я подглядывал за тобой, мам..." if punreason[2]:
                Ann_14 "Очень хочу надеяться, что это было случайно. Тем не менее, ты пойман и как уже сказала, получаешь предупреждение."
                $ punreason[2] = 0
                Max_00 "Больше это не повторится!"
            "Ну, я подглядывал за вами с Эриком..." if punreason[3]:
                Ann_14 "Очень хочу надеяться, что ты это сделал не специально и просто проходил мимо. Тем не менее, ты пойман и как уже сказала, получаешь предупреждение."
                $ punreason[3] = 0
                Max_00 "Да, я мимо проходил!"
            "Ну, я оказался случайно рядом с душем, где мылась Алиса..." if punreason[4]:
                Alice_14 "Случайно? Мам! Он всё врёт! Он подглядывал и может быть даже паука подбросил! А ты знаешь, как я боюсь пауков..."
                $ punreason[4] = 0
                Max_00 "Трусиха!"
        if max(punreason):
            $ _r1 = renpy.random.randint(1, 3)
            if _r1 == 1:
                Ann_20 "Ты не закончил, Макс. Продолжай..."
            elif _r1 == 2:
                Ann_20 "Дальше, Макс, мы тебя внимательно слушаем..."
            else:
                Ann_20 "А еще что?"
            jump .pun_reson
        Ann_20 "У тебя есть ещё время подумать над своим поведением. Надеюсь, следующего раза не будет!"
        Max_00 "Да, мам..."
        Ann_00 "В общем, на этот раз вопрос уладили. Все сделали выводы, а кое-кто и серьёзно задумается. Да, Макс? Можешь не отвечать."
    elif newpunishment == 0:  # стандартное наказание без штанов, но в трусах и майке
        $ _chance = GetChance(mgg.social, 2, 900)
        $ _chance_color = GetChanceColor(_chance)
        $ ch_vis = str(int(_chance/10)) + "%"
        menu:
            Ann_00 "Макс! Сейчас ты будешь наказан, сам знаешь за что!"
            "Я же не виноват! {color=[_chance_color]}(Убеждение. Шанс: [ch_vis]){/color}":
                pass
        if RandomChance(_chance):
            $ mgg.social += 0.2
            Ann_14 "{color=[lime]}{i}Убеждение удалось!{/i}{/color}\nТы знаешь, Макс, всё говорит о том, что ты виноват и должен быть наказан. Но поверю тебе на слово, что это была какая-то ошибка. Надеюсь, я не пожалею о своём решении..."
            Max_00 "Спасибо, мам!"
            return
        else:
            $ mgg.social += 0.1
            menu:
                Ann_20 "{color=[orange]}{i}Убеждение не удалось!{/i}{/color}\nВот так просто? \"Я не виноват\" и всё забудем? Нет, Макс, со мной эти шуточки не прокатят. Давай, снимай штаны и ложись на мои колени. Надеюсь, ты сегодня в трусах..."
                "{i}снять штаны{/i}":
                    pass
        # Макс без штанов у Анны на коленях
        if punreason.count(1) > 1:  # несколько причин для наказания, общая фраза
            Ann_20 "У Макса несколько провинностей, он их знает, перечислять не будем. Сейчас он получит за все сразу."
        else:  # конкретная причина для наказания
            $ _text = {
                0 : _("Для тех, кто не в курсе: Макс будет наказан за то, что подглядывал за Лизой. Я уже предупреждала, что не люблю, когда кто-то нарушает личное пространство..."),
                1 : _("Для тех, кто не в курсе: Макс будет наказан за то, что подглядывал за Алисой. Я уже предупреждала, что не люблю, когда кто-то нарушает личное пространство..."),
                2 : _("Для тех, кто не в курсе: Макс будет наказан за то, что подглядывал за мной. Я уже предупреждала, что не люблю, когда кто-то нарушает личное пространство..."),
                3 : _("Для тех, кто не в курсе: Макс будет наказан за то, что подглядывал за мной... с Эриком. Я уже предупреждала, что такое недопустимо!"),
                4 : _("Для тех, кто не в курсе: Макс будет наказан за своё отвратительное поведение. Надеюсь, теперь будешь думать о том, что делать и что говорить!"),
                5 : _("Для тех, кто не в курсе: Макс будет наказан за то, что подглядывал за Алисой в душе и, возможно, даже подбросил паука. Ты знаешь, что такое я не потерплю!"),
                }[punreason.index(1)]
            Ann_04 "[_text!tq]"
        ### сцена наказания
        Max_00 "{i}Мама наказывает меня прямо перед сёстрами... Это так унизительно...{/i}\n\n{color=[orange]}{b}Внимание:{/b} Ваше влияние на присутствующих понизилось!{/color}"
        ## здесь снижение влияния Макса для присутствующих персонажей
        python:
            for cr in current_room.cur_char:
                cr.infmax = clip(cr.infmax - 5, 0, 100)

        if tm < "14:00":
            scene BG punish-morning 00
            $ renpy.show("Ann punish-morning 00"+chars["ann"].dress)
        else:
            scene BG punish-evening 00
            $ renpy.show("Ann punish-evening 00"+chars["ann"].dress)

        Ann_14 "Ну вот. Теперь все всё поняли? Ведите себя хорошо и вас не ждёт эта участь..."
    # elif newpunishment == 1:  # второй вариант наказания
    #
    return

label punishment_lisa:
    $ renpy.block_rollback()

    scene BG punish-evening 01
    $ renpy.show("Lisa punish-evening 01"+chars["lisa"].dress)
    $ renpy.show("Ann punish-evening 01"+chars["ann"].dress)

    $ __mood = 0

    # Лиза стоит в одежде, Макс может вмешаться и прервать наказание (если получится)
    if chars["lisa"].dress == "a":  # Лиза в обычной одежде
        $ _text = _("Ближе подходи, Лиза. И да, штаны снимай, ты заслужила!")
    else: # Лиза в халате
        $ _text = _("Ближе подходи, Лиза. И да, халат свой снимай, ты заслужила!")
    if defend:  # Макс уже не может заступиться
        Ann_20 "[_text!tq]"
    else:
        $ _chance = GetChance(mgg.social, 2, 900)
        $ _chance_color = GetChanceColor(_chance)
        $ ch_vis = str(int(_chance/10)) + "%"
        menu:
            Ann_20 "[_text!tq]"
            "{i}Заступиться за Лизу {color=[_chance_color]}(Убеждение. Шанс: [ch_vis]){/color}{/i}":
                $ defend = True
                Max_00 "Мам, не нужно наказывать Лизу. Она правда старалась, я сам видел. Ну и я помогу ей подтянуть оценки."
                if "mgg" in pun_list:
                    Ann_14 "Нет, Макс, и даже не пытайся меня уговаривыть. Ты и сам накосячил... А ты, Лиза, не стой столбом, шевелись давай..."
                elif RandomChance(_chance):  # Удалось уговорить Анну
                    $ mgg.social += 0.2
                    Ann_14 "{color=[lime]}{i}Убеждение удалось!{/i}{/color}\nХорошо, Макс, в этот раз наказывать не буду. Надеюсь, я не пожалею о своём решении... А ты, Лиза, благодари брата, да учись давай, а то в следующий раз не помилую..."
                    Lisa_00 "Списибо тебе, Макс!"
                    $ punlisa[0][2] = 2
                    return
                else:
                    $ mgg.social += 0.1
                    Ann_14 "{color=[orange]}{i}Убеждение не удалось!{/i}{/color}\nНет, Макс, не уговаривай. Получит то, что заслужила. А ты, Лиза, не стой столбом, шевелись давай..."
                    $ punlisa[0][2] = 1
            "{i}далее{/i}":
                pass

    Lisa_10 "Мам... Я не специально... Просто, задание было сложное..."
    if chars["lisa"].dress == "a":  # Лиза в обычной одежде
        $ _text = _("Быстро снимай штаны!")
    else: # Лиза в халате
        $ _text = _("Быстро снимай халат!")
    Ann_01 "Сложное? У тебя была куча времени, чтобы подготовиться! Сидишь в своём телефоне вечно вместо того, чтобы учиться. [_text!tq]"

    # Лиза стоит частично/полностью раздетая, если Макс не вмешивался, то может попробовать прервать наказание
    $ renpy.show("Lisa punish-evening 02"+chars["lisa"].dress)
    $ _text = _("Теперь ложись, и побыстрее, все есть хотят...")

    if defend:  # Макс уже заступался
        Ann_14 "[_text!tq]"
    else:
        menu:  # У Макса есть шанс заступиться за Лизу
            Ann_14 "[_text!tq]"
            "{i}Заступиться за Лизу {color=[_chance_color]}(Убеждение. Шанс: [ch_vis]){/color}{/i}":
                $ defend = True
                Max_00 "Мам, не нужно наказывать Лизу. Она правда старалась, я сам видел. Ну и я помогу ей подтянуть оценки."
                if "mgg" in pun_list:
                    Ann_14 "Нет, Макс, и даже не пытайся меня уговаривыть. Ты и сам накосячил... А ты, Лиза, не стой столбом, шевелись давай..."
                elif RandomChance(_chance):  # Удалось уговорить Анну
                    $ mgg.social += 0.2
                    Ann_14 "{color=[lime]}{i}Убеждение удалось!{/i}{/color}\nХорошо, Макс, в этот раз наказывать не буду. Надеюсь, я не пожалею о своём решении... А ты, Лиза, можешь одеваться. Скажи Максу спасибо, что сегодня отсалась безнаказанной. Но не думай, что я всегда буду такой доброй..."
                    Lisa_00 "Списибо тебе, Макс!"
                    $ punlisa[0][2] = 2
                    return
                else:
                    $ mgg.social += 0.1
                    Ann_14 "{color=[orange]}{i}Убеждение не удалось!{/i}{/color}\nНет, Макс, не уговаривай. Получит то, что заслужила. А ты, Лиза, не стой столбом, шевелись давай..."
                    $ punlisa[0][2] = 1
            "{i}далее{/i}":
                pass

    # сцена наказания Лизы
    scene BG punish-evening 02
    if chars["lisa"].dress == "a":
        $ renpy.show("Ann punish-evening lisa-01"+chars["ann"].dress)
    else:
        $ renpy.show("Ann punish-evening lisa-03"+chars["ann"].dress)

    $ __mood -= 50 # если Лизу наказывают, ее настроение портится

    Lisa_09 "Ма-ам, я больше не буду... Ай... Всмысле буду лучше учиться. Извини..."
    if chars["lisa"].dress == "a":
        $ renpy.show("Ann punish-evening lisa-02"+chars["ann"].dress)
    else:
        $ renpy.show("Ann punish-evening lisa-04"+chars["ann"].dress)

    $ _text = _("Говоришь тебе, говоришь, все как об стенку горох...") # вставка, если Макс не помогал за последнюю неделю

    # посмотрим, помогал ли Макс за последние 5 дней
    python:
        for i in range(min(5, len(punlisa))):
            if punlisa[i][0] == 1 or punlisa[i][0] > 2:
                _text = _("Поразительно. Тебе даже Макс помогает, а ты двойки хватаешь!") # вставка, если Макс помогал
                break

    Ann_00 "Конечно, будешь. [_text!tq] Совсем расслабилась."

    # сцена с наказанной Лизой
    scene BG punish-evening 01
    $ renpy.show("Lisa punish-evening 03"+chars["lisa"].dress)
    $ renpy.show("Ann punish-evening 01"+chars["ann"].dress)
    Ann_01 "Лиза, надеюсь, ты извлекла урок из этого наказания и больше это не повторится. А теперь одевайся!"

    return

label punishment_alice:

    return
