
label OliviaTalkStart:
    # $ renpy.dynamic('plan')
    $ dial = TalkMenuItems()

    $ plan = olivia.get_plan()
    if plan.talklabel is not None:
        call expression plan.talklabel from _call_expression_15

    if len(dial) > 0:
        $ dial.append((_("Хотя, нет... У меня вообще-то дела есть..."), "exit"))
    else:
        stop music
        jump Waiting

    $ renpy.block_rollback()
    if olivia.plan_name == 'sun':
        play music olivia
        Max_01 "Вы не против, если я тоже позагораю?"
        Olivia_01 "Конечно, Макс... Мы не против! Такая классная погода стоит..." nointeract
    else:
        Olivia_00 "А, Макс. Хотел поговорить?" nointeract

    $ rez = renpy.display_menu(dial)

    if rez != "exit":
        if renpy.has_label(talks[rez].label):   # если такая метка существует, запускаем ее
            call expression talks[rez].label from _call_expression_16
        jump OliviaTalkStart        # а затем возвращаемся в начало диалога, если в разговоре не указан переход на ожидание

    jump AfterWaiting               # если же выбрано "уйти", уходим в после ожидания


label olivia_talk1:
    # 1-ый разговор Макса и Оливии, возможен, начиная с дня знакомства (среда)

    # "Учтите, я испытываю... некоторый подъём!"
    # sun2-01 + sun2-01-max-03 + sun2-01-olivia-01
    scene BG sun-talk-01
    show Max sun-talk 03
    show Olivia sun-talk 01a
    Lisa_01 "Настроения?!"
    Olivia_02 "Ага... И настроения тоже... Я, Макс, к этому абсолютно спокойно отношусь и всё понимаю. У тебя там, кстати, резинка в шортах не тугая? А то ещё передавит тебе там всё..."
    Max_07 "Нормально. Не особо мешает."
    Olivia_05 "Смотри... Если больно или туго, то ты не стесняйся, снимай шорты..."
    Lisa_02 "Эй! Хватит моего брата тут раздевать!"
    Max_02 "Я сниму, если вы снимите!"
    Olivia_03 "Ох, я только за! Мне куда комфортнее отдыхать голышом... Такой у меня образ жизни!"
    Lisa_10 "Так, во-первых, я не стану раздеваться, а во-вторых, своим голым видом вы меня только смутите... Я к такому не готова!"
    Max_11 "Какой облом!"
    Olivia_05 "Да всё нормально, Лиза... Никто не будет оголяться сейчас, не волнуйся. Я думала, раз вы живёте в одной комнате, о уже привыкли к особенностям друг друга..."
    Lisa_03 "О да... Я его особенность почти каждое утро наблюдаю..."
    Max_03 "Я как просыпаюсь, успеваю увидеть, как она мой член разглядывает!"
    Lisa_01 "Мне просто любопытно было..."
    Max_04 "Честное слово, один раз даже видел, как она потрогать его собиралась!"
    Lisa_11 "Макс! Это неправда!"
    Olivia_03 "Так Лиза у нас оказывается шалунья! А со стороны такой скромняжкой кажется..."
    Lisa_13 "Ну всё! А Макс тут за всеми нами подглядывает, когда мы голые! За мной, за моей старшей сестрой, даже за мамой..."
    Max_09 "И прошу заметить, это всё происходит в доме, где окна размерами со стену! Здесь просто не реально куда-то пойти, чтобы не застать кого-нибудь за интересными делами..."

    # 2sun-00 + 2sun-max-01 + 2sun-olivia-04
    scene BG 2sun-00
    show Max sun-talk 04
    show Olivia sun-talk 04a
    Olivia_02 "Весело у вас тут! У тебя, Макс, девушка-то есть? Или ты любишь женщин постарше..."
    Max_10 "Э-э-э... В смысле?!"
    Olivia_03 "Ну... мне Лиза рассказала, почему тебя из школы выгнали..."
    Max_07 "Ах, ты про это..."
    Olivia_04 "Хм... Мне уже интересно о чём ты сейчас подумал..."
    Max_00 "Нет, девушки у меня нет. Пока что..."
    Olivia_06 "Эх, такой большой потенциал пропадает..."
    Max_02 "То, что у меня нет девушки не значит, что потенциал пропадает! Активен и ещё как..."
    Olivia_05 "Ой, Макс, это здорово! Я за тебя рада! Молодец."

    # sun2-01 + sun2-01-max-03 + sun2-01-olivia-01
    scene BG sun-talk-01
    show Max sun-talk 03
    show Olivia sun-talk 01a
    Max_04 "Ну а у тебя как дела на личном фронте? Лиза говорила, у тебя есть парень."
    Olivia_01 "Ага, есть. У нас с ним по-всякому бывает. Иногда ссоримся по пустякам, но всё хорошо."
    Max_07 "Ты уж приглядывай за ним! Сестрёнка у меня симпатичная, может спокойно у тебя парня увести. Не расхваливай его сильно при ней."
    Lisa_09 "Я вообще-то всё слышу! Хватит уже на меня наговаривать!"
    Olivia_03 "Да его и не за что расхваливать. Он красивый, спортивный... но простой, как деревянная табуретка..."
    Max_09 "Раз так, тогда почему ты с ним встречаешься?"
    Olivia_02 "А чтобы завидовали все в школе! Не то, чтобы я была совсем бесчувственной, просто не хочу к нему сильно привязываться."
    Max_03 "Н-да... Внешность не главное..."
    Olivia_03 "Ты, Макс, смотри... Будешь так залипать на мою грудь - ослепнешь!"
    Max_04 "Ага. Что-то я залежался тут с вами. Пора и делами заниматься. А вы - развлекайтесь..."
    Olivia_02 "Рада была пообщаться... Думаю, я загляну к вам в следующую среду. Ведь можно?"
    Max_01 "Конечно, Оливия. Мы будем только рады."

    $ poss['Schoolmate'].open(6)
    $ olivia.dcv.feature.stage = 1
    $ olivia.dcv.feature.set_lost(1)    # нельзя начать следующий разговор в тот же день
    $ AttitudeChange('olivia', 1)   # Неплохие
    $ spent_time += 30
    stop music
    jump Waiting


label olivia_talk2:
    #после 1-ого разговора, возможен, начиная со 2-ого прихода Оливии на виллу (среда)
    # "Пошепчемся немного о моей сестрёнке?"
    # sun2-01 + sun2-01-max-03 + sun2-01-olivia-01
    scene BG sun-talk-01
    show Max sun-talk 03
    show Olivia sun-talk 01a
    Olivia_03 "Что, Макс? Какие-то гадости хочешь рассказать о Лизе?"
    Max_04 "Нет. Она славная девчонка. Такая вся добрая, умная..."
    Olivia_02 "Она у вас вся такая невинная... Но это очень мило. Она мне нравится. Обычно такие скромницы, на самом деле, те ещё проказницы!"
    Max_05 "Правда?"
    Olivia_01 "Лиза ещё не опытна в плане своей сексуальности, но мне кажется с ней будет очень интересно..."
    Max_02 "Ну, ты же как её подружка, поможешь ей так сказать, раскрепоститься?"
    # 2sun-00 + 2sun-max-01 + 2sun-olivia-04
    scene BG 2sun-00
    show Max sun-talk 04
    show Olivia sun-talk 04a
    Olivia_03 "А ты уже, наверно, во всю представляешь, как мы с ней лежим голенькие в постели и предаёмся различным утехам?"
    Max_03 "Мечтать не вредно!"
    Olivia_05 "Ты, Макс, весьма озорной, мне это нравится... Хотя, не удивительно. Как я поняла, ты парень, окружённый семьёй из одних женщин!"
    Max_07 "Ага. Ты даже не представляешь, как сложно построить тёплые и доверительные отношения со всеми ними. Лиза так до сих пор настораживается, когда я в \"приподнятом настроении\", как сейчас..."
    Olivia_06 "Здесь я не многим могу помочь... Одно дело, если она будет чувствовать себя комфортно рядом с мной, и совсем другое - с тобой."
    Max_09 "Может тогда что-нибудь посоветуешь?"
    # sun2-01 + sun2-01-max-03 + sun2-01-olivia-01
    scene BG sun-talk-01
    show Max sun-talk 03
    show Olivia sun-talk 01a
    Olivia_02 "Ну... Вам нужно больше времени проводить вместе. И будет хорошо, если с вами будет кто-то, кто сексуально раскрепощён..."
    Max_07 "Моя тётя такая, только вот она мало времени проводит у нас дома... работает допоздна. Так что вся надежда только на тебя!"
    Olivia_03 "Посмотрим, чем я смогу помочь. Нужно подумать..."
    Max_02 "Спасибо. Ох, как же с вами жарко загорать! И это я не только про солнце..."
    Olivia_02 "Да, мы такие!"
    Max_04 "Пойду я... Рад был повидаться! Когда снова к нам заглянешь?"
    Olivia_05 "Взаимно, Макс... Наверное, теперь буду приходить почаще, скажем во вторник и пятницу... Я же не помешаю?"
    Max_01 "Нисколько. Обязательно приходи..."

    $ poss['Schoolmate'].open(7)
    $ olivia.dcv.feature.stage = 2
    $ olivia.dcv.feature.set_lost(1)    # нельзя начать следующий разговор в тот же день
    $ spent_time += 30
    stop music
    jump Waiting


label olivia_talk3:
    #после 2-ого разговора, возможен, начиная с 3-его прихода Оливии на виллу (вторник)
    # "Что новенького, Оливия?"
    # sun2-01 + sun2-01-max-03 + sun2-01-olivia-01
    scene BG sun-talk-01
    show Max sun-talk 03
    show Olivia sun-talk 01a
    Olivia_06 "Да как всегда, Макс. Учимся, да вот потом отдыхаем... А там и домашнее задание скоро делать. Тебе ли не знать..."
    Max_08 "Да-а-а... Как вспомню, так страшно становится!"
    Olivia_03 "Кстати, я тут узнала от Лизы, что у вас тут в пятницу вечером все разъезжаются и вы остаётесь вдвоём!"
    Max_07 "Да, старшая сестра с тётей возвращаются не раньше трёх часов ночи, а мама и вовсе только утром."
    Olivia_05 "Я вот что думаю... У вас такой большой экран в гостиной, давайте зависнем на диване и будем что-нибудь смотреть?! Прямо как в кинотеатре, только можно не запариваться по поводу одежды!"
    Lisa_01 "Слушайте, а можно... Правда раньше полуночи я не освобожусь..."
    Max_03 "Ну и ладно, у нас ещё будет несколько часов, прежде чем кто-нибудь вернётся."
    Olivia_04 "Класс! Я улизну из дома ближе к полуночи в пятницу и будем веселиться! Встретишь меня, Макс?"
    Max_04 "Обязательно!"
    Lisa_02 "Может и Алекса с собой приведёшь?"
    Olivia_03 "Вот ещё! Всего лишь одна ночь на неделе, когда можно спокойно поваляться на диване перед огромным экраном с друзьями, а Алекс у меня в распоряжении почти всегда. Так что обойдётся!"
    Max_07 "Какая ты суровая!"
    Olivia_02 "Ничего, он любит меня продинамить, когда в свои любимые видеоигры играет или в тренажёрном зале торчит. Я лучше с вами время проведу."
    Max_02 "А мы с тобой!"
    Olivia_03 "В тебе, Макс, я нисколько не сомневалась... У тебя вон, полные шорты энтузиазма! И уже долгое время..."
    Max_01 "Да, я пожалуй пойду, развеюсь немного..."

    $ poss['Schoolmate'].open(8)
    $ olivia.dcv.feature.stage = 4
    $ olivia.dcv.feature.set_lost(1)    # нельзя начать следующий разговор в тот же день
    $ spent_time += 30
    stop music
    jump Waiting


label olivia_talk4:
    #после 1-ого визита Оливии ночью
    # "Рад тебя видеть, Оливия!"

    # sun2-01 + sun2-01-max-03 + sun2-01-olivia-01a
    scene BG sun-talk-01
    show Max sun-talk 03
    show Olivia sun-talk 01b
    Olivia_02 "Я тоже рада... Кстати, ты первый парень, который не только таращится на мои сиськи или задницу, но и глазами со мной встречается. Я перед тобой лежу голая и мы при этом спокойно можем болтать..."
    Max_07 "Это хорошо или плохо?"
    Olivia_03 "Это очень хорошо, Макс. Честно признаюсь, в первую нашу встречу ты ничем не отличался от других парней, ну... кроме как своим выдающимся... ну ты понял, а сейчас я вижу в тебе интересного человека. Это приятно..."
    Max_02 "Рассказала своему парню, как у нас ночь провела?"

    # 2sun-00 + 2sun-max-01 + 2sun-olivia-04a
    scene BG 2sun-00
    show Max sun-talk 04
    show Olivia sun-talk 04b
    Olivia_06 "Рассказала... Но, тс-с-с-с! Кажется, Алекс начал интересоваться Лизой... И вашим домом..."
    Max_09 "Тебя это беспокоит?"
    Olivia_00 "Не особо. Скорее, просто разочаровывает. Оказалось, Алекс тот ещё подарочек..."
    Max_07 "Ты о чём?"
    Olivia_06 "Ну, у нашей семьи, если ты знаешь, сейчас проблемы. Мама с папой ссорятся. Думаю, будут разводиться. Ну и финансовые проблемы начались. И Алекса как будто подменили. Сразу Лиза ему интересна стала, как только он о вашей шикарной вилле от меня услышал..."
    Max_09 "А ты ей об этом рассказала?"
    Olivia_03 "Да, сегодня как раз и рассказала. Она полдня сегодня в школе притихшая ходила... Похоже, она запала на моего парня, а теперь в смятении."
    Max_08 "Ты уж не вини её за это. Видимо, в этого вашего красавчика Алекса все влюбляются."
    Olivia_02 "Если бы я не знала Лизу, то подумала бы, что попытка подружиться со мной, была с целью как раз к нему подкатить..."
    Max_07 "Но?"
    Olivia_04 "Но она не умеет такие сложные комбинации строить. Она классная, но девчонка простая. Видимо, просто так всё совпало..."
    Max_00 "Хорошо, что теперь выяснилось, как твой парень падок на всякое красивое и дорогое."
    Olivia_03 "Как бы уже не бывший парень... Но нужно посмотреть, что он дальше думает делать."

    # sun2-01 + sun2-01-max-03 + sun2-01-olivia-01a
    scene BG sun-talk-01
    show Max sun-talk 03
    show Olivia sun-talk 01b
    Max_14 "Постарайся надеяться на лучшее, но приготовься к худшему."
    Olivia_05 "Спасибо. Хотя, в данной ситуации - худшее, может быть не таким уж и плохим."
    Max_03 "Ага, может..."
    Lisa_09 "Эй, ребята, вы там чего расшушукались?! Со мной не хотите поделиться?"
    Olivia_02 "Да так, всякие школьные байки травим. Пойдём купаться или ещё позагораем?"
    Lisa_01 "Я ещё хочу позагорать немного..."
    Max_04 "Ну а я пойду по своим делам."

    if olivia.dcv.battle.stage > 1:
        # Эрик ещё не вмешался в жизнь Лизы на момент знакомства с Оливией
        if weekday==2 and not olivia.dcv.special.done:
            # (диалог состоялся во вторник, неделя без ТВ)
            Olivia_01 "Хорошо поболтали, Макс. В эту пятницу я успею к вам только днём забежать на пару часов, так что устроим ночные посиделки в следующую пятницу, ладно?"
            Max_01 "Не вопрос. Будем только рады..."
        elif weekday==5 and not olivia.dcv.special.done:
            # (диалог состоялся в пятницу, неделя без ТВ)
            Olivia_01 "Хорошо поболтали, Макс. Мы же устроим ночные посиделки в следующую пятницу, да?"
            Max_01 "Конечно. Будем только рады..."
        else:
            # (диалог состоялся во вторник, неделя с ТВ)
            Olivia_01 "Хорошо поболтали, Макс. Мы же устроим ночные посиделки в эту пятницу, да?"
            Max_01 "Конечно. Будем тебя ждать..."
    else:
        # Эрик уже вмешался в жизнь Лизы  на момент знакомства с Оливией
        Olivia_01 "Хорошо поболтали, Макс. Мы же устроим ночные посиделки в эту пятницу, да?"
        Max_01 "Конечно. Будем тебя ждать..."

    if lisa.flags.showdown_e:
        $ poss['Schoolmate'].open(18)
    else:
        $ poss['Schoolmate'].open(11)
    $ olivia.dcv.feature.stage = 5
    $ olivia.dcv.feature.set_lost(1)    # нельзя начать следующий разговор в тот же день
    $ spent_time += 30
    stop music
    jump Waiting


label olivia_about_film_punish:
    if not olivia.flags.promise:    # первый разговор при Оливии
        Max_09 "Лиза... А что насчёт сегодняшнего наказания?"
        Lisa_11 "Ой! А я и забыла уже..."
        Olivia_04 "Что это у вас там за наказания?"
        Lisa_09 "Я заметила, как Макс за мной подглядывал в душе..."
        Max_07 "Естественно, случайно!"
        Lisa_02 "Да, как всегда. И чтобы мама об этом ничего не узнала, я мучаю Макса просмотром фильмов, которые ему не нравятся. Если он заснул или не досмотрел, то не прощён!"
        Max_09 "Да, да, да... Так что с этим решим?"
        if lisa.dcv.special.stage < 5: # не было просмотров ужастиков с Лизой
            jump .postpone
        else:      # был просмотр ужастиков с Лизой
            menu:
                Lisa_01 "Сам выбирай, Макс! Можем либо перенести наказание на следующую ночь, либо у нас сегодня девичник..."
                "Давай перенесём...":
                    menu:
                        Lisa_02 "Вот и разобрались. Пойдёмте уже..."
                        "{i}идти в гостиную{/i}":
                            $ lisa.dcv.special.lost += 1    # перенесем "наказание" на следующую ночь
                            jump night_tv_with_olivia
                "Тогда приятного вам просмотра...":
                    jump .nice_view
    else:
        Max_09 "Лиза... А что насчёт сегодняшнего наказания?"
        if lisa.dcv.special.stage < 5: # не было просмотров ужастиков с Лизой
            jump .postpone
        else:
            menu:
                Lisa_01 "Сам выбирай, Макс! Можем либо перенести наказание на следующую ночь, либо у нас сегодня девичник..."
                "Давай перенесём...":
                    menu:
                        Lisa_02 "Вот и разобрались. Пойдёмте уже..."
                        "{i}идти в гостиную{/i}":
                            $ lisa.dcv.special.lost += 1    # перенесем "наказание" на следующую ночь
                            jump night_tv_with_olivia
                "Тогда приятного вам просмотра...":
                    jump .nice_view

    label .postpone:
        Lisa_01 "Просто перенесём на следующую ночь, вот и всё."
        Max_01 "Хорошо. Тогда приятного вам просмотра..."
        #перенос Макса в его комнату
        $ lisa.dcv.special.lost += 1        # перенесем "наказание" на следующую ночь
        $ spent_time = 20
        $ current_room = house[0]
        stop music
        jump Waiting

    label .nice_view:
        Olivia_06 "Как?! Не останешься с нами? Жалко..."
        Max_01 "Мне тоже. Не скучайте."
        #перенос Макса в его комнату
        $ lisa.dcv.special.disable()        # отменяем наказание
        $ spent_time = 20
        $ current_room = house[0]
        stop music
        jump Waiting


label about_first_sunscreen:
    #отдельное 1-ое нанесение крема девочками, если у Макса появился крем

    # sun2-01 + sun2-01-max-03 + sun2-01-olivia-01a
    scene BG sun-talk-01
    show Max sun-talk 03
    show Olivia sun-talk 01b
    with dis2
    Olivia_03 "Ах, да. Ты же предлагал нам в тот раз. Но у нас, вроде, и так всё в порядке."
    Max_07 "Но лишний ультрафиолет здоровья никому не прибавляет. Так что кожу лучше защитить."
    Lisa_03 "Ну, раз в ход пошли слова, вроде ультрафиолета, то лучше намазаться кремом!"
    menu:
        Olivia_05 "Ага, давай крем. Чур гостей мажут первыми!"
        "{i}устроиться поудобнее и наблюдать{/i}":
                call olivia_lisa_sunscreen_0 from _call_olivia_lisa_sunscreen_0_1

    jump Waiting


label ran_out_sunscreen:
    # крема нет или его не хватит на 2 порции
    Max_11 "{m}А крема-то нет! Теперь только до следующего раза...{/m}"
    $ items['solar'].unblock()
    jump AfterWaiting


label olivia_second_sunscreen:
    $ olivia.daily.oiled = 1

    if kol_cream < 2:
        jump ran_out_sunscreen

    # sun2-01 + sun2-01-max-03 + sun2-01-olivia-01a
    scene BG sun-talk-01
    show Max sun-talk 03
    show Olivia sun-talk 01b
    # with dis2
    Olivia_03 "Давай крем и мы сами намажемся. Или ты думаешь, что мы сделаем это так себе?"
    Max_02 "Я бы точно сделал это приятнее."
    Lisa_02 "Да что ты. Поводить руками по спине - большого ума не нужно."
    Max_09 "Ну как знаете. Моё дело предложить."

    # sun2-01 + sun2-01-lisa&olivia-02 + одежда + sun2-01-max-01 + одежда
    scene BG char Lisa Olivia sun2-01
    show Olivia sun2 01-02
    show Lisa sun2 01-02a        # бикини
    show Max sun2 01-01
    show cloth1 Max sun2 01-01    # плавки
    # with diss2
    Lisa_01 "Сейчас я намажу Оливии спинку, чтобы она была защищена от этого яркого солнышка."
    Olivia_04 "Ммм... Неплохо, Лиза... У тебя приятные ручки. Мне очень нравится."
    Max_07 "Решили надо мной поиздеваться, да? Переигрываете..."
    Lisa_03 "А вдруг нет! Может, я просто стараюсь и из-за этого Оливии так приятно..."
    Olivia_05 "А ты, Макс, хотя бы пока на пальму слазил за веткой и стоял ветерок ей создавал. Жарковато..."
    Max_01 "Я бы мог. Да вот только с моим приподнятым настроением лазить по пальмам будет неудобно. Да и поцарапаюсь наверняка."

    # sun2-02 + sun2-02-max-01 + одежда + sun2-02-lisa&olivia-02 + одежда
    scene BG char Lisa Olivia sun2-02
    show Max sun2 02-01
    show cloth1 Max sun2 02-01    # плавки
    show Olivia sun2 02-02
    show Lisa sun2 02-02a        # бикини
    # with diss2
    Lisa_01 "Моя очередь подставлять спинку! На меня плохо загар ложиться, может хоть этот крем как-то поможет."
    Olivia_02 "У меня и без загара красивая подружка! Но я всё равно хорошенько тебя намажу."
    Max_07 "Вы бы хоть разок попробовали для сравнения то, как это делаю я. Я сколько раз натирал Алису кремом и ей нравилось. А ты, Лиза, знаешь какая она привереда."
    Lisa_05 "Ну... Врать не буду, мне тоже нравится, как Макс делает мне массаж ног."
    Olivia_06 "А почему я узнаю об этом только сейчас??? Когда мы уже намазались..."
    Max_03 "Тогда решено. В следующий раз я натру вас кремом. Уверяю, недовольных не будет!"

    # 2sun-00 + 2sun-max-01 + 2sun-olivia-04a
    scene BG 2sun-00
    show Max sun-talk 04
    show Olivia sun-talk 04b
    # with diss2

    Olivia_03 "Ой, я буду очень ждать следующего раза. Только на пальму слазь заранее."
    menu:
        Max_04 "Вот мне делать больше нечего, только по пальмам лазать. Фух, жарко. Пойду я. Отдыхайте, девчонки."
        "{i}уйти{/i}":
            pass

    $ kol_cream -= 2
    if kol_cream < 3:
        call left_cream from _call_left_cream_6         ## солнцезащитный крем закончился
    elif kol_cream < 7:
        call left_cream(1) from _call_left_cream_7      ## осталось мало крема

    $ olivia.flags.incident = 3
    $ spent_time += 20
    jump Waiting


label olivia_third_sunscreen:
    $ olivia.daily.oiled = 1

    if kol_cream < 2:
        jump ran_out_sunscreen

    # sun2-01 + sun2-01-max-03 + sun2-01-olivia-01a
    scene BG sun-talk-01
    show Max sun-talk 03
    show Olivia sun-talk 01b
    # with dis2
    menu:
        Olivia_04 "Да, давай... На таком солнцепёке запросто можно сгореть. Чур гостей мажут первыми!"
        "{i}массировать Оливию кремом для загара{/i}":
            pass

    # sun2-01 + sun2-01-max&olivia-01 + одежда Макса
    scene BG char Lisa Olivia sun2-01
    show Olivia sun2 03-01
    show cloth1 Max sun2 03-01      # плавки
    # with diss2
    Olivia_08 "О да! А у тебя, Макс, руки сильнее, чем может показаться. Можно даже представить, что мы на пляже и с нами персональный массажист."
    Max_02 "Ну, если тебя это заводит..."
    Lisa_01 "О, ну всё... Точно тебе говорю, скоро Макс начнёт распускать руки."
    Olivia_07 "Ммм... Как раз то, что мне нужно... В смысле, лёгкий массаж, а не распускание рук. А то я вижу краем глаза, что ты, Макс, уже оживился."
    Max_04 "Это потому что у тебя такая нежная кожа... Я же не сильно давлю?"

    ###(sun2-01 + sun2-01-max&olivia-02 + одежда Макса) или ---------------- (04-02)
    # (sun2-02 + sun2-02-max&olivia-02 + одежда Макса)
    scene BG char Lisa Olivia sun2-02
    show Olivia sun2 03-02
    show cloth1 Max sun2 03-02      # плавки
    # with diss2
    Olivia_08 "Охх... Нет... Продолжай, у тебя прекрасно получается. Даже не хочется, чтобы это заканчивалось..."
    Max_01 "Это можно устроить."
    Lisa_09 "Эй, голубки, хватит там ворковать, вы здесь не одни! Не забудьте, что меня тоже нужно защитить от солнышка."
    menu:
        Max_03 "Не забыли. Уже иду!"
        "{i}массировать Лизу кремом для загара{/i}":
            pass

    # sun3-01 + sun3-01-max&lisa-01 + одежда Макса и Лизы
    scene BG char Lisa Olivia sun3-01
    show Lisa sun2 03-01
    show cloth1 Lisa sun2 03-01a   # бикини
    show Max sun2 03-01            # плавки
    # with diss2
    Olivia_05 "Спасибо тебе, Макс! Позаботься там о своей сестрёнке. А я теперь, с ещё большим наслаждением, буду загорать."
    Max_07 "Правильно, Оливия, отдыхай. Ухх... А ты, Лиза, горячая штучка!"
    Lisa_10 "Только давай без этих вот приставаний. Просто натри мне спинку кремом."
    Max_09 "Я вообще-то говорил о том, что у тебя кожа горячая от солнца. А ты всё озабоченный, озабоченный..."
    Olivia_02 "Мне кажется, Макс как раз таки ведёт себя профессионально, хотя мы и ожидаем от него подвоха. У меня к нему нареканий никаких, он молодец."
    Lisa_02 "Хм... И правда. Ммм... Хорошо-то как становится после его рук!"

    ###(sun3-01 + sun3-01-max&lisa-02 + одежда Макса и Лизы) или --------------- (04-02)
    # (sun3-02-max&lisa-02 + одежда Макса и Лизы)
    scene Lisa sun2 03-02
    show cloth1 Lisa sun2 03-02a   # бикини
    show Max sun2 03-02            # плавки
    # with diss2
    Max_07 "А я не знаю, почему вы меня сразу в штыки поставили."
    Olivia_03 "Тебе, Макс, надо становиться массажистом. Будет повод лапать девушек на законных основаниях. Хи-хи... Шутка! Хотя, под такой классный массаж я бы разрешила трогать что угодно."
    Max_05 "Да?! Всё, ловлю на слове!"
    Lisa_05 "Оливия, ты осторожнее такое говори, а то он возьмёт и убежит к тебе. Правда, я бы сказала, что он уже заканчивает..."
    Max_01 "Верно. Я мог бы пошутить про то, что у тебя ещё много чего можно намазать кремом, но не буду. Отдыхайте, девчонки."
    Lisa_01 "Спасибо, Макс."

    $ kol_cream -= 2
    if kol_cream < 3:
        call left_cream from _call_left_cream_8         ## солнцезащитный крем закончился
    elif kol_cream < 7:
        call left_cream(1) from _call_left_cream_9      ## осталось мало крема

    $ olivia.flags.incident = 4
    $ olivia.flags.m_back = 1
    $ lisa.flags.m_back = 1
    $ spent_time += 20
    jump Waiting


label olivia_repeat_sunscreen:
    $ olivia.daily.oiled = 1

    if kol_cream < 2:
        jump ran_out_sunscreen

    # "Давайте, я намажу вас кремом для загара?"

    # $ renpy.dynamic('pester', 'r0')

    $ pester = 0
    # sun2-01 + sun2-01-max-03 + sun2-01-olivia-01a
    scene BG sun-talk-01
    show Max sun-talk 03
    show Olivia sun-talk 01b
    Olivia_04 "Да, давай... На таком солнцепёке запросто можно сгореть."
    Lisa_01 "Только с лёгким массажем! Потому что просто намазаться кремом я и сама могу."
    menu:
        Max_04 "Значит, массаж всем! Но по очереди..."
        "{i}сначала делать массаж Оливии{/i}":
            jump .olivia_first

        "{i}сначала делать массаж Лизе{/i}":
            jump .lisa_first

    label .olivia_first:
        $ r0 = renpy.random.randint(0, 1)

        # sun2-01 + sun2-01-max&olivia-01 + одежда Макса
        scene BG char Lisa Olivia sun2-01
        show Olivia sun2 03-01
        show cloth1 Max sun2 03-01      # плавки
        Max_01 "Думаю, в этот раз я начну с Оливии, она всё таки наша гостья!"
        Lisa_09 "Ну вот... Младшая сестрёнка сразу перестала что-то значить..."
        Olivia_03 "Брось, Лиза, тебя твой любимый братик тоже намажет кремом с таким же усердием, как и меня. Правда, Макс?"
        Max_03 "Конечно намажу! Хорошо будет всем. Не переживайте."
        Lisa_10 "Тогда я жду... Главное, надолго не задерживайся."

        if r0:
            # (sun2-01 + sun2-01-max&olivia-02 + одежда Макса)
            # scene BG char Lisa Olivia sun2-01
            show Olivia sun2 04-01
            show cloth1 Max sun2 03-01     # плавки
        else:
            # (sun2-02 + sun2-02-max&olivia-02 + одежда Макса)
            scene BG char Lisa Olivia sun2-02
            show Olivia sun2 03-02
            show cloth1 Max sun2 03-02      # плавки
        Olivia_07 "У тебя просто бесподобно получается это делать, Макс... Я сейчас начну мурлыкать, как киска. Ммм..."
        Max_02 "Так сильно нравится?"
        Olivia_08 "О да! Ещё как. Если бы у меня был такой старший брат, я бы постоянно млела в его руках совершенно голенькая, как сейчас. Повезло тебе, Лиза..."
        Lisa_13 "Не сильно-то мне и везёт, я вот лежу тут совсем без внимания и вся горю... Макс, иди уже ко мне!"
        menu:
            Max_01 "Я почти закончил. Уже иду..."
            "{i}обнаглеть и массировать Оливии попку{/i}":
                if r0:
                    # (sun2-01 + sun2-01-max&olivia-03 + одежда Макса)
                    # scene BG char Lisa Olivia sun2-01
                    show Olivia sun2 04-03
                    show cloth1 Max sun2 03-01     # плавки
                else:
                    # (sun2-02 + sun2-02-max&olivia-03 + одежда Макса)
                    # scene BG char Lisa Olivia sun2-02
                    show Olivia sun2 03-03
                    show cloth1 Max sun2 03-02     # плавки
                Olivia_04 "Ой, Макс... Не то, чтобы я была против этого, но попку я и сама могу натереть кремом, а вот Лиза уже совсем тебя заждалась."
                Max_02 "Да мне не трудно."
                menu:
                    Lisa_12 "Эй! Я тут жду, вообще-то..."
                    "{i}делать массаж Лизе{/i}":
                        $ pester = 1
            "{i}делать массаж Лизе{/i}":
                pass

        # sun3-01 + sun3-01-max&lisa-01 + одежда Макса и Лизы
        scene BG char Lisa Olivia sun3-01
        show Lisa sun2 03-01
        show cloth1 Lisa sun2 03-01a   # бикини
        show Max sun2 03-01            # плавки
        Lisa_01 "Давай, Макс, начинай... Моя спинка полностью в твоём распоряжении!"
        Olivia_05 "Спасибо тебе, Макс! Позаботься там о своей сестрёнке. А я теперь, с ещё большим наслаждением, буду загорать."
        Max_02 "Массажист к вашим услугам."
        Lisa_05 "Ах, как классно! Под этими руками можно с лёгкостью растаять... Ммм..."
        Max_04 "Только растекайся после того, как я закончу."
        if r0:
            # (sun3-01 + sun3-01-max&lisa-02 + одежда Макса и Лизы)
            # scene BG char Lisa Olivia sun3-01
            show Lisa sun2 04-02
            show cloth1 Lisa sun2 04-02a            # бикини
            show Max sun2 03-01    # плавки

        else:
            # (sun3-02-max&lisa-02 + одежда Макса и Лизы)
            scene Lisa sun2 03-02
            show cloth1 Lisa sun2 03-02a   # бикини
            show Max sun2 03-02            # плавки

        Olivia_03 "Как я тебя понимаю, подруга! Лично мне очень нравится вот так вот отдыхать с вами."
        Lisa_02 "Если бы Макс при этом постоянно не пялился, куда не нужно, я бы смогла расслабиться на все сто."
        Max_02 "Тебе кажется."

        if pester:
            # Макс массировал попку Оливии
            menu:
                Lisa_01 "А ладно, будем считать, что убедил. Давай заканчивай. А то знаю я тебя, скоро начнёшь руки распускать, как с Оливией."
                "{i}закончить массаж{/i}":
                    pass
        else:
            # Макс не стал массировать попку Оливии
            menu:
                Lisa_01 "А ладно, будем считать, что убедил. Можно уже и заканчивать, Макс. Хоть мне и не хочется."
                "{i}развязать Лизе купальник{/i}":
                    if r0:
                        # (sun3-01 + sun3-01-max&lisa-03 + одежда Макса и Лизы)
                        # scene BG char Lisa Olivia sun3-01
                        show Lisa sun2 04-03
                        show cloth1 Lisa sun2 04-03a    # бикини
                        show Max sun2 03-01             # плавки
                    else:
                        # (sun3-02-max&lisa-03 + одежда Макса и Лизы)
                        scene Lisa sun2 03-03
                        show cloth1 Lisa sun2 03-03a    # бикини
                        show Max sun2 03-02             # плавки
                    Lisa_10 "Макс, ну ты что делаешь, а? Зачем без предупреждения мой купальник развязываешь?"
                    Max_03 "Надо было. Твои завязки мешались."
                    menu:
                        Lisa_09 "Да ну тебя! На грудь мою поглазеть захотел, как всегда... Всё, отстань!"
                        "{i}уйти{/i}":
                            pass
                "{i}закончить массаж{/i}":
                    pass

        jump .end

    label .lisa_first:
        $ r0 = renpy.random.randint(0, 1)

        # sun3-01 + sun3-01-max&lisa-01 + одежда Макса и Лизы
        scene BG char Lisa Olivia sun3-01
        show Lisa sun2 03-01
        show cloth1 Lisa sun2 03-01a   # бикини
        show Max sun2 03-01            # плавки
        Max_01 "Думаю, в этот раз я начну с Лизы. Мне хочется побыстрее защитить её белоснежную спинку от солнца!"
        Lisa_01 "Давай, Макс, начинай... Моя спинка полностью в твоём распоряжении!"
        Olivia_03 "Я совсем не против, тем более, что я всё равно буду следующей. Ты ведь намажешь меня кремом так же усердно, как и свою любимую сестрёнку, да, Макс?"
        Max_03 "Конечно намажу! Хорошо будет всем. Не переживайте."
        Olivia_06 "Ох, как жарко! Я жду не дождусь тебя, Макс..."
        if r0:
            # (sun3-01 + sun3-01-max&lisa-02 + одежда Макса и Лизы)
            # scene BG char Lisa Olivia sun3-01
            show Lisa sun2 04-02
            show cloth1 Lisa sun2 04-02a            # бикини
            show Max sun2 03-01    # плавки
        else:
            # (sun3-02-max&lisa-02 + одежда Макса и Лизы)
            scene Lisa sun2 03-02
            show cloth1 Lisa sun2 03-02a   # бикини
            show Max sun2 03-02            # плавки

        Lisa_05 "Ах, как классно! Под этими руками можно с лёгкостью растаять... Ммм..."
        Max_04 "Только растекайся после того, как я закончу."
        Lisa_02 "Если бы ты при этом постоянно не пялился, куда не нужно, я бы смогла расслабиться на все сто."
        Olivia_02 "Как я тебя понимаю, подруга! Макс, ты там скоро освободишься? Я уже не могу..."
        menu:
            Max_01 "Я почти закончил. Уже иду..."
            "{i}развязать Лизе купальник{/i}":
                if r0:
                    # (sun3-01 + sun3-01-max&lisa-03 + одежда Макса и Лизы)
                    # scene BG char Lisa Olivia sun3-01
                    show Lisa sun2 04-03
                    show cloth1 Lisa sun2 04-03a    # бикини
                    show Max sun2 03-01             # плавки
                else:
                    # (sun3-02-max&lisa-03 + одежда Макса и Лизы)
                    scene Lisa sun2 03-03
                    show cloth1 Lisa sun2 03-03a    # бикини
                    show Max sun2 03-02             # плавки
                Lisa_10 "Макс, ну ты что делаешь, а? Зачем без предупреждения мой купальник развязываешь?"
                Max_03 "Надо было. Твои завязки мешались."
                menu:
                    Lisa_09 "Да ну тебя! На грудь мою поглазеть захотел, как всегда... Всё, отстань!"
                    "{i}делать массаж Оливии{/i}":
                        $ pester = 1
            "{i}делать массаж Оливии{/i}":
                pass
        # sun2-01 + sun2-01-max&olivia-01 + одежда Макса
        scene BG char Lisa Olivia sun2-01
        show Olivia sun2 03-01
        show cloth1 Max sun2 03-01      # плавки
        Olivia_04 "Вот я и дождалась! Делай с моей спинкой всё, что потребуется... Лиза, тебе понравился массаж?"

        if pester:
            # Макс развязал купальник
            Lisa_01 "Если бы Макс мне купальник не развязал, было бы шикарно. Хотя, не буду обманывать, это было потрясно! Спасибо, Макс!"
        else:
            # Макс не развязывал купальник
            Lisa_05 "Это было потрясно! Теперь я могу загорать без всякой опаски сгореть на солнце... Спасибо, Макс!"

        Max_02 "Массажист к вашим услугам."
        Olivia_07 "У тебя просто бесподобно получается это делать, Макс... Я сейчас начну мурлыкать, как киска. Ммм..."
        Max_02 "Так сильно нравится?"
        if r0:
            # (sun2-01 + sun2-01-max&olivia-02 + одежда Макса)
            # scene BG char Lisa Olivia sun2-01
            show Olivia sun2 04-01
            show cloth1 Max sun2 03-01     # плавки
        else:
            # (sun2-02 + sun2-02-max&olivia-02 + одежда Макса)
            scene BG char Lisa Olivia sun2-02
            show Olivia sun2 03-02
            show cloth1 Max sun2 03-02      # плавки

        Olivia_08 "О да! Ещё как. Если бы у меня был такой старший брат, я бы постоянно млела в его руках совершенно голенькая, как сейчас. Повезло тебе, Лиза..."
        Lisa_02 "Когда везёт, когда нет... Макс у нас любит попроказничать. Хоть бы немножко меру знал."
        Max_03 "Зато не скучаем!"

        if pester:
            # Макс развязал купальник
            menu:
                Olivia_05 "И здорово, что не скучаете! Пора заканчивать, Макс. Руки у тебя приятные, но, как я увидела по Лизе, любят попроказничать, а мне сейчас так хорошо, что со мной можно делать всё, что захочется."
                "{i}закончить массаж{/i}":
                    pass

        else:
            # Макс не развязывал купальник
            menu:
                Olivia_05 "И здорово, что не скучаете! Пора заканчивать, Макс. Руки у тебя приятные, а мне сейчас так хорошо, что со мной можно делать всё, что захочется. Я себя знаю..."
                "{i}обнаглеть и массировать Оливии попку{/i}":
                    if r0:
                        # (sun2-01 + sun2-01-max&olivia-03 + одежда Макса)
                        # scene BG char Lisa Olivia sun2-01
                        show Olivia sun2 04-03
                        show cloth1 Max sun2 03-01     # плавки
                    else:
                        # (sun2-02 + sun2-02-max&olivia-03 + одежда Макса)
                        # scene BG char Lisa Olivia sun2-02
                        show Olivia sun2 03-03
                        show cloth1 Max sun2 03-02     # плавки
                    Olivia_04 "Ой, Макс... Не то, чтобы я была против этого, но попку я и сама могу натереть кремом."
                    Max_02 "Да мне не трудно."
                    menu:
                        Olivia_03 "Я заметила. Но хорошего - понемножку. А то при Лизе как-то неудобно."
                        "{i}уйти{/i}":
                            pass
                "{i}закончить массаж{/i}":
                    pass

        jump .end


    label .end:
    # на фоне загорающих Лизы и Оливии
    scene BG char Lisa Olivia 2sun-01
    $ renpy.show('Lisa 2sun '+pose3_1+lisa.dress)
    $ renpy.show('Olivia 2sun '+pose3_3+olivia.dress)
    Lisa_02 "Мы тебе очень благодарны, Макс. Прекрасный массаж!"
    Olivia_02 "Да, спасибо тебе!"
    menu:
        Max_01 "Не за что. Отдыхайте, девчонки."
        "{i}уйти{/i}":
            $ renpy.end_replay()

    $ kol_cream -= 2
    if kol_cream < 3:
        call left_cream from _call_left_cream_10         ## солнцезащитный крем закончился
    elif kol_cream < 7:
        call left_cream(1) from _call_left_cream_11      ## осталось мало крема

    $ olivia.flags.m_back += 1
    $ olivia.flags.handmass = 1     # теперь можно дать девушкам крем, чтобы они намазались сами
    $ lisa.flags.m_back += 1
    $ spent_time += 20
    jump Waiting


label olivia_give_sunscreen:
    $ olivia.daily.oiled = 1

    if kol_cream < 2:
        jump ran_out_sunscreen

    # "Вам дать крем для загара?"
    # $ renpy.dynamic('r0')

    # sun2-01 + sun2-01-max-03 + sun2-01-olivia-01a
    scene BG sun-talk-01
    show Max sun-talk 03
    show Olivia sun-talk 01b
    Olivia_03 "Да, давай... На таком солнцепёке запросто можно сгореть. А ты нас разве не намажешь?"
    Max_04 "Сегодня давайте сами... А то у меня руки устали. Им тоже нужно отдыхать."
    Lisa_02 "Если это и правда, то я не хочу знать, отчего они у тебя устали. Мы и сами прекрасно можем намазаться."
    Max_02 "Вот и хорошо. Но я за вами смотрю, а то начнёте тут натирать что-нибудь интересное..."

    # sun2-01 + sun2-01-lisa&olivia-(01/02 + одежда) + sun2-01-max-01 + одежда
    scene BG char Lisa Olivia sun2-01
    if r0:
        show Olivia sun2 01-01
        show Lisa sun2 01-01a        # бикини
    else:
        show Olivia sun2 01-02
        show Lisa sun2 01-02a        # бикини
    show Max sun2 01-01
    show cloth1 Max sun2 01-01    # плавки
    Lisa_01 "Мечтай, Макс! Сейчас я намажу Оливии спинку, чтобы она была защищена от этого яркого солнышка."
    Olivia_04 "Ммм... Неплохо, Лиза... У тебя приятные ручки. Мне очень нравится. Не такие сильные, как у твоего брата, но массируешь ты ими прекрасно."
    Max_07 "Решили надо мной поиздеваться, да? Переигрываете..."
    Lisa_03 "А вдруг нет! Может, я просто стараюсь и из-за этого Оливии так приятно..."
    Olivia_05 "Но я всё равно не отказалась бы от массажа в исполнении Макса. Остаётся надеяться, что в следующий раз он не откажется нас намазать."
    Max_03 "Очень может быть. Говоря по-правде, наблюдать за вами со стороны тоже классно."

    # sun2-02 + sun2-02-max-01 + одежда + sun2-02-lisa&olivia-(01/02 + одежда)
    scene BG char Lisa Olivia sun2-02
    show Max sun2 02-01
    show cloth1 Max sun2 02-01    # плавки
    if r0:
        show Olivia sun2 02-01
        show Lisa sun2 02-01a        # бикини
    else:
        show Olivia sun2 02-02
        show Lisa sun2 02-02a        # бикини

    Lisa_01 "Моя очередь подставлять спинку! На меня плохо загар ложиться, но благодаря этому крему я могу не переживать, что сгорю на солнце."
    Olivia_02 "У меня и без загара красивая подружка! Но я всё равно хорошенько тебя намажу."
    Max_05 "Без Лизиного купальника это было бы невероятное шоу! Я бы для этого даже научился двойному массажу. По руке на каждую."
    Lisa_05 "Какой у меня братик фантазёр. Как будто нам будет достаточно только одной твоей руки... Мы хотим, чтобы нас массировали двумя руками. Так ведь, Оливия?"
    Olivia_06 "Конечно! Если назвался массажистом, то будь добр, массируй, как надо. А то устал он..."
    menu:
        Max_01 "Тогда, до следующего раза, девчонки."
        "{i}уйти{/i}":
            pass

    $ kol_cream -= 2
    if kol_cream < 3:
        call left_cream from _call_left_cream_12         ## солнцезащитный крем закончился
    elif kol_cream < 7:
        call left_cream(1) from _call_left_cream_13      ## осталось мало крема

    $ olivia.flags.handmass = 0     # недоступно, пока Макс снова не намажет девушек
    $ spent_time += 20
    jump Waiting
