
label KiraTalkStart:

    $ dial = TalkMenuItems()

    $ __cur_plan = kira.get_plan()
    if __cur_plan.talklabel is not None:
        call expression __cur_plan.talklabel from _call_expression_10

    if len(dial) > 0:
        $ dial.append((_("{i}Ой, в другой раз...{/i}"), "exit"))
    else:
        jump Waiting

    $ renpy.block_rollback()

    if flags.eric_wallet == 2 and not kira.flags.talkblock:
        jump kira_about_wallet

    if kira.plan_name == 'sun':
        Max_01 "Не против компании?"
        Kira_01 "Конечно, Макс. Ложись рядом. Погода сегодня отличная!" nointeract
    else:
        Kira_00 "Макс, чем тебе помочь?" nointeract
    $ rez =  renpy.display_menu(dial)

    if rez != "exit":
        if renpy.has_label(talks[rez].label): # если такая метка сушествует, запускаем ее
            call expression talks[rez].label from _call_expression_11
        jump KiraTalkStart       # а затем возвращаемся в начало диалога, если в разговоре не указан переход на ожидание

    jump AfterWaiting            # если же выбрано "уйти", уходим в после ожидания


label kira_firsttalk:
    scene BG sun-talk-01
    show Kira sun-talk 01
    show Max sun-talk 01
    Kira_04 "Ну, рассказывай, Макс, как ты тут живёшь, как твои дела, что нового?"
    Max_04 "Ну, я... э..."
    menu:
        Kira_02 "Макс, тебя смущает мой купальник? Может быть, его снять?"
        "О... Да, было бы супер!":
            menu:
                Kira_07 "Ну да, не сомневаюсь. Только что же с тобой будет? Макс, ты можешь смотреть мне в глаза или мы тебя потеряли?"
                "Извини, тётя Кира...":
                    pass
                "Просто я отвлёкся...":
                    pass
        "Ну, я не знаю...":
            menu:
                Kira_07 "Зато знаю я. Макс, постарайся не пялиться на мою грудь. Глаза чуть выше. Ау. Ты здесь, Макс?"
                "Извини, тётя Кира...":
                    pass
                "Просто я отвлёкся...":
                    pass
    Kira_04 "Да, это моя ошибка, что выбрала такой купальник. Если честно, то я обычно загораю голая, но вы же дети и стоит хоть немного прикрываться. Вот я и прикрылась... Макс!"
    Max_07 "А? Что?"
    show Kira sun-talk 02
    show Max sun-talk 02
    menu:
        Kira_05 "Ого... Да я смотрю твой рост пошёл куда надо! И давно ты такое сокровище скрываешь?"
        "Ну, я не знаю...":
            pass
        "Да я и не скрываю...":
            pass
    ## открытие первого этапа возможности "Любимая тётя"
    menu:
        Kira_06 "Как я вижу, ты весь пошёл в отца... А кое в чём, так особенно..."
        "Что?!":
            Kira_03 "Ой, я конечно про твою... скромность. Да, речь о ней. И, вообще, давай сменим тему!"
        "Откуда ты знаешь, что...":
            Kira_03 "Что я знаю? Ничего. Я так, про твою скромность, конечно... И, вообще, давай сменим тему!"
    Max_08 "Тётя Кира!"
    Kira_04 "А что тётя Кира? Проехали. Кое-кому, так вообще нужно принять душ и расслабиться. Может быть, и мне тоже, но вдвоём не стоит это делать. Так что, иди ты в душ. И включи холодную воду. Очень холодную!"
    Max_00 "Хорошо..."
    scene BG shower-closer
    show Max shower 04
    show FG shower-closer
    menu:
        Max_03 "{m}Расслабиться в душе? Легко. Особенно, когда рядом такая женщина. Ух! Я и не задумывался о своей тёте в этом плане никогда, но сейчас что-то изменилось... или во мне или в ней...{/m}"
        "{i}снять напряжение{/i}":
            Max_20 "О, Кира... Ты когда-нибудь станешь моей..."
            show Max shower 05
            Max_07 "Я же не сказал это вслух?"
            $ mgg.cleanness = 100
    $ kira.dcv.feature.stage = 1   # 1
    $ kira.dcv.feature.set_lost(1)
    $ poss['aunt'].open(0)  # состоялся первый разговор с Кирой
    $ AttitudeChange('kira', 1)
    $ SetCamsGrow(house[6], 200)
    $ spent_time += 30
    jump Waiting


label kira_talk2:
    scene BG sun-talk-01
    show Kira sun-talk 01
    show Max sun-talk 01
    menu:
        Kira_02 "В прошлый раз наш разговор не очень удался. Ты был такой возбуждённый... Слушай, Макс, а у тебя есть девушка?"
        "Есть!":
            menu:
                Kira_04 "А она об этом знает?"
                "Конечно!":
                    Kira_08 "Ну ничего себе! А как её зовут?"
                    Max_00 "Это не важно..."
                    Kira_01 "Хорошо... Тогда последний вопрос. Ты её видишь прямо сейчас, она где-то здесь?"
                    Max_08 "Да я её не выдумал! Она не вымышленная девушка!"
                    Kira_07 "Ну тогда я рада за тебя, Макс. И давно вы встречаетесь?"
                    Max_10 "Ну... Э..."
                    Kira_02 "Я так и думала. Ладно, Макс, не стоит переживать по этому поводу. Ты же только приехал в новый город, из дома не выходишь. Ну скажи честно, нет же у тебя никакой девушки?"
                    Max_11 "Ну да, ты права, нет..."
                "Ты права, нет девушки...":
                    pass
        "Нет...":
            pass
    Kira_07 "Ну, это многое объясняет. Возраст такой, гормоны, кругом полуобнажённые девушки и женщины... Наверное, для тебя это каторга? Со мной можешь говорить откровенно. Я никому ни слова!"
    Max_00 "Да, бывает возбуждаюсь..."
    Kira_06 "Это потому-что пялишься не туда, куда надо. В глаза смотри..."
    Max_04 "Да, хорошо... Просто так само получается..."
    menu:
        Kira_05 "Понятно. Ну, старайся держать себя в руках. Хотя, я в прошлый раз видела как ты себя держал. Это не совсем то, что я имею в виду... Я слишком пошлая, да? Мне надо быть тактичнее?"
        "Что ты, всё хорошо!":
            pass
        "Меня всё устраивает...":
            pass
    Kira_04 "Я рада. Обычно я не слежу за языком и он меня в такие места заводит... Но с детьми надо быть... Хотя, ты уже совсем не ребёнок."
    Max_01 "Верно..."
    Kira_01 "Слушай, Макс, а как тебе удаётся с Лизой в одной комнате уживаться? Наверное, тяжело? Она же сверкает своим нижним бельём, да и спите вы рядом."
    Max_02 "Ну, был один инцидент..."
    Kira_02 "Утренний стояк, наверное? И как она отреагировала?"
    Max_03 "Панику подняла, маму позвала!"
    menu:
        Kira_08 "Представляю! Ну, я с Лизой тоже побеседую. Она уже не девочка... хотя, с вашей строгой мамой она о жизни толком ничего и не знает, похоже."
        "Верно...":
            pass
        "И что ты ей скажешь?":
            Kira_03 "Расскажу откуда дети берутся, конечно! Макс, ты такой смешной. У девочек свои секреты, тебе всё знать не обязательно..."
            Max_01 "Я понял..."
    ## У Макса опять стояк
    show Kira sun-talk 02
    show Max sun-talk 02
    menu:
        Kira_07 "Макс... Кажется, тебе снова пора в душ. Может быть, мне как-то прикрываться, когда ты рядом, раз у тебя все мысли где-то в другом месте?"
        "Не надо прикрываться!":
            Kira_04 "Ладно, не буду. Но в душ ты сходи. И обязательно холодный. А ещё лучше ледяной..."
            Max_00 "Хорошо..."
        "Пожалуй, я пойду...":
            pass
    scene BG shower-closer
    show Max shower 04
    show FG shower-closer
    menu:
        Max_20 "{m}Вроде тётя Кира специально и не провоцирует, но меня к ней так тянет. Её горячее, почти обнажённое тело так и манит...{/m}"
        "{i}кончить{/i}":
            show Max shower 05
            Max_07 "{m}О да... Надеюсь, она не поняла, что я сейчас делал, глядя на неё. Хотя, всё она понимает. Да ещё и видела меня, мой...{/m}"
            $ mgg.cleanness = 100
    $ kira.dcv.feature.stage = 2
    $ kira.dcv.feature.set_lost(1)
    $ SetCamsGrow(house[6], 200)
    $ spent_time += 30
    jump Waiting


label kira_talk3:
    scene BG sun-talk-01
    show Kira sun-talk 01
    show Max sun-talk 01
    menu:
        Kira_05 "Сегодня уже привык к моему купальнику? Или опять только одно на уме?"
        "Сегодня точно всё в порядке!":
            pass
        "Да я не могу ни о чём думать, когда тут такое...":
            Kira_07 "Понимаю, Макс. Ну, тут надо либо смириться, либо избегать. Как я вижу, избегать меня ты не планируешь и я очень рада этому. Но давай сменим тему, вдруг поможет..."
            Max_01 "Давай!"
    Kira_02 "Расскажи тогда, Макс, чем ты занимаешься, пока сидишь дома? Не может быть, чтобы только на порносайтах зависал?"
    Max_08 "У нас не работают порносайты..."
    Kira_14 "Ах вот в чём дело! А я никак не могла понять, что такое с моим телефоном. И почему? Мама установила такие порядки?"
    Max_09 "Ага, на стороне провайдера защита от детей..."
    Kira_04 "Тогда, Макс, я тебе ещё больше сочувствую и теперь лучше тебя понимаю... Может быть, сменим тему?"
    Max_01 "Хорошо..."
    menu:
        Kira_13 "Слушай, Макс, а тебе не показалось, что твоя мама какая-то... странная. Я имею в виду её одержимость Эриком..."
        "Вот именно!":
            menu:
                Kira_14 "Значит, не мне одной это показалось? Ну, в чём дело, рассказывай..."
                "{i}рассказать всю правду про Эрика{/i}":
                    pass
        "Нет, тебе показалось...":
            menu:
                Kira_02 "Макс, если что, я на твоей стороне. Мне можно доверять!"
                "Ну ладно... {i}Рассказать всю правду про Эрика{/i}":
                    pass
    menu:
        Kira_13 "Макс, ты наверное шутишь, да? Не может быть, чтобы всё было вот так. Твоя мама не слепая. Конечно, ею всегда было легко манипулировать, чем я и пользовалась, но такое..."
        "Чем ты пользовалась?":
            Kira_14 "Мне кажется, сейчас речь не об этом. Расскажу как-нибудь в другой раз... Значит, всё что ты рассказал про Эрика... это правда? Прямо не верится..."
            Max_07 "У меня нет причин врать!"
        "Я говорю правду!":
            pass
    Kira_03 "Думаю, мне нужно услышать другую сторону. Пообщаться или с Эриком или с твоей мамой, чтобы выяснить всё ли так. Может быть, ты немного... приукрасил..."
    Max_09 "И ничего я не приукрасил..."
    Kira_04 "Ладно, не дуйся. Давай сменим тему, а ещё лучше полежим на солнышке в тишине, если ты не возражаешь..."
    Max_01 "Ладно, не буду тебе мешать..."

    ## второй этап "Любимой тёти"
    $ kira.dcv.feature.stage = 3
    $ kira.dcv.feature.set_lost(1)
    $ poss['aunt'].open(1)  # состоялся третий разговор с Кирой
    $ SetCamsGrow(house[6], 200)
    $ spent_time += 30
    jump Waiting


label kira_about_kiss:
    $ renpy.block_rollback()
    menu:
        Kira_07 "Вот это подход, я понимаю! Может быть, тебе ещё что-то нужно?"
        "А можно что-то ещё?":
            Kira_08 "Шутник ты, Макс... Постой, ты нашёл себе девушку? Кто она? Откуда? Как познакомились? Как зовут? Хочу всё знать!"
        "Для начала только целоваться!":
            Kira_08 "Для начала? Забавно... Постой, ты нашёл себе девушку? Кто она? Откуда? Как познакомились? Как зовут? Хочу всё знать!"
    Max_01 "Как ты догадалась?"
    menu:
        Kira_05 "Ну зачем ещё парню может это понадобиться, сам подумай... И что, больше не у кого просить помощи, решил ко мне обратиться?"
        "Мама не помогла..." if 'ann' in flags.how_to_kiss:
            Kira_01 "Даже мама не помогла? Ого! Наверняка, сказала, чтобы ты учился целоваться со своей девушкой?"
            Max_07 "Точно!"
        "Да, Алиса меня отшила..." if 'alice' in flags.how_to_kiss:
            Kira_01 "Неудивительно. Наверняка, полез целоваться и получил... по... Хотя, нет. Видимо, тебе повезло. Ну я бы не стала к Алисе с такими вопросами обращаться на твоём месте..."
            Max_07 "Да, уже жалею..."
        "Ага...":
            pass
    menu:
        Kira_07 "Ну что же... Думаю, я смогу тебе помочь. Правда, не здесь и не сейчас. Нужна более интимная обстановка. О, я иногда вечерами смотрю телевизор. Если ещё не будешь спать, напомни о своей просьбе..."
        "Договорились!":
            pass
        "Хорошо! А ты только этому учишь?":
            Kira_04 "Макс, тебе стоит быть чуть более... тактичным. В общем, помочь я тебе помогу, только выбери подходящий момент. Договорились?"
            Max_01 "Конечно!"

    $ poss['seduction'].open(6)
    $ flags.how_to_kiss.append('kira')
    $ spent_time += 10
    return


label kira_talk4:
    # стартовая фраза "Ну как, ты с мамой-то поговорила?"
    scene BG sun-talk-01
    show Kira sun-talk 01
    show Max sun-talk 01

    Kira_10 "Вот так вот! Сразу к делу, да? Нет, Макс, мне не удалось поговорить. Но я обязательно во всём разберусь и выясню что происходит."
    Max_00 "Ну ладно. Значит, ты работаешь горничной?"
    Kira_01 "Ну... Типа того. А что, не похоже?"
    Max_09 "Да странно это всё..."

    menu:
        Kira_07 "Что же тебя смущает? Я разве не могу работать горничной?"
        "Кто же вечером порядок наводит?":
            Kira_04 "Может быть, они любят поспать и только вечером не бывают дома?"
            Max_07 "У тебя слишком нежные руки для такой работы..."

        "Ну, твои руки не как у горничной...":
            Kira_04 "Ну ты совсем как твоя мама. Всё подозреваешь... Может быть, я в перчатках работаю. Берегу кожу..."
            Max_07 "Кто же вечером порядок наводит?"

    menu:
        Kira_08 "Ну ладно, ладно, ты меня раскусил, Макс. Я не совсем горничной работаю..."
        "А кем тогда?":
            pass
        "И для чего тебе форма горничной?":
            pass

    Kira_14 "Если ты расскажешь своей маме, она меня убьёт и выгонит в гостиницу... В лучшем случае..."
    Max_01 "Рассказывай. Я никому ни слова!"
    Kira_13 "Точно? В общем, я снимаюсь в одном фильме... И форма горничной как раз из него. Я её случайно испачкала и брала постирать, а твоя мама заметила..."
    Max_07 "Так ты теперь актриса?!"
    menu:
        Kira_08 "Да, почти звезда! Но никому не говори. Это большой секрет от всех!"
        "Почему?":
            pass
        "И что это за фильм?":
            pass
    Kira_05 "Ну, это не совсем те фильмы, которыми хвастаются среди своей семьи, если ты меня понимаешь..."
    Max_08 "Это порно?!"
    Kira_03 "Макс, ну почему сразу порно? Может быть, это утончённая эротика с интересным сюжетом и отличной игрой актёров..."
    Max_07 "Значит, правда порно..."
    Kira_14 "Ну да. Нужно же девушке как-то зарабатывать. Правда, платят не очень, но продюсер обещал мне главную роль..."
    Max_09 "Они всем это обещают"
    Kira_07 "Думаешь, я этого не знаю? Ну а что делать. Там мне хотя бы стабильно платят, хоть и копейки..."

    show Kira sun-talk 02
    show Max sun-talk 02

    Max_03 "Моя тётя снимается в порно... С ума сойти!"
    Kira_08 "Макс, ты опять слишком перевозбудился. Боюсь представить что будет, если ты увидишь какой-нибудь фильм с моим участием! Хотя, мы и так с тобой почти как в порно развлекаемся..."
    Max_05 "А что, можно увидеть?"
    Kira_04 "Думаю, на сегодня с тебя и так хватит. Бегом в душ, а то меня начинает возбуждать твой стоящий член..."
    Max_02 "Не поможешь мне с этим?"
    Kira_06 "Макс! Что ты такое своей тёте предлагаешь! Как тебе не стыдно..."
    Max_09 "Ладно, смейся..."

    scene BG shower-closer
    show Max shower 04
    show FG shower-closer

    Max_04 "Моя тётя снимается в порно! О таком лучше вообще никому не рассказывать. Ох... вот это мне повезло!"

    show FG kira-voyeur-shower-00

    Max_19 "О да, Кира, мы с тобой ещё снимемся в фильме...  и даже не в одном. Я сделаю тебя звездой и не только!"
    Kira_05 "Что, прости? Я не расслышала последнее..."

    show Max shower 04a

    Max_08 "Но... Ты же была там... Я..."
    Kira_08 "Просто немного пофантазировал о своей тёте?"
    Max_09 "Ну а как ты думала? Приходится, раз уж ты не помогаешь..."
    menu:
        Kira_01 "Ладно, ты тут заканчивай, я позже в душ схожу. Не подскользнись!"
        "{i}закончить{/i}":
            show Max shower 05
            Max_07 "{m}Ну, тётя Кира, ты доприкалываешься! Я ещё реализую свои фантазии с тобой...{/m}"
            $ mgg.cleanness = 100
    $ kira.dcv.feature.stage = 4
    $ kira.dcv.feature.set_lost(1)
    $ poss['aunt'].open(7)  # четвёртый разговор во дворе
    $ SetCamsGrow(house[6], 200)
    $ spent_time += 30
    jump Waiting


label kira_talk5:
    # стартовая фраза "Как отдыхается, тётя Кира?"
    scene BG sun-talk-01
    show Kira sun-talk 01
    show Max sun-talk 01

    Kira_01 "Прекрасно, Макс! Я, кстати, ждала, что ты подойдёшь. Мне нужна помощь в одном деле и твой энтузиазм и энергия, могут мне помочь с этим."
    Max_01 "Что за дело? Я слушаю."

    Kira_03 "Ты как зарабатываешь? У тебя есть какой-то доход или живёшь на карманные деньги, которые тебе мама даёт?"
    Max_07 "Ну, типа того..."
    Kira_07 "Тогда, у меня есть для тебя деловое предложение. Правда, оно потребует вложений, но окупится, обещаю..."
    Max_09 "Ты о чём?"
    menu:
        Kira_05 "Мне нужно сделать серию снимков для портфолио. Я могла бы обратиться к профессиональному фотографу, но если у тебя найдётся фотоаппарат и будет желание, то я лучше заплачу тебе... Ты умеешь фотографировать?"
        "Конечно!" if 'sexbody1' in alice.gifts:  #если Макс фотографировал Алису
            Kira_04 "Да? И где же ты учился? А самое главное - когда? Или ты фотографии на телефон считаешь за опыт?"
            Max_02 "Так у нас же Алиса блог свой ведёт. Вот я ей и помогал с его развитием. Хорошие фотографии нужны всем!"
            Kira_03 "Так ты с телефона её фотографировал?"
            Max_05 "Нет. Я купил хороший и серьёзный фотоаппарат. Опыта у меня пока маловато, но я готов пощёлкать тебя бесплатно, если ты попозируешь..."
        "Нет..."if 'sexbody1' not in alice.gifts:   #если Макс не фотографировал Алису
            Kira_14 "Очень жаль... Без опыта, скорее всего, ничего толком не получится..."
            Max_07 "А если я попробую бесплатно, ты попозируешь?"
            Kira_02 "Конечно. Почему нет? Вдруг, у тебя хорошо получится? Кстати, а фотоаппарат у тебя есть?"
            if items['photocamera'].have or items['photocamera'].bought:
                # у Макса в инвентаре есть фотоаппарат или его ещё не доставили после покупки
                Max_05 "Ага. Вот прямо недавно купил!"
            else:
                # не открыт доступ к фотоаппарату или открыт, но он ещё не покупал его
                Max_08 "Найду!"
    Kira_08 "Ну, тогда не вижу причин для отказа такому целеустремлённому мальчику..."

    show Kira sun-talk 02
    show Max sun-talk 02

    Max_03 "Вот и договорились!"
    Kira_06 "Уж не знаю, Макс, что ты уже нафантазировал насчёт фотосессии, но тебе опять пора в душ. Что же ты такой впечатлительный? Эх..."
    Max_09 "Да, я пойду. И если не хочешь помочь мне с этим, то не мешай, как в тот раз!"

    scene BG shower-closer
    show Max shower 04
    show FG shower-closer

    Max_04 "{m}Вот это удача! Порно-звезда будет мне позировать для фотосессии! Я где-то слышал, что порно-модели всегда дают фотографам в качестве оплаты за съёмку... Или нет? Но вдруг!{/m}"
    if items['photocamera'].have or items['photocamera'].bought:
        Max_20 "{m}Фотоаппарат у меня уже есть. Дело за тем, чтобы повысить мои шансы на что-нибудь особенное от тёти Киры за эту фотосессию. Не прикупить ли для неё какую-нибудь очень сексуальную сорочку?! Я думаю, это будет совсем не лишним... О да...{/m}" nointeract
    else:
        Max_20 "{m}Дело за малым. Осталось где-то взять фотоаппарат и тогда... Да... Интересно, сколько они стоят? А, не важно. Важно, что мне может дать сама тётя Кира! Ради этого стоит попотеть... О да...{/m}" nointeract
    menu:
        "{i}закончить{/i}":
            show Max shower 05
            Max_09 "{m}Эх, с тётей Кирой кончать поинтереснее... и намного!{/m}"
    $ kira.dcv.feature.stage = 5
    $ kira.dcv.feature.set_lost(1)
    if items['photocamera'].have or items['photocamera'].bought:
        $ poss['aunt'].open(8)  # 5-разговор, уже есть фотоаппарат
        $ items['nightie2'].unblock()
    else:
        $ poss['aunt'].open(9)  # 5-й разговор, фотоаппарата ещё нет
        $ items['photocamera'].unblock()
    $ notify_list.append(_("В интернет-магазине доступен новый товар."))
    $ SetCamsGrow(house[6], 180)
    $ SetCamsGrow(house[3], 180)
    $ spent_time += 30
    jump Waiting


label kira_talk6:
    # стартовая фраза "Насчёт фотосессии..."
    scene BG sun-talk-01
    show Kira sun-talk 01
    show Max sun-talk 01
    menu:
        Kira_01 "Ты уже нашёл фотоаппарат?"
        "Ещё нет..." if not items['photocamera'].have:
            menu:
                Kira_02 "Понятно. Ну, как найдёшь, дай знать. Я почти всегда готова фотографироваться, но нужно, чтобы ещё и дома никого не было... А то нас не поймут..."
                "Всё понял...":
                    $ talks['kt6'].req = "all([kira.dcv.feature.done, kira.plan_name=='sun', kira.dcv.feature.stage==5, items['photocamera'].have, items['nightie2'].have])"
        "Ага, он у меня!" if all([items['photocamera'].have, items['nightie2'].have, weekday!=6]):
            Kira_04 "Очень хорошо. Теперь нужно выбрать подходящий момент, когда дома никого не будет..."
            Max_01 "Точно!"
            $ talks['kt6'].req = "all([kira.dcv.feature.done, kira.plan_name=='sun', kira.dcv.feature.stage==5, items['photocamera'].have, items['nightie2'].have, weekday==6])"
        "Ага, он у меня!" if all([items['photocamera'].have, items['nightie2'].have, weekday==6]):
            Kira_07 "Макс, ты просто чудо! Я всегда бодра, красива и свежа, чтобы фотографироваться. Нужно только решить где это будет..."
            Max_04 "Может быть, в гостиной?"
            jump first_photoset
    $ kira.dcv.feature.set_lost(1)
    $ spent_time += 10
    jump Waiting


label first_photoset:
    $ expected_photo = []

    play music photosession

    scene BG char Kira dresses
    show Kira dresses 01
    with fade4
    Kira_01 "А что, отличная идея! Только немного подожди, я переоденусь в свою ночнушку... Она ведь довольно сексуальная, как ты считаешь?"
    Max_02 "Да, но у меня есть идея получше."

    show Kira dresses 02
    Kira_05 "Сейчас угадаю, Макс, сниматься полностью голой! Верно?"
    menu:
        Max_04 "Не угадала! Я решил купить тебе сорочку, ещё сексуальнее твоей..."
        "{i}подарить сорочку{/i}":
            pass

    show Kira dresses 03
    Kira_08 "Ничего себе! Это очень приятно... И этот подарок, и то как ты подготовился к фотосессии. И как ты выпросил у своей мамы столько денег на это?"
    Max_03 "Да так, были кое-какие накопления... Для такой классной тёти не жалко!"

    show Kira dresses 04
    Kira_07 "Да, этот комплект отлично подойдёт для нашей съёмки. Ну что, как на мне смотрятся трусики? Вроде, в самый раз?"
    Max_05 "Ага. Мне очень нравится. То, что надо!"

    show Kira dresses 05
    Kira_06 "Ну вот, Макс, я полностью готова! Где начнём съёмку?"
    Max_01 "Давай начнём на диване. Ты ведь всё-таки в сорочке, так что покажем, как ты просыпаешься..."

    $ rl = create_random_list(3)
    menu (rand='all'):
        Kira_03 "О, мне эта идея нравится! Значит, я просто ложусь на диван или..."
        "Да, сделаем сперва снимок, будто ты только-только проснулась...":
            pass
        "Ты уже проснулась и решила красиво потянуться...":
            jump .set2

    # set1
    scene photoshot 01-Kira 01
    Kira_04 "Макс, постарайся меня держать в фокусе и следи, чтобы горизонт не был завален... Хотя, ты же всё умеешь, говоришь... В общем, я тебе доверяю."
    show FG photocamera
    play sound "<from 1>audio/PhotoshootSound.ogg"
    Max_04 "И правильно... Моя тётя очень красиво просыпается! {p=1.5}{nw}"
    hide FG
    extend "Готово! Теперь садись. Ты уже проснулась и решила красиво потянуться..."
    $ expected_photo.append('01')

    label .set2:
        scene photoshot 01-Kira 02
    Kira_02 "Как тебе такие потягушки, Макс? Смотрится хорошо?"
    show FG photocamera
    play sound "<from 1>audio/PhotoshootSound.ogg"
    Max_02 "То, что надо! Сейчас только ракурс подберу поинтереснее и будет отлично... {p=1.5}{nw}"
    hide FG
    extend "Ага, есть! А дальше мы щёлкнем твою попку..."
    $ expected_photo.append('02')

    $ rl = create_random_list(3)
    menu (rand='all'):
        Kira_01 "С радостью, Макс! Как мне для этого лучше устроиться?"
        "Просто повернись в сторону окна...":
            pass
        "Немного привстань, а остальное за мной...":
            jump .set4

    # set3
    scene photoshot 01-Kira 03
    Kira_05 "Вот так должно красиво смотреться... Или сесть по-другому?"
    show FG photocamera
    play sound "<from 1>audio/PhotoshootSound.ogg"
    Max_05 "Нет, тётя Кира, вид обалденный! Лучше некуда! Только фокус настрою сейчас... {p=1.5}{nw}"
    hide FG
    extend "Вот и готово. Давай, привстань немного, я сделаю ещё снимок..."
    $ expected_photo.append('03')

    label .set4:
        scene photoshot 01-Kira 04
    Kira_07 "Я вижу, тебе нравится? Ясно, почему ты в джинсах всё время ходил..."
    $ expected_photo.append('04')
    show FG photocamera
    play sound "<from 1>audio/PhotoshootSound.ogg"

    Max_01 "Не отвлекайся, тётя Кира! А то снимок выйдет так себе... {p=1.5}{nw}"
    hide FG
    extend "Отлично! Вот что делаем дальше..." nointeract

    $ rl = create_random_list(3)
    menu (rand='all'):
        "Сделаем снимок не для портфолио в монастырь...":
            pass
        "Переходим с дивана к столу...":
            jump .set6

    # set5
    scene photoshot 01-Kira 05
    Kira_09 "Ухх... Макс! Меня и саму очень заводит, когда я завожу фотографа..."
    show FG photocamera
    play sound "<from 1>audio/PhotoshootSound.ogg"
    Max_03 "Да... Покажем твою сочную попку во всей красе! {p=1.5}{nw}"
    hide FG
    extend "Снимок выйдет чумовой... Теперь переходим с дивана к столу."
    $ expected_photo.append('05')

    label .set6:
        scene photoshot 01-Kira 06
    Kira_04 "Если я обопрусь на стол вот так, слегка поддразнивая своей грудью, получится неплохой снимок, как мне кажется..."
    show FG photocamera
    play sound "<from 1>audio/PhotoshootSound.ogg"
    Max_02 "О да... Твоя грудь никого не оставит равнодушным. Она у тебя - огонь! {p=1.5}{nw}"
    hide FG
    extend "Снимок готов. Давай, покажи мне что-нибудь погорячее..."
    $ expected_photo.append('06')

    scene photoshot 01-Kira 07
    Kira_06 "Вроде такого? Думаю кадр получится отменный..."
    show FG photocamera
    play sound "<from 1>audio/PhotoshootSound.ogg"
    $ expected_photo.append('07')
    Max_04 "Ага, замри... Сейчас я тебя щёлкну. {p=1.5}{nw}"
    hide FG
    menu:
        extend "Да, кадр получился что надо!"
        "Хотя, я знаю, как он станет ещё лучше...":
            scene photoshot 01-Kira 08
    Kira_07 "Мне нужно, чтобы кадры были ещё лучше! Думаешь, мы можем ограничиться лишь снятием с меня трусиков?"
    show FG photocamera
    play sound "<from 1>audio/PhotoshootSound.ogg"
    Max_01 "Конечно нет, тётя Кира! {p=1.5}{nw}"
    hide FG
    extend "Это же все для портфолио, а в нём должны быть продемонстрированы твои умения..."
    $ expected_photo.append('08')

    scene photoshot 01-Kira 09
    Kira_09 "И я с удовольствием их продемонстрирую... Особенно, если при этом у тебя получатся очень хорошие кадры!"
    show FG photocamera
    play sound "<from 1>audio/PhotoshootSound.ogg"
    $ expected_photo.append('09')

    Max_02 "Я думаю, кадры будут отличные...{p=1.5}{nw}"
    hide FG
    extend "" nointeract

    $ rl = create_random_list(3)
    menu (rand='all'):
        "Пора показать класс, тётя Кира!":
            pass
        "Может, сразу удивишь меня чем-нибудь?":
            jump .set11

    # set10
    scene photoshot 01-Kira 10
    Kira_02 "Как скажешь, Макс. Держись... Это будет сумасшедший минет!"
    show FG photocamera
    play sound "<from 1>audio/PhotoshootSound.ogg"
    $ expected_photo.append('10')
    Max_20 "Охх... Полегче, тётя Кира! Ты так чувственно берёшь его в рот, что я могу забыть сделать снимки. {p=1.5}{nw}"
    hide FG
    extend "Да... продолжай вот так, это будет отличный кадр! Может тебе есть, чем меня удивить?"

    label .set11:
        scene photoshot 01-Kira 11
    Kira_05 "Как тебе такой пируэт, Макс? Уверена, ты будешь в восторге!"
    show FG photocamera
    play sound "<from 1>audio/PhotoshootSound.ogg"
    $ expected_photo.append('11')
    Max_22 "Вау! Такое нужно обязательно заснять... Ох, как же классно ты сосёшь... {p=1.5}{nw}"
    hide FG
    extend "Думаю, я уже почти готов для последнего снимка! Готовься, тётя Кира..."

    scene photoshot 01-Kira 12
    Kira_08 "Да, вот так, Макс! Не сдерживайся! Я должна выглядеть как самая настоящая порно-звезда..."
    show FG photocamera
    play sound "<from 1>audio/PhotoshootSound.ogg"
    $ expected_photo.append('12')
    Max_05 "И снято! {p=1.5}{nw}"
    hide FG
    extend "Да уж, это было потрясающе! Думаю, снимки получились удачными..."
    Kira_06 "Надеюсь... Я скину себе фотографии, мой знакомый их обработает и там уже посмотрим, что получилось. Спасибо тебе, Макс. Кстати, если что, ты же не откажешь ещё пощёлкать свою тётю?"
    Max_03 "Да, я бы пощёлкал..."
    Kira_01 "Вот и здорово, Макс! Ладно, пойду приведу себя в порядок и переоденусь, а тебе ещё раз спасибо..."
    Max_07 "{m}Это было намного лучше, чем ничего! Может, в следующий раз, мне обломится куда больше...{/m}"

    $ renpy.end_replay()
    stop music
    $ added_mem_var('kira_photoset1')
    $ SetCamsGrow(house[4], 300)
    $ poss['aunt'].open(10) # состоялас первая фотосессия
    $ items['photocamera'].block()
    $ items['nightie2'].give()
    $ kira.dcv.feature.stage = 6
    $ kira.dcv.feature.set_lost(1)
    if GetRelMax('kira')[0]<4:
        $ AttitudeChange('kira', 1) # Дружеские
    $ spent_time += 90
    $ current_room = house[0]
    jump Waiting


label kira_about_photo1:
    # стартовая фраза "Понравились фотографии?"
    scene BG sun-talk-01
    show Kira sun-talk 01
    show Max sun-talk 01

    Kira_04 "А, Макс. Я сразу отправила фотографии своему знакомому и он сказал, что его всё устраивает. Фокус отличный, ракурс тоже. Так что, я бы ещё раз обратилась к тебе за помощью, если ты не против..."
    Max_01 "В любое время, тётя Кира"
    Kira_14 "Да в том-то и дело, что не в любое. Нужно выбирать момент, когда никого нет дома. Потому-что, если нас заметят за таким, то точно это будет в последний раз, а мне бы не хотелось этого..."
    Max_04 "Да, мне тоже... А какой будет следующая фотосессия?"

    show Kira sun-talk 02
    show Max sun-talk 02

    Kira_05 "Какой ты любопытный, Макс. Вижу, твой дружок уже догадался, что эта фотосессия будет ещё более откровенной..."
    Max_05 "Ого! А куда ещё откровеннее?"
    Kira_07 "А вот... Думаю, тебе понравится. А сейчас кое-кому стоит принять холодный... очень холодный душ!"
    Max_02 "А ты поможешь мне с этим?"
    Kira_02 "Пожалуй, я ещё немного понежусь на солнышке. Ты и сам прекрасно справишься..."
    Max_09 "Эх, всё приходится делать самому..."

    scene BG shower-closer
    show Max shower 04
    show FG shower-closer

    Max_19 "{m}Интересно, когда будет следующая фотосессия? Мне уже не терпится перейти на новый уровень близости с тётей! Вот я бы её хорошенько...{/m}"

    show FG kira-voyeur-shower-00

    Kira_04 "Не паникуй, Макс, это всего лишь я. Стало как-то слишком жарко... Ты же ведь не против моей компании?"
    Max_03 "Конечно не против! Ещё спрашиваешь..."

    scene BG shower-closer
    show Kira shower-Max 01
    show FG shower-closer

    Kira_07 "Я подумала, фотосессия фотосессией, а за сорочку нужно поблагодарить отдельно. Как считаешь?"
    Max_05 "Не обязательно, но можно!"

    show Kira shower-Max 03

    Kira_10 "Ты хоть представляешь, Макс, что будет, если нас застукают за таким?"
    Max_07 "Боюсь даже представить... Может, сделать всё по-быстрому?"

    scene BG char Kira shower-04
    show Kira shower-Max 04
    show FG shower-water

    Kira_02 "Тогда нам лучше выключить воду... И смотри по сторонам, чтобы никто не увидел, что мы делаем! Мы ведь с тобой такими плохими делишками занимаемся..."
    Max_02 "Само собой, тётя Кира!"

    scene BG char Kira shower-05
    show AnimMaxKira1

    menu:
        Max_20 "{m}Ох, как она сосёт... Да так глубоко... Похоже, у неё чёрный пояс по минету! Блин, я уже на грани... Вот-вот и кончу... Отпадно!{/m}"
        "{i}кончить!{/i}":
            pass
    scene BG char Kira shower-06
    show Kira shower-Max 06
    Max_22 "{m}Да... Я кончаю... Прямо ей в рот! О боже... Она так смачно посасывает головку моего члена... Вот это отсос, так отсос! Кажется, это самое офигенное развлечение в мире!{/m}"

    show Kira shower-Max 07
    Kira_05 "Ну вот. Понравилось? И ни капли мимо..."
    Max_05 "Ты потрясающая! Это было классно!"
    Kira_01 "Я рада, что смогла снять твоё напряжение. А теперь, давай разбегаться, пока нас кто-нибудь не увидел..."
    Max_04 "Конечно!"
    $ renpy.end_replay()
    $ mgg.cleanness = 100
    $ SetCamsGrow(house[3], 250)
    $ poss['aunt'].open(11) # разговор о снимках
    $ kira.dcv.feature.stage = 7
    $ kira.dcv.feature.set_lost(1)
    $ kira.dcv.photo.stage = 1
    $ kira.dcv.photo.set_lost(10)
    $ spent_time += 30
    $ current_room = house[0]
    $ append_photo('01-Kira', 12)
    jump Waiting


label kira_about_cuni:
    # стартовая фраза "Не злишься на меня, тётя Кира?"
    Kira_13 "А что, Макс, должна?"
    Max_07 "Наверно, да. Я ведь не заглянул к тебе в душ..."
    menu:
        Kira_14 "Я не злюсь, но немного расстроена... А разозлюсь я, если ты просто забыл!"
        "Нет, я не забыл... были дела с Лизой.":
            pass
        "Нет, просто были дела с Алисой.":
            pass
    Kira_07 "Должно быть, это было что-то важное, раз ты решил не приходить к своей любимой, совершенно голой тёте в душ!"
    Max_09 "Сестрёнкам надо помогать! А твоё приглашение всё ещё в силе?"
    if weekday!=5:
        Kira_04 "Если только ночью, Макс. Когда я вернусь с работы. Если, конечно, ты не завалишься спать до моего возвращения..."
        $ kira.dcv.sweets.set_lost(1)
    else:
        Kira_04 "Если только завтра ночью, Макс. Когда я вернусь с работы. Если, конечно, ты не завалишься спать до моего возвращения..."
        $ kira.dcv.sweets.set_lost(2)
    Max_02 "Не завалюсь, буду тебя ждать!"
    Kira_01 "Посмотрим, Макс."
    $ spent_time += 10
    jump Waiting


label kira_about_photo2:
    # стартовая фраза "Так когда будем снова фотографироваться, тётя Кира?"   #доступна через 10 дней после первой фотосессии
    # #фоновая картинка с загорающей Кирой
    scene BG sun-talk-01
    show Kira sun-talk 01
    show Max sun-talk 01
    Kira_05 "Что, Макс, не терпится?"
    Max_01 "Ага! Я готов! А ты?"

    # #если не суббота или поздно по времени
    if not _in_replay and (weekday!=6 or not check_only_home('kira')):
        Kira_04 "У меня тоже всё готово, но как ты знаешь, сейчас мы дома не одни. Представь, что подумают, если увидят такое..."
        Max_07 "Э... Какое?"
        Kira_07 "А это сюрприз, Макс. Так что, наберись терпения..."
        Max_04 "Понял..."
        $ kira.dcv.feature.set_lost(1)
        $ kira.dcv.photo.set_lost(6-weekday)
        # $ talks['kt.ft2'].req = "all([weekday==6, kira.dcv.feature.stage==7, kira.plan_name=='sun', not expected_photo, kira.dcv.photo.stage==1, kira.dcv.photo.done, kira.dcv.feature.done])"
        $ spent_time += 10
        jump Waiting

    #если суббота и подходящее время
    menu:
        Kira_04 "Я тоже готова. На этот раз мне нужна особая фотосессия..."
        "Всё, что угодно!":
            Kira_07 "Что, даже не любопытно о чём речь? Ну что же, тогда пойдём в комнату твоей мамы..."
            Max_07 "Хорошо, а зачем?"
        "Особая? Это какая?":
            Kira_07 "Увидишь. Пойдём в комнату твоей мамы."
            Max_07 "Хорошо, а зачем?"
    Kira_02 "А вот и узнаешь. Пойдём быстрее, нужно всё успеть сделать, пока наши не вернулись с шопинга..."
    Max_01 "Ага..."

    # annroom-morning-01 + dresses-kira-04a
    scene BG annroom-morning-01
    show Kira dresses 04a
    with fade4
    Kira_06 "В общем, на этот раз мне нужна серия снимков в стиле... БДСМ... Верёвки, кожа, вот это всё..."
    Max_03 "Ого, круто!"
    Kira_07 "А чего ты так радуешься, я же не сказала кого привязывать будем..."
    Max_02 "А мне не важно! Но я - фотограф, так что привязывать будем однозначно тебя."
    Kira_05 "Да ты полон сюрпризов, Макс! Рада, что ты не против. Подожди минутку за дверью, я переоденусь..."
    Max_01 "Хорошо, жду."

    #на фоне двери в комнату Анны
    scene location house annroom door-day
    menu:
        Max_04 "{m}Верёвки и кожа - это интересно! А ещё интереснее то, насколько далеко мне удастся зайти с тётей Кирой во время этой фотосессии...{/m}"
        "{i}ждать{/i}":
            # annroom-morning-01 + dresses-kira-04
            scene BG annroom-morning-01
            show Kira dresses 2-04
            Kira_06 "Ну, как тебе мой наряд? Уже не жалеешь, что столько ждал у двери?"
            Max_05 "Что ты, тётя Кира... Выглядишь шикарно!"

        "{i}подсмотреть за Кирой{/i}":
            # dresses-annroom-01 + dresses-max-(01a/01b) + dresses-kira-01
            scene BG char Ann voyeur-01
            show Kira dresses 2-01
            show FG voyeur-morning-01c
            Max_03 "{m}Ого! Вот это не дурно тётя Кира принаряжается! Надеюсь, она меня не заметит, а то... Хотя, о чём это я?! Это же Кира! Её это даже заводит...{/m}"

            # dresses-annroom-01 + dresses-max-(01a/01b) + dresses-kira-02
            show Kira dresses 2-02
            Max_07 "{m}Ну прямо всё самое интересное закрывает этим кожаным костюмом... Хотя, мне же интереснее будет её раздевать! Мне кажется или она как-то слишком эротично одевается?{/m}"

            # dresses-annroom-01 + dresses-max-(01a/01b) + dresses-kira-03
            show Kira dresses 2-03
            Kira_06 "Думаешь, я не заметила, как ты подглядываешь? Именно этого я и ожидала от тебя. Ну, как тебе мой наряд?"
            Max_04 "Выглядишь шикарно!"

    $ expected_photo = []

    play music photosession

    # photoshot-02-kira-01 + photocamera
    scene photoshot 02-Kira 01
    Kira_04 "Вот и отлично. Тогда, начнём. Нужно сделать несколько кадров на фоне комнаты и кровати. Кстати, не говори маме, что я в сапогах тут топталась..."
    show FG photocamera
    play sound "<from 1>audio/PhotoshootSound.ogg"
    Max_01 "Я - могила! О да, так и задержись, как будто девушка с картины на стене не удержалась и решила прикоснуться к твоей груди! {p=1.5}{nw}"
    hide FG
    extend "Готово! Теперь приляг на кровать как-нибудь интересно..."   #открывает снимок 01
    $ expected_photo.append('01')

    # photoshot-02-kira-02 + photocamera
    scene photoshot 02-Kira 02
    Kira_05 "Так будет достаточно интересно? Или слишком скромно для первых кадров?"
    show FG photocamera
    play sound "<from 1>audio/PhotoshootSound.ogg"
    Max_02 "В самый раз! Сейчас только ракурс подберу поинтереснее и будет отлично... {p=1.5}{nw}"
    hide FG
    extend "Ага, есть! А дальше мы щёлкнем твою попку ещё раз, ложись как будто устала и отдыхаешь..."   #открывает снимок 02
    $ expected_photo.append('02')

    # photoshot-02-kira-03 + photocamera
    scene photoshot 02-Kira 03
    Kira_09 "После всех этих шнурков и застёжек мне не составит труда изобразить усталость... Но сил на всё, что будет дальше, у меня хватит."
    show FG photocamera
    play sound "<from 1>audio/PhotoshootSound.ogg"
    Max_03 "А это у тебя молния на трусиках, тётя Кира? {p=1.5}{nw}"
    hide FG
    extend "С радостью бы её расстегнул, но давай начнём с верху. Наверно, во всей этой коже очень жарко..."   #открывает снимок 03
    $ expected_photo.append('03')

    # photoshot-02-kira-04 + photocamera
    scene photoshot 02-Kira 04
    Kira_07 "Очень, Макс! Да и преступление, скрывать моих прелестных девочек от глаз. Не так ли?"
    show FG photocamera
    play sound "<from 1>audio/PhotoshootSound.ogg"
    Max_04 "О да... Твоя грудь никого не оставит равнодушным. Она у тебя - огонь! {p=1.5}{nw}"
    hide FG
    extend "Снимок готов. А что ты там говорила про верёвки?"   #открывает снимок 04
    $ expected_photo.append('04')

    menu:
        Kira_04 "Да, я думаю самое время привязать меня к кровати! Только так, чтобы выглядело натурально, хорошо?"
        "{i}крепко привязать Киру{/i}":   #открывает снимок 05
            # photoshot-02-kira-05 + photocamera
            scene photoshot 02-Kira 05
            show FG photocamera
            play sound "<from 1>audio/PhotoshootSound.ogg"
            $ expected_photo.append('05')
            menu:
                Kira_05 "Отлично, Макс. Теперь нам нужно сделать несколько снимков, на которых я должна выглядеть очень развратно! Я знаю, что тебе уже неймётся..."
                "{i}раздеться и поиграть с её грудью{/i}":   #открывает снимок 06
                    # photoshot-02-kira-06 + photocamera
                    $ expected_photo.append('06')
                    scene photoshot 02-Kira 06
                    Kira_13 "Какой же ты непослушный мальчишка! Такое со своей тётей вытворяешь... Быстро развяжи меня, а не то твоя мама всё узнает! Это я так, для придания соответствующего настроения..."
                    show FG photocamera
                    play sound "<from 1>audio/PhotoshootSound.ogg"
                    Max_01 "Я понял. И что же она узнает? Какая у неё бессовестная и развратная сестра? {p=1.5}{nw}"
                    hide FG
                    extend "И даже, как любишь сосать мой член расскажешь?"
                    menu:
                        Kira_10 "Думаешь, тебе удастся заткнуть мне рот?! Твоя мама узнает всё!"
                        "А вот это вряд ли! (дать ей пососать свой член)":   #открывает снимок 07
                            pass
                "{i}раздеться и дать ей пососать свой член{/i}":   #пропускает снимок 06, открывает снимок 07
                    pass

    $ expected_photo.append('07')
    # annroom-shot-02-max&kira-01-f + annroom-shot-02-max&kira-01
    scene BG char Kira annroom-shot 01
    show Kira annroom-shot 01
    play sound "<from 1>audio/PhotoshootSound.ogg"
    Max_02 "Да... Вот так... Эта фотосессия мне нравится гораздо больше... Я могу делать с тобой всё, что только придёт в голову! Хотя, сперва я бы сделал ещё один снимок..."   #открывает снимок 08

    # photoshot-02-kira-08 + photocamera
    $ expected_photo.append('08')
    scene photoshot 02-Kira 08
    show FG photocamera
    play sound "<from 1>audio/PhotoshootSound.ogg"
    Kira_03 "Ну Макс... Ты увидел, что хотел? Нравится? Ой, я хотела сказать, теперь застегни молнию и развязывай меня..."
    hide FG
    menu:
        Max_07 "Я бы с этим не торопился. А ещё, я бы лучше снял с тебя эти кожаные трусы. Так что отчасти твоё желание по развязыванию я осуществлю... Чтобы было удобнее..."
        "{i}ласкать её киску языком{/i}":
            # annroom-shot-02-max&kira-03-f + annroom-shot-02-max&kira-03
            scene BG char Kira annroom-shot 03
            show Kira annroom-shot 03
            menu:
                Kira_06 "Оу... Теперь понятно, что будет удобнее... ахх... делать... там. Да... Ты стал очень хорош в этом деле. Ох, обожаю, когда ты так делаешь!"
                "{i}активнее работать языком и губами{/i}":
                    pass
            # annroom-shot-02-max&kira-02-f + annroom-shot-02-max&kira-02
            scene BG char Kira annroom-shot 02
            show Kira annroom-shot 02
            menu:
                Kira_08 "Да... Так приятно... Ммм... Может, самое время, пустить в дело кое-что ещё..."
                "{i}проникнуть в её киску пальцами{/i}":
                    # annroom-shot-02-max&kira-03-f + annroom-shot-02-max&kira-03a
                    scene BG char Kira annroom-shot 03
                    show Kira annroom-shot 03a
                    menu:
                        Kira_09 "Макс... Если ты продолжишь так и дальше, я не смогу сдержаться... У тебя такие ловкие пальчики... Да..."
                        "{i}трахать её пальцами{/i}":
                            # annroom-shot-02-max&kira-02-f + annroom-shot-02-max&kira-02a
                            scene BG char Kira annroom-shot 02
                            show Kira annroom-shot 02a
                            menu:
                                Kira_10 "Да... Ещё чуть-чуть и я кончу... Может быть, ты воспользуешься чем-то потолще пальчиков и язычка? Только не торопись..."
                                "{i}тереться членом о её киску{/i}":
                                    # annroom-shot-02-max&kira-04-f + annroom-shot-02-max&kira-04
                                    scene BG char Kira annroom-shot 04
                                    show Kira annroom-shot 04
                                    Kira_07 "Ох, Макс... Должно быть тебе уже неймётся засадить мне! А уж как сильно я этого хочу..."   #уход на "медленно проникнуть в неё членом"
                                "{i}медленно проникнуть в неё членом{/i}":
                                    pass

                "{i}тереться членом о её киску{/i}":
                    # annroom-shot-02-max&kira-04-f + annroom-shot-02-max&kira-04
                    scene BG char Kira annroom-shot 04
                    show Kira annroom-shot 04
                    Kira_07 "Ох, Макс... Должно быть тебе уже неймётся засадить мне! А уж как сильно я этого хочу..."   #уход на "медленно проникнуть в неё членом"

        "{i}проникнуть в её киску пальцами{/i}":
            # annroom-shot-02-max&kira-03-f + annroom-shot-02-max&kira-03a
            scene BG char Kira annroom-shot 03
            show Kira annroom-shot 03a
            menu:
                Kira_06 "Оу... Теперь понятно, что будет удобнее... ахх... делать... там. Да... Ты стал очень хорош в этом деле. Ох, обожаю, когда ты так делаешь!"
                "{i}трахать её пальцами{/i}":
                    # annroom-shot-02-max&kira-02-f + annroom-shot-02-max&kira-02a
                    scene BG char Kira annroom-shot 02
                    show Kira annroom-shot 02a
                    menu:
                        Kira_08 "Да... Так приятно... Ммм... Может, самое время, пустить в дело кое-что ещё..."
                        "{i}тереться членом о её киску{/i}":
                            # annroom-shot-02-max&kira-04-f + annroom-shot-02-max&kira-04
                            scene BG char Kira annroom-shot 04
                            show Kira annroom-shot 04
                            Kira_07 "Ох, Макс... Должно быть тебе уже неймётся засадить мне! А уж как сильно я этого хочу..."   #уход на "медленно проникнуть в неё членом"
                        "{i}медленно проникнуть в неё членом{/i}":
                            pass

    # медленно проникнуть в неё членом
    # annroom-shot-02-max&kira-04a
    scene Kira annroom-shot 04a
    menu:
        Kira_09 "Ухх, давно во мне не было такого большого... Д-а-а... Вот так, Макс... Не спеши, дай мне привыкнуть к нему... А вот теперь, трахни свою тётю!"
        "{i}трахать тётю Киру{/i}":
            pass

    # annroom-shot-02-max&kira-05-f + annroom-shot-02-max&kira-05
    scene BG char Kira annroom-shot 05
    show Kira annroom-shot 05
    $ expected_photo.append('09')
    menu:
        Kira_11 "Ах, я правильно сделала, что решила подготовить тебя к первому разу! Ммм... Иначе, ты бы кончил почти сразу, как проник в меня... А я хотела, чтобы мой любимый племянник насладился сексом со мной! Ты ещё и фотографировать успеваешь! Ох..."   #открывает снимок 09
        "{i}ускориться{/i}":
            pass

    # annroom-shot-02-max&kira-05a
    scene Kira annroom-shot 05a
    menu:
        Kira_12 "Да, так... Глубже, быстрее! К чёрту нежности, оттрахай меня так, как тебе хочется! Да, вот так... Ох, я кончаю... Д-а-а..."
        "{i}кончить Кире на живот{/i}":
            pass

    # annroom-shot-02-max&kira-06
    scene Kira annroom-shot 06
    Kira_07 "Ну что, полегчало, Макс? Как тебе первый раз? Понравилось так, что трахал бы меня весь день?"

    # annroom-shot-02-max&kira-05-f + annroom-shot-02-max&kira-06a
    scene BG char Kira annroom-shot 05
    show Kira annroom-shot 06a
    Max_05 "Фух, было обалденно! Но я бы и ночью не перестал это делать!"
    menu:
        Kira_04 "Ну всё, развязывай меня, а то если кто-то придёт, будет... неловко, правда?"
        "{i}развязать Киру{/i}":
            stop music

    # annroom-watch-01 + annroom-watch-01-max-01d + annroom-watch-01-kira-01
    scene BG annroom-watch-01
    show Kira annroom-watch 01
    show Max annroom-watch 01d
    Kira_07 "Спасибо, Макс, что не оставил меня так до прихода твоей мамы с Эриком... Пришлось бы им многое объяснять, а мы же не хотим, чтобы наши секреты стали кому-то известны, правда?"
    Max_02 "Конечно, тётя Кира!"
    Kira_05 "Мы отлично повеселились. Только не вздумай ко мне приставать! На публике, разумеется. А теперь я пойду. Приятного дня, если он может быть ещё приятнее..."
    Max_01 "Спасибо за фотосессию!"

    $ renpy.end_replay()
    $ SetCamsGrow(house[2], 300)
    $ poss['aunt'].open(12) # вторая фотосессия
    $ kira.dcv.photo.stage = 2
    $ kira.dcv.feature.stage = 8
    $ kira.dcv.feature.set_lost(1)
    $ append_photo('02-Kira', 9)
    if GetRelMax('kira')[0]<5:
        $ AttitudeChange('kira', 1) # Близкие

    $ spent_time += 60
    jump Waiting


label kira_about_ann_secret0:
    # прошел 2-ой урок для Лизы от Анны и Эрика (дрочка и грудь)
    # стартова фраза:  "Тётя Кира, когда ты уже с мамой поговоришь?!"
    Kira_14 "Да я поговорила с ней, Макс... Просто... Даже не знаю, что тут можно сделать."
    Max_09 "Как что?! Нужно показать ей, что на самом деле Эрик за тип! Ну... я так думаю..."
    Kira_01 "Тут всё несколько сложнее, чем ты думаешь. А почему ты не допускаешь, что твоя мама могла просто влюбится в Эрика? Ты же знаешь, любовь слепа, и не такое бывает..."
    Max_07 "Она вытворяет вещи, которые до Эрика, никогда не стала бы делать! Ты видимо не знаешь, но Эрик напрямую мне заявил, что намерен трахать не только маму, но и моих сестёр!"
    Kira_13 "О как! Этого я действительно не знала... Тогда дела и правда плохи!"
    Max_10 "Вот-вот. И я понятия не имею, как повлиять на маму! Она вся в его власти. Ты как, хочешь жить в месте, где все делают то, что пожелает Эрик? А скоро так и будет, если ничего не делать..."
    Kira_13 "Ты прав, такого я не хочу... Я догадываюсь, что с твоей мамой. Но это наш с ней большой секрет, точнее, скорее её секрет. И я не уверена, что могу тебе рассказать..."
    Max_07 "О чём? Что за секрет?"
    Kira_14 "Поговори с мамой, спроси про случай из детства. Если она разрешит, я расскажу. А может быть, она и сама всё объяснит..."
    Max_08 "Ладно, попробую."

    $ ann.dcv.feature.stage = 1
    $ poss['aunt'].open(13) # разговор с Кирой после второго урока Лизы у АиЭ
    $ spent_time += 10
    jump Waiting


label kira_about_ann_secret2:
    # стартова фраза:  "Я хотел спросить про тот случай из детства мамы..."
    menu:
        Kira_14 "Я так понимаю, мама тебе ничего не рассказала? Значит, не хочет, чтобы кто-то об этом что-то знал. А значит, я ничем не могу тебе помочь..."
        "Но это моя семья!" ('soc', mgg.social, 90):
            if rand_result:
                jump story_from_childhood_ann
            else:
                jump .failed

    label .failed:
        Kira_13 "Я это понимаю, Макс. Но это также и частная жизнь и история твоей мамы... Я не могу в обход её запрета раскрыть эту тайну."
        Max_11 "Ясно..."
        $ ann.dcv.feature.set_lost(1)

    $ spent_time += 10
    $ ann.dcv.feature.stage = 3
    jump Waiting


label kira_about_ann_secret_r:
    # стартова фраза:  "Расскажи уже про тот случай из детства мамы..."
    menu:
        Kira_14 "Макс, твоя мама не хочет, чтобы кто-то об этом что-то знал. На это есть причины и я не хочу подорвать её, и без того довольно шаткое, доверие ко мне..."
        "Я же не из корыстных целей это спрашиваю! Я хочу ей помочь..." ('soc', mgg.social, 90):
            if rand_result:
                jump story_from_childhood_ann
            else:
                jump kira_about_ann_secret2.failed


label story_from_childhood_ann:
    Kira_03 "[succes!t]Ты же не отстанешь, да? Только учти, если ты обмолвишься об этом кому-то или самой маме, то к обоим доверие пропадёт."
    Max_07 "Конечно. Рассказывай!"

    Kira_01 "Наверняка, тебе мама рассказывала, что когда мы были совсем юные, мы жили в небольшом городке, где все друг друга знали. Двери не закрывали, не было никакой сигнализации, ничего."
    Max_09 "Что-то такое было..."
    Kira_14 "В общем, жили в безопасности и можно сказать полной идиллии... Пока не произошёл тот случай, о котором твоя мама и не хочет вспоминать..."
    Max_08 "Слушаю внимательно..."
    Kira_13 "Однажды вечером мы с Аней заигрались на опушке леса и когда собрались возвращаться домой, к нам подошёл мужчина. Предложил нам воду... А нас как раз мучила жажда..."
    Max_10 "Звучит как не очень хорошее начало..."
    Kira_15 "Так и было. Очнулись мы в каком-то подвале, обе связанные. Представляешь, как мы перепугались! Я до сих пор иногда просыпаюсь в холодном поту..."
    Max_08 "Ого! И что дальше?"
    menu:
        Kira_13 "Мне повезло, верёвки на руках не слишком туго были затянуты и я смогла освободиться. Затем, попыталась развязать сестрёнку, но услышала шаги и вылезла в малюсенькое окошко под потолком..."
        "Так ты бросила там мою маму?":
            Kira_03 "Макс, ну я была маленькая, напуганная, а там кто-то шёл. Я подумала, что лучше позову кого-то взрослого на помощь, чем полезу обратно и меня снова поймают..."
        "Тебе удалось сбежать?":
            Kira_03 "Конечно, я была жутко напугана и хотела помочь маме, но этот человек начал меня искать и я побежала. Думала, что позову кого-то на помощь, потому-что мне самой с ним не справиться..."
    Max_07 "Да, это разумно..."

    Kira_15 "Конечно, я жутко испугалась и до сих пор себя виню, что оставила Аню там одну с этим мужиком..."
    Max_08 "Ты позвала на помощь и всё было хорошо?"
    Kira_13 "Ну, не совсем. Я долго бродила по лесу одна, в темноте... Потом уснула и только к вечеру следующего дня я вышла на дорогу, где меня подобрали добрые люди."
    Max_07 "Повезло... А что потом?"
    Kira_14 "Потом начались поиски. Аню вышли искать почти все жители нашего городка. Многие её знали и любили... Поиски продолжались почти неделю..."
    Max_12 "Неделю?! Это же где-то недалеко от дома было?"
    Kira_03 "К лесу мы ездили на велосипедах, так что, не очень близко. Да и ночью от того домика я не помню в какую сторону бежала... В общем, мы его нашли..."
    Max_09 "Того похитителя?"
    Kira_14 "Нет, тот домик, где её держали. К счастью, она была жива! Правда, ещё месяц от неё не могли услышать ни слова..."
    Max_07 "Но потом она заговорила?"
    Kira_15 "Да, но про тот случай она отказалась тогда говорить, да и сейчас, как видишь, не хочет. Никто не знает что там было, но её держали там больше недели. Скорее всего, издевались..."
    Max_14 "Это ужасно..."
    Kira_13 "Не то слово. С тех пор твоя мама сильно изменилась. Хотя она и смогла вернуться к обычной жизни, что происходит не часто, но её как будто надломили..."
    Max_09 "В каком смысле?"
    Kira_01 "До того инцидента Аня была своевольной, капризной, непослушной. Она всеми командовала и не подчинялась никому, кроме нашей мамы, твоей бабушки..."
    Max_07 "А после?"
    Kira_03 "А после... Мы как-то поссорились и я потребовала, чтобы она сделала за меня домашнюю работу... И представляешь, она покорно согласилась! Я тогда подумала, что просто повезло."
    Max_08 "Видимо, не в этом дело?"
    Kira_07 "Да. Однажды, когда нашей мамы не было дома, мы позвали друзей на ночь, устроили вечеринку. Хорошо отдохнули, но на утро весь дом был вверх дном. И я потребовала, чтобы Аня прибралась сама..."
    Max_07 "Видимо, она и прибралась?"
    Kira_13 "Именно! Представляешь? Вот тогда я и поняла, что она стала с радостью подчиняться чужой воле. Так что, я думаю, что тут дело не в вашем Эрике, а скорее, в самой маме..."
    Max_15 "Но Эрик всё равно козёл!"
    Kira_04 "А кто спорит? Одно другому не мешает. Зато теперь ты лучше понимаешь, что творится в голове твоей мамы. Ну или как минимум, что-то разъяснилось, верно?"
    Max_07 "Значит, если я прикажу что-то, то она сделает?"
    Kira_05 "Что-то я сомневаюсь. Вы же её дети и к вам у неё особое отношение. К тому же, тут сильнее всего материнский инстинкт... Постой, а что ты собрался ей приказывать?"
    Max_08 "Нет, нет, ничего..."
    Kira_07 "Понятно. Ну, хотела бы я посмотреть, как ты пытаешься. Спорим, по заднице получишь в тот же момент!"
    Max_10 "Да, есть такое подозрение..."
    Kira_02 "Кстати, нас в детстве тоже пороли. Правда, до гола не заставляли раздеваться. Видимо, это уже инициатива Эрика. Кстати, забавная инициатива и вроде даже эффективная..."
    Max_11 "И это очень унизительно!"
    Kira_05 "Да, да, я именно так и сказала. Ну это уже не моё дело. Как вас воспитывать будет решать ваша мама. Я могу лишь понаблюдать со стороны на всё это..."
    Max_07 "Ясно. Ну, спасибо за историю. Теперь многое стало понятно!"
    Kira_01 "Да не за что. Мне кажется, что ты должен знать. Может быть, теперь ты сможешь лучше понять свою маму и не будешь так резко реагировать на эти её отклонения..."
    Max_09 "Это да, но с Эриком всё равно нужно что-то делать. Иначе он тут не на шутку разгуляется!"
    Kira_13 "В глазах твоей мамы, Эрик сейчас - идеальный мужчина! Сомневаюсь, что ты можешь что-то сделать, чтобы это изменить."
    Max_07 "Как думаешь, а если все будут против Эрика, то она начнёт в нём сомневаться?"
    Kira_02 "Хороший вопрос, Макс. Может быть. А может и нет. Такое не предугадаешь... Но хорошо бы, чтобы она при этом достаточно доверительно относилась к тебе."
    Max_10 "Вот только она столько времени проводит на работе и с Эриком..."
    Kira_07 "Она у нас ранняя пташка, так что придётся и тебе рано вставать, чтобы проводить с ней больше времени. Помоги ей с чем-нибудь..."
    Max_01 "Да, спасибо за совет!"
    Max_09 "{m}Если я буду проводить с мамой больше времени вместе, то может быть и от Эрика будет не так уж сложно избавиться?! Главное, не вести себя по идиотски с ней... И про сестёр нужно не забывать, хорошие и доверительные отношения с ними тоже могут мне помочь. По-моему, мама каждое утро занимается йогой. Пожалуй, стоит попробовать составить ей компанию...{/m}"

    $ ann.dcv.feature.stage = 4
    $ kira.dcv.feature.set_lost(3)
    $ poss['aunt'].open(15) # уговорил Киру рассказать секрет из детства Анны
    $ spent_time += 40
    jump Waiting


label kira_about_photo3_0:

    Kira_05 "Что, Макс, так не терпится увидеть свою тётю в непристойном виде?"
    Max_02 "Ну почему сразу в непристойном? В крайне непристойном! И желательно поскорее..."
    if kira.flags.held_out > 2:
        # Макс уже набрал 3 максимальных минета после секса с Кирой
        Kira_07 "Боже, какой же ты нетерпеливый, Макс! Терпение, мой мальчик... Думаю, мы с тобой по ночам уже достаточно порезвились, чтобы ты смог продержаться на этой фотосессии до самого конца."
        Max_07 "Интересно, что ты за фотосессию такую придумала?"
        Kira_01 "Всему своё время, Макс! Пока это секрет. Мне нужно сперва кое с кем кое о чём договориться и после этого я тебе сразу скажу, когда мы устроим фотосессию."
        Max_01 "Конечно. Жду не дождусь!"

        $ poss['aunt'].open(16) # 16 / 17   # разговор о третьей фотосессии, Макс готов
    else:
        # Макс ещё не набрал 3 максимальных минета после секса с Кирой
        Kira_07 "Боже, какой же ты нетерпеливый, Макс! Терпение, мой мальчик... Дело в том, что я не совсем уверена, что ты с ней справишься..."
        Max_09 "Это почему? Мы ведь уже устраивали две фотосессии и всё прошло нормально. Ты же сама говорила!"
        Kira_03 "Да, но... в этот раз она будет такая... Я не уверена, что ты со своей юношеской гипервозбудимостью сможешь довести её до конца."
        Max_07 "Что? Да я... Что ты за фотосессию такую придумала?"
        Kira_01 "Всему своё время, Макс! Всему своё время... Пока это секрет. А чтобы я была уверена, что ты выдержишь эту фотосессию, ты приходи ко мне по ночам. Мы подтянем твою сексуальную выдержку!"
        Max_10 "А что, я разве ещё не крут?"
        Kira_04 "Н-у-у... Ещё нет. Как говорится, нет предела совершенству. И твоя любимая тётя всегда поможет..."
        Max_05 "Круто! Я буду стараться..."
        Kira_07 "Ммм... Я люблю, когда мужчины стараются... Ну а как только я пойму, что ты справишься с нашей будущей фотосессией, я тебе сразу скажу."
        Max_01 "Хорошо..."
        $ poss['aunt'].open(17) # разговор о третьей фотосессии, Макс ещё не готов

    $ kira.dcv.feature.stage = 9
    $ kira.dcv.feature.set_lost(3)
    $ spent_time += 20
    jump Waiting


label kira_about_photo3_1:

    # after-breakfast + max&lisa-(02a/02b) + after-breakfast-kira-01
    scene BG after-breakfast
    $ renpy.show('Max talk-terrace 03'+mgg.dress)
    show Kira talk-terrace 01

    Kira_03 "Эй, Макс, у меня есть новости о нашей будущей фотосессии... Только тихо! Тссс..."
    Max_05 "Наконец-то! Хорошие новости?"
    Kira_07 "Тише-тише... Не так же громко! Похвально, что ты относишься к этому с таким энтузиазмом, но эти новости всем остальным слышать не стоит."
    Max_04 "Ага... Я понимаю. Просто я так ждал..."
    Kira_05 "Ну, конечно... Представляю, что ты себе нафантазировал, бесстыдник..."
    Max_03 "Так когда? Скоро?"

    # after-breakfast + max&lisa-(01a/01b) + after-breakfast-kira-02
    $ renpy.show('Max talk-terrace 02'+mgg.dress)
    show Kira talk-terrace 02

    if weekday == 6:
        #если сб
        Kira_01 "В следующую субботу, когда все отправятся на шопинг."
        $ kira.dcv.photo.set_lost(7)
    else:
        #если вс-пт
        Kira_01 "Вот как все отправятся на шопинг, так сразу и мы за дело возьмёмся."
        $ kira.dcv.photo.set_lost(6-weekday)

    Max_02 "Ох, я тебя и нащёлкаю! И так, и этак..."
    Kira_04 "Мне придётся спустить тебя с небес на землю - фотографировать ты не будешь. И нам даже не понадобится в этот раз твой фотоаппарат."
    Max_08 "Подожди... Я что, не буду участвовать в фотосессии?"
    Kira_06 "Нет, не переживай. Ты будешь там очень нужен."
    Max_09 "Хм... Тогда я ничего не понимаю..."
    Kira_07 "Дождись субботы, Макс, и тогда ты всё поймёшь. Уверяю тебя, ты не разочаруешься. А пока это секрет."
    Max_07 "Да? Ну ладно, буду ждать..."

    $ kira.dcv.feature.stage = 10
    $ kira.dcv.feature.set_lost(1)
    $ spent_time += 10
    jump Waiting


label kira_photoset3:
    #автозапуск в субботу, после того, как все уезжают на шопинг, если соблюдены все условия
    # $ renpy.dynamic('ch')

    if not _in_replay:
        scene BG black with fade4

    #фоновая картинка с загорающей Кирой
    call kira_sun from _call_kira_sun

    Kira_05 "Что, Макс? Наверняка не терпится начать фотосессию?"
    Max_01 "Ага! Я готов! А ты?"
    Kira_04 "Я тоже готова. Вернее, почти готова. Только нужно переодеться. Пойдём наверх, в комнату твоей мамы..."
    Max_07 "Снова там? А что будет-то?"
    Kira_06 "Если в двух словах, то эта фотосессия, как и предыдущая, будет в лёгком БДСМ стиле... Верёвки, кожа и так далее..."
    Max_09 "Ага. А отличаться она чем будет от прошлой фотосессии? Ты говорила, что фотографировать буду не я... А кто тогда?"
    Kira_01 "Одна моя подруга."
    Max_08 "И что, она фотографирует лучше меня?"
    Kira_03 "Может быть... Но дело не в этом, а в том, что тебе будет не до фотографирования. Так, мы что-то разговорились тут, пойдём уже скорее наверх!"
    Max_01 "Ага..."

    # annroom-morning-01 + dresses-kira-04a
    scene BG annroom-morning-01
    show Kira dresses 04a
    with fade4
    Kira_05 "Немного подожди за дверью. Я подготовлюсь. Только не подглядывай, Макс! Хорошо?"
    Max_11 "Ладно, не буду."

    # momroomdoor-01-morning
    scene location house annroom door-morning
    Max_09 "А когда твоя подруга придёт, тётя Кира?"
    Kira "{b}Кира:{/b} Скоро, Макс... Не переживай."
    Max_02 "{m}Интересно, а эта подружка... она такая же распутница, как моя тётя?{/m}"
    Max_07 "Тётя Кира, а вы с ней просто подруги или... не только..."
    Kira "{b}Кира:{/b} О, Макс, мы с ней любим пошалить... и ещё как!"
    Max_03 "Супер! Я бы на это с удовольствием посмотрел..."

    # annroom-morning-01 + dresses-kira-05
    scene BG annroom-morning-01
    show Kira dresses 3-05
    Kira_07 "Да что ты... А то, что ты видишь сейчас разве не приносит тебе удовольствия?!"
    Max_05 "Вау! Ты такая сексуальная в этом прикиде! Вся такая в сеточку... И блестишь!"
    Kira_02 "Да, Макс, я такая! Не жалеешь, что не подглядывал и столько прождал у двери?"
    Max_04 "Нисколько, тётя Кира... Ты просто обалденно выглядишь!"
    menu:
        Kira_05 "Заходи в комнату. Теперь нужно подготовить к фотосессии тебя..."
        "{i}зайти в комнату{/i}":
            pass

    play music photosession

    # annroom-watch-02-bj03 + annroom-watch-01-max-(02a/02b) + annroom-watch-01-kira-02
    scene BG annroom-watch-02-bj03
    $ renpy.show('Max annroom-watch 02'+mgg.dress)
    show Kira annroom-watch 02
    Kira_07 "Итак, Макс, поскольку фотографировать сегодня будешь не ты, твоё место именно здесь - быть привязанным на этой кровати."
    Max_08 "Что? Я буду... А ты?"
    Kira_04 "Не бойся, я тоже здесь буду. Раздевайся и ложись. Сегодня развлечёмся от души."
    Max_07 "Ну ладно, если так, то я готов..."

    # annroom-shot-02-max&kira-03-f + annroom-shot-03-kira&max-02
    scene BG char Kira annroom-shot 03
    show Kira annroom-shot 03 02
    Kira_05 "Ну что, нигде не жмёт? Я надеюсь, ты не возражаешь, что я тебя привязала..."
    Max_03 "Самое время спрашивать такое, тётя Кира, но с такой красоткой, как ты, я на всё согласен!"
    Kira_06 "Запомни эту красотку как следует, потому что больше ты ничего не увидишь."
    Max_10 "В смысле, не увижу?"

    # annroom-watch-04-bj01 + annroom-shot-03-kira&max-04
    scene BG annroom-watch-04-bj01
    show Kira annroom-shot 03 04
    Kira_02 "В том самом, мой мальчик! Вся фотосессия пройдёт для тебя в этой повязке. Это чтобы ты не застеснялся фотосъёмки..."
    Max_09 "Нуу... С повязкой на глазах, конечно, хуже, но... я готов потерпеть."
    Kira_07 "Потерпеть?! Неужели тебе не нравится, что твоя тётя села на тебя и чувствует, как в её попку упирается большой член?"
    Max_02 "Нравится, и даже очень, просто видеть это было бы ещё лучше!"
    Kira_05 "Уверяю тебя, Макс, ты ни о чём не пожалеешь, к тому же, подобная интрига только усиливает возбуждение. С завязанными глазами все чувства обостряются."
    Max_04 "Это да..."

    # annroom-shot-03-01 + annroom-shot-03-caprice-03 + annroom-shot-03-kira-03
    scene BG char Kira annroom-shot 03 01
    show Caprice annroom-shot 03 03
    show Kira annroom-shot 03 03
    Kira_01 "О... А вот и наш фотограф... Как раз вовремя. Проходи, чувствуй себя как дома..."
    Max_01 "Привет! Я, Макс. А вы?"

    # annroom-watch-04-bj01 + annroom-shot-03-kira&max-04 + annroom-shot-03-caprice-04
    scene BG annroom-watch-04-bj01
    show Kira annroom-shot 03 04
    show Caprice annroom-shot 03 04
    Kira_02 "Макс, ты не отвлекайся и представь, что здесь только мы с тобой вдвоём. А на то, что нас там кто-то снимает - не обращай внимания."
    Max_07 "Ох... Ну ладно."
    Kira_05 "Начнём... Нам предстоит сделать несколько снимков, на которых и ты, и я должны показать всю свою страсть!"
    Max_09 "Я постараюсь, но... не знаю, как это сделать, будучи связанным."

    # annroom-watch-03-bj02 + annroom-shot-03-kira&max-05 + annroom-shot-03-caprice-05
    scene BG annroom-watch-03-bj02
    show Kira annroom-shot 03 05
    show Caprice annroom-shot 03 05
    Kira_06 "Ну, да... Здесь нужна поправка. Это я должна показать всю свою страсть. А твоя задача - её выдержать и не кончить раньше времени."
    Max_03 "Пока это проще простого. Я даже не особо возбудился..."

    # photoshot-03-kira-01 + photocamera
    scene photoshot 03-Kira 01
    show FG photocamera
    # $ expected_photo.append('01')
    play sound "<from 1>audio/PhotoshootSound.ogg"  # 1
    Kira_07 "Это потому что ты не видишь, как я прямо сейчас стою над тобой и медленно... и красиво...  {p=1.5}{nw}"
    hide FG
    extend "стягиваю с себя те блестящие трусики, которые ты видел..."
    Max_02 "Хорошо, что я могу это хотя бы представить..."

    # annroom-watch-02-bj03 + annroom-shot-03-kira&max-06 + annroom-shot-03-caprice-06
    scene BG annroom-watch-02-bj03
    show Kira annroom-shot 03 06
    show Caprice annroom-shot 03 06
    # $ expected_photo.append('02')
    Kira_02 "Ну, а теперь, приступим к самому главному... Не помню, говорила я тебе или нет, но наша прошлая фотосессия произвела настоящий фурор..."
    Max_07 "Даже так?"
    play sound "<from 1>audio/PhotoshootSound.ogg"  # 2
    Kira_04 "Да, Макс. Я ещё до неё хотела, чтобы ты меня трахнул, но подумала, что это должно произойти как-то... необычно... А теперь, я подумала, что было весьма неплохо снять её продолжение, где мы меняемся местами. Тем более, что это могло бы принести неплохие деньги."
    Max_03 "Ничего себе... Так я в доле?"

    # annroom-watch-04-bj02 + annroom-shot-03-caprice-07 + annroom-shot-03-kira&max-07
    scene BG annroom-watch-04-bj02
    show Caprice annroom-shot 03 07
    show Kira annroom-shot 03 07
    Kira_07 "А разве тебе не достаточно того, что ты вытворяешь со мной на этих фотосессиях?!"
    Max_07 "Ну... Сейчас я как раз таки ничего и не вытворяю!"
    Kira_06 "Ммм... Это ты верно подметил. Это я совсем потеряла голову, привязала своего племянника к кровати его матери и играюсь своим нежным язычком с его пульсирующим от возбуждения членом!"
    Max_04 "Ох... А как приятно... Только вот..."

    # photoshot-03-kira-03 + photocamera
    scene photoshot 03-Kira 03
    show FG photocamera
    # $ expected_photo.append('03')
    play sound "<from 1>audio/PhotoshootSound.ogg"  # 3
    Kira_03 "Я делаю что-то не так, Макс? {p=1.5}{nw}"
    hide FG
    extend "Ты можешь сказать, как тебе больше нравится..."
    Max_09 "Нет, всё так... Просто меня ещё немного смущает моё положение и то, что фотографирую не я..."
    Kira_02 "Вот значит в чём дело... Видимо, я действую недостаточно отвлекающе, раз ты обращаешь внимание на подобные мелочи."
    Max_01 "Вовсе нет..."

    # annroom-shot-02-max&kira-03-f + annroom-shot-03-kira&max-08
    scene BG char Kira annroom-shot 03
    show Kira annroom-shot 03 08
    # $ expected_photo.append('04')
    Kira_05 "Я так думаю, чтобы отвлечь тебя от разных посторонних мыслей мне нужно несколько углубить процесс и... заняться твоим членом по-настоящему. Ты ведь не будешь против?"
    Max_03 "О, нет... Такое только приветствуется!"
    Kira_09 "Я так и подумала... Ммм... Он такой напряжённый... О да! Я чувствую, что тебе нравится то, как смачно и медленно я это делаю..."
    play sound "<from 1>audio/PhotoshootSound.ogg"  # 4
    Max_19 "Охх... Тётя Кира, твои губы... они... такие..."

    # annroom-watch-02-bj03 + annroom-shot-03-kira&max-09
    scene BG annroom-watch-02-bj03
    show Kira annroom-shot 03 09
    Kira_06 "Судя по тому, что читается в глазах моей подруги, ей тоже нравится, как мои губы обхаживают твой член... Ммм..."
    Max_20 "О да... Только не торопись..."

    # photoshot-03-kira-05 + photocamera
    scene photoshot 03-Kira 05
    show FG photocamera
    # $ expected_photo.append('05')
    play sound "<from 1>audio/PhotoshootSound.ogg"  # 5
    Max_21 "О, Господи... Если фотографии получатся не такими же сочными, как твой минет, тётя Кира... {p=1.5}{nw}"
    hide FG
    extend "Ухх... Твою подругу придётся хорошенько отшлёпать!"

    # annroom-shot-02-max&kira-03-f + annroom-shot-03-kira&max-10
    scene BG char Kira annroom-shot 03
    show Kira annroom-shot 03 10
    # $ expected_photo.append('06')
    Kira_08 "Не знаю, что заводит меня больше, твой упругий член, только что побывавший у меня во рту или фантазии о том, что мы тут втроём могли бы устроить..."
    Max_05 "Ого! От такого я не откажусь..."
    Kira_07 "Я в этом не сомневалась, но для начала я бы на месте моей подруги сделала снимок того, как классно мы смотримся, когда я трусь грудью о твой член. Или не стоит, как думаешь, Макс?"
    play sound "<from 1>audio/PhotoshootSound.ogg"  # 6
    Max_02 "Однозначно стоит!"

    # annroom-watch-02-bj02 + annroom-shot-03-kira&max-11
    scene BG annroom-watch-02-bj02
    show Kira annroom-shot 03 11
    Kira_05 "Дорогая, не поможешь мне? Я уже вся изнываю от желания потрахаться с Максом! Ну как тебе? Очень заводит, держать такой член в руке, не правда ли? Да... Вижу по твоей улыбке, что заводит..."
    Max_04 "{m}У тётиной подруги приятные руки! Она очень возбуждает своими прикосновениями... Она так уверенно водит моим членом по Кириной киске, что складывается впечатление, будто она не просто фотограф, а порноактриса! Хотя, может у неё просто большой опыт в таких фотосессиях...{/m}"

    # photoshot-03-kira-07 + photocamera
    scene photoshot 03-Kira 07
    show FG photocamera
    # $ expected_photo.append('07')
    play sound "<from 1>audio/PhotoshootSound.ogg"  # 7
    Kira_02 "Чувствуешь, какая я мокренькая, Макс? {p=1.5}{nw}"
    hide FG
    extend "Ты как, готов трахнуть свою тётю или тебя ещё помучить..."
    Max_10 "Не томи, тётя Кира! Опустись на него..."

    # annroom-watch-04-bj01 + annroom-shot-03-kira&max-12
    scene BG annroom-watch-04-bj01
    show Kira annroom-shot 03 12
    # $ expected_photo.append('08')
    play sound "<from 1>audio/PhotoshootSound.ogg"  # 8
    Kira_09 "Ну, конечно, мой мальчик... С превеликим удовольствием... Ахх... Боже мой, Макс... Ммм... Как же я хотела почувствовать его внутри... Д-а-а..."
    Max_20 "Вот так, тётя Кира... Эта фотосессия мне нравится всё больше и больше!"
    Kira_10 "Охх... Да... Вот так, Макс! Я просто схожу с ума от твоего члена... Он так глубоко проникает в мою маленькую беззащитную киску... Д-а-а... Ещё... Пожалуй, я развернусь и покажу себя во всей красе..."
    Max_19 "Да! Делай всё, что нужно, чтобы снимки получились отпадными, прямо как ты!"

    # annroom-shot-02-max&kira-04-f + annroom-shot-03-kira&max-13
    scene BG char Kira annroom-shot 04
    show Kira annroom-shot 03 13
    Kira_12 "Ох, как хорошо! Хочу ещё! Вот так... Ммм... Я уже так близко! Больше не могу, Макс... Ммм... Да, я кончаю... Ахх... Это такое наслаждение!"
    Max_21 "{m}Ого! Вот это она разошлась! Скачет на мне так громко, что шлепки о её попку мне уши закладывают... И это обалденно!{/m}"

    # photoshot-03-kira-09 + photocamera
    scene photoshot 03-Kira 09
    show FG photocamera
    # $ expected_photo.append('09')
    play sound "<from 1>audio/PhotoshootSound.ogg"  # 9
    Kira_11 "О Боже, Макс... Я так сладко кончила! {p=1.5}{nw}"
    hide FG
    extend "У меня до сих пор дрожь по телу..."
    Max_05 "Я сам чуть не кончил под такой горячей наездницей, как ты! Еле сдержался."

    # annroom-shot-03-02 + annroom-shot-03-kira&max-14
    scene BG char Kira annroom-shot 03 02
    show Kira annroom-shot 03 14
    Kira_02 "Я бы, конечно, хотела, чтобы мы кончили вместе, но раз уж ты настроен на продолжение, то нам с подругой есть чем тебя впечатлить..."
    Max_07 "С подругой? То есть прямо вместе с ней?!"
    Kira_07 "Нуу... Ты же не будешь против, если мы с ней воспользуемся твоим безвыходным положением и немного пошалим?"
    Max_08 "Смотря как! Но кто-то из вас уже во всю шалит с моими яичками... И это не ты, тётя Кира!"
    Kira_05 "Не переживай, я тоже не оставлю тебя без внимания... Наслаждайся!"

    # annroom-shot-03-03 + annroom-shot-03-kira&max-15
    scene BG char Kira annroom-shot 03 03
    show Kira annroom-shot 03 15
    menu:
        Max_19 "{m}Вот чёрт! Ничего себе! Вот она, мечта всех мужчин - оказаться во власти двух опытных женщин, которые будто соревнуются друг с другом в мастерстве оральных ласк! Ухх... Я просто улетаю...{/m}"
        "{i}Наслаждаться{/i}" ('sex', mgg.sex+5, 90):
            pass
    if rand_result:
        # (Удалось сдержаться!)
        # annroom-shot-03-04 + annroom-shot-03-kira&max-16
        scene BG char Kira annroom-shot 03 04
        show Kira annroom-shot 03 16
        Max_22 "Девчонки... То, что вы делаете... Охх... Это что-то невероятное! Д-а-а... Давайте ещё быстрее... Сейчас я взорвусь! Ахх... Да!!!"

        # annroom-shot-03-03 + annroom-shot-03-kira&max-15 + annroom-shot-03-kira&max-15a
        scene BG char Kira annroom-shot 03 03
        show Kira annroom-shot 03 15
        show FG Kira annroom-shot 03 15
        Max_06 "{m}Фух... Не знаю, в чей такой жаркий и влажный ротик я кончил, но было потрясающе! А девчонки всё не унимаются и продолжают полировать мой член... Просто сказка!{/m}"

        # annroom-shot-03-04 + annroom-shot-03-kira&max-17 + annroom-shot-03-kira&max-17a
        scene BG char Kira annroom-shot 03 04
        show Kira annroom-shot 03 17
        show FG Kira annroom-shot 03 17
        Kira_07 "Вау! Боже мой, Макс... Сколько спермы... Нашему мальчику явно очень понравилось! Давайте сделаем фото на память о нашей небольшой групповушке..."
    else:
        # (Не удалось сдержаться!)
        # annroom-shot-03-04 + annroom-shot-03-kira&max-17 + annroom-shot-03-kira&max-17a
        scene BG char Kira annroom-shot 03 04
        show Kira annroom-shot 03 17
        show FG Kira annroom-shot 03 17
        Kira_07 "Вау! Боже мой, Макс... Сколько спермы... Не удивлена, что ты не мог больше сдерживаться! Давайте сделаем фото на память о нашей небольшой групповушке..."

    # $ expected_photo.append('10')
    play sound "<from 1>audio/PhotoshootSound.ogg"  # 10
    Max_05 "Ты потрясающая, тётя Кира! Вернее, вы обе..."
    Kira_05 "Да, мы такое слышали... Спасибо за комплимент! И тебе спасибо, что поучаствовал в этой фотосессии."
    Max_03 "Очень рад был помочь! Вы почаще обращайтесь..."
    menu:
        Kira_04 "Мы обязательно придумаем что-нибудь ещё, а пока полежи немного, мы тут немного приведём себя в порядок и я тебя развяжу."
        "{i}ждать{/i}":
            stop music

    # annroom-shot-02-max&kira-03-f + annroom-shot-03-kira&max-02a
    scene BG char Kira annroom-shot 03
    show Kira annroom-shot 03 02a
    Kira_07 "Ну всё, отлично повеселились... А теперь иди, твоей утомлённой тётушке нужно отдохнуть. Да и наши уже вот-вот вернутся..."
    Max_10 "О! А твоя подруга что, уже ушла?! А я так хотел её увидеть..."
    Kira_06 "Ещё увидишь, Макс, за это можешь не переживать. Я тут уже серьёзно думаю своими собственными порно-проектами заниматься и она в стороне не останется. Мы ведь с ней очень близкие подруги!"
    Max_07 "А для меня там местечко в проектах найдётся?"
    Kira_02 "На тебя я как раз таки и рассчитывала больше всего!"
    Max_04 "Шикарно!"

    $ renpy.end_replay()
    $ poss['aunt'].open(18) # состоялась 3-я фотосессия
    $ kira.dcv.feature.stage = 11
    $ kira.dcv.feature.set_lost(1)
    $ kira.dcv.photo.stage = 3
    $ kira.dcv.photo.set_lost(3)

    # $ append_photo('03-Kira', 10)

    $ spent_time = 150
    $ current_room = house[2]
    $ SetCamsGrow(house[2], 400)
    jump Waiting


label kira_about_wallet:

    Kira_14 "Как ты, Макс?"
    Max_07 "Ты уже в курсе, что Эрик заявил?"
    Kira_15 "Да, Макс, уже наслышана... Скажи, это же не ты взял его деньги?"
    Max_09 "Ты ещё спрашиваешь?"
    Kira_10 "Ну, я так, для формальности. Конечно, я тебе верю... Что же делать..."
    Max_09 "Есть у меня мысль обновить газон у нас во дворе... Заодно куплю пищевую плёнку. И кажется ещё лопаты у нас нет..."
    Kira_17 "Макс, ты это серьёзно?!"
    Max_11 "Увы, нет. Потому что меня это в глазах семьи не оправдает. Так что буду действовать умнее..."
    Kira_00 "Может, тебе нужна моя помощь в чём-то?"
    Max_01 "Нет, тётя Кира. Мне нужно самому разобраться с Эриком. Но всё равно спасибо, за то, что ты на моей стороне!"
    Kira_10 "Ты только осторожнее, Макс! Хорошо?"
    Max_07 "Конечно. Что будет, то будет..."

    $ spent_time = 10
    $ kira.flags.talkblock = 1
    jump Waiting


label kira_about_showdown:
    # "Уже слышала новость, тётя Кира?"

    Kira_01 "Про Эрика? Слышала..."
    Max_04 "Ну и как настроение?"
    Kira_05 "Настроение? Потрясающее! Такое ощущение, что в доме даже стало легче дышать! Правда все девочки ходят ещё немного опустошённые..."
    Max_07 "Ну да, это всё из-за правды об Эрике."

    if wcv.catch_Kira.stage:
        # Эрик спалил Макса с Кирой в ванне
        Kira_03 "Понимаю. Все в нём души не чаяли и вдруг такой облом... А я радуюсь! Радуюсь тому, что ты, наконец-то, избавил меня от этого шантажиста. Ты просто не представляешь, что приходилось делать для этого мерзавца. Спасибо тебе огромное!"
        Max_10 "Уж извини, что тебе так досталось."
        Kira_15 "Я переживу, Макс. Не знаю, каким чудом он ещё не узнал, что я порно-актриса! Наверняка бы сразу все свои наигрязнейшие фантазии стал воплощать в жизнь..."
    else:
        #если Эрик не знает о развлечениях Макса с Кирой
        Kira_03 "Понимаю. Все в нём души не чаяли и вдруг такой облом - пригрели под своей крышей матёрого извращенца! А я радуюсь! Радуюсь тому, что моя сестра наконец-то послала этого ублюдка. Спасибо огромное, Макс, это всё благодаря тебе!"
        Max_10 "Не за что. Обращайтесь, если что. А, вообще, меня это тоже касалось, как никого другого. Он меня и вором выставил перед всеми... и в лагерь хотел сослать..."
        Kira_14 "Да, это всё было бы крайне печально... То, что здесь началось бы без тебя."

    Max_03 "Нашла о чём думать! Всё уже в прошлом."
    Kira_07 "Мне нравится, как это звучит! Надеюсь, девочки совсем скоро придут в себя и всё будет, как раньше!"
    Max_01 "Я тоже на это надеюсь!"


    $ spent_time = 20
    $ kira.flags.showdown_e = 1
    jump Waiting
