
label KiraTalkStart:

    $ dial = TalkMenuItems()

    # $ __cur_plan = GetPlan(plan_ann, day, tm)
    $ __cur_plan = kira.get_plan()
    if __cur_plan.talklabel is not None:
        call expression __cur_plan.talklabel from _call_expression_10

    if len(dial) > 0:
        $ dial.append((_("{i}Ой, в другой раз...{/i}"), "exit"))
    else:
        jump Waiting

    $ renpy.block_rollback()
    if kira.plan_name == 'sun':
        Max_01 "Не против компании?"
        Kira_01 "Конечно, Макс. Ложись рядом. Погода сегодня отличная!" nointeract
    else:
        Kira_00 "Макс, чем тебе помочь?" nointeract
    $ rez =  renpy.display_menu(dial)

    if rez != "exit":
        if renpy.has_label(talks[rez].label): # если такая метка сушествует, запускаем ее
            call expression talks[rez].label from _call_expression_11
        jump KiraTalkStart       # а затем возвращаемся в начало диалога, если в разговоре не указан переход на ожидание

    jump AfterWaiting            # если же выбрано "уйти", уходим в после ожидания


label kira_firsttalk:
    scene BG char Kira sun-talk-01
    show Kira sun-talk 01
    show Max sun-talk 01
    Kira_04 "Ну, рассказывай, Макс, как ты тут живёшь, как твои дела, что нового?"
    Max_04 "Ну, я... э..."
    menu:
        Kira_02 "Макс, тебя смущает мой купальник? Может быть, его снять?"
        "О... Да, было бы супер!":
            menu:
                Kira_07 "Ну да, не сомневаюсь. Только что же с тобой будет? Макс, ты можешь смотреть мне в глаза или мы тебя потеряли?"
                "Извини, тётя Кира...":
                    pass
                "Просто я отвлёкся...":
                    pass
        "Ну, я не знаю...":
            menu:
                Kira_07 "Зато знаю я. Макс, постарайся не пялиться на мою грудь. Глаза чуть выше. Ау. Ты здесь, Макс?"
                "Извини, тётя Кира...":
                    pass
                "Просто я отвлёкся...":
                    pass
    Kira_04 "Да, это моя ошибка, что выбрала такой купальник. Если честно, то я обычно загораю голая, но вы же дети и стоит хоть немного прикрываться. Вот я и прикрылась... Макс!"
    Max_07 "А? Что?"
    show Kira sun-talk 02
    show Max sun-talk 02
    menu:
        Kira_05 "Ого... Да я смотрю твой рост пошёл куда надо! И давно ты такое сокровище скрываешь?"
        "Ну, я не знаю...":
            pass
        "Да я и не скрываю...":
            pass
    ## открытие первого этапа возможности "Любимая тётя"
    menu:
        Kira_06 "Как я вижу, ты весь пошёл в отца... А кое в чём, так особенно..."
        "Что?!":
            Kira_03 "Ой, я конечно про твою... скромность. Да, речь о ней. И, вообще, давай сменим тему!"
        "Откуда ты знаешь, что...":
            Kira_03 "Что я знаю? Ничего. Я так, про твою скромность, конечно... И, вообще, давай сменим тему!"
    Max_08 "Тётя Кира!"
    Kira_04 "А что тётя Кира? Проехали. Кое-кому, так вообще нужно принять душ и расслабиться. Может быть, и мне тоже, но вдвоём не стоит это делать. Так что, иди ты в душ. И включи холодную воду. Очень холодную!"
    Max_00 "Хорошо..."
    scene BG shower-closer
    show Max shower 04
    show FG shower-closer
    menu:
        Max_03 "{i}( Расслабиться в душе? Легко. Особенно, когда рядом такая женщина. Ух! Я и не задумывался о своей тёте в этом плане никогда, но сейчас что-то изменилось... или во мне или в ней... ){/i}"
        "{i}снять напряжение{/i}":
            Max_20 "О, Кира... Ты когда-нибудь станешь моей..."
            show Max shower 05
            Max_07 "Я же не сказал это вслух?"
            $ mgg.cleanness = 100
    $ dcv['kiratalk'].stage += 1
    $ dcv['kiratalk'].set_lost(1)
    $ poss['aunt'].OpenStage(0)
    $ SetCamsGrow(house[6], 200)
    $ spent_time += 30
    jump Waiting


label kira_talk2:
    scene BG char Kira sun-talk-01
    show Kira sun-talk 01
    show Max sun-talk 01
    menu:
        Kira_02 "В прошлый раз наш разговор не очень удался. Ты был такой возбуждённый... Слушай, Макс, а у тебя есть девушка?"
        "Есть!":
            menu:
                Kira_04 "А она об этом знает?"
                "Конечно!":
                    Kira_08 "Ну ничего себе! А как её зовут?"
                    Max_00 "Это не важно..."
                    Kira_01 "Хорошо... Тогда последний вопрос. Ты её видишь прямо сейчас, она где-то здесь?"
                    Max_08 "Да я её не выдумал! Она не вымышленная девушка!"
                    Kira_07 "Ну тогда я рада за тебя, Макс. И давно вы встречаетесь?"
                    Max_10 "Ну... Э..."
                    Kira_02 "Я так и думала. Ладно, Макс, не стоит переживать по этому поводу. Ты же только приехал в новый город, из дома не выходишь. Ну скажи честно, нет же у тебя никакой девушки?"
                    Max_11 "Ну да, ты права, нет..."
                "Ты права, нет девушки...":
                    pass
        "Нет...":
            pass
    Kira_07 "Ну, это многое объясняет. Возраст такой, гормоны, кругом полуобнажённые девушки и женщины... Наверное, для тебя это каторга? Со мной можешь говорить откровенно. Я никому ни слова!"
    Max_00 "Да, бывает возбуждаюсь..."
    Kira_06 "Это потому-что пялишься не туда, куда надо. В глаза смотри..."
    Max_04 "Да, хорошо... Просто так само получается..."
    menu:
        Kira_05 "Понятно. Ну, старайся держать себя в руках. Хотя, я в прошлый раз видела как ты себя держал. Это не совсем то, что я имею в виду... Я слишком пошлая, да? Мне надо быть тактичнее?"
        "Что ты, всё хорошо!":
            pass
        "Меня всё устраивает...":
            pass
    Kira_04 "Я рада. Обычно я не слежу за языком и он меня в такие места заводит... Но с детьми надо быть... Хотя, ты уже совсем не ребёнок."
    Max_01 "Верно..."
    Kira_01 "Слушай, Макс, а как тебе удаётся с Лизой в одной комнате уживаться? Наверное, тяжело? Она же сверкает своим нижним бельём, да и спите вы рядом."
    Max_02 "Ну, был один инцидент..."
    Kira_02 "Утренний стояк, наверное? И как она отреагировала?"
    Max_03 "Панику подняла, маму позвала!"
    menu:
        Kira_08 "Представляю! Ну, я с Лизой тоже побеседую. Она уже не девочка... хотя, с вашей строгой мамой она о жизни толком ничего и не знает, похоже."
        "Верно...":
            pass
        "И что ты ей скажешь?":
            Kira_03 "Расскажу откуда дети берутся, конечно! Макс, ты такой смешной. У девочек свои секреты, тебе всё знать не обязательно..."
            Max_01 "Я понял..."
    ## У Макса опять стояк
    show Kira sun-talk 02
    show Max sun-talk 02
    menu:
        Kira_07 "Макс... Кажется, тебе снова пора в душ. Может быть, мне как-то прикрываться, когда ты рядом, раз у тебя все мысли где-то в другом месте?"
        "Не надо прикрываться!":
            Kira_04 "Ладно, не буду. Но в душ ты сходи. И обязательно холодный. А ещё лучше ледяной..."
            Max_00 "Хорошо..."
        "Пожалуй, я пойду...":
            pass
    scene BG shower-closer
    show Max shower 04
    show FG shower-closer
    menu:
        Max_20 "{i}( Вроде тётя Кира специально и не провоцирует, но меня к ней так тянет. Её горячее, почти обнажённое тело так и манит... ){/i}"
        "{i}кончить{/i}":
            show Max shower 05
            Max_07 "{i}( О да... Надеюсь, она не поняла, что я сейчас делал, глядя на неё. Хотя, всё она понимает. Да ещё и видела меня, мой... ){/i}"
            $ mgg.cleanness = 100
    $ dcv['kiratalk'].stage += 1
    $ dcv['kiratalk'].set_lost(1)
    $ SetCamsGrow(house[6], 200)
    $ spent_time += 30
    jump Waiting


label kira_talk3:
    scene BG char Kira sun-talk-01
    show Kira sun-talk 01
    show Max sun-talk 01
    menu:
        Kira_05 "Сегодня уже привык к моему купальнику? Или опять только одно на уме?"
        "Сегодня точно всё в порядке!":
            pass
        "Да я не могу ни о чём думать, когда тут такое...":
            Kira_07 "Понимаю, Макс. Ну, тут надо либо смириться, либо избегать. Как я вижу, избегать меня ты не планируешь и я очень рада этому. Но давай сменим тему, вдруг поможет..."
            Max_01 "Давай!"
    Kira_02 "Расскажи тогда, Макс, чем ты занимаешься, пока сидишь дома? Не может быть, чтобы только на порносайтах зависал?"
    Max_08 "У нас не работают порносайты..."
    Kira_14 "Ах вот в чём дело! А я никак не могла понять, что такое с моим телефоном. И почему? Мама установила такие порядки?"
    Max_09 "Ага, на стороне провайдера защита от детей..."
    Kira_04 "Тогда, Макс, я тебе ещё больше сочувствую и теперь лучше тебя понимаю... Может быть, сменим тему?"
    Max_01 "Хорошо..."
    menu:
        Kira_13 "Слушай, Макс, а тебе не показалось, что твоя мама какая-то... странная. Я имею в виду её одержимость Эриком..."
        "Вот именно!":
            menu:
                Kira_14 "Значит, не мне одной это показалось? Ну, в чём дело, рассказывай..."
                "{i}рассказать всю правду про Эрика{/i}":
                    pass
        "Нет, тебе показалось...":
            menu:
                Kira_02 "Макс, если что, я на твоей стороне. Мне можно доверять!"
                "Ну ладно... {i}Рассказать всю правду про Эрика{/i}":
                    pass
    menu:
        Kira_13 "Макс, ты наверное шутишь, да? Не может быть, чтобы всё было вот так. Твоя мама не слепая. Конечно, ею всегда было легко манипулировать, чем я и пользовалась, но такое..."
        "Чем ты пользовалась?":
            Kira_14 "Мне кажется, сейчас речь не об этом. Расскажу как-нибудь в другой раз... Значит, всё что ты рассказал про Эрика... это правда? Прямо не верится..."
            Max_07 "У меня нет причин врать!"
        "Я говорю правду!":
            pass
    Kira_03 "Думаю, мне нужно услышать другую сторону. Пообщаться или с Эриком или с твоей мамой, чтобы выяснить всё ли так. Может быть, ты немного... приукрасил..."
    Max_09 "И ничего я не приукрасил..."
    Kira_04 "Ладно, не дуйся. Давай сменим тему, а ещё лучше полежим на солнышке в тишине, если ты не возражаешь..."
    Max_01 "Ладно, не буду тебе мешать..."

    ## второй этап "Любимой тёти"

    $ dcv['kiratalk'].stage += 1
    $ dcv['kiratalk'].set_lost(1)
    $ poss['aunt'].OpenStage(1)
    $ SetCamsGrow(house[6], 200)
    $ spent_time += 30
    jump Waiting


label kira_about_kiss:
    $ renpy.block_rollback()
    menu:
        Kira_07 "Вот это подход, я понимаю! Может быть, тебе ещё что-то нужно?"
        "А можно что-то ещё?":
            Kira_08 "Шутник ты, Макс... Постой, ты нашёл себе девушку? Кто она? Откуда? Как познакомились? Как зовут? Хочу всё знать!"
        "Для начала только целоваться!":
            Kira_08 "Для начала? Забавно... Постой, ты нашёл себе девушку? Кто она? Откуда? Как познакомились? Как зовут? Хочу всё знать!"
    Max_01 "Как ты догадалась?"
    menu:
        Kira_05 "Ну зачем ещё парню может это понадобиться, сам подумай... И что, больше не у кого просить помощи, решил ко мне обратиться?"
        "Мама не помогла..." if 'ann' in talk_var['ask.teachkiss']:
            Kira_01 "Даже мама не помогла? Ого! Наверняка, сказала, чтобы ты учился целоваться со своей девушкой?"
            Max_07 "Точно!"
        "Да, Алиса меня отшила..." if 'alice' in talk_var['ask.teachkiss']:
            Kira_01 "Неудивительно. Наверняка, полез целоваться и получил... по... Хотя, нет. Видимо, тебе повезло. Ну я бы не стала к Алисе с такими вопросами обращаться на твоём месте..."
            Max_07 "Да, уже жалею..."
        "Ага...":
            pass
    menu:
        Kira_07 "Ну что же... Думаю, я смогу тебе помочь. Правда, не здесь и не сейчас. Нужна более интимная обстановка. О, я иногда вечерами смотрю телевизор. Если ещё не будешь спать, напомни о своей просьбе..."
        "Договорились!":
            pass
        "Хорошо! А ты только этому учишь?":
            Kira_04 "Макс, тебе стоит быть чуть более... тактичным. В общем, помочь я тебе помогу, только выбери подходящий момент. Договорились?"
            Max_01 "Конечно!"

    $ poss['seduction'].OpenStage(6)
    $ talk_var['ask.teachkiss'].append('kira')
    $ spent_time += 10
    return


label kira_talk4:
    # стартовая фраза "Ну как, ты с мамой-то поговорила?"
    scene BG char Kira sun-talk-01
    show Kira sun-talk 01
    show Max sun-talk 01

    Kira_10 "Вот так вот! Сразу к делу, да? Нет, Макс, мне не удалось поговорить. Но я обязательно во всём разберусь и выясню что происходит."
    Max_00 "Ну ладно. Значит, ты работаешь горничной?"
    Kira_01 "Ну... Типа того. А что, не похоже?"
    Max_09 "Да странно это всё..."

    menu:
        Kira_07 "Что же тебя смущает? Я разве не могу работать горничной?"
        "Кто же вечером порядок наводит?":
            Kira_04 "Может быть, они любят поспать и только вечером не бывают дома?"
            Max_07 "У тебя слишком нежные руки для такой работы..."

        "Ну, твои руки не как у горничной...":
            Kira_04 "Ну ты совсем как твоя мама. Всё подозреваешь... Может быть, я в перчатках работаю. Берегу кожу..."
            Max_07 "Кто же вечером порядок наводит?"

    menu:
        Kira_08 "Ну ладно, ладно, ты меня раскусил, Макс. Я не совсем горничной работаю..."
        "А кем тогда?":
            pass
        "И для чего тебе форма горничной?":
            pass

    Kira_14 "Если ты расскажешь своей маме, она меня убьёт и выгонит в гостиницу... В лучшем случае..."
    Max_01 "Рассказывай. Я никому ни слова!"
    Kira_13 "Точно? В общем, я снимаюсь в одном фильме... И форма горничной как раз из него. Я её случайно испачкала и брала постирать, а твоя мама заметила..."
    Max_07 "Так ты теперь актриса?!"
    menu:
        Kira_08 "Да, почти звезда! Но никому не говори. Это большой секрет от всех!"
        "Почему?":
            pass
        "И что это за фильм?":
            pass
    Kira_05 "Ну, это не совсем те фильмы, которыми хвастаются среди своей семьи, если ты меня понимаешь..."
    Max_08 "Это порно?!"
    Kira_03 "Макс, ну почему сразу порно? Может быть, это утончённая эротика с интересным сюжетом и отличной игрой актёров..."
    Max_07 "Значит, правда порно..."
    Kira_14 "Ну да. Нужно же девушке как-то зарабатывать. Правда, платят не очень, но продюсер обещал мне главную роль..."
    Max_09 "Они всем это обещают"
    Kira_07 "Думаешь, я этого не знаю? Ну а что делать. Там мне хотя бы стабильно платят, хоть и копейки..."

    show Kira sun-talk 02
    show Max sun-talk 02

    Max_03 "Моя тётя снимается в порно... С ума сойти!"
    Kira_08 "Макс, ты опять слишком перевозбудился. Боюсь представить что будет, если ты увидишь какой-нибудь фильм с моим участием! Хотя, мы и так с тобой почти как в порно развлекаемся..."
    Max_05 "А что, можно увидеть?"
    Kira_04 "Думаю, на сегодня с тебя и так хватит. Бегом в душ, а то меня начинает возбуждать твой стоящий член..."
    Max_02 "Не поможешь мне с этим?"
    Kira_06 "Макс! Что ты такое своей тёте предлагаешь! Как тебе не стыдно..."
    Max_09 "Ладно, смейся..."

    scene BG shower-closer
    show Max shower 04
    show FG shower-closer

    Max_04 "{i}( Моя тётя снимается в порно! О таком лучше вообще никому не рассказывать. Ох... вот это мне повезло! ){/i}"

    show FG kira-voyeur-shower-00

    Max_19 "{i}( О да, Кира, мы с тобой ещё снимемся в фильме...  и даже не в одном. Я сделаю тебя звездой и не только! ){/i}"
    Kira_05 "Что, прости? Я не расслышала последнее..."

    show Max shower 04a

    Max_08 "Но... Ты же была там... Я..."
    Kira_08 "Просто немного пофантазировал о своей тёте?"
    Max_09 "Ну а как ты думала? Приходится, раз уж ты не помогаешь..."
    menu:
        Kira_01 "Ладно, ты тут заканчивай, я позже в душ схожу. Не подскользнись!"
        "{i}закончить{/i}":
            show Max shower 05
            Max_07 "{i}( Ну, тётя Кира, ты доприкалываешься! Я ещё реализую свои фантазии с тобой... ){/i}"
            $ mgg.cleanness = 100
    $ dcv['kiratalk'].stage += 1
    $ dcv['kiratalk'].set_lost(1)
    $ poss['aunt'].OpenStage(2)
    $ SetCamsGrow(house[6], 200)
    $ spent_time += 30
    jump Waiting


label kira_talk5:
    # стартовая фраза "Как отдыхается, тётя Кира?"
    scene BG char Kira sun-talk-01
    show Kira sun-talk 01
    show Max sun-talk 01

    Kira_01 "Прекрасно, Макс! Я, кстати, ждала, что ты подойдёшь. Мне нужна помощь в одном деле и твой энтузиазм и энергия, могут мне помочь с этим."
    Max_01 "Что за дело? Я слушаю."

    Kira_03 "Ты как зарабатываешь? У тебя есть какой-то доход или живёшь на карманные деньги, которые тебе мама даёт?"
    Max_07 "Ну, типа того..."
    Kira_07 "Тогда, у меня есть для тебя деловое предложение. Правда, оно потребует вложений, но окупится, обещаю..."
    Max_09 "Ты о чём?"
    menu:
        Kira_05 "Мне нужно сделать серию снимков для портфолио. Я могла бы обратиться к профессиональному фотографу, но если у тебя найдётся фотоаппарат и будет желание, то я лучше заплачу тебе... Ты умеешь фотографировать?"
        "Конечно!":
            Kira_04 "Да? И где же ты учился? А самое главное - когда? Или ты фотографии на телефон считаешь за опыт?"
            Max_02 "Ну... Зато снимки были хорошие!"
        "Нет...":
            pass
    Kira_14 "Очень жаль... Без опыта, скорее всего, ничего толком не получится..."
    Max_07 "А если я попробую бесплатно, ты попозируешь?"
    Kira_02 "Конечно. Почему нет? Вдруг, у тебя хорошо получится? Кстати, а фотоаппарат у тебя есть?"
    Max_04 "Найду!"
    Kira_08 "Ну, тогда не вижу причин для отказа такому целеустремлённому мальчику..."

    show Kira sun-talk 02
    show Max sun-talk 02

    Max_03 "Вот и договорились!"
    Kira_06 "Уж не знаю, Макс, что ты уже нафантазировал насчёт фотосессии, но тебе опять пора в душ. Что же ты такой впечатлительный? Эх..."
    Max_09 "Да, я пойду. И если не хочешь помочь мне с этим, то не мешай, как в тот раз!"

    scene BG shower-closer
    show Max shower 04
    show FG shower-closer

    Max_04 "{i}( Вот это удача! Порно-звезда будет мне позировать для фотосессии! Я где-то слышал, что порно-модели всегда дают фотографам в качестве оплаты за съёмку... Или нет? Но вдруг! ){/i}"
    menu:
        Max_20 "Дело за малым. Осталось где-то взять фотоаппарат и тогда... Да... Интересно, сколько они стоят? А, не важно. Важно, что мне может дать сама тётя Кира! Ради этого стоит попотеть... О да..."
        "{i}закончить{/i}":
            show Max shower 05
            Max_09 "{i}( Эх, с тётей Кирой кончать поинтереснее... и намного! ){/i}"
    $ dcv['kiratalk'].stage += 1
    $ dcv['kiratalk'].set_lost(1)
    $ poss['aunt'].OpenStage(3)
    $ items['photocamera'].InShop = True
    $ notify_list.append(_("В интернет-магазине доступен новый товар."))
    $ SetCamsGrow(house[6], 180)
    $ SetCamsGrow(house[3], 180)
    $ spent_time += 30
    jump Waiting


label kira_talk6:
    # стартовая фраза "Насчёт фотосессии..."
    scene BG char Kira sun-talk-01
    show Kira sun-talk 01
    show Max sun-talk 01
    menu:
        Kira_01 "Ты уже нашёл фотоаппарат?"
        "Ещё нет..." if not items['photocamera'].have:
            menu:
                Kira_02 "Понятно. Ну, как найдёшь, дай знать. Я почти всегда готова фотографироваться, но нужно, чтобы ещё и дома никого не было... А то нас не поймут..."
                "Всё понял...":
                    $ talks['kt6'].req = "all([dcv['kiratalk'].done, kira.plan_name=='sun', dcv['kiratalk'].stage==5, items['photocamera'].have, items['nightie2'].have])"
        "Ага, он у меня!" if all([items['photocamera'].have, items['nightie2'].have, GetWeekday(day)!=6]):
            Kira_04 "Очень хорошо. Теперь нужно выбрать подходящий момент, когда дома никого не будет..."
            Max_01 "Точно!"
            $ talks['kt6'].req = "all([dcv['kiratalk'].done, kira.plan_name=='sun', dcv['kiratalk'].stage==5, items['photocamera'].have, items['nightie2'].have, GetWeekday(day)==6])"
        "Ага, он у меня!" if all([items['photocamera'].have, items['nightie2'].have, GetWeekday(day)==6]):
            Kira_07 "Макс, ты просто чудо! Я всегда бодра, красива и свежа, чтобы фотографироваться. Нужно только решить где это будет..."
            Max_04 "Может быть, в гостиной?"
            jump first_photoset
    $ dcv['kiratalk'].set_lost(1)
    $ spent_time += 10
    jump Waiting


label first_photoset:
    $ expected_photo = []
    scene BG char Kira dresses
    show Kira dresses 01
    with fade
    Kira_01 "А что, отличная идея! Только немного подожди, я переоденусь в свою ночнушку... Она ведь довольно сексуальная, как ты считаешь?"
    Max_02 "Да, но у меня есть идея получше."

    show Kira dresses 02
    Kira_05 "Сейчас угадаю, Макс, сниматься полностью голой! Верно?"
    menu:
        Max_04 "Не угадала! Я решил купить тебе сорочку, ещё сексуальнее твоей..."
        "{i}подарить сорочку{/i}":
            pass

    show Kira dresses 03
    Kira_08 "Ничего себе! Это очень приятно... И этот подарок, и то как ты подготовился к фотосессии. И как ты выпросил у своей мамы столько денег на это?"
    Max_03 "Да так, были кое-какие накопления... Для такой классной тёти не жалко!"

    show Kira dresses 04
    Kira_07 "Да, этот комплект отлично подойдёт для нашей съёмки. Ну что, как на мне смотрятся трусики? Вроде, в самый раз?"
    Max_05 "Ага. Мне очень нравится. То, что надо!"

    show Kira dresses 05
    Kira_06 "Ну вот, Макс, я полностью готова! Где начнём съёмку?"
    Max_01 "Давай начнём на диване. Ты ведь всё-таки в сорочке, так что покажем, как ты просыпаешься..."

    Kira_03 "О, мне эта идея нравится! Значит, я просто ложусь на диван или..." nointeract
    $ dial = [(_("Да, сделаем сперва снимок, будто ты только-только проснулась..."), '1'), (_("Ты уже проснулась и решила красиво потянуться..."), '2')]
    $ renpy.random.shuffle(dial)
    $ rez =  renpy.display_menu(dial)
    if rez > '1':
        jump .set2

    # set1
    scene photoshot 01-Kira 01
    Kira_04 "Макс, постарайся меня держать в фокусе и следи, чтобы горизонт не был завален... Хотя, ты же всё умеешь, говоришь... В общем, я тебе доверяю."
    show FG photocamera
    play sound "<from 1>audio/PhotoshootSound.ogg"
    Max_04 "И правильно... Моя тётя очень красиво просыпается! {p=1.5}{nw}"
    hide FG
    extend "Готово! Теперь садись. Ты уже проснулась и решила красиво потянуться..."
    $ expected_photo.append('01')

    label .set2:
        scene photoshot 01-Kira 02
    Kira_02 "Как тебе такие потягушки, Макс? Смотрится хорошо?"
    show FG photocamera
    play sound "<from 1>audio/PhotoshootSound.ogg"
    Max_02 "То, что надо! Сейчас только ракурс подберу поинтереснее и будет отлично... {p=1.5}{nw}"
    hide FG
    extend "Ага, есть! А дальше мы щёлкнем твою попку..."
    $ expected_photo.append('02')

    Kira_01 "С радостью, Макс! Как мне для этого лучше устроиться?" nointeract
    $ dial = [(_("Просто повернись в сторону окна..."), '1'), (_("Немного привстань, а остальное за мной..."), '2')]
    $ renpy.random.shuffle(dial)
    $ rez =  renpy.display_menu(dial)
    if rez > '1':
        jump .set4

    # set3
    scene photoshot 01-Kira 03
    Kira_05 "Вот так должно красиво смотреться... Или сесть по-другому?"
    show FG photocamera
    play sound "<from 1>audio/PhotoshootSound.ogg"
    Max_05 "Нет, тётя Кира, вид обалденный! Лучше некуда! Только фокус настрою сейчас... {p=1.5}{nw}"
    hide FG
    extend "Вот и готово. Давай, привстань немного, я сделаю ещё снимок..."
    $ expected_photo.append('03')

    label .set4:
        scene photoshot 01-Kira 04
    Kira_07 "Я вижу, тебе нравится? Ясно, почему ты в джинсах всё время ходил..."
    $ expected_photo.append('04')
    show FG photocamera
    play sound "<from 1>audio/PhotoshootSound.ogg"

    Max_01 "Не отвлекайся, тётя Кира! А то снимок выйдет так себе... {p=1.5}{nw}"
    hide FG
    extend "Отлично! Вот что делаем дальше..." nointeract
    $ dial = [(_("Сделаем снимок не для портфолио в монастырь..."), '1'), (_("Переходим с дивана к столу..."), '2')]
    $ renpy.random.shuffle(dial)
    $ rez =  renpy.display_menu(dial)
    if rez > '1':
        jump .set6

    # set5
    scene photoshot 01-Kira 05
    Kira_09 "Ухх... Макс! Меня и саму очень заводит, когда я завожу фотографа..."
    show FG photocamera
    play sound "<from 1>audio/PhotoshootSound.ogg"
    Max_03 "Да... Покажем твою сочную попку во всей красе! {p=1.5}{nw}"
    hide FG
    extend "Снимок выйдет чумовой... Теперь переходим с дивана к столу."
    $ expected_photo.append('05')

    label .set6:
        scene photoshot 01-Kira 06
    Kira_04 "Если я обопрусь на стол вот так, слегка подразнивая своей грудью, получится неплохой снимок, как мне кажется..."
    show FG photocamera
    play sound "<from 1>audio/PhotoshootSound.ogg"
    Max_02 "О да... Твоя грудь никого не оставит равнодушным. Она у тебя - огонь! {p=1.5}{nw}"
    hide FG
    extend "Снимок готов. Давай, покажи мне что-нибудь погорячее..."
    $ expected_photo.append('06')

    scene photoshot 01-Kira 07
    Kira_06 "Вроде такого? Думаю кадр получится отменный..."
    show FG photocamera
    play sound "<from 1>audio/PhotoshootSound.ogg"
    $ expected_photo.append('07')
    Max_04 "Ага, замри... Сейчас я тебя щёлкну. {p=1.5}{nw}"
    hide FG
    menu:
        extend "Да, кадр получился что надо!"
        "Хотя, я знаю, как он станет ещё лучше...":
            scene photoshot 01-Kira 08
    Kira_07 "Мне нужно, чтобы кадры были ещё лучше! Думаешь, мы можем ограничиться лишь снятием с меня трусиков?"
    show FG photocamera
    play sound "<from 1>audio/PhotoshootSound.ogg"
    Max_01 "Конечно нет, тётя Кира! {p=1.5}{nw}"
    hide FG
    extend "Это же все для портфолио, а в нём должны быть продемонстрированы твои умения..."
    $ expected_photo.append('08')

    scene photoshot 01-Kira 09
    Kira_09 "И я с удовольствием их продемонстрирую... Особенно, если при этом у тебя получатся очень хорошие кадры!"
    show FG photocamera
    play sound "<from 1>audio/PhotoshootSound.ogg"
    $ expected_photo.append('09')

    Max_02 "Я думаю, кадры будут отличные...{p=1.5}{nw}"
    hide FG
    extend "" nointeract
    $ dial = [(_("Пора показать класс, тётя Кира!"), '1'), (_("Может, сразу удивишь меня чем-нибудь?"), '2')]
    $ renpy.random.shuffle(dial)
    $ rez =  renpy.display_menu(dial)
    if rez > '1':
        jump .set11

    # set10
    scene photoshot 01-Kira 10
    Kira_02 "Как скажешь, Макс. Держись... Это будет сумасшедший минет!"
    show FG photocamera
    play sound "<from 1>audio/PhotoshootSound.ogg"
    $ expected_photo.append('10')
    Max_20 "Охх... Полегче, тётя Кира! Ты так чувственно берёшь его в рот, что я могу забыть сделать снимки. {p=1.5}{nw}"
    hide FG
    extend "Да... продолжай вот так, это будет отличный кадр! Может тебе есть, чем меня удивить?"

    label .set11:
        scene photoshot 01-Kira 11
    Kira_05 "Как тебе такой пируэт, Макс? Уверена, ты будешь в восторге!"
    show FG photocamera
    play sound "<from 1>audio/PhotoshootSound.ogg"
    $ expected_photo.append('11')
    Max_22 "Вау! Такое нужно обязательно заснять... Ох, как же класно ты сосёшь... {p=1.5}{nw}"
    hide FG
    extend "Думаю, я уже почти готов для последнего снимка! Готовься, тётя Кира..."

    scene photoshot 01-Kira 12
    Kira_08 "Да, вот так, Макс! Не сдерживайся! Я должна выглядеть как самая настоящая порно-звезда..."
    show FG photocamera
    play sound "<from 1>audio/PhotoshootSound.ogg"
    $ expected_photo.append('12')
    Max_05 "И снято! {p=1.5}{nw}"
    hide FG
    extend "Да уж, это было потрясающе! Думаю, снимки получились удачными..."
    Kira_06 "Надеюсь... Я скину себе фотографии, мой знакомый их обработает и там уже посмотрим, что получилось. Спасибо тебе, Макс. Кстати, если что, ты же не откажешь ещё пощёлкать свою тётю?"
    Max_03 "Да, я бы пощёлкал..."
    Kira_01 "Вот и здорово, Макс! Ладно, пойду приведу себя в порядок и переоденусь, а тебе ещё раз спасибо..."
    Max_07 "{i}( Это было намного лучше, чем ничего! Может, в следующий раз, мне обломится куда больше... ){/i}"

    $ renpy.end_replay()
    $ added_mem_var('kira_photoset1')
    $ SetCamsGrow(house[4], 300)
    $ poss['aunt'].OpenStage(4)
    $ items['nightie2'].InShop = False
    $ items['photocamera'].InShop = False
    $ items['nightie2'].have = False
    $ dcv['kiratalk'].stage += 1
    $ dcv['kiratalk'].set_lost(1)
    $ spent_time += 90
    $ current_room = house[0]
    jump Waiting


label kira_about_photo1:
    # стартовая фраза "Понравились фотографии?"
    scene BG char Kira sun-talk-01
    show Kira sun-talk 01
    show Max sun-talk 01

    Kira_04 "А, Макс. Я сразу отправила фотографии своему знакомому и он сказал, что его всё устраивает. Фокус отличный, ракурс тоже. Так что, я бы ещё раз обратилась к тебе за помощью, если ты не против..."
    Max_01 "В любое время, тётя Кира"
    Kira_14 "Да в том-то и дело, что не в любое. Нужно выбирать момент, когда никого нет дома. Потому-что, если нас заметят за таким, то точно это будет в последний раз, а мне бы не хотелось этого..."
    Max_04 "Да, мне тоже... А какой будет следующая фотосессия?"

    show Kira sun-talk 02
    show Max sun-talk 02

    Kira_05 "Какой ты любопытный, Макс. Вижу, твой дружок уже догадался, что эта фотосессия будет ещё более откровенной..."
    Max_05 "Ого! А куда ещё откровеннее?"
    Kira_07 "А вот... Думаю, тебе понравится. А сейчас кое-кому стоит принять холодный... очень холодный душ!"
    Max_02 "А ты поможешь мне с этим?"
    Kira_02 "Пожалуй, я ещё немного понежусь на солнышке. Ты и сам прекрасно справишься..."
    Max_09 "Эх, всё приходится делать самому..."

    scene BG shower-closer
    show Max shower 04
    show FG shower-closer

    Max_19 "{i}( Интересно, когда будет следующая фотосессия? Мне уже не терпиться перейти на новый уровень близости с тётей! Вот я бы её хорошенько... ){/i}"

    show FG kira-voyeur-shower-00

    Kira_04 "Не паникуй, Макс, это всего лишь я. Стало как-то слишком жарко... Ты же ведь не против моей компании?"
    Max_03 "Конечно не против! Ещё спрашиваешь..."

    scene BG shower-closer
    show Kira shower-Max 01
    show FG shower-closer

    Kira_07 "Я подумала, фотосессия фотосессией, а за сорочку нужно поблагодарить отдельно. Как считаешь?"
    Max_05 "Не обязательно, но можно!"

    show Kira shower-Max 03

    Kira_10 "Ты хоть представляешь, Макс, что будет, если нас застукают за таким?"
    Max_07 "Боюсь даже представить... Может, сделать всё по-быстрому?"

    scene BG char Kira shower-04
    show Kira shower-Max 04

    Kira_02 "Тогда нам лучше выключить воду... И смотри по сторонам, чтобы никто не увидел, что мы делаем! Мы ведь с тобой такими плохими делишками занимаемся..."
    Max_02 "Само собой, тётя Кира!"

    scene BG char Kira shower-05
    show AnimMaxKira1

    menu:
        Max_20 "{i}( Ох, как она сосёт... Да так глубоко... Похоже, у неё чёрный пояс по минету! Блин, я уже на грани... Вот-вот и кончу... Отпадно! ){/i}"
        "{i}кончить!{/i}":
            pass
    scene BG char Kira shower-06
    show Kira shower-Max 06
    Max_22 "{i}( Да... Я кончаю... Прямо ей в рот! О боже... Она так смачно посасывает головку моего члена... Вот это отсос, так отсос! Кажется, это самое офигенное развлечение в мире! ){/i}"

    show Kira shower-Max 07
    Kira_05 "Ну вот. Понравилось? И ни капли мимо..."
    Max_05 "Ты потрясающая! Это было классно!"
    Kira_01 "Я рада, что смогла снять твоё напряжение. А теперь, давай разбегаться, пока нас кто-нибудь не увидел..."
    Max_04 "Конечно!"
    $ renpy.end_replay()
    $ mgg.cleanness = 100
    $ SetCamsGrow(house[3], 250)
    $ poss['aunt'].OpenStage(5)
    $ dcv['kiratalk'].stage += 1
    $ dcv['kiratalk'].set_lost(1)
    $ spent_time += 30
    $ current_room = house[0]
    $ append_photo('01-Kira', 12)
    jump Waiting


label kira_about_cuni:
    # стартовая фраза "Не злишься на меня, тётя Кира?"
    Kira_13 "А что, Макс, должна?"
    Max_07 "Наверно, да. Я ведь не заглянул к тебе в душ..."
    menu:
        Kira_14 "Я не злюсь, но немного расстроена... А разозлюсь я, если ты просто забыл!"
        "Нет, я не забыл... были дела с Лизой.":
            pass
        "Нет, просто были дела с Алисой.":
            pass
    Kira_07 "Должно быть, это было что-то важное, раз ты решил не приходить к своей любимой, совершенно голой тёте в душ!"
    Max_09 "Сестрёнкам надо помогать! А твоё приглашение всё ещё в силе?"
    if GetWeekday(day)!=5:
        Kira_04 "Если только ночью, Макс. Когда я вернусь с работы. Если, конечно, ты не завалишься спать до моего возвращения..."
    else:
        Kira_04 "Если только завтра ночью, Макс. Когда я вернусь с работы. Если, конечно, ты не завалишься спать до моего возвращения..."
    Max_02 "Не завалюсь, буду тебя ждать!"
    Kira_01 "Посмотрим, Макс."
    $ dcv['kiratalkcuni'].set_lost(1)
    $ spent_time += 10
    jump Waiting


label kira_about_photo2:
    # стартовая фраза "Так когда будем снова фотографироваться, тётя Кира?"   #доступна через 10 дней после первой фотосессии
    # #фоновая картинка с загорающей Кирой
    scene BG char Kira sun-talk-01
    show Kira sun-talk 01
    show Max sun-talk 01
    Kira_05 "Что, Макс, не терпится?"
    Max_01 "Ага! Я готов! А ты?"

    # #если не суббота или поздно по времени
    if not _in_replay and (GetWeekday(day)!=6 or not check_only_home('kira')):
        Kira_04 "У меня тоже всё готово, но как ты знаешь, сейчас мы дома не одни. Представь, что подумают, если увидят такое..."
        Max_07 "Э... Какое?"
        Kira_07 "А это сюрприз, Макс. Так что, наберись терпения..."
        Max_04 "Понял..."
        $ dcv['kiratalk'].set_lost(1)
        $ talks['kt.ft2'].req = "all([GetWeekday(day)==6, dcv['kiratalk'].stage==7, kira.plan_name=='sun', not expected_photo, dcv['kira.nextphoto'].enabled, dcv['kira.nextphoto'].stage==1, dcv['kira.nextphoto'].done, dcv['kiratalk'].done])"
        $ spent_time += 10
        jump Waiting

    #если суббота и подходящее время
    menu:
        Kira_04 "Я тоже готова. На этот раз мне нужна особая фотосессия..."
        "Всё, что угодно!":
            Kira_07 "Что, даже не любопытно о чём речь? Ну что же, тогда пойдём в комнату твоей мамы..."
            Max_07 "Хорошо, а зачем?"
        "Особая? Это какая?":
            Kira_07 "Увидишь. Пойдём в комнату твоей мамы."
            Max_07 "Хорошо, а зачем?"
    Kira_02 "А вот и узнаешь. Пойдём быстрее, нужно всё успеть сделать, пока наши не вернулись с шопинга..."
    Max_01 "Ага..."

    #annroom-morning-01 + dresses-kira-04a
    scene BG annroom-morning-01
    show Kira dresses 04a
    Kira_06 "В общем, на этот раз мне нужна серия снимков в стиле... БДСМ... Верёвки, кожа, вот это всё..."
    Max_03 "Ого, круто!"
    Kira_07 "А чего ты так радуешься, я же не сказала кого привязывать будем..."
    Max_02 "А мне не важно! Но я - фотограф, так что привязывать будем однозначно тебя."
    Kira_05 "Да ты полон сюрпризов, Макс! Рада, что ты не против. Подожди минутку за дверью, я переоденусь..."
    Max_01 "Хорошо, жду."

    #на фоне двери в комнату Анны
    scene location house annroom door-day
    menu:
        Max_04 "{i}( Верёвки и кожа - это интересно! А ещё интереснее то, насколько далеко мне удастся зайти с тётей Кирой во время этой фотосессии... ){/i}"
        "{i}ждать{/i}":
            #annroom-morning-01 + dresses-kira-04
            scene BG annroom-morning-01
            show Kira dresses 2-04
            Kira_06 "Ну, как тебе мой наряд? Уже не жалеешь, что столько ждал у двери?"
            Max_05 "Что ты, тётя Кира... Выглядишь шикарно!"

        "{i}подсмотреть за Кирой{/i}":
            #dresses-annroom-01 + dresses-max-(01a/01b) + dresses-kira-01
            scene BG char Ann voyeur-01
            show Kira dresses 2-01
            show FG voyeur-morning-01c
            Max_03 "{i}( Ого! Вот это не дурно тётя Кира принаряжается! Надеюсь, она меня не заметит, а то... Хотя, о чём это я?! Это же Кира! Её это даже заводит... ){/i}"

            #dresses-annroom-01 + dresses-max-(01a/01b) + dresses-kira-02
            show Kira dresses 2-02
            Max_07 "{i}( Ну прямо всё самое интересное закрывает этим кожаным костюмом... Хотя, мне же интереснее будет её раздевать! Мне кажется или она как-то слишком эротично одевается? ){/i}"

            #dresses-annroom-01 + dresses-max-(01a/01b) + dresses-kira-03
            show Kira dresses 2-03
            Kira_06 "Думаешь, я не заметила, как ты подглядываешь? Именно этого я и ожидала от тебя. Ну, как тебе мой наряд?"
            Max_04 "Выглядишь шикарно!"

    $ expected_photo = []

    #photoshot-02-kira-01 + photocamera
    scene photoshot 02-Kira 01
    Kira_04 "Вот и отлично. Тогда, начнём. Нужно сделать несколько кадров на фоне комнаты и кровати. Кстати, не говори маме, что я в сапогах тут топталась..."
    show FG photocamera
    play sound "<from 1>audio/PhotoshootSound.ogg"
    Max_01 "Я - могила! О да, так и задержись, как будто девушка с картины на стене не удержалась и решила прикоснуться к твоей груди! {p=1.5}{nw}"
    hide FG
    extend "Готово! Теперь приляг на кровать как-нибудь интересно..."   #открывает снимок 01
    $ expected_photo.append('01')

    #photoshot-02-kira-02 + photocamera
    scene photoshot 02-Kira 02
    Kira_05 "Так будет достаточно интересно? Или слишком скромно для первых кадров?"
    show FG photocamera
    play sound "<from 1>audio/PhotoshootSound.ogg"
    Max_02 "В самый раз! Сейчас только ракурс подберу поинтереснее и будет отлично... {p=1.5}{nw}"
    hide FG
    extend "Ага, есть! А дальше мы щёлкнем твою попку ещё раз, ложись как будто устала и отдыхаешь..."   #открывает снимок 02
    $ expected_photo.append('02')

    #photoshot-02-kira-03 + photocamera
    scene photoshot 02-Kira 03
    Kira_09 "После всех этих шнурков и застёжек мне не составит труда изобразить усталость... Но сил на всё, что будет дальше, у меня хватит."
    show FG photocamera
    play sound "<from 1>audio/PhotoshootSound.ogg"
    Max_03 "А это у тебя молния на трусиках, тётя Кира? {p=1.5}{nw}"
    hide FG
    extend "С радостью бы её расстегнул, но давай начнём с верху. Наверно, во всей этой коже очень жарко..."   #открывает снимок 03
    $ expected_photo.append('03')

    #photoshot-02-kira-04 + photocamera
    scene photoshot 02-Kira 04
    Kira_07 "Очень, Макс! Да и преступление, скрывать моих прелестных девочек от глаз. Не так ли?"
    show FG photocamera
    play sound "<from 1>audio/PhotoshootSound.ogg"
    Max_04 "О да... Твоя грудь никого не оставит равнодушным. Она у тебя - огонь! {p=1.5}{nw}"
    hide FG
    extend "Снимок готов. А что ты там говорила про верёвки?"   #открывает снимок 04
    $ expected_photo.append('04')

    menu:
        Kira_04 "Да, я думаю самое время привязать меня к кровати! Только так, чтобы выглядело натурально, хорошо?"
        "{i}крепко привязать Киру{/i}":   #открывает снимок 05
            #photoshot-02-kira-05 + photocamera
            scene photoshot 02-Kira 05
            show FG photocamera
            play sound "<from 1>audio/PhotoshootSound.ogg"
            $ expected_photo.append('05')
            menu:
                Kira_05 "Отлично, Макс. Теперь нам нужно сделать несколько снимков, на которых я должна выглядеть очень развратно! Я знаю, что тебе уже неймётся..."
                "{i}раздеться и поиграть с её грудью{/i}":   #открывает снимок 06
                    #photoshot-02-kira-06 + photocamera
                    $ expected_photo.append('06')
                    scene photoshot 02-Kira 06
                    Kira_13 "Какой же ты непослушный мальчишка! Такое со своей тётей вытворяешь... Быстро развяжи меня, а не то твоя мама всё узнает! Это я так, для придания соответствующего настроения..."
                    show FG photocamera
                    play sound "<from 1>audio/PhotoshootSound.ogg"
                    Max_01 "Я понял. И что же она узнает? Какая у неё бессовестная и развратная сестра? {p=1.5}{nw}"
                    hide FG
                    extend "И даже, как любишь сосать мой член расскажешь?"
                    menu:
                        Kira_10 "Думаешь, тебе удастся заткнуть мне рот?! Твоя мама узнает всё!"
                        "А вот это вряд ли! (дать ей пососать свой член)":   #открывает снимок 07
                            pass
                "{i}раздеться и дать ей пососать свой член{/i}":   #пропускает снимок 06, открывает снимок 07
                    pass

    $ expected_photo.append('07')
    #annroom-shot-02-max&kira-01-f + annroom-shot-02-max&kira-01
    scene BG char Kira annroom-shot 01
    show Kira annroom-shot 01
    play sound "<from 1>audio/PhotoshootSound.ogg"
    Max_02 "Да... Вот так... Эта фотосессия мне нравится гораздо больше... Я могу делать с тобой всё, что только придёт в голову! Хотя, сперва я бы сделал ещё один снимок..."   #открывает снимок 08

    #photoshot-02-kira-08 + photocamera
    $ expected_photo.append('08')
    scene photoshot 02-Kira 08
    show FG photocamera
    play sound "<from 1>audio/PhotoshootSound.ogg"
    Kira_03 "Ну Макс... Ты увидел, что хотел? Нравится? Ой, я хотела сказать, теперь застегни молнию и развязывай меня..."
    hide FG
    menu:
        Max_07 "Я бы с этим не торопился. А ещё, я бы лучше снял с тебя эти кожаные трусы. Так что отчасти твоё желание по развязыванию я осуществлю... Чтобы было удобнее..."
        "{i}ласкать её киску языком{/i}":
            #annroom-shot-02-max&kira-03-f + annroom-shot-02-max&kira-03
            scene BG char Kira annroom-shot 03
            show Kira annroom-shot 03
            menu:
                Kira_06 "Оу... Теперь понятно, что будет удобнее... ахх... делать... там. Да... Ты стал очень хорош в этом деле. Ох, обожаю, когда ты так делаешь!"
                "{i}активнее работать языком и губами{/i}":
                    pass
            #annroom-shot-02-max&kira-02-f + annroom-shot-02-max&kira-02
            scene BG char Kira annroom-shot 02
            show Kira annroom-shot 02
            menu:
                Kira_08 "Да... Так приятно... Ммм... Может, самое время, пустить в дело кое-что ещё..."
                "{i}проникнуть в её киску пальцами{/i}":
                    #annroom-shot-02-max&kira-03-f + annroom-shot-02-max&kira-03a
                    scene BG char Kira annroom-shot 03
                    show Kira annroom-shot 03a
                    menu:
                        Kira_09 "Макс... Если ты продолжишь так и дальше, я не смогу сдержаться... У тебя такие ловкие пальчики... Да..."
                        "{i}трахать её пальцами{/i}":
                            #annroom-shot-02-max&kira-02-f + annroom-shot-02-max&kira-02a
                            scene BG char Kira annroom-shot 02
                            show Kira annroom-shot 02a
                            menu:
                                Kira_10 "Да... Ещё чуть-чуть и я кончу... Может быть, ты воспользуешься чем-то потолще пальчиков и язычка? Только не торопись..."
                                "{i}тереться членом о её киску{/i}":
                                    #annroom-shot-02-max&kira-04-f + annroom-shot-02-max&kira-04
                                    scene BG char Kira annroom-shot 04
                                    show Kira annroom-shot 04
                                    Kira_07 "Ох, Макс... Должно быть тебе уже неймётся засадить мне! А уж как сильно я этого хочу..."   #уход на "медленно проникнуть в неё членом"
                                "{i}медленно проникнуть в неё членом{/i}":
                                    pass

                "{i}тереться членом о её киску{/i}":
                    #annroom-shot-02-max&kira-04-f + annroom-shot-02-max&kira-04
                    scene BG char Kira annroom-shot 04
                    show Kira annroom-shot 04
                    Kira_07 "Ох, Макс... Должно быть тебе уже неймётся засадить мне! А уж как сильно я этого хочу..."   #уход на "медленно проникнуть в неё членом"

        "{i}проникнуть в её киску пальцами{/i}":
            #annroom-shot-02-max&kira-03-f + annroom-shot-02-max&kira-03a
            scene BG char Kira annroom-shot 03
            show Kira annroom-shot 03a
            menu:
                Kira_06 "Оу... Теперь понятно, что будет удобнее... ахх... делать... там. Да... Ты стал очень хорош в этом деле. Ох, обожаю, когда ты так делаешь!"
                "{i}трахать её пальцами{/i}":
                    #annroom-shot-02-max&kira-02-f + annroom-shot-02-max&kira-02a
                    scene BG char Kira annroom-shot 02
                    show Kira annroom-shot 02a
                    menu:
                        Kira_08 "Да... Так приятно... Ммм... Может, самое время, пустить в дело кое-что ещё..."
                        "{i}тереться членом о её киску{/i}":
                            #annroom-shot-02-max&kira-04-f + annroom-shot-02-max&kira-04
                            scene BG char Kira annroom-shot 04
                            show Kira annroom-shot 04
                            Kira_07 "Ох, Макс... Должно быть тебе уже неймётся засадить мне! А уж как сильно я этого хочу..."   #уход на "медленно проникнуть в неё членом"
                        "{i}медленно проникнуть в неё членом{/i}":
                            pass

    # медленно проникнуть в неё членом
    #annroom-shot-02-max&kira-04a
    scene Kira annroom-shot 04a
    menu:
        Kira_09 "Ухх, давно во мне не было такого большого... Д-а-а... Вот так, Макс... Не спеши, дай мне привыкнуть к нему... А вот теперь, трахни свою тётю!"
        "{i}трахать тётю Киру{/i}":
            pass

    #annroom-shot-02-max&kira-05-f + annroom-shot-02-max&kira-05
    scene BG char Kira annroom-shot 05
    show Kira annroom-shot 05
    $ expected_photo.append('09')
    menu:
        Kira_11 "Ах, я правильно сделала, что решила подготовить тебя к первому разу! Ммм... Иначе, ты бы кончил почти сразу, как проник в меня... А я хотела, чтобы мой любимый племянник насладился сексом со мной! Ты ещё и фотографировать успеваешь! Ох..."   #открывает снимок 09
        "{i}ускориться{/i}":
            pass

    #annroom-shot-02-max&kira-05a
    scene Kira annroom-shot 05a
    menu:
        Kira_12 "Да, так... Глубже, быстрее! К чёрту нежности, оттрахай меня так, как тебе хочется! Да, вот так... Ох, я кончаю... Д-а-а..."
        "{i}кончить Кире на живот{/i}":
            pass

    #annroom-shot-02-max&kira-06
    scene Kira annroom-shot 06
    Kira_07 "Ну что, полегчало, Макс? Как тебе первый раз? Понравилось так, что трахал бы меня весь день?"

    #annroom-shot-02-max&kira-05-f + annroom-shot-02-max&kira-06a
    scene BG char Kira annroom-shot 05
    show Kira annroom-shot 06a
    Max_05 "Фух, было обалденно! Но я бы и ночью не перестал это делать!"
    menu:
        Kira_04 "Ну всё, развязывай меня, а то если кто-то придёт, будет... неловко, правда?"
        "{i}развязать Киру{/i}":
            pass

    #annroom-watch-01 + annroom-watch-01-max-01d + annroom-watch-01-kira-01
    scene BG annroom-watch-01
    show Kira annroom-watch 01
    show Max annroom-watch 01d
    Kira_07 "Спасибо, Макс, что не оставил меня так до прихода твоей мамы с Эриком... Пришлось бы им многое объяснять, а мы же не хотим, чтобы наши секреты стали кому-то известны, правда?"
    Max_02 "Конечно, тётя Кира!"
    Kira_05 "Мы отлично повеселились. Только не вздумай ко мне приставать! На публике, разумеется. А теперь я пойду. Приятного дня, если он может быть ещё приятнее..."
    Max_01 "Спасибо за фотосессию!"

    $ renpy.end_replay()
    $ SetCamsGrow(house[2], 300)
    $ poss['aunt'].OpenStage(6)
    $ dcv['kiratalk'].stage += 1
    $ dcv['kiratalk'].set_lost(1)
    $ append_photo('02-Kira', 9)

    $ spent_time += 60
    jump Waiting
