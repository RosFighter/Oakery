
label AliceTalkStart:

    $ dial = TalkMenuItems()

    $ __CurShedRec = GetPlan(plan_alice, day, tm)
    if __CurShedRec.talklabel is not None:
        call expression __CurShedRec.talklabel from _call_expression

    if len(dial) > 0:
        $ dial.append((_("{i}уйти{/i}"), "exit"))
    else:
        jump Waiting


    $ renpy.block_rollback()
    Alice_00 "Ну, Макс, чего надо?" nointeract

    $ rez =  renpy.display_menu(dial)

    if rez != "exit":
        $ __mood = GetMood("alice")[0]
        if rez in gifts['alice']:
            if renpy.has_label(rez.label):
                call expression rez.label from _call_expression_8
        elif __mood < talks[rez].mood:
            if __mood < -2: # Настроение -4... -3, т.е. всё ну совсем плохо
                jump Alice_badbadmood
            elif __mood < 0: # Настроение -2... -1, т.е. всё ещё всё очень плохо
                jump Alice_badmood
            else: # Настроение хорошее, но ещё недостаточное для разговора
                jump Alice_normalmood
        elif talks[rez].kd_id != "" and talks[rez].kd_id in cooldown and not ItsTime(cooldown[talks[rez].kd_id]):
            jump Alice_cooldown
        elif renpy.has_label(talks[rez].label): # если такая метка сушествует, запускаем ее
            call expression talks[rez].label from _call_expression_1
        jump AliceTalkStart       # а затем возвращаемся в начало диалога, если в разговоре не указан переход на ожидание

    jump Waiting            # если же выбрано "уйти", уходим в после ожидания


label Alice_badbadmood:
    menu:
        Alice_09 "Да пошёл ты! Не хочу тебя видеть даже!"
        "Ок...":
            jump Waiting
        "Я хотел извиниться":
            jump Alice_asksorry


label Alice_badmood:
    menu:
        Alice_09 "Макс, отвали! Я не хочу с тобой разговаривать."
        "Ок...":
            jump Waiting
        "Я хотел извиниться":
            jump Alice_asksorry


label Alice_asksorry:
    menu:
        Alice_13 "Хотел извиниться? Каким образом?"
        "Ты знаешь, я передумал...":
            jump Waiting


label Alice_normalmood:
    menu:
        Alice_09 "Макс, давай не сейчас..."
        "Ок...":
            jump Waiting


label Alice_cooldown:
    Alice_09 "Макс... Не сейчас."
    Max_00 "Ладно..."
    jump AfterWaiting


label wash_dishes_alice:
    $ talk_var["alice_dw"] = 1
    menu:
        Alice_13 "Хочешь о посуде поговорить или пришёл помочь?"
        "Давай, я домою остальное":
            menu:
                Alice_07 "Что это с тобой? Но я не откажусь. И... спасибо."
                "{i}мыть посуду{/i}":
                    if GetRelMax("alice")[0] < 3:
                        $ AddRelMood("alice", 10, 60)
                    else:
                        $ AddRelMood("alice", 0, 60)
                    $ dishes_washed = True
                    $ spent_time = max((60 - int(tm[-2:])), 30)
                    scene BG crockery-morning-00
                    $ renpy.show("Max crockery-morning 01"+mgg.dress)
                    menu:
                        Max_11 "И почему здесь нет посудомоечной машины..."
                        "{i}закончить{/i}":
                            $ cur_ratio = 2
                            jump Waiting
        "Нет, просто хотел поглазеть":
            menu:
                Alice_09 "Знаешь что, вали отсюда, пока мокрой тряпкой по голове не получил!"
                "{i}уйти{/i}":
                    $ spent_time = 10
                    jump Waiting


label talkblog1:
    if "blog" in cooldown:
        if ItsTime(cooldown["blog"]): # кулдаун прошел, можно поговорить
            $ del cooldown["blog"]
        else:
            jump Alice_cooldown

    menu:
        Alice_00 "А типа ты не знаешь? Позлорадствовать пришёл?"
        "Нет, хотел просто больше узнать":
            menu:
                Alice_02 "Что, даже не начнёшь подкалывать? И что ты хотел узнать?"
                "Расскажи, что ты там делаешь":
                    menu:
                        Alice_13 "Ну, пока наши вещи не пропали во время переезда, я показывала как наносить лак, как применять различные средства и делилась разными хитростями..."
                        "Хитростями? А откуда ты сама всё это узнала?":
                            menu:
                                Alice_01 "Да у других таких же блогеров подсмотрела, конечно. Все так делают! Ну и сама в интернете разное читаю, изучаю..." #Alice_02
                                "Тема бьюти разве единственная?":
                                    menu:
                                        Alice_00 "Нет, но мне это всё как-то по душе. Говорят, у человека лучше получается то, что нравится. А мне это нравится..."
                                        "Давай что-то придумаем вместе!":
                                            jump .together
                                        "Может быть, изменить твой блог?":
                                            jump .otherway
                                "Понятно. Ну, и что теперь?":
                                    jump .whatnow
                        "И что теперь без этих своих вещей делать будешь?":
                            jump .whatnow
                "Как планируешь развиваться?":
                    menu:
                        Alice_00 "Развиваться? Шутишь? Все мои вещи, лаки, материалы и прочее было в том контейнере, который пропал. Теперь у меня нет ничего..."
                        "Что, совсем всё пропало?":
                            menu:
                                Alice_13 "Совсем всё. У меня даже нет подходящей одежды, чтобы вести блог. Нельзя же постоянно быть в одной майке перед зрителями..."
                                "Может, это твоя фишка. Да и майка счастливая...":
                                    menu:
                                        Alice_00 "Ага, потому что единственная, да? А ещё какая фишка? Нет материалов, нечего показывать?"
                                        "Да уж, грусть-печаль...":
                                            jump .sad
                                        "Тебе как-то можно помочь?":
                                            jump .help
                                "Разве это важно?":
                                    menu:
                                        Alice_00 "Очень важно. Ну, ладно, чёрт с ней, с одеждой, а что мне в блоге показывать? Как себя правильно расчёсывать? У меня нет ничего..."
                                        "Да уж, грусть-печаль...":
                                            jump .sad
                                        "Тебе как-то можно помочь?":
                                            jump .help
                        "Постой, и что теперь будет?":
                            jump .whatnow
        "Много подписчиков уже?":
            menu:
                Alice_13 "Да ты издеваешься, да?"
                "Нет. С чего ты взяла?":
                    pass
                "Ты о чём, вообще?":
                    pass
            menu .no:
                Alice_00 "Типа ты не в курсе, что у нас пропала большая часть вещей во время переезда?"
                "Да ладно?":
                    $ cooldown["blog"] = CooldownTime("03:00")
                    Alice_09 "Знаешь что, Макс, отвали!"
                    Max_00 "Ну и ладно..."
                    $ AddRelMood("alice", -5, -50)
                    jump .end
                "В курсе, конечно":
                    menu:
                        Alice_13 "Ну, вот среди тех вещей было всё, что я использовала для ведения своего блога. Одежда, различные лаки, кремы... вообще всё!"
                        "Мне очень жаль...":
                            jump .sad
                        "И как тебе помочь?":
                            jump .help
        "Заработала уже на нём что-то?":
            menu:
                Alice_13 "Макс, тебе заняться нечем, кроме как меня доставать? Ты же знаешь, что у меня всё пропало!"
                "Что пропало? Ты о чём?":
                    jump .no
                "Ты о тех вещах во время переезда?":
                    menu:
                        Alice_00 "Конечно! Там же было вообще всё, что мне нужно для ведения блога. Шмотки, мои любимые лаки, косметика... вообще всё!"
                        "Да, печально...":
                            jump .sad
                        "Тебе можно как-то помочь?":
                            jump .help
    menu .sad:
        Alice_13 "Вот-вот... Надо было всё с собой брать, а не складывать в тот контейнер.."
        "Может быть, попробуем вместе найти решение?":
            jump .together
        "Я обязательно придумаю, что можно с этим сделать":
            jump .findout

    menu .help:
        Alice_01 "Ты у нас внезапно стал миллионером? Или просто деньги появились? Самый простой способ - это купить недостающее. Ну, или найти то, что пропало" #Alice_07
        "Денег у меня нет...":
            menu:
                Alice_13 "А без денег тут ничем не поможешь. Вообще, я в депрессии из-за всей этой истории. Вся жизнь перевернулась..."
                "Вся жизнь? Но это всё к лучшему же. Такой дом, бассейн, место отличное!":
                    menu:
                        Alice_00 "Да, в этом плане ты прав, Макс. Но я хотела чего-то добиться. Стать известным блогером и заработать кучу денег. А теперь..."
                        "Давай что-то придумаем вместе!":
                            jump .together
                        "Ты всё ещё можешь. Может быть, просто смени формат блога":
                            jump .otherway
                "Не грусти. Могло быть и хуже":
                    menu:
                        Alice_00 "Верно. Но всё равно, это всё очень грустно. Я даже не представляю, как теперь быть..."
                        "Давай что-то придумаем вместе!":
                            jump .together
                        "Ты всё ещё можешь. Может быть, просто смени формат блога":
                            jump .otherway
                "Может быть, зарабатывать на чём-то другом?":
                    jump .otherway
        "Я не детектив, чтобы искать вещи...":
            menu:
                Alice_00 "А кто ты тогда? Чем же именно можешь помочь?"
                "Ну, у меня много разных идей":
                    menu:
                        Alice_07 "Много разных идей? Например?"
                        "Давай что-то придумаем вместе!":
                            jump .together
                        "Может быть, изменить твой блог?":
                            jump .otherway
                "Советами!":
                    menu:
                        Alice_06 "И кому уже помогли твои советы? Знаешь, советчиков много. Лучше бы что-то конкретное предложил..."
                        "Давай что-то придумаем вместе!":
                            jump .together
                        "Может быть, изменить твой блог?":
                            jump .otherway

    menu .whatnow:
        Alice_00 "Не знаю, если честно. Мне даже показаться перед зрителями не в чем. Какой же я бьюти-блогер, если на мне всегда одна и та же одежда..."
        "Тебе как-то можно помочь?":
            jump .help
        "Может быть, заняться чем-то другим?":
            jump .otherway
        "Да уж, грусть-печаль...":
            jump .sad

    menu .otherway:
        Alice_00 "Ты о чём? Есть какие-то мысли?"
        "Мы можем с тобой вместе что-то придумать":
            jump .together
        "Давай, я подумаю и, когда будут мысли, продолжим разговор":
            jump .findout
        "Пока нет, но я подумаю что можно сделать":
            jump .findout

    label .together:
        $ talk_var["blog"] = 2
        menu:
            Alice_14 "Вместе? Ещё ничего нет, а уже в партнёры набиваешься?"
            "Ну если придумаю что-то, то почему нет?":
                Alice_01 "Ну, если придумаешь. Если. Да и смотря что... Сильно удаляться от этой темы не хочется. Но попробовать что-то новое можно... В общем, когда что-то придумаешь, тогда и поговорим..." #Alice_07
                Max_00 "Хорошо!"
            "Конечно! Будет у нас семейный бизнес!":
                menu:
                    Alice_07 "Семейный бизнес? На моём блоге? Может ещё и сам вести будешь?"
                    "Ну да, многие так делают!":
                        pass
                    "Нет, из меня ведущий так себе...":
                        pass
                    "Попробовать то можем?":
                        pass
                Alice_00 "Знаешь, мне кажется, что это всё пустые разговоры. Сначала что-то предложи, тогда и обсудим..."
                Max_00 "Хорошо, я подумаю"
            "Ну, можно попробовать же?":
                Alice_00 "Попробовать можно, но сначала надо понять что. Ты пока ничего конкретного не предложил. Когда будут идеи, тогда и подходи..."
                Max_00 "Ладно, когда будут идеи, обсудим..."
        jump .end

    label .findout:
        $ talk_var["blog"] = 2
        menu:
            Alice_07 "Ну, давай. Сомневаюсь, конечно, что это не пустая болтовня, но вдруг... Удиви меня!"
            "Постараюсь...":
                Alice_00 "Постарайся. Я тоже буду думать. Но бросать блог не хочу. Может быть и правда стоит сменить формат, но на какой?"
                Max_00 "Договорились!"
            "Но ничего не обещаю":
                Alice_00 "А я другого и не жду. Если бы у тебя были мысли, ты бы и сам мог на таком заработать... В общем, будем думать..."
                Max_00 "Ага..."
            "Обещаю, что-нибудь придумаю":
                Alice_06 "Обещание, конечно, обнадёживает... Но я бы не стала на твоём месте что-то обещать. Конечно, если у тебя уже нет идей. Так или иначе, я тоже буду думать. Может быть, и правда не всё так плохо..."
                Max_00 "Да прорвёмся!"

    label .end:
        if talk_var["blog"] == 2:
            $ spent_time = 10
            jump Waiting


label alice_talk_tv:
    Alice_00 "Нет, садись. Тут места много..."
    $ talk_var["alice_tv"] = 1
    $ renpy.show("Max tv-closer "+pose3_1+mgg.dress)
    Max_00 "Хорошо. Что смотришь?"
    menu:
        Alice_13 "Да так, всякую ерунду. Я просто отдыхаю, и мне без разницы, что смотреть. Поэтому смотрю всё подряд..."
        "Ну, давай смотреть всё подряд...":
            $ SetCamsGrow(house[4], 110)
            $ __mood = 50
            Max_11 "{i}По телеку сегодня нет ничего интересного... Ни порнушки, ни даже эротики... А было бы забавно посмотреть такое с сестрёнкой...{/i}"
            Max_00 "Ладно, пойду я..."
    $ spent_time = max((60 - int(tm[-2:])), 40)
    $ AddRelMood("alice", 0, __mood)
    $ cur_ratio = 0.5
    jump Waiting


label alice_aboutbooks:
    menu:
        Alice_13 "Книжку, очевидно..."
        "Спасибо, кэп!":
            Alice_01 "Так чего хотел, Макс?"
            Max_00 "Хотел узнать что именно читаешь..."
        "Очень смешно...":
            Alice_01 "Так чего хотел, Макс?"
            Max_00 "Хотел узнать что именно читаешь..."
        "Это понятно, а какую? Только не говори, что бумажную...":
            Alice_01 "Ну вот... Хотела пошутить, и ты так меня обломал... А если серьёзно, то чего хотел?"
            Max_00 "Да вот и хотел узнать, что именно читаешь..."
    menu:
        Alice_02 "Забавно. А тебе не всё равно? Или делать нечего?"
        "Не всё равно, раз спрашиваю":
            pass
        "Так ты скажешь или нет?":
            pass
    menu:
        Alice_00 "Не скажу. Сиди теперь и гадай! \n\n{i}Алиса прикрыла обложку рукой{/i}"
        "Какой-то дамский роман?":
            pass
        "Какие-то сопли с сахаром?":
            pass
        "Неужели справочник по квантовой механике?":
            pass
    Alice_01 "Думай, что хочешь, а я всё равно не скажу."
    Max_00 "Ну и ладно!"
    $ spent_time += 10
    $ SetPossStage("secretbook", 0)
    $ AvailableActions["searchbook"].enabled = True
    jump Waiting


label first_talk_smoke:
    $ __mood = 0
    Alice_13 "Упс. Макс, ты ничего не видел!"
    $ SetPossStage('smoke', 0)
    Max_08 "Алиса, ты куришь?!"
    $ dcv['smoke'].stage = 1
    menu:
        Alice_12 "Нет, блин, просто зажгла сигарету, посмотреть как горит... Давай так, ты уйдёшь и сделаешь вид, что ничего не было, хорошо?"
        "А если уйду, что мне за это будет?":
            Alice_16 "Вот если не уйдёшь, то узнаешь, что тебе за это будет! Бегом отсюда!"
            Max_09 "Может быть я и уйду, но..."
        "Ну ок, только я ничего не обещаю...":
            pass
    menu:
        Alice_14 "Что это значит? Шантажировать меня вздумал?!"
        "Ну да. Мама что с тобой сделает, если узнает?":
            menu:
                Alice_13 "Макс, я тебя по-человечески прошу. Сделай вид, что ничего не было. Я не хочу расстраивать маму..."
                "Не хочешь расстраивать маму или получить по заднице?":
                    menu:
                        Alice_12 "Может быть, и то и другое. Ну так как, Макс?"
                        "Мы всё ещё можем договориться...":
                            jump .talk
                        "Посмотрим...":
                            jump .maybe
                        "Ну ладно, ладно...":
                            $ __mood += 75
                            jump .goodend
                "Мы всё ещё можем договориться...":
                    jump .talk
                "Посмотрим...":
                    jump .maybe
                "Ну ладно, ладно...":
                    $ __mood += 100
                    jump .goodend
        "Ну, мы можем договориться":
            jump .talk
        "Как знать, может быть...":
            jump .maybe
        "Нет, конечно. Мне жизнь дорога!":
            $ __mood += 50
            jump .goodend

    menu .talk:
        Alice_13 "Договориться? Ну ладно. Чего ты хочешь?"
        "Если заплатишь, буду молчать":
            menu:
                Alice_12 "Макс, ты же знаешь, что я на мели. У меня нет денег. Ну, точнее есть, но баксов 10. Тебя устроит?"
                "Ну, давай":
                    $ __mood -= 50
                    $ spent_time += 10
                    $ money += 10
                    $ flags['smoke.request'] = "money"
                    menu:
                        Alice_13 "Сейчас сбегаю за деньгами...\nВот, держи $10, и теперь-то уж точно ты ничего не видел. Так?"
                        "Так!":
                            jump .end
                        "Как знать...":
                            menu:
                                Alice_16 "И как это понимать, Макс? Мы же договорились! Ну ты и гад... Всё, вали отсюда!"
                                "Ну, как скажешь...":
                                    jump .end

                "Нет, этого мало...":
                    $ __mood -= 100
                    menu:
                        Alice_12 "Ну... больше у меня нет. Может, просто забудем обо всём?"
                        "Покажи сиськи!":
                            jump .bad
                        "Сними трусы!":
                            jump .bad
                        "Отсоси мне!":
                            jump .suck
                        "Ты будешь мне должна услугу":
                            jump .owe
        "Покажи сиськи!":
            jump .bad
        "Сними трусы!":
            jump .bad
        "Отсоси мне!":
            jump .suck
        "Ты будешь мне должна услугу":
            jump .owe

    menu .bad:
        Alice_15 "Что?! Ну ты хам! Всё, быстро свалил!"
        "Ну, как скажешь...":
            $ __mood -= 100
            jump .end

    menu .suck:
        Alice_14 "Что?! Отсоси себе сам! Пошёл вон отсюда!"
        "Ну, как скажешь...":
            $ __mood -= 150
            jump .end

    menu .owe:
        Alice_06 "И какую услугу я тебе буду должна? Например?"
        "Покажешь сиськи, когда попрошу...":
            pass
        "Разденешься, когда скажу...":
            pass
        "Отсосёшь, когда нужно будет...":
            jump .suck
        "Я ещё не решил...":
            menu:
                Alice_03 "Ну, вот когда решишь, тогда и поговорим. А теперь иди отсюда, пока ещё можешь!"
                "Ну, как скажешь...":
                    jump .end

    menu:
        Alice_15 "Да ты что?! А не охренел ли ты, мальчик? Это всего лишь сигарета, а запросы у тебя... Всё, свалил отсюда!"
        "Ну, как скажешь...":
            $ __mood -= 100
            jump .end

    menu .maybe:
        Alice_12 "Сам смотри... Воевать со мной - себе дороже. И, вообще, иди отсюда. Я уже взрослая и могу делать что хочу..."
        "Ну, как скажешь...":
            pass

    menu .goodend:
        Alice_05 "Вот это другое дело. Ладно, разговор окончен..."
        "Хорошо...":
            pass

    label .end:
        $ spent_time += 10
        $ AddRelMood("alice", 0, __mood)
        jump Waiting


label second_talk_smoke:
    $ __mood = 0
    menu:
        Alice_12 "А, Макс... Чего хотел?"
        "Да хотел узнать, что ты куришь?":
            menu:
                Alice_03 "Это так важно для тебя? Почему спрашиваешь?"
                "Просто любопытно":
                    Alice_00 "Любопытный он... Это сигареты Lucky Strike..."
                "Для поддержания разговора":
                    Alice_00 "Да уж, поддержание разговора... А курю я сигареты Lucky Strike..."
                "А, не важно...":
                    Alice_00 "Ну, раз не важно, то и не скажу..."
                    Max_01 "Конечно, если это секрет..."
                    Alice_01 "Вот ты зануда, Макс! Обычные сигареты Lucky Strike..."
        "Нет, ничего...":
            jump Waiting
    Max_07 "Они же не женские?!"
    menu:
        Alice_13 "Ну да... Просто нравится именно эти. Их наш отец курил, сначала запах нравился. Когда выросла, попробовала и теперь вот втянулась..."
        "Скучаешь о нём?":
            Alice_00 "Ну так, не очень, если честно. Уже слишком много времени прошло... А сигареты - это так, привычка..."
            Max_00 "Может, и мне попробовать?"
        "Может, и мне попробовать?":
            pass
    menu:
        Alice_06 "Нет уж, самой мало. Знаешь, как сложно их достать? Особенно, чтобы мама не узнала..."
        "Может быть, тебе помочь?":
            $ __mood += 100
            menu:
                Alice_02 "С чем? Можешь доставать такие сигареты?"
                "Ну, я попробую через интернет":
                    pass
                "Может быть...":
                    pass
            Alice_03 "Давай! Было бы чудненько. Только маме не пали меня, а то я на тебя обижусь и больше никогда разговаривать не буду..."
            Max_05 "Договорились!"

        "Ну и ладно...":
            $ __mood += 50
            Alice_12 "Я закончила. Если мама спросит, скажешь, от соседей надуло, хорошо?"
            Max_01 "Конечно!"

    $ AddRelMood('alice', 5, __mood)
    $ items['cigarettes'].InShop = True
    $ notify_list.append(_("В интернет-магазине доступен новый товар."))
    $ talk_var['alice.pun'] = 0
    $ dcv['smoke'].stage = 2
    $ dcv['smoke'].lost = 1
    $ dcv['smoke'].done = False
    $ dcv['betray_smoke'] = Daily(1, False, True)
    $ AvailableActions['searchciga'].enabled = True
    $ spent_time = 30 - int(tm[-2:])
    jump Waiting


label gift_cigarettes:
    $ __mood = 0
    menu:
        Alice_03 "Это то, что я думаю? Давай сюда!"
        "А что взамен?":
            Alice_13 "Макс! Не будь придурком. Давай сюда!"
            Max_01 "Ну, держи..."
        "Только давай условимся, что ты не скажешь маме о том, как я за тобой подглядывал в душе..." if peeping["alice_shower"] == 3:
            Alice_12 "Хоть ты и тот ещё извращенец, Макс, но мне было сложно достать эти сигареты самой... Так и быть, мама ничего не узнает, по крайней мере в этот раз."
            Max_05 "Спасибо, Алиса! И я не извращенец. Просто проходил мимо, а там такая красотища..."
            Alice_05 "Ну да, ну да... Мимо он проходил..."
            $ punreason[1] = 0
            $ peeping['alice_shower'] = 0
        "Держи!":
            if GetMood("alice")[0] < -1:
                Alice_05 "Хотя ты и полный придурок, но, похоже, начинаешь исправляться!"
                Max_08 "Я не полный придурок!"
                $ __mood += 50
            else:
                Alice_07 "Спасибо, Макс. Вот теперь я понимаю, ты настоящий брат!"
                Max_05 "Да не за что..."
    Alice_03 "Спасибо. И маме ни слова! У неё рука очень тяжёлая, особенно когда речь о сигаретах..."
    Max_01 "Конечно!"
    $ items['cigarettes'].have = False
    $ __mood += 100
    $ spent_time += 10
    $ dcv['smoke'].lost = 0
    $ dcv['smoke'].done = True
    $ AddRelMood("alice", 10, __mood)
    return


label smoke_nofear:
    Alice_00 "Макс, поглазеть пришёл?"
    Max_09 "Не боишься, что мама накажет, если узнает?"
    menu:
        Alice_03 "И как она узнает? Ты расскажешь?"
        "Может быть...":
            Alice_12 "Ты хорошо подумал, Макс? Жизнь-то у тебя одна... И что ты хочешь за... молчание?"
            Max_01 "Вот это разговор!"
            menu:
                Alice_13 "Сначала скажи, что у тебя на уме..."
                "Дай $20, и я буду молчать":
                    pass
                "Если днем ты будешь ходить без трусов, буду молчать":
                    pass
                "Если будешь курить без верха, буду молчать":
                    pass
                "Если разрешишь тебя отшлёпать, ничего не скажу":
                    pass
                "Ничего. Не переживай!":
                    jump .no
            Alice_16 "А больше ты ничего не хочешь? Свали отсюда, пока не наваляла!!"
            $ AddRelMood('alice', 0, -100)
            $ spent_time = 10
            jump Waiting
        "Нет, конечно!":
            jump .no
    menu .no:
        Alice_03 "Ну вот и пугать не надо. Не узнает. А если ты проболтаешься, я тебя во сне придушу, понял? Теперь иди, не мешай мне..."
        "Угу...":
            $ AddRelMood('alice', 0, 30)
            $ spent_time = 10
            jump Waiting


label smoke_fear:
    Alice_00 "Макс, поглазеть пришёл?"
    Max_09 "Не боишься, что мама накажет если узнает?"
    Alice_12 "Ты же ей не скажешь? Она так больно меня отшлёпала в прошлый раз, что до сих пор сидеть неприятно..."
    Max_01 "Ну, это зависит от тебя..."
    $ _ch8 = GetChanceConvince(punalice, 8)
    $ _ch8_col = GetChanceColor(_ch8)
    $ ch8_vis = str(int(_ch8/10)) + "%"
    $ _ch4 = GetChanceConvince(punalice, 4)
    $ _ch4_col = GetChanceColor(_ch4)
    $ ch4_vis = str(int(_ch4/10)) + "%"
    $ _ch3 = GetChanceConvince(punalice, 3)
    $ _ch3_col = GetChanceColor(_ch3)
    $ ch3_vis = str(int(_ch3/10)) + "%"
    $ _ch1 = GetChanceConvince(punalice)
    $ _ch1_col = GetChanceColor(_ch1)
    $ ch1_vis = str(int(_ch1/10)) + "%"
    menu:
        Alice_13 "Говори, что ты хочешь за молчание?"
        "Дай $20, и я ничего не скажу {color=[_ch8_col]}(Убеждение. Шанс: [ch8_vis]){/color}":
            if RandomChance(_ch8):
                $ punalice[0][0] = 3
                $ spent_time += 10
                $ mgg.social += 0.2
                $ _ch2 = GetChanceConvince(punalice, 2)
                $ _ch2_col = GetChanceColor(_ch2)
                $ ch2_vis = str(int(_ch2/10)) + "%"
                $ flags['smoke.request'] = "money"
                menu:
                    Alice_12 "[succes!t]Ладно, Макс, я дам тебе денег, но только $10, ок?"
                    "Нет, давай $20 {color=[_ch2_col]}(Убеждение. Шанс: [ch2_vis]){/color}":
                        if RandomChance(_ch2):
                            Alice_13 "[succes!t]Чёрт с тобой, Макс. Совсем без денег оставить хочешь... Сейчас принесу..."
                            Max_03 "Я жду..."
                            $ money += 20
                            $ mgg.social += 0.2
                            $ AddRelMood('alice', 0, -50)
                        else:
                            Alice_16 "[failed!t]Макс, не наглей! Сейчас принесу $10. Жди..."
                            Max_04 "Ну ладно, я жду..."
                            $ money += 10
                            $ mgg.social += 0.1
                            $ AddRelMood('alice', 0, -75)
                    "Хорошо, устроит и $10":
                        $ money += 10
                        $ AddRelMood('alice', 0, -25)
                menu:
                    Alice_12 "Держи свои деньги... И больше меня не шантажируй. Я очень это не люблю... А теперь вали отсюда!"
                    "Удачи!":
                        jump .end
            else:
                jump .fail
        "Если днем ты будешь ходить без трусов, буду молчать {color=[_ch4_col]}(Убеждение. Шанс: [ch4_vis]){/color}":
            # когда будет подарена пижамка, этот пункт будет недоступен, т.к. пижаму Алиса носит без трусиков
            if RandomChance(_ch4):
                Alice_13 "[succes!t]Тебя так заботят мои трусы? Ну, хорошо. Всё равно я почти всё время в джинсах, так что не страшно. Значит, договорились?"
                Max_02 "Конечно!"
                $ mgg.social += 0.2
                $ punalice[0][0] = 3
                $ flags['smoke.request'] = "nopants"
                menu:
                    Alice_01 "А теперь вали отсюда. Дай спокойно покурить!"
                    "{i}уйти{/i}":
                        jump .end
            else:
                jump .fail
        "Если больше не будешь носить лифчик, буду молчать {color=[_ch3_col]}(Убеждение. Шанс: [ch3_vis]){/color}":
            if RandomChance(_ch3):
                Alice_03 "[succes!t]Да я вообще-то и так без лифчика все время хожу, только когда сплю одеваю..."
                Max_01 "Значит, тогда просто спи без него."
                Alice_05 "Не знаю, зачем тебе, извращенцу, это нужно, но лучше я соглашусь на этот пустяк... Пока ты что-нибудь ещё не попросил."
                Max_04 "Вот и отлично!"
                $ mgg.social += 0.2
                $ punalice[0][0] = 3
                $ flags['smoke.request'] = "sleep"
                menu:
                    Alice_01 "А теперь вали отсюда. Дай спокойно покурить!"
                    "{i}уйти{/i}":
                        jump .end
            else:
                jump .fail
        "Если будешь курить без верха купальника, буду молчать {color=[_ch3_col]}(Убеждение. Шанс: [ch3_vis]){/color}":
            if RandomChance(_ch3):
                Alice_12 "[succes!t]Маленький извращенец... Ладно, но при условии, что маме не будешь ничего говорить. И разденусь только с завтрашнего дня. Договорились?"
                Max_03 "Само собой!"
                $ mgg.social += 0.2
                $ punalice[0][0] = 3
                $ flags['smoke.request'] = "toples"
                menu:
                    Alice_01 "А теперь вали отсюда. Дай спокойно покурить!"
                    "{i}уйти{/i}":
                        jump .end
            else:
                jump .fail
        # "Если разрешишь тебя отшлёпать, то я ничего не скажу {color=[_ch1_col]}(Убеждение. Шанс: [ch1_vis]){/color}":
        #     # будет доступно в следующих версиях
        #     if RandomChance(_ch1):
        #         $ punalice[0][0] = 3
        #     else:
        #         $ mgg.social += 0.1
        #         jump .fail
        "Ты знаешь, я сегодня добрый...":
            $ punalice[0][0] = 1
            menu:
                Alice_06 "Сегодня? Значит, попросишь в следующий раз?"
                "Как знать, может быть...":
                    pass
                "Что ты! Нет, конечно...":
                    pass
            menu:
                Alice_13 "Ну вот тогда иди чем-нибудь займись, а меня не отвлекай..."
                "{i}уйти{/i}":
                    $ flags['smoke'] = None
                    $ flags['smoke.request'] = None
                    jump .end

    menu .fail:
        Alice_16 "[failed!t]Ага, сейчас! Ну ты и хам, Макс... Всё, отвали, дай покурить спокойно..."
        "{i}уйти{/i}":
            $ mgg.social += 0.1
            $ flags['smoke'] = None
            $ flags['smoke.request'] = None
            $ punalice[0][0] = 2
            $ AddRelMood('alice', 0, -50)
            jump .end

    label .end:
        $ spent_time += 10
        jump Waiting


label smoke_toples:
    menu:
        Alice_02 "Ну что, извращенец, доволен видом?"
        "Доволен, конечно!":
            menu:
                Alice_05 "Надеюсь, маме ничего не расскажешь? А то это опасно для твоей жизни..."
                "Не переживай, не скажу":
                    Alice_04 "Вот и молодец. А теперь иди, займись чем-нибудь..."
                    Max_01 "Хорошо..."
                "Ну... это зависит от тебя!":
                    Alice_09 "Макс, не играй с огнём! Мы договоривались. А теперь сгинь с глаз моих!"
                    Max_07 "Уже ухожу..."
                    $ AddRelMood('alice', 0, -50)
        "А чего ты прикрываешься?":
            Alice_04 "А про руки мы не договаривались. Хочу - прикрываюсь. Хочу - нет. А тебя так радовать я точно не хочу... Так что вали уже..."
            Max_09 "Ну и ладно..."
        "Я передумал. Можешь одеться.":
            menu:
                Alice_12 "Спасибо, ваше величество! А чего это ты так расщедрился? Я больше не в твоём вкусе?"
                "Ну, я перегнул палку...":
                    menu:
                        Alice_04 "Спасибо, Макс! Это мудрый поступок... Но ты можешь в последний раз поглазеть..."
                        "{i}уйти{/i}":
                            $ AddRelMood('alice', 0, 100)
                "Это мой шаг к дружбе":
                    menu:
                        Alice_03 "Даже так?! Ну, дружбу я не обещаю, но могу пытаться с тобой как-то уживаться. А если серьёзно, то спасибо, Макс. Я это оценила..."
                        "{i}уйти{/i}":
                            $ AddRelMood('alice', 0, 120)
                "Да надоела!":
                    menu:
                        Alice_15 "Что?! А ну-ка вали отсюда, пока цел! И оденусь теперь, будь уверен!"
                        "{i}свалить{/i}":
                            pass
                "Ты знаешь, пусть всё остаётся как есть...":
                    menu:
                        Alice_12 "Ах вот как? Опять передумал? Всё, ты мне надоел, свали отсюда!"
                        "{i}свалить{/i}":
                            $ AddRelMood('alice', 0, -50)
                            $ spent_time += 10
                            jump Waiting
            $ flags['smoke'] = None
            $ flags['smoke.request'] = None
            $ flags['noted'] = False

    label .end:
        $ spent_time += 10
        jump Waiting


label smoke_not_toples:
    Alice_02 "Ты чего-то хотел, Макс?"
    Max_07 "Да, хотел... Мы ведь договорились, что ты будешь курить без верха купальника!"
    Alice_13 "Знаешь, Макс, мне это надоело... Сколько можно? Я хочу спокойно курить и не волноваться, что ты за мной подглядываешь!"
    Max_09 "Ну хорошо, Алиса, как скажешь, можешь курить одетой. Только вот, если ты решила нарушить условия нашей договорённости, то почему бы тогда и мне не поступить так же?"
    Alice_06 "Только не надо маме рассказывать о об этом..."
    Max_00 "Всё зависит от тебя, сестрёнка... Если сейчас снимешь верх и в качестве извинения покажешь грудь, то я представлю, будто ты ничего не нарушала."
    Alice_15 "Ах ещё и грудь показать! Может сразу и полапать её дать?!"
    Max_03 "Очень заманчивое предложение... Но просто показать - я считаю справедливо! Сама накосячила..."

    $ renpy.show("Alice smoke "+renpy.random.choice(["01", "02", "03"])+"c")

    Alice_06 "Ладно, один разок и быстро... Но не вздумай маме рассказывать! Ни про это, ни про сигареты."
    Max_05 "Конечно, я ведь своё слово держу. Симпатичные сосочки!"
    menu:
        Alice_13 "Ну всё, полюбовался и хватит. Вали отсюда, дай спокойно покурить..."
        "Ага...":
            pass
    $ renpy.show("Alice smoke "+pose3_3+chars["alice"].dress)
    $ flags['smoke'] = None
    $ flags['smoke.request'] = None
    $ flags['noted'] = False
    $ spent_time += 10
    jump Waiting


label smoke_nopants:
    menu:
        Alice_02 "Макс, чего хотел?"
        "Ничего, просто любуюсь...":
            Alice_13 "Налюбовался? А вот теперь постой в сторонке, пока я покурю! Давай, вали уже..."
            Max_01 "Хорошо, хорошо..."
        "А ты чего в трусах?":
            menu:
                Alice_12 "В каком смысле?!"
                "Мы же договорились - без трусов!":
                    Alice_01 "Ты совсем идиот, Макс? Я в купальнике. Это не трусы. А трусы я и так не ношу под джинсами. Можешь себе фантазировать теперь сколько хочешь... Всё, уйди с глаз моих долой!"
                    Max_01 "Ухожу, ухожу..."
                "Да шучу я...":
                    Alice_02 "Ты наверное думаешь, что у тебя забавные шутки, да? Так вот нет. Абсолютно несмешные, дебильные шутки, как и ты сам. Всё, свалил отсюда. Я занята..."
                    Max_01 "Как скажешь..."
        "Я передумал. Можешь носить трусы...":
            menu:
                Alice_03 "А чего передумал? А, хотя не важно. Я рада, а то мне всё натирает в джинсах... Хотя, тебе такие подробности знать не нужно. Спасибо за разрешение, ваше величество. Теперь дай покурю..."
                "{i}уйти{/i}":
                    $ flags['smoke'] = None
                    $ flags['smoke.request'] = None
                    $ flags['noted'] = False
                    $ chars['alice'].nopants = False
                    $ AddRelMood('alice', 0, 100)

    $ spent_time += 10
    jump Waiting


label smoke_not_nopants:
    Alice_02 "Ты чего-то хотел, Макс?"
    Max_09 "Алиса, ну что за дела?! Я думал у нас уговор!"
    Alice_06 "Это ты сейчас о чём, Макс?"
    Max_08 "Мы ведь договорились, что ты не будешь одевать трусы днём. А я их на тебе видел!"
    Alice_14 "А мне вот интересно, когда это ты их мог увидеть?! Подглядывал, как я одеваюсь?"
    Max_07 "Да здесь и подглядывать не нужно, они у тебя иногда прямо из-под джинс слегка торчат... Так что важно не то, как и где я это увидел, а то, что они на тебе были!"
    Alice_00 "Ладно, ладно, признаю, я их снова ношу, потому что без них мне все натирает. Да и мама, если видит, что я без трусов во время наказания, шлепает гораздо сильней."
    Max_00 "Ну хорошо, я тебя освобождаю от уговора. Но только, если в качестве компенсации ты прямо сейчас покажешь мне сиськи!"
    Alice_16 "А не многого ли ты, мелкий извращенец, хочешь?!"
    Max_01 "Посмотреть на красивые сиськи - не извращение! И мне кажется, проще один раз показать, чем всё натирать будет..."

    $ renpy.show("Alice smoke "+renpy.random.choice(["01", "02", "03"])+"c")

    Alice_06 "Ну на, любуйся, раз уж и дня не можешь прожить без извращений."
    Max_03 "Классные сиськи!"
    Alice_13 "Спасибо. А теперь иди уже, погуляй где-нибудь. Дай докурить спокойно."
    Max_01 "Хорошо. Я ушёл..."

    $ renpy.show("Alice smoke "+pose3_3+chars["alice"].dress)
    $ flags['smoke'] = None
    $ flags['smoke.request'] = None
    $ flags['noted'] = False
    $ spent_time += 10
    jump Waiting


label smoke_sleep:
    menu:
        Alice_02 "Макс, чего хотел?"
        "Ничего, просто любуюсь...":
            Alice_13 "Налюбовался? А вот теперь постой в сторонке, пока я покурю! Давай, вали уже..."
            Max_01 "Хорошо, хорошо..."
        "Я передумал. Ты можешь спать в лифчике, если хочешь.":
            menu:
                Alice_04 "А чего это ты передумал? А, хотя не важно. Я рада, а то мне неудобно ночью, если выйти куда-то нужно, да и мама заметить может. Спасибо за разрешение, ваше величество. Теперь дай покурю..."
                "{i}уйти{/i}":
                    $ flags['smoke'] = None
                    $ flags['smoke.request'] = None
                    $ flags['noted'] = False
                    $ AddRelMood('alice', 0, 100)
    $ spent_time += 10
    jump Waiting


label Alice_sorry:
    Alice_15 "Ух ты, у тебя, извращенца мелкого, совесть проснулась?! Неожиданно..."
    Max_10 "Нет, я думаю, ты вряд ли правильно поняла то, что случилось. Я за тобой не подглядывал..."
    Alice_16 "Макс, я по-твоему полная дура что ли?! Ты стоял за стеной, и нагло смотрел, как я принимала душ!"
    Max_14 "Но на деле же, так получилось не специально... Я просто шёл мимо..."
    Alice_05 "Да, да, конечно... Я очень хочу посмотреть, что с тобой сделает мама, когда об этом узнает..."
    Max_10 "Да, знаю, в такое очень трудно поверить, но я просто шёл мимо, а ты душ как раз принимала... Ну я и отскочил к стене... где ты меня и заметила... Вот и всё, я даже и не видел ничего такого!"
    Alice_03 "Ну вот никак мне в это не верится, Макс! Никаких твоих оправданий не хватит, чтобы я в это поверила."
    Max_07 "В таком случае, предлагаю представить, что ничего такого утром не было. Ты ничего не говоришь маме, а я в свою очередь куплю тебе чего-нибудь вкусненького?"
    Alice_08 "Ах ты... паршивец... Это подлый ход, потому что от вкусняшки я бы не отказалась... Хорошо, но обещать ничего не буду, сперва посмотрю, что это будет за вкусность... Если ты конечно успеешь до завтрашего вечера!"
    Max_01 "Обязательно успею! Завтра всё будет..."
    $ punreason[1] = 0
    $ peeping['alice_shower'] = 0
    Alice_07 "И смотри, чтобы мне понравилось..."
    $ spent_time += 10
    jump Waiting


label gift_dress:
    Alice_15 "Макс? Это платье для клуба? Правда?!"
    Max_01 "Ага..."
    menu:
        Alice_07 "Спасибо, Макс! Ты такой... Не знаю даже, я просто в шоке!"
        "Держи...":
            jump .gift
        "Не так быстро...":
            menu:
                Alice_02 "Так и знала, что есть какой-то подвох... И что ты хочешь за него?"
                "Да ничего, просто держи...":
                    jump .gift
                "Устрой мне показ в нём...":
                    Alice_05 "Ты хочешь, чтобы я его примерила прямо при тебе?"
                    Max_03 "Конечно! Я это и хочу!"
                    Alice_03 "Макс... А жить... хочешь?"
                    Max_07 "Эй, что за угрозы?"
                    jump .newdress_show

    label .gift:
        Alice_02 "Вот так вот сразу и без подвоха? Обалдеть... Не ожидала от тебя, Макс... Спасибо!"
        Max_04 "Не за что!"
        $ AddRelMood("alice", 100, 300)
        jump .end

    label .newdress_show:
        if '09:00' <= tm < '20:00':
            $ __suf = ""
        else:
            $ __suf = "a"

        if "06:00" <= tm < "11:00":
            scene location house aliceroom door-morning
        elif "11:00" <= tm < "18:00":
            scene location house aliceroom door-day
        elif "18:00" <= tm < "21:00":
            scene location house aliceroom door-evening
        else:
            scene location house aliceroom door-night

        menu:
            Alice_03 "Жди за дверью. Я сейчас надену платье и тебе покажу, так уж и быть..."
            "Э... Хорошо...":
                pass
        if __suf == "":
            scene BG char Alice newdress
        else:
            scene BG char Alice spider-night-05
        $ renpy.show("Alice newdress 01"+__suf)
        Alice_05 "Ну как, Макс? Мне идёт?"
        Max_05 "Выглядишь... шикарно!"
        $ renpy.show("Alice newdress 02"+__suf)
        Alice_07 "Спасибо, Макс! Честно говоря, я не ожидала от тебя такого подарка. Спасибо! И..."
        Max_07 "И?"
        $ renpy.show("Alice newdress 03"+__suf)
        menu:
            Alice_05 "...И небольшой бонус. Я знаю, что ты ждал чего-то подобного..."
            "Очень... очень хорошо...":
                pass
            "А можешь наклониться?":
                pass
        $ renpy.show("Alice newdress 04"+__suf)
        Alice_02 "Хорошего в меру... Правда, ты меня очень сильно выручил. Спасибо ещё раз!"
        Max_01 "Не за что!"
        $ AddRelMood("alice", 100, 200)
        $ spent_time += max((50 - int(tm[-2:])), 30)
        $ current_room = house[5]

    label .end:
        $ items['dress'].have = False
        $ items['dress'].InShop = False
        $ SetPossStage('nightclub', 4)
        $ chars['alice'].gifts.append('dress')
        if chars['alice'].inferic is not None:
            $ chars['alice'].inferic = clip(chars['alice'].inferic-50.0, 0.0, 100.0)
        if chars['alice'].infmax is not None:
            $ chars['alice'].infmax = clip(chars['alice'].infmax+20.0, 0.0, 100.0)
        else:
            $ chars['alice'].infmax = 20.0
        $ spent_time += 10
        jump Waiting


label gift_book:
    if items['erobook_1'].have:
        Alice_02 "У тебя для меня подарок? У ТЕБЯ... для МЕНЯ? Какая прелесть. Давай, показывай, что за книжка?"
        Max_01 "Держи..."
        menu:
            Alice_01 "Прикольно... Давно хотела её почитать. А ты как узнал, что мне такие нравятся?"
            "Порылся в твоих вещах и нашёл, что читаешь!":
                Alice_14 "Что?! Да как ты посмел?!"
                Max_02 "Да я пошутил. Просто угадал!"
                Alice_13 "Шуточки у тебя, как и прежде, дурацкие! А книжку я возьму. Молодец, что угадал... Спасибо."
                Max_04 "Не за что"
            "Ну, я догадался! Я же умный!":
                Alice_07 "Умный он... Ну, молодец, что догадался. Спасибо, Макс! Если ещё попадутся из этой серии, буду рада принять их от тебя. Безвозмездно!"
                Max_04 "Хорошо..."
            "Я и не знал. Просто угадал видимо...":
                Alice_05 "Поздравляю, попал в десятку! Если найдёшь ещё что-то подобное, буду рада такому подарку. Даже от тебя..."
                Max_04 "Ну, если даже от меня, то ладно..."
        $ SetPossStage('secretbook', 3)
        $ AddRelMood('alice', 50, 100)
        $ items['erobook_1'].have = False
        $ items['erobook_1'].InShop = False
        $ chars['alice'].gifts.append('erobook_1')
        $ dcv['secretbook'] = Daily(7, False, True) # Покупка второй книги возможна через неделю.
        $ dcv['secretbook'].stage = 2
    elif items['erobook_2'].have:
        Alice_04 "Да? И какая на этот раз? Давай сюда..."
        Max_01 "Держи..."
        Alice_07 "Супер! Ты меня удивляешь, Макс! Если ещё что будет почитать, приноси. Я люблю подобную... литературу."
        Max_04 "Конечно!"
        $ AddRelMood('alice', 60, 120)
        $ items['erobook_2'].have = False
        $ items['erobook_2'].InShop = False
        $ chars['alice'].gifts.append('erobook_2')
        $ dcv['secretbook'] = Daily(9, False, True) # Покупка третьей книги возможна через девять дней.
        $ dcv['secretbook'].stage = 3
    elif items['erobook_3'].have or items['erobook_4'].have:
        Alice_04 "Супер! Давай, показывай, что тут у нас..."
        Max_01 "Держи..."
        Alice_05 "То, что нужно! Если ещё что будет почитать, приноси. Ты же знаешь, как я люблю такие книги..."
        Max_04 "Конечно!"
        $ AddRelMood('alice', 50, 100)
        if items['erobook_4'].have:
            $ items['erobook_4'].have = False
            $ items['erobook_4'].InShop = False
            $ chars['alice'].gifts.append('erobook_4')
            $ dcv['secretbook'] = Daily(13, False, True) # Покупка пятой книги возможна через тринадцать дней.
            $ dcv['secretbook'].stage = 5
        else:
            $ items['erobook_3'].have = False
            $ items['erobook_3'].InShop = False
            $ chars['alice'].gifts.append('erobook_3')
            $ dcv['secretbook'] = Daily(11, False, True) # Покупка четвертой книги возможна через одинадцать дней.
            $ dcv['secretbook'].stage = 4
    elif items['erobook_5'].have:
        Alice_04 "Да? И какая на этот раз? Давай сюда..."
        Max_01 "Держи..."
        Alice_07 "Забавная книжка. Давно хотела почитать... Спасибо, Макс. Ты меня балуешь!"
        Max_04 "Конечно!"
        $ AddRelMood('alice', 80, 160)
        $ items['erobook_5'].have = False
        $ items['erobook_5'].InShop = False
        $ chars['alice'].gifts.append('erobook_5')

    $ spent_time += 10
    return


label Alice_solar:
    $ talk_var['alice_sun'] = 1
    ## Загораешь?
    menu:
        Alice_02 "Как ты догадался, Шерлок?"
        "Может быть, тебя намазать кремом для загара?" if talk_var['sun_oiled'] == 3:
            Alice_00 "Достаточно на сегодня, Макс..."
            Max_00 "Ясно. Ну, тогда, может, завтра..."
            $ talk_var['sun_oiled'] = 4
            jump AfterWaiting
        "Может быть, тебя намазать кремом для загара?" if not items['solar'].have:  # нет крема
                Max_00 "Может быть, тебя намазать кремом для загара?"
                Alice_13 "Может быть. Вот только у меня его нет..."
                Max_00 "Ясно. Ну, в другой раз значит..."
                $ items['solar'].InShop = True
                jump AfterWaiting
        "{i}Предложить Алисе намазать ее кремом{/i}" if any([mgg.dress == 'a', kol_cream < 2]):
            if mgg.dress == 'a':  # Максу нужна одежда
                Max_00 "{i}Прежде чем пытаться поприставать к сестрёнке таким образом, стоит обзавестись одёжкой полегче.{/i}"
                $ items['max-a'].InShop = True
                jump AfterWaiting

            if kol_cream < 2:  # крема не хватит даже просто нанести
                Max_00 "{i}Крем почти закончился. Нужно купить еще.{/i}"
                $ items['solar'].InShop = True
                jump AfterWaiting

        "Может быть, тебя намазать кремом для загара?" if all([talk_var['sun_oiled']!=3, kol_cream>=2]):
            Alice_00 "Если у тебя есть крем, то давай, раз тебе делать нечего."
            Max_00 "Ложись на живот тогда..."
            $ talk_var['sun_oiled'] = 1
        "Ладно, загорай...":
            jump AfterWaiting

    scene BG char Alice sun-alone 01-01
    $ renpy.show('Alice sun-alone 01-01'+mgg.dress)
    menu .type_choice:
        Alice_07 "Эти шезлонги всем хороши, но на животе загорать не получается. Приходится коврик для йоги использовать..."
        "{i}нанести крем{/i}" if kol_cream >= 2:  # просто наносим крем. близко к оригиналу
            $ SetCamsGrow(house[6], 140)
            $ _suf = 'a'
            $ spent_time += 20
            $ kol_cream -= 2
            scene BG char Alice sun-alone 05
            $ renpy.show('Alice sun-alone 05-01'+_suf+'-01'+mgg.dress)
            Max_00 "{i}Намажем ножки...{/i}"
            scene BG char Alice sun-alone 04
            $ renpy.show('Alice sun-alone 04-01'+_suf+'-01'+mgg.dress)
            Max_00 "{i}Теперь плечи...{/i}" nointeract
            $ __res = renpy.display_menu([("{i}наносить крем молча{/i}", 0), ("А тебе нравится, что следы от лямок остаются?", 1)])
            if __res > 0:
                $ _talk_top = True
                call talk_topless()
            $ __r1 = renpy.random.choice(['02','03'])
            $ renpy.scene()
            $ renpy.show('BG char Alice sun-alone '+__r1)
            $ renpy.show('Alice sun-alone '+__r1+'-01'+_suf+'-01'+mgg.dress)
            Max_00 "{i}И закончим, хорошенько намазав спину...{/i}"
            Alice_02 "Спасибо, Макс. Так намного лучше..."
            Max_00 "Обращайся, если что..."
        "{i}сделать массаж с кремом{/i}" if kol_cream >= 7:  # попытка сделать массаж с кремом
            $ _massaged = []
            $ _talk_top = False
            $ SetCamsGrow(house[6], 160)
            jump massage_sunscreen
        "{i}{color=[gray]}сделать массаж с кремом{/color}{color=[red]}\nкрема недостаточно{/color}{/i}" if kol_cream < 7:
            jump .type_choice
    jump Waiting


label massage_sunscreen:
    if talk_var['sun_oiled'] == 2:
        scene BG char Alice sun-alone 01-01a
        $ _suf = 'b'
    else:
        scene BG char Alice sun-alone 01-01
        $ _suf = 'a'
    $ renpy.show('Alice sun-alone 01-01'+mgg.dress)
    if len(online_cources) > 1 and online_cources[1].current > 0:
        if len(_massaged) == 4: # 5: # временно доступны только 4 зоны
            scene BG char Alice sun-alone 01
            if talk_var['sun_oiled'] == 2:
                show Alice sun-alone 01a
            else:
                show Alice sun-alone 01
            Alice_00 "Макс, ты делаешь успехи! Ещё немного попрактикуешься, и к тебе будет сложно записаться на приём!"
            Alice_00 "Ладно, хватит на сегодня, Макс. И... спасибо!"
            Max_00 "Не за что!"
            $ AddRelMood('alice', 15, 150)
            jump .end  # если Макс прошел курсы массажа ног, ему доступны 5 зон
    elif len(_massaged) == 4:
        scene BG char Alice sun-alone 01
        if talk_var['sun_oiled'] == 2:
            show Alice sun-alone 01a
        else:
            show Alice sun-alone 01
        Alice_00 "Спасибо, Макс. На сегодня достаточно. У тебя неплохо получается, а если поучишься, может стать еще лучше."
        Max_00 "Да не за что, обращайся!"
        if len(online_cources) == 1:  # Курс массажа еще не был доступен
            Max_00 "{i}В чём-то Алиса права, поучиться, пожалуй, стоит.{/i}"
            $ online_cources.append(
                OnLineCources(_("Массаж"), "massage", "bm", [
                    OnLineCource(_("Массаж ступней"), _("Это уникальная методика массажа с целью оказания оздоравливающего воздействия на организм. Она эффективна и в тоже время несложна в исполнении."), 3, 100, 2),
                    OnLineCource(_("Массаж кистей рук"), _("Это уникальная методика массажа с целью оказания оздоравливающего воздействия на организм. Она эффективна и в тоже время несложна в исполнении."), 3, 200, 2),
                    ]),
                )
        $ AddRelMood('alice', 10, 100)
        jump .end  # если курсы не пройдены и первыми массировались ступни, доступно 4 зоны
    elif len(_massaged) == 2 and _massaged[0] != 'foot':
        # в противном случае доступны только 2 зоны
        scene BG char Alice sun-alone 01
        if talk_var['sun_oiled'] == 2:
            show Alice sun-alone 01a
        else:
            show Alice sun-alone 01
        Alice_00 "Спасибо, Макс. На сегодня достаточно."
        Max_00 "Да не за что, обращайся!"
        $ AddRelMood('alice', 5, 50)
        jump .end

    ### выбирааем зону массажа
    $ kol_cream -= 1
    call screen choice_zone_sunscreen

    label .left_foot:
        scene BG char Alice sun-alone 06
        $ renpy.show('Alice sun-alone 06-01'+_suf+'-01'+mgg.dress)
        Max_00 "{i}Разомнём левую пяточку...{/i}"
        scene BG char Alice sun-alone 07
        $ renpy.show('Alice sun-alone 07-01'+_suf+'-01'+mgg.dress)
        Max_00 "{i}Теперь правую...{/i}"
        jump .foot

    label .right_foot:
        scene BG char Alice sun-alone 07
        $ renpy.show('Alice sun-alone 07-01'+_suf+'-01'+mgg.dress)
        Max_00 "{i}Разомнём правую ступню...{/i}"
        scene BG char Alice sun-alone 06
        $ renpy.show('Alice sun-alone 06-01'+_suf+'-01'+mgg.dress)
        Max_00 "{i}Теперь левую...{/i}"
        jump .foot

    label .shin:
        scene BG char Alice sun-alone 05
        $ renpy.show('Alice sun-alone 05-01'+_suf+'-01'+mgg.dress)
        Max_00 "{i}Помассируем ножки вот здесь...{/i}"
        if 'shin' in _massaged:
            # голени уже массировались
            jump .double
        elif len(online_cources) > 1 and online_cources[1].current > 0:
            # Макс прошёл курс массажа ступней
            $ _multipler = max(1, 5 - len(_massaged))
            if len(_massaged)>0 and _massaged[0]=='foot':
                $ _multipler *= 2 # множитель навыка удваивается, если ступни были первыми

            if RandomChance(GetChance(mgg.massage, _multipler, 950)):
                # Алисе понравилось
                Alice_00 "Ух, как приятно..."
                $ mgg.massage = clip(mgg.massage+0.02, 0.0, 10.0)
            else:
                $ mgg.massage = clip(mgg.massage+0.005, 0.0, 10.0)
                jump .fail
        elif len(_massaged) > 0 and _massaged[0]=='foot':
            # ступни массировались первыми, реакция Алисы расчитывается через шансы
            # при этом навык у Макса меньше 1 ( меньше 10 в оригинале), поэтому идет расчет от фиксированного значения
            if RandomChance(650-50*len(_massaged)+mgg.massage*10):  # шанс падает на 10% с каждой следующей зоной. Для ступней 65-75%%, для второй зоны 55-65, 3й - 45-55, 4й - 35-45
                # Алисе понравилось
                Alice_00 "Ах, моим ножкам так хорошо..."
                $ mgg.massage = clip(mgg.massage+0.02, 0.0, 1.0)
            else:
                $ mgg.massage = clip(mgg.massage+0.005, 0.0, 1.0)
                jump .fail
        else:
            # ступни массировались не первыми
            Alice_00 "Неплохо, Макс..."
            $ mgg.massage = clip(mgg.massage+0.005, 0.0, 1.0)
        $ _massaged.append('shin')
        jump massage_sunscreen

    label .hips:
        # scene BG char Alice sun-alone 05
        # $ renpy.show('Alice sun-alone 05-01'+_suf+'01'+mgg.dress)
        Max_00 "{i}Помассируем бёдра...{/i}"
        if 'hips' in _massaged:
            # бёдра уже массировались
            jump .double

        # бёдра доступны только если Макс прошёл курс массажа ступней
        $ _multipler = max(1, 5 - len(_massaged))
        if len(_massaged)>0 and _massaged[0]=='foot':
            $ _multipler *= 2 # множитель навыка удваивается, если ступни были первыми

        if RandomChance(GetChance(mgg.massage, _multipler, 950)):
            # Алисе понравилось
            Alice_00 "А я и не знала, что твои руки могут быть одновременно сильными и нежными..."
            $ mgg.massage = clip(mgg.massage+0.02, 0.0, 10.0)
        else:
            $ mgg.massage = clip(mgg.massage+0.005, 0.0, 10.0)
            jump .fail
        $ _massaged.append('hips')
        jump massage_sunscreen

    label .shoulders:
        scene BG char Alice sun-alone 04
        $ renpy.show('Alice sun-alone 04-01'+_suf+'-01'+mgg.dress)
        Max_00 "{i}Хорошенько разомнём плечи...{/i}" nointeract
        # Max_00 "{i}Попробовать что ли уговорить Алису снять топ?{/i}" nointeract
        if not _talk_top:
            $ __res = renpy.display_menu([("{i}массировать молча{/i}", 0), ("А тебе нравится, что следы от лямок остаются?", 1)])
            if __res > 0:
                $ _talk_top = True
                # call .talk_topless('.shoulders')
                call talk_topless()
                $ renpy.show('Alice sun-alone 04-01'+_suf+'-01'+mgg.dress)
                Max_00 "И ещё немного..."

        if 'shoulders' in _massaged:
            # плечи уже массировались
            jump .double
        elif len(online_cources) > 1 and online_cources[1].current > 0:
            # Макс прошёл курс массажа ступней
            $ _multipler = max(1, 5 - len(_massaged))
            if len(_massaged)>0 and _massaged[0]=='foot':
                $ _multipler *= 2 # множитель навыка удваивается, если ступни были первыми

            if RandomChance(GetChance(mgg.massage, _multipler, 950)):
                # Алисе понравилось
                Alice_00 "Ка-а-йф..."
                $ mgg.massage = clip(mgg.massage+0.02, 0.0, 10.0)
            else:
                $ mgg.massage = clip(mgg.massage+0.005, 0.0, 10.0)
                jump .fail
        elif len(_massaged) > 0 and _massaged[0]=='foot':
            # ступни массировались первыми, реакция Алисы расчитывается через шансы
            # при этом навык у Макса меньше 1 ( меньше 10 в оригинале), поэтому идет расчет от фиксированного значения
            if RandomChance(650-50*len(_massaged)+mgg.massage*10):  # шанс падает на 10% с каждой следующей зоной. Для ступней 65-75%%, для второй зоны 55-65, 3й - 45-55, 4й - 35-45
                # Алисе понравилось
                Alice_00 "Мои плечи в восторге..."
                $ mgg.massage = clip(mgg.massage+0.02, 0.0, 1.0)
            else:
                $ mgg.massage = clip(mgg.massage+0.005, 0.0, 1.0)
                jump .fail
        else:
            # ступни массировались не первыми
            Alice_00 "Весьма неплохо, Макс..."
            $ mgg.massage = clip(mgg.massage+0.005, 0.0, 1.0)
        $ _massaged.append('shoulders')
        jump massage_sunscreen

    label .spine:
        $ __r1 = renpy.random.choice(['02','03'])
        $ renpy.scene()
        $ renpy.show('BG char Alice sun-alone '+__r1)
        $ renpy.show('Alice sun-alone '+__r1+'-01'+_suf+'-01'+mgg.dress)
        Max_00 "{i}Тщательно помнём спинку...{/i}" nointeract
        # Max_00 "{i}Попробовать что ли уговорить Алису снять топ?{/i}" nointeract
        if not _talk_top:
            $ __res = renpy.display_menu([("{i}массировать молча{/i}", 0), ("А тебе нравится, что следы от лямок остаются?", 1)])
            if __res > 0:
                $ _talk_top = True
                # call .talk_topless('.spine')
                call talk_topless()
                $ renpy.show('Alice sun-alone '+__r1+'-01'+_suf+'-01'+mgg.dress)
                Max_00 "Ещё немного крема..."


        if 'spine' in _massaged:
            # спина уже массировалась
            jump .double
        elif len(online_cources) > 1 and online_cources[1].current > 0:
            # Макс прошёл курс массажа ступней
            $ _multipler = max(1, 5 - len(_massaged))
            if len(_massaged)>0 and _massaged[0]=='foot':
                $ _multipler *= 2 # множитель навыка удваивается, если ступни были первыми

            if RandomChance(GetChance(mgg.massage, _multipler, 950)):
                # Алисе понравилось
                Alice_00 "Как приятно... Макс, ты делаешь успехи!"
                $ mgg.massage = clip(mgg.massage+0.02, 0.0, 10.0)
            else:
                $ mgg.massage = clip(mgg.massage+0.005, 0.0, 10.0)
                jump .fail
        elif len(_massaged) > 0 and _massaged[0]=='foot':
            # ступни массировались первыми, реакция Алисы расчитывается через шансы
            # при этом навык у Макса меньше 1 ( меньше 10 в оригинале), поэтому идет расчет от фиксированного значения
            if RandomChance(650-50*len(_massaged)+mgg.massage*10):  # шанс падает на 10% с каждой следующей зоной. Для ступней 65-75%%, для второй зоны 55-65, 3й - 45-55, 4й - 35-45
                # Алисе понравилось
                Alice_00 "Руки у тебя нежные. Приятно очень..."
                $ mgg.massage = clip(mgg.massage+0.02, 0.0, 1.0)
            else:
                $ mgg.massage = clip(mgg.massage+0.005, 0.0, 1.0)
                jump .fail
        else:
            # ступни массировались не первыми
            Alice_00 "Весьма неплохо, Макс..."
            $ mgg.massage = clip(mgg.massage+0.005, 0.0, 1.0)
        $ _massaged.append('spine')
        jump massage_sunscreen

    label .ass:  # попка пока недоступна, поэтому не прописывал алгоритм
        "попка"
        jump massage_sunscreen

    label .foot:
        if 'foot' in _massaged:
            # ступни уже массировались
            jump .double
        elif len(online_cources) > 1 and online_cources[1].current > 0:
            # Макс прошёл курс массажа ступней
            $ _multipler = 6 - len(_massaged) if len(_massaged) else 10  # множитель навыка. Если ступни первые, шанс удваивается
            if RandomChance(GetChance(mgg.massage, _multipler, 950)):
                # Алисе понравилось
                Alice_00 "Ух, как приятно..."
                $ mgg.massage = clip(mgg.massage+0.02, 0.0, 10.0)
            else:
                $ mgg.massage = clip(mgg.massage+0.005, 0.0, 10.0)
                jump .fail
        elif len(_massaged) > 0:
            # ступни массировались не первыми
            Alice_00 "А ничего так, мне приятно..."
            $ mgg.massage = clip(mgg.massage+0.005, 0.0, 1.0)
        else:
            # ступни массировались первыми, реакция Алисы расчитывается через шансы
            # при этом навык у Макса меньше 1 ( меньше 10 в оригинале), поэтому идет расчет от фиксированного значения
            if RandomChance(650+mgg.massage*10):  # шанс 65-75%
                # Алисе понравилось
                Alice_00 "А ты неплох сегодня в этом деле... Моим пяточкам приятны твои руки..."
                $ mgg.massage = clip(mgg.massage+0.02, 0.0, 1.0)
            else:
                $ mgg.massage = clip(mgg.massage+0.005, 0.0, 1.0)
                jump .fail
        $ _massaged.append('foot')
        jump massage_sunscreen

    label .double:
        Alice_00 "Взялся делать массаж, а сам не знаешь что делать. Хватит, иди отсюда, дай позагорать спокойно."
        Max_00 "Эх... Ладно..."
        jump .end

    label .fail:
        if len(_massaged) > 0:  # есть успешно помассированные участки
            Alice_00 "Хватит, Макс... Что-то у тебя не так пошло... А ведь так хорошо начал..."
        else:  # первый же массаж
            Alice_00 "Макс, хватит... Что-то не похоже, что ты знаешь, что делаешь... Давай на этом закончим..."
        Max_00 "Хорошо, извини..."
        jump .end

    label .end:
        scene BG char Alice sun-alone 01
        if talk_var['sun_oiled'] == 2:
            show Alice sun-alone 01a
        else:
            show Alice sun-alone 01
        $ spent_time += 10 + clip(int(round(5*len(_massaged), -1)), 0, 30)
        if kol_cream < 2:
            Max_00 "{i}Ну вот, крем закончился. Надо еще купить.{/i}"
            if kol_cream == 0:
                $ items['solar'].have = False
        elif kol_cream < 7:
            Max_00 "{i}Крема мало осталось, в следующий раз может не хватить, лучше купить заранее.{/i}"

        # "помассированны [_massaged] "
        jump Waiting


label talk_topless():
    $ _ch1 = GetChance(mgg.social, 3)
    $ _ch1_color = GetChanceColor(_ch1)
    $ ch1_vis = str(int(_ch1/10)) + "%"
    menu:
        Alice_06 "Нет, конечно. Но тебя я так радовать не собираюсь!"
        "Что, стесняешься? {color=[_ch1_color]}(Убеждение. Шанс: [ch1_vis]){/color}":
            if RandomChance(_ch1):
                Alice_07 "[succes!t]Нет, но... Ладно, всё равно тебе ничего не видно..."
                Max_00 "Так держать, сестрёнка!"
                $ talk_var['sun_oiled'] = 2
                $ _suf = 'b'
                $ SetCamsGrow(house[6], 200)
            else:
                Alice_04 "[failed!t]Вот только на \"слабо\" меня брать не надо!"
                Max_00 "Ладно, как скажешь..."
        "Ну, как хочешь...":
            pass
    return
