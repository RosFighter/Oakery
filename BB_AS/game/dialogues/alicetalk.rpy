
label AliceTalkStart:

    $ dial = TalkMenuItems()

    # $ __cur_plan = GetPlan(plan_alice, day, tm)
    $ __cur_plan = alice.get_plan()
    if __cur_plan.talklabel is not None:
        call expression __cur_plan.talklabel from _call_expression_1

    if len(dial) > 0:
        $ dial.append((_("{i}уйти{/i}"), "exit"))
    else:
        jump Waiting


    $ renpy.block_rollback()
    Alice_00 "Ну, Макс, чего надо?" nointeract

    $ rez =  renpy.display_menu(dial)

    if rez != "exit":
        $ __mood = alice.GetMood()[0]
        if rez in gifts['alice']:
            if renpy.has_label(rez.label):
                call expression rez.label from _call_expression_2
        elif __mood < talks[rez].mood:
            if __mood < -2: # Настроение -4... -3, т.е. всё ну совсем плохо
                jump Alice_badbadmood
            elif __mood < 0: # Настроение -2... -1, т.е. всё ещё всё очень плохо
                jump Alice_badmood
            else: # Настроение хорошее, но ещё недостаточное для разговора
                jump Alice_normalmood
        elif talks[rez].kd_id != "" and talks[rez].kd_id in cooldown and not ItsTime(cooldown[talks[rez].kd_id]):
            jump Alice_cooldown
        elif renpy.has_label(talks[rez].label): # если такая метка сушествует, запускаем ее
            call expression talks[rez].label from _call_expression_3
        jump AliceTalkStart       # а затем возвращаемся в начало диалога, если в разговоре не указан переход на ожидание

    jump Waiting            # если же выбрано "уйти", уходим в после ожидания


label Alice_badbadmood:
    menu:
        Alice_09 "Да пошёл ты! Не хочу тебя видеть даже!"
        "Ок...":
            jump Waiting
        "Я хотел извиниться":
            jump Alice_asksorry


label Alice_badmood:
    menu:
        Alice_09 "Макс, отвали! Я не хочу с тобой разговаривать."
        "Ок...":
            jump Waiting
        "Я хотел извиниться":
            jump Alice_asksorry


label Alice_asksorry:
    menu:
        Alice_13 "Хотел извиниться? Каким образом?"
        "Ты знаешь, я передумал...":
            jump Waiting


label Alice_normalmood:
    menu:
        Alice_09 "Макс, давай не сейчас..."
        "Ок...":
            jump Waiting


label Alice_cooldown:
    Alice_09 "Макс... Не сейчас."
    Max_00 "Ладно..."
    jump AfterWaiting


label wash_dishes_alice:
    $ talk_var['alice_dw'] = 1
    menu:
        Alice_13 "Хочешь о посуде поговорить или пришёл помочь?"
        "Давай, я домою остальное":
            menu:
                Alice_07 "Что это с тобой? Но я не откажусь. И... спасибо."
                "{i}мыть посуду{/i}":
                    if GetRelMax("alice")[0] < 3:
                        $ AddRelMood('alice', 10, 60)
                    else:
                        $ AddRelMood('alice', 0, 60)
                    $ dishes_washed = True
                    $ spent_time = max((60 - int(tm[-2:])), 30)
                    scene BG crockery-morning-00
                    $ renpy.show("Max crockery-morning 01"+mgg.dress)
                    menu:
                        Max_11 "И почему здесь нет посудомоечной машины..."
                        "{i}закончить{/i}":
                            $ cur_ratio = 2
                            jump Waiting
        "Нет, просто хотел поглазеть":
            menu:
                Alice_09 "Знаешь что, вали отсюда, пока мокрой тряпкой по голове не получил!"
                "{i}уйти{/i}":
                    $ spent_time = 10
                    jump Waiting


label talkblog1:
    if "blog" in cooldown:
        if ItsTime(cooldown['blog']): # кулдаун прошел, можно поговорить
            $ del cooldown['blog']
        else:
            jump Alice_cooldown

    menu:
        Alice_00 "А типа ты не знаешь? Позлорадствовать пришёл?"
        "Нет, хотел просто больше узнать":
            menu:
                Alice_02 "Что, даже не начнёшь подкалывать? И что ты хотел узнать?"
                "Расскажи, что ты там делаешь":
                    menu:
                        Alice_13 "Ну, пока наши вещи не пропали во время переезда, я показывала как наносить лак, как применять различные средства и делилась разными хитростями..."
                        "Хитростями? А откуда ты сама всё это узнала?":
                            menu:
                                Alice_01 "Да у других таких же блогеров подсмотрела, конечно. Все так делают! Ну и сама в интернете разное читаю, изучаю..." #Alice_02
                                "Тема бьюти разве единственная?":
                                    menu:
                                        Alice_00 "Нет, но мне это всё как-то по душе. Говорят, у человека лучше получается то, что нравится. А мне это нравится..."
                                        "Давай что-то придумаем вместе!":
                                            jump .together
                                        "Может быть, изменить твой блог?":
                                            jump .otherway
                                "Понятно. Ну, и что теперь?":
                                    jump .whatnow
                        "И что теперь без этих своих вещей делать будешь?":
                            jump .whatnow
                "Как планируешь развиваться?":
                    menu:
                        Alice_00 "Развиваться? Шутишь? Все мои вещи, лаки, материалы и прочее было в том контейнере, который пропал. Теперь у меня нет ничего..."
                        "Что, совсем всё пропало?":
                            menu:
                                Alice_13 "Совсем всё. У меня даже нет подходящей одежды, чтобы вести блог. Нельзя же постоянно быть в одной майке перед зрителями..."
                                "Может, это твоя фишка. Да и майка счастливая...":
                                    menu:
                                        Alice_00 "Ага, потому что единственная, да? А ещё какая фишка? Нет материалов, нечего показывать?"
                                        "Да уж, грусть-печаль...":
                                            jump .sad
                                        "Тебе как-то можно помочь?":
                                            jump .help
                                "Разве это важно?":
                                    menu:
                                        Alice_00 "Очень важно. Ну, ладно, чёрт с ней, с одеждой, а что мне в блоге показывать? Как себя правильно расчёсывать? У меня нет ничего..."
                                        "Да уж, грусть-печаль...":
                                            jump .sad
                                        "Тебе как-то можно помочь?":
                                            jump .help
                        "Постой, и что теперь будет?":
                            jump .whatnow
        "Много подписчиков уже?":
            menu:
                Alice_13 "Да ты издеваешься, да?"
                "Нет. С чего ты взяла?":
                    pass
                "Ты о чём, вообще?":
                    pass
            menu .no:
                Alice_00 "Типа ты не в курсе, что у нас пропала большая часть вещей во время переезда?"
                "Да ладно?":
                    $ cooldown['blog'] = CooldownTime("03:00")
                    Alice_09 "Знаешь что, Макс, отвали!"
                    Max_00 "Ну и ладно..."
                    $ AddRelMood('alice', -5, -50)
                    jump .end
                "В курсе, конечно":
                    menu:
                        Alice_13 "Ну, вот среди тех вещей было всё, что я использовала для ведения своего блога. Одежда, различные лаки, кремы... вообще всё!"
                        "Мне очень жаль...":
                            jump .sad
                        "И как тебе помочь?":
                            jump .help
        "Заработала уже на нём что-то?":
            menu:
                Alice_13 "Макс, тебе заняться нечем, кроме как меня доставать? Ты же знаешь, что у меня всё пропало!"
                "Что пропало? Ты о чём?":
                    jump .no
                "Ты о тех вещах во время переезда?":
                    menu:
                        Alice_00 "Конечно! Там же было вообще всё, что мне нужно для ведения блога. Шмотки, мои любимые лаки, косметика... вообще всё!"
                        "Да, печально...":
                            jump .sad
                        "Тебе можно как-то помочь?":
                            jump .help
    menu .sad:
        Alice_13 "Вот-вот... Надо было всё с собой брать, а не складывать в тот контейнер.."
        "Может быть, попробуем вместе найти решение?":
            jump .together
        "Я обязательно придумаю, что можно с этим сделать":
            jump .findout

    menu .help:
        Alice_01 "Ты у нас внезапно стал миллионером? Или просто деньги появились? Самый простой способ - это купить недостающее. Ну, или найти то, что пропало" #Alice_07
        "Денег у меня нет...":
            menu:
                Alice_13 "А без денег тут ничем не поможешь. Вообще, я в депрессии из-за всей этой истории. Вся жизнь перевернулась..."
                "Вся жизнь? Но это всё к лучшему же. Такой дом, бассейн, место отличное!":
                    menu:
                        Alice_00 "Да, в этом плане ты прав, Макс. Но я хотела чего-то добиться. Стать известным блогером и заработать кучу денег. А теперь..."
                        "Давай что-то придумаем вместе!":
                            jump .together
                        "Ты всё ещё можешь. Может быть, просто смени формат блога":
                            jump .otherway
                "Не грусти. Могло быть и хуже":
                    menu:
                        Alice_00 "Верно. Но всё равно, это всё очень грустно. Я даже не представляю, как теперь быть..."
                        "Давай что-то придумаем вместе!":
                            jump .together
                        "Ты всё ещё можешь. Может быть, просто смени формат блога":
                            jump .otherway
                "Может быть, зарабатывать на чём-то другом?":
                    jump .otherway
        "Я не детектив, чтобы искать вещи...":
            menu:
                Alice_00 "А кто ты тогда? Чем же именно можешь помочь?"
                "Ну, у меня много разных идей":
                    menu:
                        Alice_07 "Много разных идей? Например?"
                        "Давай что-то придумаем вместе!":
                            jump .together
                        "Может быть, изменить твой блог?":
                            jump .otherway
                "Советами!":
                    menu:
                        Alice_06 "И кому уже помогли твои советы? Знаешь, советчиков много. Лучше бы что-то конкретное предложил..."
                        "Давай что-то придумаем вместе!":
                            jump .together
                        "Может быть, изменить твой блог?":
                            jump .otherway

    menu .whatnow:
        Alice_00 "Не знаю, если честно. Мне даже показаться перед зрителями не в чем. Какой же я бьюти-блогер, если на мне всегда одна и та же одежда..."
        "Тебе как-то можно помочь?":
            jump .help
        "Может быть, заняться чем-то другим?":
            jump .otherway
        "Да уж, грусть-печаль...":
            jump .sad

    menu .otherway:
        Alice_00 "Ты о чём? Есть какие-то мысли?"
        "Мы можем с тобой вместе что-то придумать":
            jump .together
        "Давай, я подумаю и, когда будут мысли, продолжим разговор":
            jump .findout
        "Пока нет, но я подумаю что можно сделать":
            jump .findout

    label .together:
        $ talk_var['blog'] = 2
        menu:
            Alice_14 "Вместе? Ещё ничего нет, а уже в партнёры набиваешься?"
            "Ну если придумаю что-то, то почему нет?":
                Alice_01 "Ну, если придумаешь. Если. Да и смотря что... Сильно удаляться от этой темы не хочется. Но попробовать что-то новое можно... В общем, когда что-то придумаешь, тогда и поговорим..." #Alice_07
                Max_00 "Хорошо!"
            "Конечно! Будет у нас семейный бизнес!":
                menu:
                    Alice_07 "Семейный бизнес? На моём блоге? Может ещё и сам вести будешь?"
                    "Ну да, многие так делают!":
                        pass
                    "Нет, из меня ведущий так себе...":
                        pass
                    "Попробовать то можем?":
                        pass
                Alice_00 "Знаешь, мне кажется, что это всё пустые разговоры. Сначала что-то предложи, тогда и обсудим..."
                Max_00 "Хорошо, я подумаю"
            "Ну, можно попробовать же?":
                Alice_00 "Попробовать можно, но сначала надо понять что. Ты пока ничего конкретного не предложил. Когда будут идеи, тогда и подходи..."
                Max_00 "Ладно, когда будут идеи, обсудим..."
        jump .end

    label .findout:
        $ talk_var['blog'] = 2
        menu:
            Alice_07 "Ну, давай. Сомневаюсь, конечно, что это не пустая болтовня, но вдруг... Удиви меня!"
            "Постараюсь...":
                Alice_00 "Постарайся. Я тоже буду думать. Но бросать блог не хочу. Может быть и правда стоит сменить формат, но на какой?"
                Max_00 "Договорились!"
            "Но ничего не обещаю":
                Alice_00 "А я другого и не жду. Если бы у тебя были мысли, ты бы и сам мог на таком заработать... В общем, будем думать..."
                Max_00 "Ага..."
            "Обещаю, что-нибудь придумаю":
                Alice_06 "Обещание, конечно, обнадёживает... Но я бы не стала на твоём месте что-то обещать. Конечно, если у тебя уже нет идей. Так или иначе, я тоже буду думать. Может быть, и правда не всё так плохо..."
                Max_00 "Да прорвёмся!"

    label .end:
        if talk_var['blog'] == 2:
            $ spent_time = 10
            jump Waiting


label alice_talk_tv:
    if _in_replay:
        call alice_tv_closer from _call_alice_tv_closer
    else:
        if 'alice_talk_tv' not in persistent.memories:
            $ persistent.memories['alice_talk_tv'] = 0
    Alice_00 "Нет, садись. Тут места много..."
    $ talk_var['alice_tv'] = 1
    $ renpy.show("Max tv-closer "+pose3_1+mgg.dress)
    Max_00 "Хорошо. Что смотришь?"
    $ __mood = 50
    $ SetCamsGrow(house[4], 110)
    menu:
        Alice_13 "Да так, всякую ерунду. Я просто отдыхаю, и мне без разницы, что смотреть. Поэтому смотрю всё подряд..."
        "Ну, давай смотреть всё подряд..." if not _in_replay:
            Max_11 "{i}( По телеку сегодня нет ничего интересного... Ни порнушки, ни даже эротики... А было бы забавно посмотреть такое с сестрёнкой... ){/i}"
            Max_00 "Ладно, пойду я..."
            jump .end
        "Тебе сделать массаж ног?" if _in_replay or all([not talk_var['al.tv.mas'], (len(online_cources)>1 and online_cources[1].cources[0].less > 0)]):
            $ renpy.show("Max tv-closer 04"+mgg.dress)

    $ talk_var['al.tv.mas'] = 1
    $ _ch4 = GetChance(mgg.social, 4, 900)
    $ _drink = 0
    if flags['alice.tv.mass'] == 0:     # Первая беседа о массаже
        Alice_02 "Что-то новенькое... А ты умеешь?"
        Max_01 "Само собой!"
        Alice_01 "Могу я спросить откуда? Раньше ты, вроде бы, не умел. Да и не представляю, где бы ты мог этому научиться..."
        Max_02 "Онлайн-курсы!"
        menu:
            Alice_02 "Очень смешно, Макс. Разве можно научиться массажу через ютуб?"
            "Почему ютуб? Это платные курсы...":
                Alice_01 "А, ну если ты им ещё и заплатил, то это всё меняет!" nointeract
            "Конечно! Я же научился...":
                Alice_01 "Что-то я очень сомневаюсь, Макс..." nointeract
        if renpy.display_menu([(_("Так тебе продемонстрировать или как? \n{color=[_ch4.col]}(Убеждение. Шанс: [_ch4.vis]){/color}"), 1)]) == 1:
            pass
        $ flags['alice.tv.mass'] = 1
    elif flags['alice.tv.mass'] in range(1, 5):
        menu:
            Alice_02 "Ну, не знаю, не знаю..."
            "Тебе понравится! \n{color=[_ch4.col]}(Убеждение. Шанс: [_ch4.vis]){/color}":
                pass
    else:
        menu:
            Alice_07 "Дай-ка подумаю... Да! Я готова..."
            "Хорошо {i}(начать массаж){/i}" if not _in_replay:
                jump .massage
            "Может конфетку перед массажем?" if kol_choco or _in_replay:  ### если Макс знает о слабости Алисы
                jump .choco

    if not RandomChance(_ch4.ch): ###Убеждение не удалось
        $ Skill('social', 0.05)
        Alice_02 "[failed!t]Нет, Макс, в другой раз. Что-то я сомневаюсь. Вдруг, ты мне что-то сломаешь... Нет, спасибо."
        Max_08 "Ну, как хочешь... Не буду тебе мешать..."
        jump .end

    ### Убеждение удалось
    $ Skill('social', 0.1)
    menu:
        Alice_03 "[succes!t]Ну, давай. Только я очень привередлива в вопросах массажа. Если сделаешь что-то не так, сразу закончим."
        "Хорошо {i}(начать массаж){/i}":
            jump .massage
        "Может конфетку перед массажем?" if kol_choco:  ### если Макс знает о слабости Алисы
            jump .choco

    label .choco:
        if dcv['tvchoco'].done:
            menu:
                Alice_02 "У меня такое чувство, что ты чего-то от меня хочешь... Но не сознаешься ведь?"
                "Хочу тебя!" if not _in_replay:
                    menu:
                        Alice_15 "Макс! Вали нахрен отсюда со своими шуточками. Дай спокойно телевизор посмотреть!"
                        "{i}уйти{/i}":
                            jump .end
                "Просто я такой хороший парень!":
                    Alice_05 "Угу... Точно! Извини, а где мой брат Макс?"
                    Max_01 "Очень смешно. Так ты хочешь конфеты?"
                "Ты узнаешь... В своё время...":
                    Alice_02 "Звучит очень зловеще... И что же я узнаю, интересно? Ах да. Это же секрет... Самому не смешно?"
                    Max_01 "Да, да, очень! Так ты хочешь конфеты?"

    $ _ch3 = GetChance(mgg.social, 3, 900)
    menu:
        Alice_13 "Честно говоря, не знаю. Конфеты я люблю, но не хочу портить фигуру..."
        "От конфетки не поправишься! {color=[_ch3.col]}(Убеждение. Шанс: [_ch3.vis]){/color}":
            if RandomChance(_ch3.ch) or _in_replay:
                ## Алиса съела конфетку
                $ give_choco()
                $ dcv['tvchoco'].set_lost(3)
                $ _drink = 1
                $ Skill('social', 0.1)
                menu:   ### Убеждение удалось
                    Alice_07 "[succes!t]Эх.. Уболтал, чертяка языкастый! Давай сюда конфетку. Но только одну... Вкусно... Теперь я готова, начинай массаж!"
                    "Ну, хорошо {i}(начать массаж){/i}":
                        jump .massage
            else:
                $ dcv['tvchoco'].set_lost(2)
                $ Skill('social', 0.05)
                menu:   ###Убеждение не удалось
                    Alice_01 "[failed!t]Нет, Макс. Спасибо, конечно, но рисковать я не буду. Ну так что, массаж делать будешь или забыл, что собирался?"
                    "Ну, хорошо {i}(начать массаж){/i}":
                        jump .massage

    label .massage:
        $ _pose = {'01':'01', '03':'02', '02':renpy.random.choice(['01','02'])}[pose3_2]
        $ _dress = mgg.dress+alice.dress
        $ _ch25 = GetChance(mgg.massage, 15)  # множители массажа снижены, иначе слишком легко
        $ _ch20 = GetChance(mgg.massage, 10)
        $ _ch15 = GetChance(mgg.massage, 7)
        ### сцена массажа 01 или 02
        scene BG char Alice tv-mass-01
        $ renpy.show('Alice tv-mass ' + _pose + _dress)
        menu:
            Max_03 "{i}( Какая у Алисы нежная кожа... Интересно, о чём она сейчас думает? ){/i}"
            "{i}продолжить{/i} \n{color=[_ch20.col]}(Массаж. Шанс: [_ch20.vis]){/color}":
                pass
        if RandomChance(_ch20.ch) or _in_replay:  ### {i}Алисе понравился массаж!{/i}
            $ Skill('massage', 0.1)
            $ _ch2 = GetChance(mgg.social, 2)
            $ flags['alice.tv.mass'] += 1
            $ _can_double_choko = _drink>0 and kol_choco>0
            $ _pose = {'01':'03', '02':'04'}[_pose]
            scene BG char Alice tv-mass-03
            $ renpy.show('Alice tv-mass ' + _pose + _dress)
            Alice_04 "[alice_good_mass!t]А ты неплох сегодня в этом деле... Хорошо, что ты никакой не работяга. Руки у тебя нежные. Приятно очень..." nointeract
            jump .choice_mass
        else:
            $ Skill('massage', 0.05)
            menu: ### {i}Алисе не понравился массаж!{/i}
                Alice_12 "[alice_bad_mass!t]Ой, Макс, больно! Не надо так. Ты чуть лодыжку не вывихнул мне... Иди ещё потренируйся там на кошках или в ютубе поучись!"
                "{i}закончить{/i}":
                    jump .end

    label .choice_mass:
        $ dial = []
        if alice.dress=='a' and _can_double_choko:
            $ dial.append((_("Может, ещё конфетку? \n{color=[_ch3.col]}(Убеждение. Шанс: [_ch3.vis]){/color}"), 'double_drink'))
        if alice.dress=='a':
            $ dial.append((_("Тебе джинсы не мешают? \n{color=[_ch2.col]}(Убеждение. Шанс: [_ch2.vis]){/color}"), 'jeans'))
        if alice.dress!='a':
            if _drink:
                $ dial.append((_("{i}продолжить{/i} \n{color=[_ch20.col]}(Массаж. Шанс: [_ch20.vis]){/color}"), 'mass'))
            else:
                $ dial.append((_("{i}продолжить{/i} \n{color=[_ch15.col]}(Массаж. Шанс: [_ch15.vis]){/color}"), 'mass'))
        if not _in_replay:
            $ dial.append((_("{i}закончить массаж{/i}"), 'end_mass'))
        $ _can_double_choko = False
        $ rez =  renpy.display_menu(dial)

        if rez == 'double_drink':
            if RandomChance(_ch3.ch) or _in_replay:
                $ _drink = 2
                $ give_choco()
                $ Skill('social', 0.1)
                Alice_02 "[succes!t]Макс, ну какой же ты... А, ладно, давай ещё одну... Но это последняя, больше не предлагай, а то пну сам знаешь куда! А эта конфета, кажется, ещё вкуснее той! От них стало так жарко..."
                Max_01 "Может, тогда тебе стоит снять джинсы? Не будет так жарко..."
                Alice_04 "Только давай ты снимешь их с меня сам, а то я уже так расслабилась, что двигаться не хочется."
                Max_03 "О, это я с радостью сделаю!"
                Alice_07 "Я немного приподнимусь, чтобы тебе было проще их стянуть..."   #спрайт со стягиванием джинсов
                if flags['smoke'] == 'nopants':
                    $ renpy.show('Alice tv-mass ' + _pose + _dress+'-3')
                    Max_06 "О да, это ты классно придумала!"   #если на Алисе нет трусиков
                else:
                    $ renpy.show('Alice tv-mass ' + _pose + _dress+'-2')
                    Max_05 "О да, так гораздо лучше..."   #если на Алисе есть трусики
                Alice_05 "Ты только там сильно не заглядывайся, куда не нужно! Лучше скорее продолжай массаж, пока я не расхотела..."
                if flags['smoke'] == 'nopants':
                    Max_07 "Ну да... точно... я же... это... массаж делал."   #если на Алисе нет трусиков
                    Alice_03 "Ты чего там так тормозишь? Как будто в трусиках меня никогда не видел..."
                    # стянуть джинсы до конца
                    $ renpy.show('Alice tv-mass ' + _pose + _dress+'-4')
                    Alice_15 "Ой, Макс, я же сегодня без них! Вот чёрт! Чего глазеешь, иди отсюда, ты и так увидел больше положенного..."   #спрайт с прикрыванием
                    Max_05 "Ладно, но это было так сногсшибательно, что я аж забыл, как ходить!"
                    Alice_18 "Макс!!!"
                    Max_04 "Всё, ушёл."
                    if not _in_replay:
                        $ current_room = house[0]
                    jump .end
                else:
                    Max_02 "Ага, сейчас продолжим..."   #если на Алисе есть трусики
                    # Дальше все продолжается, как и в случае, если Алиса сама сняла джинсы.
                    $ _dress = mgg.dress+'c'
                    $ alice.dress = 'c'
                    $ renpy.show('Alice tv-mass ' + _pose + _dress)
                    jump .not_jeans
            else:
                Alice_03 "[failed!t]Нет, мне хватит одной... А то я мигом фигуру испорчу. Лучше продолжай массировать мои ножки..." nointeract
                jump .choice_mass

        elif rez == 'jeans':
            if RandomChance(_ch2.ch) or _in_replay:
                jump .jeans
            else:
                $ Skill('social', 0.05)
                menu:   ### Убеждение не удалось!
                    Alice_05 "[failed!t]Это так ты к девушкам подкатываешь, сразу предлагаешь снять штаны?"
                    "Э... Я к тебе не подкатываю. Просто, жарко же...":
                        pass
                    "Ну у тебя и фантазии, Алиса... Я не подкатываю!":
                        pass
                Alice_01 "Да шучу я. Но джинсы снимать не стану. Даже не надейся. Кстати, ты закончил с массажем? Спасибо большое, можешь идти..."
                Max_00 "Вот так вот..."
                jump .end

        elif rez == 'mass':
            jump .massage_next

        else:
            Alice_07 "Как, всё? А мне понравилось... Спасибо, Макс. Вот ты и сделал девушке приятно!"
            Max_07 "Я и не так могу..."
            Alice_05 "Ах ты и не так можешь? Боюсь даже представить, как... Но не буду. И тебе не советую. Так что давай, дуй отсюда!"
            Max_00 "Угу..."
            jump .end


    label .jeans:
        $ Skill('social', 0.1)
        ### Убеждение снять джинсы удалось
        if 'smoke' in talk_var and flags['smoke'] == 'nopants':  # Алиса сейчас без трусиков
            menu:   ### Убеждение удалось, но Алиса без трусов
                Alice_02 "[succes!t]Да, что-то тесновато в них и так жарко... Хотя...Не-е-ет, нет, нет! Не буду снимать я сейчас джинсы. Не дождёшься!"
                "Почему?":
                    Alice_01 "Сам догадайся, глупый. Но я намекну: возможно, под джинсами ничего нет. Понял? Всё, а теперь иди отсюда, фантазируй..."
                    Max_01 "Ух, пойду тогда... Пофантазирую где-нибудь..."
                    jump .end
                "Потому-что ты без трусиков?":
                    Alice_03 "Сам догадался, или кто подсказал? Ну всё, теперь ты всё обо мне знаешь, иди и фантазируй о чём хочешь..."
                    Max_01 "Ладно, пойду пофантазирую где-нибудь..."
                    jump .end
        else:
            Alice_04 "[succes!t]Ты знаешь, мешают. И очень жарко. Пожалуй, порадую тебя немного, раз ты так хорошо массаж делаешь..."
            Max_07 "{i}( Ого... ){/i}"
            ### Алиса без джинсов
            $ _dress = mgg.dress+'c'
            $ renpy.show('Alice tv-mass ' + _pose + _dress)
            menu .not_jeans:
                Alice_05 "Да, так гораздо лучше. Только ты не пялься, куда не надо. Вижу, краем глаза пытаешься что-то разглядеть. Вот не надо. Лучше, продолжай массаж..."
                "А почему на тебе трусики?" if flags['smoke'] == 'not_nopants':
                    Alice_07 "А с чего бы мне быть без них!"
                    Alice_14 "Ой..."
                    Max_09 "Вот ты и попалась! Я значит тут со всей любезностью массаж сестрёнке делаю, конфетами угощаю, а она..."
                    Alice_12 "Просто забыла..."
                    Max_07 "Тогда, если хочешь продолжения массажа, то снимай их!"
                    Alice_06 "Макс! Какой же ты... Ладно, только не смотри. И когда продолжишь массаж, не пялься на меня!"
                    Max_03 "Да, да, конечно."
                    $ renpy.show('Alice tv-mass ' + _pose + mgg.dress+alice.dress+'-4')
                    Alice_13 "Хотя, нет, не пойдёт! У меня так всё видно будет... И хватит уже пялиться! Лучше иди уже по своим делам."   #спрайт с прикрыванием
                    Max_05 "Как скажешь. Трусы не потеряй."
                    if not _in_replay:
                        $ added_mem_var('alice_not_nopants')
                        $ current_room = house[0]
                    jump .end
                "{i}продолжить{/i} \n{color=[_ch25.col]}(Массаж. Шанс: [_ch25.vis]){/color}" if _drink==2:
                    $ alice.dress = 'c'
                    jump .massage_next
                "{i}продолжить{/i} \n{color=[_ch20.col]}(Массаж. Шанс: [_ch20.vis]){/color}" if _drink==1:
                    $ alice.dress = 'c'
                    jump .massage_next
                "{i}продолжить{/i} \n{color=[_ch15.col]}(Массаж. Шанс: [_ch15.vis]){/color}" if not _drink:
                    $ alice.dress = 'c'
                    jump .massage_next

    label .massage_next:
        if (RandomChance(_ch20.ch) and _drink==1) or (RandomChance(_ch25.ch) and _drink==2) or _in_replay: ### {i}Алисе понравился массаж!{/i} Алиса съела конфетку
            $ Skill('massage', 0.1)
            $ _pose = {'03':'05', '04':'06'}[_pose]
            scene BG char Alice tv-mass-05
            $ renpy.show('Alice tv-mass ' + _pose + _dress)
            menu:
                Alice_07 "Макс... Сегодня твои ручки творят чудеса... А во что это моя нога упёрлась? Это часть программы или как?"
                "Да, это будет на десерт...":
                    menu:
                        Alice_08 "Ты так в себе уверен, Макс... Забыл, что я твоя сестра? Не говори глупости... Просто продолжай массировать мои ножки. Если ты ещё не в курсе, они у меня целиком - эрогенная зона..."
                        "{i}попытаться приставать{/i}" if not _in_replay:
                            jump .fail
                        "{i}продолжать массаж{/i}":
                            pass
                "{i}продолжать молча{/i}":
                    menu:
                        Alice_04 "Эх, Макс... А я бы захотела продолжения, если бы ты был моим парнем... Жаль, что ты только мой брат..."
                        "Ну я могу стать твоим парнем... Хотя бы на час... или насколько захочешь..." if not _in_replay:
                            menu:
                                Alice_05 "На сколько захочу? На секунду! Ой. Она прошла... Всё, Макс, твоё время вышло... Ладно, засовывай свою штуку обратно. Что-то голова кружится... Макс, уйди по хорошему, а..."
                                "{i}уйти{/i}":
                                    jump .end
                        "{i}попытаться приставать{/i}" if not _in_replay:
                            jump .fail
                        "{i}продолжать массаж{/i}":
                            pass

        elif RandomChance(_ch15.ch) and not _drink: ### {i}Алисе понравился массаж!{/i} конфету Алиса не ела
            $ Skill('massage', 0.1)
            Alice_03 "Ух, как хорошо... Макс, а ты молодец сегодня! Не ожидала такой чувственности и в то же время силы... Ну всё спасибо, иди..."
            Max_04 "Не за что..."
            jump .end

        else: ### {i}Алисе не понравился массаж!{/i}
            $ Skill('massage', 0.05)
            Alice_13 "[alice_bad_mass!t]Ой, нет, что-то не то. Ты же так хорошо начал, и что-то неприятно стало... Иди, ещё поучись этому своему массажу на ютубе. Так не пойдёт..."
            Max_00 "Ладно..."
            jump .end

    $ _pose = {'05':'07', '06':'08'}[_pose]
    scene BG char Alice tv-mass-07
    $ renpy.show('Alice tv-mass ' + _pose + _dress)
    Alice_04 "Ну всё, кажется хватит. Во всяком случае, тебе. А то мне ногу испачкаешь... Но ручки у тебя - что надо. Даже не ожидала такого от тебя..."
    Max_05 "Я тоже не ожидал... такого..."
    Alice_08 "Значит, мы оба полны сюрпризов. Ну всё, хорошего помаленьку. Давай, засовывай свой член обратно, а то до добра это всё дело не дойдёт... Да, и спасибо за массаж..."
    Max_03 "Тебе спасибо..."
    $ talk_var['al.tvgood'] += 1
    $ persistent.memories['alice_talk_tv'] = 1
    $ talk_var['al.tv.mas'] = 3
    jump .end

    menu .fail:
        Alice_12 "Макс! Ещё одно лишнее движение, и я дам тебе по шарам вот это самой ногой. Ты меня понял? Всё, массаж окончен, вали отсюда!"
        "{i}уйти{/i}":
            jump .end

    label .end:
        $ renpy.end_replay()
        $ spent_time = max((60 - int(tm[-2:])), 40)
        $ AddRelMood('alice', 0, __mood)
        $ cur_ratio = 0.5
        jump Waiting


label alice_aboutbooks:
    menu:
        Alice_13 "Книжку, очевидно..."
        "Спасибо, кэп!":
            Alice_01 "Так чего хотел, Макс?"
            Max_00 "Хотел узнать что именно читаешь..."
        "Очень смешно...":
            Alice_01 "Так чего хотел, Макс?"
            Max_00 "Хотел узнать что именно читаешь..."
        "Это понятно, а какую? Только не говори, что бумажную...":
            Alice_01 "Ну вот... Хотела пошутить, и ты так меня обломал... А если серьёзно, то чего хотел?"
            Max_00 "Да вот и хотел узнать, что именно читаешь..."
    menu:
        Alice_02 "Забавно. А тебе не всё равно? Или делать нечего?"
        "Не всё равно, раз спрашиваю":
            pass
        "Так ты скажешь или нет?":
            pass
    menu:
        Alice_00 "Не скажу. Сиди теперь и гадай! \n\n{i}Алиса прикрыла обложку рукой{/i}"
        "Какой-то дамский роман?":
            pass
        "Какие-то сопли с сахаром?":
            pass
        "Неужели справочник по квантовой механике?":
            pass
    Alice_01 "Думай, что хочешь, а я всё равно не скажу."
    Max_00 "Ну и ладно!"
    $ spent_time += 10
    $ poss['secretbook'].OpenStage(0)
    $ AvailableActions['searchbook'].enabled = True
    jump Waiting


label first_talk_smoke:
    $ __mood = 0
    Alice_13 "Упс. Макс, ты ничего не видел!"
    $ poss['smoke'].OpenStage(0)
    Max_08 "Алиса, ты куришь?!"
    $ dcv['smoke'].stage = 1
    menu:
        Alice_12 "Нет, блин, просто зажгла сигарету, посмотреть как горит... Давай так, ты уйдёшь и сделаешь вид, что ничего не было, хорошо?"
        "А если уйду, что мне за это будет?":
            Alice_16 "Вот если не уйдёшь, то узнаешь, что тебе за это будет! Бегом отсюда!"
            Max_09 "Может быть я и уйду, но..."
        "Ну ок, только я ничего не обещаю...":
            pass
    menu:
        Alice_14 "Что это значит? Шантажировать меня вздумал?!"
        "Ну да. Мама что с тобой сделает, если узнает?":
            menu:
                Alice_13 "Макс, я тебя по-человечески прошу. Сделай вид, что ничего не было. Я не хочу расстраивать маму..."
                "Не хочешь расстраивать маму или получить по заднице?":
                    menu:
                        Alice_12 "Может быть, и то и другое. Ну так как, Макс?"
                        "Мы всё ещё можем договориться...":
                            jump .talk
                        "Посмотрим...":
                            jump .maybe
                        "Ну ладно, ладно...":
                            $ __mood += 75
                            jump .goodend
                "Мы всё ещё можем договориться...":
                    jump .talk
                "Посмотрим...":
                    jump .maybe
                "Ну ладно, ладно...":
                    $ __mood += 100
                    jump .goodend
        "Ну, мы можем договориться":
            jump .talk
        "Как знать, может быть...":
            jump .maybe
        "Нет, конечно. Мне жизнь дорога!":
            $ __mood += 50
            jump .goodend

    menu .talk:
        Alice_13 "Договориться? Ну ладно. Чего ты хочешь?"
        "Если заплатишь, буду молчать":
            menu:
                Alice_12 "Макс, ты же знаешь, что я на мели. У меня нет денег. Ну, точнее есть, но баксов 10. Тебя устроит?"
                "Ну, давай":
                    $ __mood -= 50
                    $ spent_time += 10
                    $ mgg.ask(0)
                    $ flags['smoke.request'] = 'money'
                    menu:
                        Alice_13 "Сейчас сбегаю за деньгами...\nВот, держи $10, и теперь-то уж точно ты ничего не видел. Так?"
                        "Так!":
                            jump .end
                        "Как знать...":
                            menu:
                                Alice_16 "И как это понимать, Макс? Мы же договорились! Ну ты и гад... Всё, вали отсюда!"
                                "Ну, как скажешь...":
                                    jump .end

                "Нет, этого мало...":
                    $ __mood -= 100
                    menu:
                        Alice_12 "Ну... больше у меня нет. Может, просто забудем обо всём?"
                        "Покажи сиськи!":
                            jump .bad
                        "Сними трусы!":
                            jump .bad
                        "Отсоси мне!":
                            jump .suck
                        "Ты будешь мне должна услугу":
                            jump .owe
        "Покажи сиськи!":
            jump .bad
        "Сними трусы!":
            jump .bad
        "Отсоси мне!":
            jump .suck
        "Ты будешь мне должна услугу":
            jump .owe

    menu .bad:
        Alice_15 "Что?! Ну ты хам! Всё, быстро свалил!"
        "Ну, как скажешь...":
            $ __mood -= 100
            jump .end

    menu .suck:
        Alice_14 "Что?! Отсоси себе сам! Пошёл вон отсюда!"
        "Ну, как скажешь...":
            $ __mood -= 150
            jump .end

    menu .owe:
        Alice_06 "И какую услугу я тебе буду должна? Например?"
        "Покажешь сиськи, когда попрошу...":
            pass
        "Разденешься, когда скажу...":
            pass
        "Отсосёшь, когда нужно будет...":
            jump .suck
        "Я ещё не решил...":
            menu:
                Alice_03 "Ну, вот когда решишь, тогда и поговорим. А теперь иди отсюда, пока ещё можешь!"
                "Ну, как скажешь...":
                    jump .end

    menu:
        Alice_15 "Да ты что?! А не охренел ли ты, мальчик? Это всего лишь сигарета, а запросы у тебя... Всё, свалил отсюда!"
        "Ну, как скажешь...":
            $ __mood -= 100
            jump .end

    menu .maybe:
        Alice_12 "Сам смотри... Воевать со мной - себе дороже. И, вообще, иди отсюда. Я уже взрослая и могу делать что хочу..."
        "Ну, как скажешь...":
            pass

    menu .goodend:
        Alice_05 "Вот это другое дело. Ладно, разговор окончен..."
        "Хорошо...":
            pass

    label .end:
        $ spent_time += 10
        $ AddRelMood('alice', 0, __mood)
        jump Waiting


label second_talk_smoke:
    $ __mood = 0
    menu:
        Alice_12 "А, Макс... Чего хотел?"
        "Да хотел узнать, что ты куришь?":
            menu:
                Alice_03 "Это так важно для тебя? Почему спрашиваешь?"
                "Просто любопытно":
                    Alice_00 "Любопытный он... Это сигареты Lucky Strike..."
                "Для поддержания разговора":
                    Alice_00 "Да уж, поддержание разговора... А курю я сигареты Lucky Strike..."
                "А, не важно...":
                    Alice_00 "Ну, раз не важно, то и не скажу..."
                    Max_01 "Конечно, если это секрет..."
                    Alice_01 "Вот ты зануда, Макс! Обычные сигареты Lucky Strike..."
        "Нет, ничего...":
            jump Waiting
    Max_07 "Они же не женские?!"
    menu:
        Alice_13 "Ну да... Просто нравится именно эти. Их наш отец курил, сначала запах нравился. Когда выросла, попробовала и теперь вот втянулась..."
        "Скучаешь о нём?":
            Alice_00 "Ну так, не очень, если честно. Уже слишком много времени прошло... А сигареты - это так, привычка..."
            Max_00 "Может, и мне попробовать?"
        "Может, и мне попробовать?":
            pass
    menu:
        Alice_06 "Нет уж, самой мало. Знаешь, как сложно их достать? Особенно, чтобы мама не узнала..."
        "Может быть, тебе помочь?":
            $ __mood += 100
            menu:
                Alice_02 "С чем? Можешь доставать такие сигареты?"
                "Ну, я попробую через интернет":
                    pass
                "Может быть...":
                    pass
            Alice_03 "Давай! Было бы чудненько. Только маме не пали меня, а то я на тебя обижусь и больше никогда разговаривать не буду..."
            Max_05 "Договорились!"

        "Ну и ладно...":
            $ __mood += 50
            Alice_12 "Я закончила. Если мама спросит, скажешь, от соседей надуло, хорошо?"
            Max_01 "Конечно!"

    $ AddRelMood('alice', 5, __mood)
    $ items['cigarettes'].InShop = True
    $ notify_list.append(_("В интернет-магазине доступен новый товар."))
    $ talk_var['alice.pun'] = 0
    $ dcv['smoke'].stage = 2
    $ dcv['smoke'].set_lost(1)
    $ dcv['betray_smoke'] = Daily(1, False, True)
    $ AvailableActions['searchciga'].enabled = True
    $ spent_time = 30 - int(tm[-2:])
    jump Waiting


label gift_cigarettes:
    $ __mood = 0
    menu:
        Alice_03 "Это то, что я думаю? Давай сюда!"
        "А что взамен?":
            Alice_13 "Макс! Не будь придурком. Давай сюда!"
            Max_01 "Ну, держи..."
        "Только давай условимся, что ты не скажешь маме о том, как я за тобой подглядывал в душе..." if peeping['alice_shower'] == 3:
            Alice_12 "Хоть ты и тот ещё извращенец, Макс, но мне было сложно достать эти сигареты самой... Так и быть, мама ничего не узнает, по крайней мере в этот раз."
            Max_05 "Спасибо, Алиса! И я не извращенец. Просто проходил мимо, а там такая красотища..."
            Alice_05 "Ну да, ну да... Мимо он проходил..."
            $ punreason[1] = 0
            $ peeping['alice_shower'] = 0
        "Держи!":
            if alice.GetMood()[0] < -1:
                Alice_05 "Хотя ты и полный придурок, но, похоже, начинаешь исправляться!"
                Max_08 "Я не полный придурок!"
                $ __mood += 50
            else:
                Alice_07 "Спасибо, Макс. Вот теперь я понимаю, ты настоящий брат!"
                Max_05 "Да не за что..."
    Alice_03 "Спасибо. И маме ни слова! У неё рука очень тяжёлая, особенно когда речь о сигаретах..."
    Max_01 "Конечно!"
    $ items['cigarettes'].have = False
    $ __mood += 100
    $ spent_time += 10
    $ dcv['smoke'].set_lost(0)
    $ AddRelMood('alice', 10, __mood)
    return


label smoke_nofear:
    Alice_00 "Макс, поглазеть пришёл?"
    Max_09 "Не боишься, что мама накажет, если узнает?"
    menu:
        Alice_03 "И как она узнает? Ты расскажешь?"
        "Может быть...":
            Alice_12 "Ты хорошо подумал, Макс? Жизнь-то у тебя одна... И что ты хочешь за... молчание?"
            Max_01 "Вот это разговор!"
            menu:
                Alice_13 "Сначала скажи, что у тебя на уме..."
                "Дай $20, и я буду молчать":
                    pass
                "Если днем ты будешь ходить без трусов, буду молчать":
                    pass
                "Если будешь курить без верха, буду молчать":
                    pass
                "Если разрешишь тебя отшлёпать, ничего не скажу":
                    pass
                "Ничего. Не переживай!":
                    jump .no
            Alice_16 "А больше ты ничего не хочешь? Свали отсюда, пока не наваляла!!"
            $ AddRelMood('alice', 0, -100)
            $ spent_time = 10
            jump Waiting
        "Нет, конечно!":
            jump .no
    menu .no:
        Alice_03 "Ну вот и пугать не надо. Не узнает. А если ты проболтаешься, я тебя во сне придушу, понял? Теперь иди, не мешай мне..."
        "Угу...":
            $ AddRelMood('alice', 0, 30)
            $ spent_time = 10
            jump Waiting


label smoke_fear:
    Alice_00 "Макс, поглазеть пришёл?"
    Max_09 "Не боишься, что мама накажет если узнает?"
    Alice_12 "Ты же ей не скажешь? Она так больно меня отшлёпала в прошлый раз, что до сих пор сидеть неприятно..."
    Max_01 "Ну, это зависит от тебя..."
    $ _ch8 = GetChanceConvince(punalice, 8)
    $ _ch4 = GetChanceConvince(punalice, 4)
    $ _ch3 = GetChanceConvince(punalice, 3)
    $ _ch2 = GetChanceConvince(punalice, 2)
    $ _ch1 = GetChanceConvince(punalice)
    $ __can_nojeans = all(['kira' in chars, clothes[alice].casual.cur==0, 'pajamas' in alice.gifts])
    menu:
        Alice_13 "Говори, что ты хочешь за молчание?"
        "Дай $20, и я ничего не скажу {color=[_ch8.col]}(Убеждение. Шанс: [_ch8.vis]){/color}":
            jump .money
        "Если днем ты будешь ходить без трусов, буду молчать {color=[_ch4.col]}(Убеждение. Шанс: [_ch4.vis]){/color}" if clothes[alice].casual.cur==0:
            # пункт доступен, если Алиса носит джинсы
            jump .nopants
        "Если больше не будешь носить лифчик, буду молчать {color=[_ch3.col]}(Убеждение. Шанс: [_ch3.vis]){/color}":
            jump .sleep_toples
        "Если будешь курить без верха купальника, буду молчать {color=[_ch3.col]}(Убеждение. Шанс: [_ch3.vis]){/color}":
            jump .smoke_toples
        "Если хочешь, чтобы мама ничего не знала, ты будешь ходить без джинсов." if __can_nojeans:
            Alice_12 "Ходить без джинсов? А ты не обнаглел, Макс?!"
            Max_01 "Нет, нисколько. Согласна?"
            Alice_05 "И как ты себе это представляешь? Или в твоей извращённой фантазии мама просто не заметит, что я расхаживаю по дому в трусах?!"
            Max_02 "А ты снимай джинсы когда её нет дома и всё будет в порядке."
            Alice_13 "Ладно, тётя Кира ещё может на это и не обратит внимание, а если Лиза спросит, почему я без штанов?"
            Max_08 "Ой, Алиса, хватит уже искать отговорки... Как будто тебе бы и в голову не пришло сказать ей, что дома просто жарко."
            menu:
                Alice_06 "Лучше попроси что-то другое..."
                "Нет. Или получаешь вечером по заднице или не одеваешь джинсы. {color=[_ch2.col]}(Убеждение. Шанс: [_ch2.vis]){/color}":
                    if RandomChance(_ch2.ch):
                        $ Skill('social', 0.2)
                        menu:
                            Alice_03 "[succes!t]Хорошо. Не буду я одевать джинсы, только дай уже покурить спокойно!"
                            "Конечно!":
                                $ punalice[0][0] = 7
                                $ flags['smoke.request'] = "nojeans"
                                $ flags['smoke'] = "nojeans"
                                $ added_mem_var('nojeans')
                                jump .end
                    else:
                        jump .fail
                "Дай $20, и я ничего не скажу {color=[_ch8.col]}(Убеждение. Шанс: [_ch8.vis]){/color}":
                    jump .money
                "Если днем ты будешь ходить без трусов, буду молчать {color=[_ch4.col]}(Убеждение. Шанс: [_ch4.vis]){/color}" if clothes[alice].casual.cur==0:
                    # пункт доступен, если Алиса носит джинсы
                    jump .nopants
                "Если больше не будешь носить лифчик, буду молчать {color=[_ch3.col]}(Убеждение. Шанс: [_ch3.vis]){/color}":
                    jump .sleep_toples
                "Если будешь курить без верха купальника, буду молчать {color=[_ch3.col]}(Убеждение. Шанс: [_ch3.vis]){/color}":
                    jump .smoke_toples
        # "Если разрешишь тебя отшлёпать, то я ничего не скажу {color=[_ch1.col]}(Убеждение. Шанс: [_ch1.vis]){/color}":
        #     # будет доступно в следующих версиях
        #     if RandomChance(_ch1.ch):
        #         $ punalice[0][0] = 3
        #     else:
        #         $ Skill('social', 0.1)
        #         jump .fail
        "Ты знаешь, я сегодня добрый...":
            $ punalice[0][0] = 1
            menu:
                Alice_06 "Сегодня? Значит, попросишь в следующий раз?"
                "Как знать, может быть...":
                    pass
                "Что ты! Нет, конечно...":
                    pass
            menu:
                Alice_13 "Ну вот тогда иди чем-нибудь займись, а меня не отвлекай..."
                "{i}уйти{/i}":
                    $ flags['smoke'] = None
                    $ flags['smoke.request'] = None
                    jump .end

    label .smoke_toples:
        if RandomChance(_ch3.ch):
            $ Skill('social', 0.2)
            Alice_12 "[succes!t]Маленький извращенец... Ладно, но при условии, что маме не будешь ничего говорить. И разденусь только с завтрашнего дня. Договорились?"
            Max_03 "Само собой!"
            $ punalice[0][0] = 4
            $ flags['smoke.request'] = "toples"
            menu:
                Alice_01 "А теперь вали отсюда. Дай спокойно покурить!"
                "{i}уйти{/i}":
                    jump .end
        else:
            jump .fail

    label .sleep_toples:
        if RandomChance(_ch3.ch):
            $ Skill('social', 0.2)
            Alice_03 "[succes!t]Да я вообще-то и так без лифчика все время хожу, только когда сплю одеваю..."
            Max_01 "Значит, тогда просто спи без него."
            Alice_05 "Не знаю, зачем тебе, извращенцу, это нужно, но лучше я соглашусь на этот пустяк... Пока ты что-нибудь ещё не попросил."
            Max_04 "Вот и отлично!"
            $ punalice[0][0] = 5
            $ flags['smoke.request'] = "sleep"
            $ flags['smoke'] = 'sleep'
            $ alice.sleeptoples = True
            $ added_mem_var('alice_sleeptoples')
            menu:
                Alice_01 "А теперь вали отсюда. Дай спокойно покурить!"
                "{i}уйти{/i}":
                    jump .end
        else:
            jump .fail

    label .nopants:
        if RandomChance(_ch4.ch):
            $ Skill('social', 0.2)
            Alice_13 "[succes!t]Тебя так заботят мои трусы? Ну, хорошо. Всё равно я почти всё время в джинсах, так что не страшно. Значит, договорились?"
            Max_02 "Конечно!"
            $ punalice[0][0] = 6
            $ flags['smoke.request'] = "nopants"
            $ flags['smoke'] = 'nopants'
            $ alice.nopants = True
            $ added_mem_var('alice_nopants')
            menu:
                Alice_01 "А теперь вали отсюда. Дай спокойно покурить!"
                "{i}уйти{/i}":
                    jump .end
        else:
            jump .fail

    label .money:
        if RandomChance(_ch8.ch):
            $ punalice[0][0] = 3
            $ spent_time += 10
            $ Skill('social', 0.2)
            $ _ch2 = GetChanceConvince(punalice, 2)
            $ flags['smoke.request'] = 'money'
            menu:
                Alice_12 "[succes!t]Ладно, Макс, я дам тебе денег, но только $10, ок?"
                "Нет, давай $20 {color=[_ch2.col]}(Убеждение. Шанс: [_ch2.vis]){/color}":
                    if RandomChance(_ch2.ch):
                        $ Skill('social', 0.2)
                        Alice_13 "[succes!t]Чёрт с тобой, Макс. Совсем без денег оставить хочешь... Сейчас принесу..."
                        Max_03 "Я жду..."
                        $ mgg.ask(1)
                        $ AddRelMood('alice', 0, -50)
                    else:
                        Alice_16 "[failed!t]Макс, не наглей! Сейчас принесу $10. Жди..."
                        Max_04 "Ну ладно, я жду..."
                        $ mgg.ask(0)
                        $ Skill('social', 0.1)
                        $ AddRelMood('alice', 0, -75)
                "Хорошо, устроит и $10":
                    $ mgg.ask(0)
                    $ AddRelMood('alice', 0, -25)
            menu:
                Alice_12 "Держи свои деньги... И больше меня не шантажируй. Я очень это не люблю... А теперь вали отсюда!"
                "Удачи!":
                    jump .end
        else:
            jump .fail

    menu .fail:
        Alice_16 "[failed!t]Ага, сейчас! Ну ты и хам, Макс... Всё, отвали, дай покурить спокойно..."
        "{i}уйти{/i}":
            $ Skill('social', 0.1)
            $ flags['smoke'] = None
            $ flags['smoke.request'] = None
            $ punalice[0][0] = 2
            $ AddRelMood('alice', 0, -50)
            jump .end

    menu .fail2:
        Alice_12 "[failed!t]Вот так значит? А я вот выбираю вариант, в котором ты, может быть, останешься сегодня цел, если очень быстро свалишь отсюда и не будешь мне надоедать... Пока я ещё более-менее добрая."
        "{i}Ну, как скажешь...{/i}":
            $ Skill('social', 0.1)
            $ flags['smoke'] = None
            $ flags['smoke.request'] = None
            $ punalice[0][0] = 2
            $ AddRelMood('alice', 0, -50)
            jump .end

    label .end:
        $ spent_time += 10
        jump Waiting


label smoke_toples:
    menu:
        Alice_02 "Ну что, извращенец, доволен видом?"
        "Доволен, конечно!":
            menu:
                Alice_05 "Надеюсь, маме ничего не расскажешь? А то это опасно для твоей жизни..."
                "Не переживай, не скажу":
                    Alice_04 "Вот и молодец. А теперь иди, займись чем-нибудь..."
                    Max_01 "Хорошо..."
                "Ну... это зависит от тебя!":
                    Alice_09 "Макс, не играй с огнём! Мы договоривались. А теперь сгинь с глаз моих!"
                    Max_07 "Уже ухожу..."
                    $ AddRelMood('alice', 0, -50)
        "А чего ты прикрываешься?":
            Alice_04 "А про руки мы не договаривались. Хочу - прикрываюсь. Хочу - нет. А тебя так радовать я точно не хочу... Так что вали уже..."
            Max_09 "Ну и ладно..."
        "Я передумал. Можешь одеться.":
            menu:
                Alice_12 "Спасибо, ваше величество! А чего это ты так расщедрился? Я больше не в твоём вкусе?"
                "Ну, я перегнул палку...":
                    menu:
                        Alice_04 "Спасибо, Макс! Это мудрый поступок... Но ты можешь в последний раз поглазеть..."
                        "{i}уйти{/i}":
                            $ AddRelMood('alice', 0, 100)
                "Это мой шаг к дружбе":
                    menu:
                        Alice_03 "Даже так?! Ну, дружбу я не обещаю, но могу пытаться с тобой как-то уживаться. А если серьёзно, то спасибо, Макс. Я это оценила..."
                        "{i}уйти{/i}":
                            $ AddRelMood('alice', 0, 120)
                "Да надоела!":
                    menu:
                        Alice_15 "Что?! А ну-ка вали отсюда, пока цел! И оденусь теперь, будь уверен!"
                        "{i}свалить{/i}":
                            pass
                "Ты знаешь, пусть всё остаётся как есть...":
                    menu:
                        Alice_12 "Ах вот как? Опять передумал? Всё, ты мне надоел, свали отсюда!"
                        "{i}свалить{/i}":
                            $ AddRelMood('alice', 0, -50)
                            $ spent_time += 10
                            jump Waiting
            $ flags['smoke'] = None
            $ flags['smoke.request'] = None
            $ flags['noted'] = False

    label .end:
        $ spent_time += 10
        jump Waiting


label smoke_not_toples:
    Alice_02 "Ты чего-то хотел, Макс?"
    Max_07 "Да, хотел... Мы ведь договорились, что ты будешь курить без верха купальника!"
    Alice_13 "Знаешь, Макс, мне это надоело... Сколько можно? Я хочу спокойно курить и не волноваться, что ты за мной подглядываешь!"
    Max_09 "Ну хорошо, Алиса, как скажешь, можешь курить одетой. Только вот, если ты решила нарушить условия нашей договорённости, то почему бы тогда и мне не поступить так же?"
    Alice_06 "Только не надо маме рассказывать о об этом..."
    Max_00 "Всё зависит от тебя, сестрёнка... Если сейчас снимешь верх и в качестве извинения покажешь грудь, то я представлю, будто ты ничего не нарушала."
    Alice_15 "Ах ещё и грудь показать! Может сразу и полапать её дать?!"
    Max_03 "Очень заманчивое предложение... Но просто показать - я считаю справедливо! Сама накосячила..."

    $ renpy.show("Alice smoke "+renpy.random.choice(["01", "02", "03"])+"c")

    Alice_06 "Ладно, один разок и быстро... Но не вздумай маме рассказывать! Ни про это, ни про сигареты."
    Max_05 "Конечно, я ведь своё слово держу. Симпатичные сосочки!"
    menu:
        Alice_13 "Ну всё, полюбовался и хватит. Вали отсюда, дай спокойно покурить..."
        "Ага...":
            pass
    $ renpy.show("Alice smoke "+pose3_3+alice.dress)
    $ flags['smoke'] = None
    $ flags['smoke.request'] = None
    $ flags['noted'] = False
    $ spent_time += 10
    $ current_room = house[5]
    jump Waiting


label smoke_nopants:
    menu:
        Alice_02 "Макс, чего хотел?"
        "Ничего, просто любуюсь...":
            Alice_13 "Налюбовался? А вот теперь постой в сторонке, пока я покурю! Давай, вали уже..."
            Max_01 "Хорошо, хорошо..."
        "А ты чего в трусах?":
            menu:
                Alice_12 "В каком смысле?!"
                "Мы же договорились - без трусов!":
                    Alice_01 "Ты совсем идиот, Макс? Я в купальнике. Это не трусы. А трусы я и так не ношу под джинсами. Можешь себе фантазировать теперь сколько хочешь... Всё, уйди с глаз моих долой!"
                    Max_01 "Ухожу, ухожу..."
                "Да шучу я...":
                    Alice_02 "Ты наверное думаешь, что у тебя забавные шутки, да? Так вот нет. Абсолютно несмешные, дебильные шутки, как и ты сам. Всё, свалил отсюда. Я занята..."
                    Max_01 "Как скажешь..."
        "Я передумал. Можешь носить трусы...":
            menu:
                Alice_03 "А чего передумал? А, хотя не важно. Я рада, а то мне всё натирает в джинсах... Хотя, тебе такие подробности знать не нужно. Спасибо за разрешение, ваше величество. Теперь дай покурю..."
                "{i}уйти{/i}":
                    $ flags['smoke'] = None
                    $ flags['smoke.request'] = None
                    $ flags['noted'] = False
                    $ alice.nopants = False
                    $ AddRelMood('alice', 0, 100)

    $ spent_time += 10
    jump Waiting


label smoke_not_nopants:
    Alice_02 "Ты чего-то хотел, Макс?"
    Max_09 "Алиса, ну что за дела?! Я думал у нас уговор!"
    Alice_06 "Это ты сейчас о чём, Макс?"
    Max_08 "Мы ведь договорились, что ты не будешь одевать трусы днём. А я их на тебе видел!"
    Alice_14 "А мне вот интересно, когда это ты их мог увидеть?! Подглядывал, как я одеваюсь?"
    Max_07 "Да здесь и подглядывать не нужно, они у тебя иногда прямо из-под джинс слегка торчат... Так что важно не то, как и где я это увидел, а то, что они на тебе были!"
    Alice_00 "Ладно, ладно, признаю, я их снова ношу, потому что без них мне все натирает. Да и мама, если видит, что я без трусов во время наказания, шлепает гораздо сильней."
    Max_00 "Ну хорошо, я тебя освобождаю от уговора. Но только, если в качестве компенсации ты прямо сейчас покажешь мне сиськи!"
    Alice_16 "А не многого ли ты, мелкий извращенец, хочешь?!"
    Max_01 "Посмотреть на красивые сиськи - не извращение! И мне кажется, проще один раз показать, чем всё натирать будет..."

    $ renpy.show("Alice smoke "+renpy.random.choice(["01", "02", "03"])+"c")

    Alice_06 "Ну на, любуйся, раз уж и дня не можешь прожить без извращений."
    Max_03 "Классные сиськи!"
    Alice_13 "Спасибо. А теперь иди уже, погуляй где-нибудь. Дай докурить спокойно."
    Max_01 "Хорошо. Я ушёл..."

    $ added_mem_var('alice_not_nopants')
    $ renpy.show("Alice smoke "+pose3_3+alice.dress)
    $ flags['smoke'] = None
    $ flags['smoke.request'] = None
    $ flags['noted'] = False
    $ spent_time += 10
    $ current_room = house[5]
    jump Waiting


label smoke_sleep:
    menu:
        Alice_02 "Макс, чего хотел?"
        "Ничего, просто любуюсь...":
            Alice_13 "Налюбовался? А вот теперь постой в сторонке, пока я покурю! Давай, вали уже..."
            Max_01 "Хорошо, хорошо..."
        "Я передумал. Ты можешь спать в лифчике, если хочешь.":
            menu:
                Alice_04 "А чего это ты передумал? А, хотя не важно. Я рада, а то мне неудобно ночью, если выйти куда-то нужно, да и мама заметить может. Спасибо за разрешение, ваше величество. Теперь дай покурю..."
                "{i}уйти{/i}":
                    $ flags['smoke'] = None
                    $ flags['smoke.request'] = None
                    $ flags['noted'] = False
                    $ AddRelMood('alice', 0, 100)
    $ spent_time += 10
    jump Waiting


label smoke_nojeans:
    menu:
        Alice_02 "Макс, чего хотел?"
        "Ничего, просто любуюсь...":
            Alice_13 "Налюбовался? А вот теперь постой в сторонке, пока я покурю! Давай, иди уже..."
            Max_01 "Ладно, ладно, как скажешь..."
        "Я передумал. Ты можешь носить свои джинсы, если захочешь.":
            menu:
                Alice_04 "Чего это ты вдруг передумал? Надоело глазеть на мою попку? А, не важно. Спасибо за разрешение, ваше величество. Теперь дай покурю..."
                "{i}уйти{/i}":
                    $ flags['smoke'] = None
                    $ flags['smoke.request'] = None
                    $ flags['noted'] = False
                    $ AddRelMood('alice', 0, 100)
    $ spent_time += 10
    jump Waiting


label Alice_sorry:
    if len(sorry_gifts['alice'].give) == 0:  # Первый диалог
        Alice_15 "Ух ты, у тебя, извращенца мелкого, совесть проснулась?! Неожиданно..."
        Max_10 "Нет, я думаю, ты вряд ли правильно поняла то, что случилось. Я за тобой не подглядывал..."
        Alice_16 "Макс, я по-твоему полная дура что ли?! Ты стоял за стеной, и нагло смотрел, как я принимала душ!"
        Max_14 "Но на деле же, так получилось не специально... Я просто шёл мимо..."
        Alice_05 "Да, да, конечно... Я очень хочу посмотреть, что с тобой сделает мама, когда об этом узнает..."
        Max_10 "Да, знаю, в такое очень трудно поверить, но я просто шёл мимо, а ты душ как раз принимала... Ну я и отскочил к стене... где ты меня и заметила... Вот и всё, я даже и не видел ничего такого!"
        Alice_03 "Ну вот никак мне в это не верится, Макс! Никаких твоих оправданий не хватит, чтобы я в это поверила."
        Max_07 "В таком случае, предлагаю представить, что ничего такого утром не было. Ты ничего не говоришь маме, а я в свою очередь куплю тебе чего-нибудь вкусненького?"
        if GetWeekday(day) == 6:
            Alice_08 "Ах ты... паршивец... Это подлый ход, потому что от вкусняшки я бы не отказалась... Хорошо, но обещать ничего не буду, сперва посмотрю, что это будет за вкусность... Если ты конечно успеешь до вечера понедельника!"
            Max_01 "Обязательно успею! В понедельник всё будет..."
        else:
            Alice_08 "Ах ты... паршивец... Это подлый ход, потому что от вкусняшки я бы не отказалась... Хорошо, но обещать ничего не буду, сперва посмотрю, что это будет за вкусность... Если ты конечно успеешь до завтрашего вечера!"
            Max_01 "Обязательно успею! Завтра всё будет..."
        Alice_07 "И смотри, чтобы мне понравилось..."
        Max_04 "Хорошо."
        $ sorry_gifts['alice'].valid = {'ritter-m', 'raffaello-m', 'ferrero-m'}
        if not all([items['ritter-m'].InShop, items['raffaello-m'].InShop, items['ferrero-m'].InShop]):
            $ notify_list.append(_("В интернет-магазине доступен новый товар."))
        $ items['ritter-m'].InShop = True
        $ items['raffaello-m'].InShop = True
        $ items['ferrero-m'].InShop = True
        $ poss['risk'].OpenStage(0)
    elif len(sorry_gifts['alice'].give) == 1:
        Alice_03 "Ой, Макс, конечно же я тебя прощаю! Не переживай ты так... Всё прекрасно!"
        Max_09 "Э-э-э... Правда?!"
        Alice_16 "Конечно нет, дубина! Стоял снова и глазел на меня голую! Мама обязательно об этом узнает..."
        Max_08 "А вдруг это была случайность и ты напрасно меня сдашь?"
        Alice_12 "Макс, какая это случайность, стоять за углом и глазеть на меня?!"
        Max_07 "Понимаю, верится с большим трудом. Тогда давай это разрешим без мамы?"
        Alice_05 "Снова хочешь попробовать купить моё молчание сладостью? Серьёзно?!"
        Max_10 "Да, серьёзно! Попытка - не пытка..."
        Alice_02 "Ты, конечно, можешь попробовать, от чего-нибудь сладенького я не откажусь, но ты же понимаешь, что мне должно это очень понравиться."
        if GetWeekday(day) == 6:
            Max_01 "Значит, до ужина понедельника?"
        else:
            Max_01 "Значит, до следующего ужина?"
        Alice_05 "Ничего не обещаю, но не опаздывай, Макс!"
        Max_04 "Постараюсь."
    elif len(sorry_gifts['alice'].give) == 2:
        Alice_05 "Макс, а давай всё упростим до того, что ты сейчас уходишь покупать мне конфеты, а я их жду до следующего ужина? Как тебе, а?!"
        Max_08 "Можно и так... Только мне как-то немного не по себе от того, что это предлагаешь ты, а не я!"
        Alice_03 "Просто экономлю время, которое тебе лучше потратить на то, чтобы я в итоге осталась очень довольна этим."
        Max_07 "Тогда я пойду, да?"
        Alice_01 "Бегом! Извращенец тормозной..."
        Max_01 "Ага..."
    elif len(sorry_gifts['alice'].give) == 3 and 'pajamas' not in alice.gifts:
        Alice_05 "Но ведь не только это! Ясно же, что снова пообещаешь вкусняшку за моё молчание."
        Max_07 "Ну а что мне ещё остаётся?"
        Alice_03 "Только вот на этот раз Я буду ставить условия! Сладости - это хорошо, но я хочу большего..."
        Max_09 "И чего такого ты хочешь?"
        Alice_07 "Хочу себе новую одежду, а то надоело в одном и том же дома сидеть. А именно - пижаму!"
        Max_05 "Пижаму?! А при чём тут пижама, в ней же спать положено? А если надоела одежда, просто сними её и сиди в нижнем белье. Или голая!"
        Alice_05 "Конечно, тебе бы это понравилось! Но я не хочу спать в пижаме. Я почти всё время провожу дома и хочу одевать что-то лёгкое."
        Max_07 "Тогда это должна быть какая-нибудь коротенькая пижама!"
        Alice_02 "Именно! Хочу лёгкие топик и шортики для дома. И ты мне их купишь, если не хочешь получить по заднице от мамы."
        Max_08 "Хорошо, но мне нужно больше времени..."
        Alice_01 "Согласна подождать три дня. А ты не тормози..."
        Max_00 "Хорошо."
        $ sorry_gifts['alice'].valid.clear()
        $ punreason[1] = 0
        $ peeping['alice_shower'] = 0
        $ items['pajamas'].InShop = True
        $ sorry_gifts['alice'].start(3)
        $ spent_time += 10
        $ poss['risk'].OpenStage(7)
        jump Waiting

    $ punreason[1] = 0
    $ peeping['alice_shower'] = 0
    $ sorry_gifts['alice'].start()
    $ spent_time += 10
    jump Waiting


label gift_dress:
    Alice_15 "Макс? Это платье для клуба? Правда?!"
    Max_01 "Ага..."
    menu:
        Alice_07 "Спасибо, Макс! Ты такой... Не знаю даже, я просто в шоке!"
        "Держи...":
            jump .gift
        "Не так быстро...":
            menu:
                Alice_02 "Так и знала, что есть какой-то подвох... И что ты хочешь за него?"
                "Да ничего, просто держи...":
                    jump .gift
                "Устрой мне показ в нём...":
                    Alice_05 "Ты хочешь, чтобы я его примерила прямо при тебе?"
                    Max_03 "Конечно! Я это и хочу!"
                    Alice_03 "Макс... А жить... хочешь?"
                    Max_07 "Эй, что за угрозы?"
                    jump .newdress_show

    label .gift:
        Alice_02 "Вот так вот сразу и без подвоха? Обалдеть... Не ожидала от тебя, Макс... Спасибо!"
        Max_04 "Не за что!"
        $ AddRelMood('alice', 0, 300)
        $ AttitudeChange('alice', 0.9)
        jump .end

    label .newdress_show:
        if '09:00' <= tm < '20:00':
            $ __suf = ""
        else:
            $ __suf = "a"

        if "06:00" <= tm < "11:00":
            scene location house aliceroom door-morning
        elif "11:00" <= tm < "18:00":
            scene location house aliceroom door-day
        elif "18:00" <= tm < "22:00":
            scene location house aliceroom door-evening
        else:
            scene location house aliceroom door-night

        menu:
            Alice_03 "Жди за дверью. Я сейчас надену платье и тебе покажу, так уж и быть..."
            "Э... Хорошо...":
                pass
        if __suf == "":
            scene BG char Alice newdress
        else:
            scene BG char Alice spider-night-05
        $ renpy.show("Alice newdress 01"+__suf)
        Alice_05 "Ну как, Макс? Мне идёт?"
        Max_05 "Выглядишь... шикарно!"
        $ renpy.show("Alice newdress 02"+__suf)
        Alice_07 "Спасибо, Макс! Честно говоря, я не ожидала от тебя такого подарка. Спасибо! И..."
        Max_07 "И?"
        $ renpy.show("Alice newdress 03"+__suf)
        menu:
            Alice_05 "...И небольшой бонус. Я знаю, что ты ждал чего-то подобного..."
            "Очень... очень хорошо...":
                pass
            "А можешь наклониться?":
                pass
        $ renpy.show("Alice newdress 04"+__suf)
        Alice_02 "Хорошего в меру... Правда, ты меня очень сильно выручил. Спасибо ещё раз!"
        Max_01 "Не за что!"
        $ AddRelMood('alice', 0, 200)
        $ AttitudeChange('alice', 0.9)
        $ spent_time += max((50 - int(tm[-2:])), 30)
        $ current_room = house[5]

    label .end:
        $ items['dress'].have = False
        $ items['dress'].InShop = False
        $ poss['nightclub'].OpenStage(4)
        $ alice.gifts.append('dress')
        $ dcv['alice.secret'].set_lost(1)
        if alice.inferic is not None:
            $ alice.inferic = clip(alice.inferic-50.0, 0.0, 100.0)
        if alice.infmax is not None:
            $ alice.infmax = clip(alice.infmax+20.0, 0.0, 100.0)
        else:
            $ alice.infmax = 20.0
        $ spent_time += 10
        jump Waiting


label gift_book:
    if items['erobook_1'].have:
        Alice_02 "У тебя для меня подарок? У ТЕБЯ... для МЕНЯ? Какая прелесть. Давай, показывай, что за книжка?"
        Max_01 "Держи..."
        menu:
            Alice_01 "Прикольно... Давно хотела её почитать. А ты как узнал, что мне такие нравятся?"
            "Порылся в твоих вещах и нашёл, что читаешь!":
                Alice_14 "Что?! Да как ты посмел?!"
                Max_02 "Да я пошутил. Просто угадал!"
                Alice_13 "Шуточки у тебя, как и прежде, дурацкие! А книжку я возьму. Молодец, что угадал... Спасибо."
                Max_04 "Не за что"
            "Ну, я догадался! Я же умный!":
                Alice_07 "Умный он... Ну, молодец, что догадался. Спасибо, Макс! Если ещё попадутся из этой серии, буду рада принять их от тебя. Безвозмездно!"
                Max_04 "Хорошо..."
            "Я и не знал. Просто угадал видимо...":
                Alice_05 "Поздравляю, попал в десятку! Если найдёшь ещё что-то подобное, буду рада такому подарку. Даже от тебя..."
                Max_04 "Ну, если даже от меня, то ладно..."
        $ poss['secretbook'].OpenStage(3)
        $ AddRelMood('alice', 0, 100)
        $ AttitudeChange('alice', 0.25)
        $ items['erobook_1'].have = False
        $ items['erobook_1'].InShop = False
        $ alice.gifts.append('erobook_1')
        $ dcv['secretbook'] = Daily(7, False, True) # Покупка второй книги возможна через неделю.
        $ dcv['secretbook'].stage = 2
    elif items['erobook_2'].have:
        Alice_04 "Да? И какая на этот раз? Давай сюда..."
        Max_01 "Держи..."
        Alice_07 "Супер! Ты меня удивляешь, Макс! Если ещё что будет почитать, приноси. Я люблю подобную... литературу."
        Max_04 "Конечно!"
        $ AddRelMood('alice', 0, 120)
        $ AttitudeChange('alice', 0.3)
        $ items['erobook_2'].have = False
        $ items['erobook_2'].InShop = False
        $ alice.gifts.append('erobook_2')
        $ dcv['secretbook'] = Daily(9, False, True) # Покупка третьей книги возможна через девять дней.
        $ dcv['secretbook'].stage = 3
    elif items['erobook_3'].have or items['erobook_4'].have:
        Alice_04 "Супер! Давай, показывай, что тут у нас..."
        Max_01 "Держи..."
        Alice_05 "То, что нужно! Если ещё что будет почитать, приноси. Ты же знаешь, как я люблю такие книги..."
        Max_04 "Конечно!"
        $ AddRelMood('alice', 0, 100)
        $ AttitudeChange('alice', 0.25)
        if items['erobook_4'].have:
            $ items['erobook_4'].have = False
            $ items['erobook_4'].InShop = False
            $ alice.gifts.append('erobook_4')
            $ dcv['secretbook'] = Daily(13, False, True) # Покупка пятой книги возможна через тринадцать дней.
            $ dcv['secretbook'].stage = 5
        else:
            $ items['erobook_3'].have = False
            $ items['erobook_3'].InShop = False
            $ alice.gifts.append('erobook_3')
            $ dcv['secretbook'] = Daily(11, False, True) # Покупка четвертой книги возможна через одинадцать дней.
            $ dcv['secretbook'].stage = 4
    elif items['erobook_5'].have:
        Alice_04 "Да? И какая на этот раз? Давай сюда..."
        Max_01 "Держи..."
        Alice_07 "Забавная книжка. Давно хотела почитать... Спасибо, Макс. Ты меня балуешь!"
        Max_04 "Конечно!"
        $ AddRelMood('alice', 0, 160)
        $ AttitudeChange('alice', 0.4)
        $ items['erobook_5'].have = False
        $ items['erobook_5'].InShop = False
        $ dcv['secretbook'].stage = 6
        $ alice.gifts.append('erobook_5')

    $ spent_time += 10
    return


label gift_pajamas:
    if not _in_replay:
        $ _ch1 = GetChance(mgg.social, 3, 900)
        if 'gift_pajamas' not in persistent.memories:
            $ persistent.memories['gift_pajamas'] = -1
    else:
        # формируем фон для воспоминания
        if alice.plan_name == 'sun':
            call alice_sun from _call_alice_sun
        else:
            if tm > '20:00':
                call alice_evening_closer from _call_alice_evening_closer
            else:
                call alice_morning_closer from _call_alice_morning_closer
    Alice_06 "Только скажи, что это пижамка, а не сладости! Ты же купил то, что я просила?!"
    Max_04 "Конечно! Топик и шортики, как ты хотела. Вот, держи..."
    Alice_07 "О да! Какие симпатичные! Ты такой молодец, Макс! Спасибо тебе большое..."
    Max_03 "Ну что, примеришь при мне?"
    if not sorry_gifts['alice'].owe:  # не успел подарить пижамку вовремя
        Alice_04 "А жирно тебе не будет?! В душе не нагляделся на меня и теперь хочешь подсмотреть, как я переодеваюсь, да?"
        Max_01 "Нет, просто хотел увидеть, как на тебе будет смотреться пижама..."
        Alice_05 "Ладно, поверю... Ого, а что это у тебя здесь..."   #спрайт с ушами
        call alice_sorry_gifts.kick_ears from _call_alice_sorry_gifts_kick_ears
        Max_12 "А-а-ай! Мне же больно, Алиса!"
        Alice_16 "Ещё подглядывать за мной будешь, подлиза ты эдакий?"
        Max_10 "Да я же случайно оказался около душа..."
        Alice_05 "Видимо, ты хочешь, чтобы я ещё сильнее тебе ухо выкрутила... Я только с радостью!"
        Max_14 "Ой! Я понял... Больше не буду!"
        Alice_02 "Вот и молодец! Гуляй..."
        $ poss['risk'].stages[7].ps = _("{i}{b}Внимание:{/b} Пока это всё, что можно сделать для данной \"возможности\" в текущей версии игры.{/i}")
    elif flags['alice_hugs'] > 3: # после 3-ей сладости были родственные обнимашки
        $ persistent.memories['gift_pajamas'] = 1
        if not _in_replay:
            $ poss['risk'].OpenStage(8)
        Alice_03 "Примерю при тебе? Об этом мы не договаривались. Я покажусь в ней, но... Хотя, ладно. Примерю при тебе, но ты не подглядывай! Увижу, что смотришь, получишь и пойдёшь в бассейн. Вниз головой."
        Max_02 "Как страшно... Давай уже, примеряй."
        scene BG char Alice newpajamas
        $ __suf = 's' if alice.plan_name in ['sun', 'swim'] else alice.dress
        if not ('09:00' <= tm < '20:00'):
            $ __suf += 'e'
        $ renpy.show('Alice newpajamas 01'+__suf)
        if not _in_replay:
            $ SetCamsGrow(house[1], 150)
        menu:
            Alice_05 "Макс, у тебя же есть инстинкт самосохранения, верно? Не вздумай подглядывать!"   #примерка в комнате/спрайт в одежде (01)
            "Ага, я и не подглядываю...":
                if renpy.random.randint(0, 1) > 0:   #если линейка началась без верха
                    $ renpy.show('Alice newpajamas 02'+__suf)
                    Alice_01 "Макс! Ты что, пялишься на мою грудь? Тут же кругом зеркала и я всё вижу! Быстро отвернись!"   #спрайт без верха (02)
                    Max_03 "Я не пялюсь..."
                    $ renpy.show('Alice newpajamas 04'+__suf)
                    Alice_02 "Похоже, размер мне подходит... и удобно. Очень лёгонький топик. Ну, как тебе?"   #спрайт с одетым топиком (04)
                    Max_04 "Тебе идёт! Мне нравится..."
                    if flags['smoke'] != 'nopants':
                        Alice_03 "Отлично! А теперь отвернись, не подглядывай! Нужно ещё шортики примерить."   #спрайт в топике без низа, но в трусиках (если бикини, то без трусиков) (06)
                        if not _in_replay:
                            $ SetCamsGrow(house[1], 180)
                    else:
                        Alice_05 "Класс! А теперь быстро отвернись, а то на мне трусиков нет, благодаря твоим уговорам! Нужно ещё шортики примерить."   #спрайт в топике без низа, трусиков по уговору нет
                    $ __suf = 'an' if any([alice.plan_name in ['sun', 'swim'], alice.dress=='d', flags['smoke'] == 'nopants']) else 'a'
                    if not ('09:00' <= tm < '20:00'):
                        $ __suf += 'e'
                    $ renpy.show('Alice newpajamas 06'+__suf)
                    if any([flags['smoke'] != 'not_nopants', alice.plan_name in ['sun', 'swim'], alice.dress=='d']):
                        Max_02 "Конечно, я не смотрю..."
                    else:
                        Max_08 "Конечно, я не смотрю... Эй! А ты же ведь не должна носить трусики! У нас ведь уговор!"   #если на Алисе трусики, но их не должно быть
                        Alice_06 "Вот чёрт! Да... я забыла, что сегодня не должна их носить! А ты сейчас не должен был этого увидеть, так что молчи... а то выпну отсюда..."
                        Max_01 "Ладно, считай, я ничего не видел."
                    if '09:00' <= tm < '20:00':
                        show Alice newpajamas 08
                    else:
                        show Alice newpajamas 08e
                    Alice_07 "Размер в самый раз... Удобненько и легко. Как тебе, Макс? Хорошо сидит?"   #спрайт с одетыми топиком и шортиками (08)
                    Max_05 "Не то слово, всё выглядит шикарно!"
                else:   #если линейка началась без низа
                    $ __suf = 's' if alice.plan_name in ['sun', 'swim'] else alice.dress
                    if alice.dress=='a' and flags['smoke'] == 'nopants':  # если Алиса без трусиков
                        $ __suf += 'n'
                    if not ('09:00' <= tm < '20:00'):
                        $ __suf += 'e'
                    $ renpy.show('Alice newpajamas 03'+__suf)
                    if flags['smoke'] != 'nopants':
                        Alice_03 "Макс! Ты что, пялишься на мой зад? Тут же кругом зеркала и я всё вижу! Быстро отвернись!"   #линейка началась без низа, но в трусиках (03)
                    else:
                        if not _in_replay:
                            $ SetCamsGrow(house[1], 180)
                        Alice_05 "Макс! Ты что, пялишься на мой зад? Быстро отвернись, на мне же нет трусиков, благодаря твоим уговорам!"   #линейка началась без низа, трусиков по уговору нет
                    if flags['smoke'] != 'not_nopants' or alice.plan_name in ['sun', 'swim']:
                        Max_02 "Я не пялюсь..."
                    else:
                        Max_08 "Я не пялюсь... Эй! А ты же ведь не должна носить трусики! У нас ведь уговор!"   #если на Алисе трусики, но их не должно быть
                        Alice_06 "Вот чёрт! Да... я забыла, что сегодня не должна их носить! А ты сейчас не должен был этого увидеть, так что молчи... а то выпну отсюда..."
                        Max_01 "Ладно, считай, я ничего не видел."
                    $ __suf = 's' if alice.plan_name in ['sun', 'swim'] else alice.dress
                    if not ('09:00' <= tm < '20:00'):
                        $ __suf += 'e'
                    $ renpy.show('Alice newpajamas 05'+__suf)
                    Alice_02 "Размер в самый раз... Удобненько и легко. Как тебе, Макс? Хорошо сидят?"   #спрайт с одетыми шортиками (05)
                    Max_04 "Не то слово, сидят прекрасно!"
                    Alice_01 "Здорово! А теперь отвернись, не подглядывай! Нужно ещё топик примерить."   #спрайт в шортиках без верха (07)
                    if '09:00' <= tm < '20:00':
                        show Alice newpajamas 07
                    else:
                        show Alice newpajamas 07e
                    Max_03 "Конечно, я не смотрю..."
                    if '09:00' <= tm < '20:00':
                        show Alice newpajamas 08
                    else:
                        show Alice newpajamas 08e
                    Alice_07 "Похоже, размер мне подходит... и удобно. Очень лёгонький топик. Ну, как тебе всё в целом?"   #спрайт с одетыми топиком и шортиками (08)
                    Max_05 "Тебе идёт, всё выглядит шикарно!"
        Alice_03 "Спасибо тебе ещё раз! Иди ко мне, я тебя приобниму... немного."
        Max_04 "О, это я с радостью!"
        scene BG char Alice newdress
        if '09:00' <= tm < '20:00':
            $ renpy.show("Alice hugging aliceroom 02b"+mgg.dress)
        else:
            $ renpy.show("Alice hugging aliceroom 02b"+mgg.dress+'e')
        Alice_05 "Но ты не зазнавайся, Макс. В следующий раз тебе может так не повезти, как сегодня."   #спрайт с обнимашками в комнате
        Max_02 "Буду иметь ввиду, сестрёнка."
        Alice_02 "Всё, давай шуруй по своим делам, не надоедай мне."
        Max_01 "Ага..."
        if not _in_replay:
            $ spent_time += 20
    elif flags['alice_hugs'] > 2: # после 3-ей сладости было прощение без выкручивания ушей
        Alice_04 "А жирно тебе не будет?! В душе на меня глазел, а теперь и здесь хочешь подглядеть... Нет уж! Но за пижамку я тебя всё же обниму! Ну так... совсем немного..."   #спрайт с обнимашками"
        call alice_sorry_gifts.kindred_hugs from _call_alice_sorry_gifts_kindred_hugs
        Max_03 "Вау! Это как-то очень непривычно... обнимать тебя без ущерба своему здоровью!"
        Alice_07 "Не смотря на твои замашки, ты всё-таки купил мне пижамку, которую я просила. Вот я и не вредничаю..."
        Max_05 "Да, надо бы почаще так делать."
        Alice_02 "Подглядывать или что-нибудь мне дарить?!"
        Max_02 "Второе, конечно!"
        Alice_05 "Ну да, конечно... Иди давай."
        $ poss['risk'].stages[7].ps = _("{i}{b}Внимание:{/b} Пока это всё, что можно сделать для данной \"возможности\" в текущей версии игры.{/i}")
    elif flags['alice_hugs'] > 1: # после 3-ей сладости было выкручивание ушей
        menu:
            Alice_04 "А жирно тебе не будет?! В душе не нагляделся на меня и теперь хочешь подсмотреть, как я переодеваюсь, да?"
            "Нет, просто хотел увидеть, как на тебе будет смотреться пижама... {color=[_ch1.col]}(Убеждение. Шанс: [_ch1.vis]){/color}":
                if RandomChance(_ch1.ch):
                    $ Skill('social', 0.2)
                    Alice_03 "[succes!t]Ладно, поверю, считай твои извинения приняты... Мама ничего не узнает, так что можешь не напрягаться."
                    Max_07 "Что, вот так вот просто?!"
                    Alice_05 "Ну, ты обещал купить мне пижаму и сдержал слово. А я сейчас более-менее добрая... Так что не искушай судьбу!"
                    Max_01 "Понял, сестрёнка! Не буду тебе мешать..."
                else:
                    $ Skill('social', 0.1)
                    Alice_05 "[failed!t]Ладно, поверю, считай твои извинения приняты... Ого, а что это у тебя здесь..."   #спрайт с ушами
                    call alice_sorry_gifts.kick_ears from _call_alice_sorry_gifts_kick_ears_1
                    Max_12 "А-а-ай! Мне же больно, Алиса!"
                    Alice_16 "Ещё подглядывать за мной будешь, подлиза ты эдакий?"
                    Max_10 "Да я же случайно оказался около душа..."
                    Alice_05 "Видимо, ты хочешь, чтобы я ещё сильнее тебе ухо выкрутила... Я только с радостью!"
                    Max_14 "Ой! Я понял... Больше не буду!"
                    Alice_02 "Вот и молодец! Гуляй..."
        $ poss['risk'].stages[7].ps = _("{i}{b}Внимание:{/b} Пока это всё, что можно сделать для данной \"возможности\" в текущей версии игры.{/i}")
    else:  # после 3-ей сладости было наказание
        Alice_04 "А жирно тебе не будет?! В душе не нагляделся на меня и теперь хочешь подсмотреть, как я переодеваюсь, да?"
        Max_01 "Нет, просто хотел увидеть, как на тебе будет смотреться пижама..."
        Alice_05 "Ладно, поверю, считай твои извинения приняты... Ого, а что это у тебя здесь..."   #спрайт с ушами
        call alice_sorry_gifts.kick_ears from _call_alice_sorry_gifts_kick_ears_2
        Max_12 "А-а-ай! Мне же больно, Алиса!"
        Alice_16 "Ещё подглядывать за мной будешь, подлиза ты эдакий?"
        Max_10 "Да я же случайно оказался около душа..."
        Alice_05 "Видимо, ты хочешь, чтобы я ещё сильнее тебе ухо выкрутила... Я только с радостью!"
        Max_14 "Ой! Я понял... Больше не буду!"
        Alice_02 "Вот и молодец! Гуляй..."
        $ poss['risk'].stages[7].ps = _("{i}{b}Внимание:{/b} Пока это всё, что можно сделать для данной \"возможности\" в текущей версии игры.{/i}")

    $ renpy.end_replay()
    $ AddRelMood('alice', 0, 200)
    $ AttitudeChange('alice', 0.9)
    $ items['pajamas'].have = False
    $ items['pajamas'].InShop = False
    $ alice.gifts.append('pajamas')
    $ added_mem_var('pajamas')
    if alice.inferic is not None:
        $ alice.inferic = clip(alice.inferic-50.0, 0.0, 100.0)
    if alice.infmax is not None:
        $ alice.infmax = clip(alice.infmax+20.0, 0.0, 100.0)
    else:
        $ alice.infmax = 20.0

    $ sorry_gifts['alice'].valid = {'ferrero-b', 'ferrero-m'}

    $ clothes[alice].casual.sel.insert(1, Garb('b', '01c', 'Пижама', True))
    $ clothes[alice].casual.cur = 1
    $ clothes[alice].casual.rand = True
    $ sorry_gifts['alice'].give.append(4)
    $ spent_time += 10
    $ sorry_gifts['alice'].owe = False
    jump Waiting


label Alice_solar:
    $ talk_var['alice_sun'] = 1
    ## Загораешь?
    menu:
        Alice_02 "Как ты догадался, Шерлок?"
        "Может быть, тебя намазать кремом для загара?" if talk_var['sun_oiled'] == 3:
            Alice_04 "Достаточно на сегодня, Макс..."
            Max_00 "Ясно. Ну, тогда, может, завтра..."
            $ talk_var['sun_oiled'] = 4
            jump AfterWaiting
        "Может быть, тебя намазать кремом для загара?" if not items['solar'].have:  # нет крема
                Alice_13 "Может быть. Вот только у меня его нет..."
                Max_00 "Ясно. Ну, в другой раз значит..."
                if not any([items['max-a'].InShop, items['max-a'].have]):
                    Max_09 "Такой крем наверняка можно найти в интернет-магазине. Да и прежде чем пытаться поприставать к сестрёнке таким образом, стоит обзавестись одеждой полегче."
                    $ items['solar'].InShop = True
                    $ items['max-a'].InShop = True
                    $ notify_list.append(_("В интернет-магазине доступен новый товар."))
                jump AfterWaiting
        "{i}Предложить Алисе намазать её кремом{/i}" if items['solar'].have and any([mgg.dress == 'a', kol_cream < 3]):
            if mgg.dress == 'a':  # Максу нужна одежда
                Max_07 "{i}( Прежде чем пытаться поприставать к сестрёнке таким образом, стоит обзавестись одеждой полегче. ){/i}"
                $ items['max-a'].InShop = True
                jump AfterWaiting

            if kol_cream < 3:  # крема не хватит даже просто нанести
                Max_07 "{i}( Крем почти закончился. Нужно купить ещё. ){/i}"
                $ items['solar'].InShop = True
                jump AfterWaiting

        "Может быть, тебя намазать кремом для загара?" if all([talk_var['sun_oiled']==0, kol_cream>=3, mgg.dress!='a']):
            Alice_03 "Если у тебя есть крем, то давай, раз тебе делать нечего."
            Max_01 "Ложись на живот тогда..."
            $ talk_var['sun_oiled'] = 1
        "Ладно, загорай...":
            jump AfterWaiting

    scene BG char Alice sun-alone 01-01
    $ renpy.show('Max sun-alone 01'+mgg.dress)
    menu .type_choice:
        Alice_07 "Эти шезлонги всем хороши, но на животе загорать не получается. Приходится коврик для йоги использовать..."
        "{i}нанести крем{/i}" if (kol_cream >= 3 and mgg.massage < 2.0) or 3<=kol_cream<7:  # просто наносим крем. близко к оригиналу
            $ SetCamsGrow(house[6], 140)
            $ _suf = 'a'
            $ spent_time += 20
            $ kol_cream -= 3
            scene BG char Alice sun-alone 05
            $ renpy.show('Alice sun-alone 05'+_suf+mgg.dress)
            Max_01 "{i}( Так, хорошенько намажем эти стройные ножки... ){/i}"
            scene BG char Alice sun-alone 04
            $ renpy.show('Alice sun-alone 04'+_suf+mgg.dress)
            Max_01 "{i}( Теперь плечи и совсем немного шею... ){/i}" nointeract
            $ __res = renpy.display_menu([(_("{i}наносить крем молча{/i}"), 0), (_("А тебе нравится, что следы от лямок остаются?"), 1)])
            if __res > 0:
                $ _talk_top = True
                call massage_sunscreen.talk_topless from _call_massage_sunscreen_talk_topless
            $ __r1 = renpy.random.choice(['02','03'])
            $ renpy.scene()
            $ renpy.show('BG char Alice sun-alone '+__r1)
            $ renpy.show('Alice sun-alone '+__r1+_suf+mgg.dress)
            Max_03 "{i}( И закончим, хорошенько намазав всю её спину... ){/i}"
            $ Skill('massage', 0.005)
            if mgg.massage >= 0.01 and len(online_cources) == 1:
                Alice_04 "Спасибо, Макс! На сегодня достаточно. У тебя очень неплохо получается, а если поучишься, может стать ещё лучше!"
                Max_04 "Да не за что, обращайся!"
                scene BG char Alice sun-alone 01
                if talk_var['sun_oiled'] == 2:
                    show Alice sun-alone 01a
                else:
                    show Alice sun-alone 01
                Max_07 "{i}( В чём-то Алиса права, поучиться этому, пожалуй, стоит. ){/i}"
                $ online_cources.append(
                    OnLineCources(_("Массаж"), "massage", "bm", [
                        OnLineCource(_("Массаж ступней"), _("Это уникальная методика массажа с целью оказания оздоравливающего воздействия на организм. Она эффективна и в тоже время несложна в исполнении."), 3, 100, 2),
                        OnLineCource(_("Массаж кистей рук"), _("Это уникальная методика массажа с целью оказания оздоравливающего воздействия на организм. Она эффективна и в тоже время несложна в исполнении."), 3, 200, 2),
                        ]),
                    )
            else:
                Alice_03 "Спасибо, Макс. Так намного лучше..."
                Max_04 "Обращайся, если что..."
            scene BG char Alice sun-alone 01
            if talk_var['sun_oiled'] == 2:
                show Alice sun-alone 01a
            else:
                show Alice sun-alone 01
            if kol_cream < 2:
                Max_10 "{i}( Ну вот, крем закончился. Надо ещё купить. ){/i}"
                if kol_cream == 0:
                    $ items['solar'].have = False
                    $ items['solar'].InShop = True
            elif kol_cream < 7:
                Max_08 "{i}( Осталось мало крема, в следующий раз может не хватить, лучше купить заранее. ){/i}"
                $ items['solar'].InShop = True
            $ AddRelMood('alice', 5, 50)
        "{i}сделать массаж с кремом{/i}" if all([kol_cream >= 7, (len(online_cources)>1 and online_cources[1].cources[0].less > 0)]):  # попытка сделать массаж с кремом
            $ _massaged = []
            $ _talk_top = False
            $ SetCamsGrow(house[6], 160)
            jump massage_sunscreen
        "{i}{color=[gray]}сделать массаж с кремом{/color}{color=[red]}\nкрема недостаточно{/color}{/i}" if kol_cream < 7:
            jump .type_choice
        "{i}Блин, крем практически закончился... Давай в другой раз тогда...{/i}" if kol_cream < 3:
            Alice_00 "Ну что же ты, Макс... Эх, только настроилась..."
            $ talk_var['sun_oiled'] = 4
            jump AfterWaiting

    jump Waiting


label massage_sunscreen:
    if talk_var['sun_oiled'] == 2:
        scene BG char Alice sun-alone 01-01a
        $ _suf = 'b'
    else:
        scene BG char Alice sun-alone 01-01
        $ _suf = 'a'
    $ renpy.show('Max sun-alone 01'+mgg.dress)
    if len(online_cources) > 1 and online_cources[1].current > 0:
        if len(_massaged) == 4: # 5: # временно доступны только 4 зоны
            Alice_07 "Макс, ты делаешь успехи! Ещё немного попрактикуешься, и к тебе будет сложно записаться на приём!"
            Max_03 "Да пустяки, обращайся!"
            Alice_04 "Ладно, хватит на сегодня, Макс. И... спасибо!"
            Max_05 "Не за что! Всегда рад..."
            $ AddRelMood('alice', 15, 150)
            jump .end  # если Макс прошел курсы массажа ног, ему доступны 5 зон

    elif len(_massaged) == 4:
        Alice_04 "Спасибо, Макс! На сегодня достаточно. У тебя очень неплохо получается, а если поучишься, может стать ещё лучше!"
        Max_04 "Да не за что, обращайся!"
        $ AddRelMood('alice', 10, 100)
        jump .end  # если курсы не пройдены и первыми массировались ступни, доступно 4 зоны

    elif len(_massaged) == 2 and _massaged[0] != 'foot':
        Alice_03 "Спасибо, Макс! На сегодня достаточно."
        Max_01 "Да не за что, обращайся!"
        $ AddRelMood('alice', 5, 50)
        jump .end

    ### выбираем зону массажа
    if not _in_replay:
        $ kol_cream -= 1
    call screen choice_zone_sunscreen

    label .left_foot:
        scene BG char Alice sun-alone 06
        $ renpy.show('Alice sun-alone 06'+_suf+mgg.dress)
        Max_01 "{i}( Начнём сегодня с левой пяточки... Вот так. И, пока я хорошенько её массирую, можно заодно поглазеть на аппетитную Алисину попку! ){/i}"
        scene BG char Alice sun-alone 07
        $ renpy.show('Alice sun-alone 07'+_suf+mgg.dress)
        Max_03 "{i}( А теперь правую... Вот так. Да уж, глаз не оторвать, попка - что надо! ){/i}"
        jump .foot

    label .right_foot:
        scene BG char Alice sun-alone 07
        $ renpy.show('Alice sun-alone 07'+_suf+mgg.dress)
        Max_01 "{i}( Начнём сегодня с правой пяточки... Вот так. И, пока я хорошенько её массирую, можно заодно поглазеть на аппетитную Алисину попку! ){/i}"
        scene BG char Alice sun-alone 06
        $ renpy.show('Alice sun-alone 06'+_suf+mgg.dress)
        Max_03 "{i}( А теперь левую... Вот так. Да уж, глаз не оторвать, попка - что надо! ){/i}"
        jump .foot

    label .shin:
        scene BG char Alice sun-alone 05
        $ renpy.show('Alice sun-alone 05'+_suf+mgg.dress)
        Max_02 "{i}( Помассируем эти стройные ножки, вот так... ){/i}"
        if 'shin' in _massaged:
            # голени уже массировались
            jump .double
        else:
            $ _multipler = 10 - len(_massaged)
            if len(_massaged)>0 and _massaged[0]=='foot':
                $ _multipler *= 2 # множитель навыка удваивается, если ступни были первыми

            if RandomChance(GetChance(mgg.massage, _multipler, 950).ch) or _in_replay:
                # Алисе понравилось
                $ Skill('massage', 0.05)
                Alice_07 "Ух, как приятно... Ты молодец, Макс! Моим ножкам это понравилось... Не останавливайся, продолжай..."
            else:
                $ Skill('massage', 0.02)
                jump .fail
        $ _massaged.append('shin')
        jump massage_sunscreen

    label .shoulders:
        scene BG char Alice sun-alone 04
        $ renpy.show('Alice sun-alone 04'+_suf+mgg.dress)
        Max_04 "{i}( Хорошенько разомнём плечи и немного шею... ){/i}" nointeract
        if not _talk_top:
            $ __res = renpy.display_menu([(_("{i}массировать молча{/i}"), 0), (_("А тебе нравится, что следы от лямок остаются?"), 1)])
            if __res > 0:
                $ _talk_top = True
                call massage_sunscreen.talk_topless from _call_massage_sunscreen_talk_topless_1
                $ renpy.show('Alice sun-alone 04'+_suf+mgg.dress)
                Max_01 "И ещё немного..."

        if 'shoulders' in _massaged:
            # плечи уже массировались
            jump .double
        else:
            $ _multipler = 10 - len(_massaged)
            if len(_massaged)>0 and _massaged[0]=='foot':
                $ _multipler *= 2 # множитель навыка удваивается, если ступни были первыми

            $ _massaged.append('shoulders')
            if RandomChance(GetChance(mgg.massage, _multipler, 950).ch) or _in_replay:
                # Алисе понравилось
                $ Skill('massage', 0.05)
                menu:
                    Alice_07 "Это так классно расслабляет... У тебя очень хорошо получается, Макс!"
                    "{i}продолжить{/i}":
                        pass
                    "{i}выпустить рядом паука{/i}" if items['spider'].have:
                        show FG sun-alone-04
                        jump .spider
            else:
                $ Skill('massage', 0.02)
                jump .fail
        jump massage_sunscreen

    label .spine:
        $ __r1 = renpy.random.choice(['02','03'])
        $ renpy.scene()
        $ renpy.show('BG char Alice sun-alone '+__r1)
        $ renpy.show('Alice sun-alone '+__r1+_suf+mgg.dress)
        Max_05 "{i}( Вот так, нужно хорошенько растереть крем... А теперь тщательно помнём спинку... Нежно, но сильно. ){/i}" nointeract
        if not _talk_top:
            $ __res = renpy.display_menu([(_("{i}массировать молча{/i}"), 0), (_("А тебе нравится, что следы от лямок остаются?"), 1)])
            if __res > 0:
                $ _talk_top = True
                call massage_sunscreen.talk_topless from _call_massage_sunscreen_talk_topless_2
                $ renpy.show('Alice sun-alone '+__r1+_suf+mgg.dress)
                Max_01 "Ещё немного крема..."

        if 'spine' in _massaged:
            # спина уже массировалась
            jump .double
        else:
            $ _multipler = 10 - len(_massaged)
            if len(_massaged)>0 and _massaged[0]=='foot':
                $ _multipler *= 2 # множитель навыка удваивается, если ступни были первыми

            $ _massaged.append('spine')
            if RandomChance(GetChance(mgg.massage, _multipler, 950).ch) or _in_replay:
                # Алисе понравилось
                $ Skill('massage', 0.05)
                menu:
                    Alice_07 "Как приятно... Макс, ты делаешь успехи! Мне это нравится..."
                    "{i}продолжить{/i}":
                        pass
                    "{i}выпустить рядом паука{/i}" if items['spider'].have and not _in_replay:
                        $ renpy.show("FG sun-alone-"+__r1)
                        jump .spider
            else:
                $ Skill('massage', 0.02)
                jump .fail
        jump massage_sunscreen

    label .ass:  # попка пока недоступна, поэтому не прописывал алгоритм
        "попка"
        jump massage_sunscreen

    label .foot:
        if 'foot' in _massaged:
            # ступни уже массировались
            jump .double
        else:
            $ _multipler = 10 - len(_massaged) if len(_massaged) else 20  # множитель навыка. Если ступни первые, шанс удваивается
            if RandomChance(GetChance(mgg.massage, _multipler, 950).ch) or _in_replay:
                # Алисе понравилось
                $ Skill('massage', 0.05)
                Alice_07 "Ух, как же моим пяточкам приятно... Не останавливайся, продолжай..."
            else:
                $ Skill('massage', 0.02)
                jump .fail
        $ _massaged.append('foot')
        jump massage_sunscreen

    label .double:
        Alice_13 "Взялся делать массаж, а сам не знаешь что делать! Хватит, иди отсюда, дай позагорать спокойно."
        Max_11 "Эх... Ладно..."
        jump .end

    label .fail:
        if len(_massaged) > 0:  # есть успешно помассированные участки
            Alice_06 "Хватит, Макс... Что-то у тебя не так пошло... А ведь так хорошо начал..."
        else:  # первый же массаж
            Alice_13 "Макс, хватит... Что-то не похоже, что ты знаешь, что делаешь... Давай на этом закончим..."
        Max_10 "Хорошо, извини..."
        jump .end

    label .talk_topless:
        $ _ch1 = GetChance(mgg.social, 3, 900)
        menu:
            Alice_06 "Нет, конечно. Но тебя я так радовать не собираюсь!"
            "Что, стесняешься? {color=[_ch1.col]}(Убеждение. Шанс: [_ch1.vis]){/color}":
                if RandomChance(_ch1.ch) or _in_replay:
                    $ Skill('social', 0.1)
                    Alice_07 "[succes!t]Нет, но... Ладно, всё равно тебе ничего не видно..."
                    Max_02 "Так держать, сестрёнка!"
                    $ talk_var['sun_oiled'] = 2
                    $ _suf = 'b'
                    $ SetCamsGrow(house[6], 200)
                else:
                    $ Skill('social', 0.05)
                    Alice_04 "[failed!t]Вот только на \"слабо\" меня брать не надо!"
                    Max_01 "Ладно, как скажешь..."
            "Ну, как хочешь...":
                pass
        return

    label .spider:
        if _in_replay:
            $ _suf = 'b' if talk_var['sun_oiled'] == 2 else 'a'
            $ __r1 = renpy.random.choice(['02','03'])
            $ renpy.scene()
            $ renpy.show('BG char Alice sun-alone '+__r1)
            $ renpy.show('Alice sun-alone '+__r1+_suf+mgg.dress)
            $ renpy.show("FG sun-alone-"+__r1)
        else:
            $ items['spider'].have = False
            $ SpiderKill = 0  # паук остался жив
            $ SpiderResp = 1  # поэтому поймать можно уже на следующий день
            if 'massage_sunscreen.spider' not in persistent.memories:
                $ persistent.memories['massage_sunscreen.spider'] = 0
        Max_07 "Э-э-э... Алиса, ты только не пугайся, просто лежи, как лежала..."
        Alice_13 "А чего мне пугаться, Макс? Сейчас что, будешь больно массировать?"
        Max_00 "Нет, просто у нас тут одна проблемка подкралась..."
        scene BG char Alice sun-alone 03
        $ renpy.show('Alice sun-alone 03'+_suf+mgg.dress)
        show FG sun-alone-03
        Alice_12 "Что?! Подкралась?! Ты же говоришь не о том, о чём я подумала?"
        Max_08 "Ну... Ты только не дёргайся!"
        scene BG char Alice spider-sun-01
        $ renpy.show('Alice spider-sun 01'+_suf+mgg.dress)
        show FG spider-sun-01
        Alice_15 "А-а-а! Макс! Вот чёрт! Какой он здоровенный!"   #spider-sun-01
        Max_02 "И не говори!"
        Alice_14 "Макс, чего сидишь?! Убери его отсюда! А ещё лучше убей!"
        Max_04 "Да мне как-то не хочется."
        scene BG char Alice spider-sun-02
        $ renpy.show('Alice spider-sun 02'+_suf+mgg.dress)
        Alice_06 "В смысле, не хочется?! Охренеть, он страшный!"   #spider-sun-02
        Max_05 "Так хорошо же сидим. Да и он в нашу сторону не ползёт. По-моему, он в сторону травы сменил курс..."
        Alice_16 "Да плевать мне, куда он ползёт! Я хочу, чтобы его не было!"
        if talk_var['sun_oiled'] != 2:
            # верх купальника не снят
            Max_01 "Ладно, тогда слезай, я с ним разберусь."
            Alice_06 "Не-е-ет, он тогда сразу ко мне поползёт! Что я их, не знаю что ли..."
            Max_03 "Ты определись уже, Алиса, чего хочешь. Я бы просто немного подождал, вон он, уползает..."
            Alice_12 "Точно?!"
            Max_04 "Ага. В траву убежал."
            scene BG char Alice hugging yard-01
            $ renpy.show('Alice hugging yard 02a'+mgg.dress)
            Alice_07 "Фух... Ладно. Только ты посматривай, временами, чтобы в мою сторону никто не полз."   #спрайт с родственными обнимашками
            Max_02 "Хорошо. Но, если что, зови. Ещё посидим."
            Alice_05 "Тебе хватит. Не обольщайся..."
            Max_01 "Ага."
            jump .end
        else:
            # верх купальника снят
            Max_03 "Давай лучше ещё так посидим, подождём. Вон он, уползает..."
            Alice_12 "Макс, а что это в меня такое упёрлось там внизу?!"
            Max_02 "Ну... это я, так сказать."
            Alice_14 "Ой, блин, это член твой что ли?!"
            Max_01 "Ага. Он самый."
            if mgg.dress == 'b':
                ###если Макс в майке и шортах###
                scene BG char Alice hugging yard-01
                $ renpy.show('Alice hugging yard 01b'+mgg.dress)
                Alice_15 "Ты совсем что ли извращенец? На родную сестру у него стоит!"   #спрайт с выкручиванием ушей
                Max_10 "Ай, Алиса, больно! Сама же своими голыми сиськами в моё лицо упёрлась! А они красивые... Чего ты ещё ожидала?!"
                Alice_16 "Всё, не хочу об этом говорить... Давай, шуруй отсюда. Бегом! А то я живо тебе по заднице напинаю!"
                Max_09 "Да ухожу я, уши только мои в покое оставь!"
                jump .end
            else:
                ###если Макс только в шортах###
                $ persistent.memories['massage_sunscreen.spider'] = 1
                show Alice spider-sun 03bc
                Alice_12 "Какого чёрта, Макс?! Совсем что ли извращенец? Я же твоя сестра! Блин... Прикройся хоть..."   #спрайт с прикрывающейся от Макса Алисой
                Max_01 "Да не так-то это просто, прикрыть его."
                Alice_06 "Не ожидала я от тебя такого, Макс. И что у тебя в голове творится?!"
                Max_07 "А чего ты ожидала?! Сама же на меня запрыгнула и сиськами своими голыми мне в лицо упёрлась... Кстати, не могу не отметить, они у тебя красивые и упругие!"
                Alice_13 "Нет, ну ты точно больной... Ладно, представим, что ничего не было. Убирай эту свою штуку и не появляйся в таком виде рядом со мной!"
                Max_02 "Хорошо. Не скучай."
                jump .end


    label .end:
        $ renpy.end_replay()
        scene BG char Alice sun-alone 01
        if talk_var['sun_oiled'] == 2:
            show Alice sun-alone 01a
        else:
            show Alice sun-alone 01
        $ spent_time += 10 + clip(int(round(5*len(_massaged), -1)), 0, 30)
        if kol_cream < 3 and mgg.massage < 2.0:
            Max_10 "{i}( Ну вот, крем закончился. Надо ещё купить. ){/i}"
            if kol_cream == 0:
                $ items['solar'].have = False
                $ items['solar'].InShop = True
        elif kol_cream < 7:
            Max_08 "{i}( Осталось мало крема, в следующий раз может не хватить, лучше купить заранее. ){/i}"
            $ items['solar'].InShop = True

        jump Waiting


label alice_sorry_gifts:
    if sorry_gifts['alice'].days[0] == day:
        Max_09 "Думаю, не стоит дарить вкусняшку сегодня. Это может вызвать ненужные подозрения... Лучше это сделать завтра."
        return

    $ _ch1 = GetChance(mgg.social, 3, 900)
    $ txt = {
        0 : _("Да ладно! Это мне нравится... И что там у тебя?"),
        1 : _("Ого! И правда хочешь рискнуть... И что там у тебя на этот раз?"),
        2 : _("Наконец-то! Ну давай, показывай, что у тебя на этот раз?!"),
        }[len(sorry_gifts['alice'].give)]
    $ sorry_gifts['alice'].owe = False
    menu:
        Alice_02 "[txt!t]"
        "Конфеты \"Raffaello\" (16 штук)" if items['raffaello-m'].have:
            $ __give = 'raffaello-m'
            jump .bad
        "Конфеты \"Raffaello\" (24 штуки)" if items['raffaello-b'].have:
            $ __give = 'raffaello-b'
            jump .bad
        "Конфеты \"Ferrero Rocher\" (16 штук)" if items['ferrero-m'].have:
            $ __give = 'ferrero-m'
            jump .good
        "Конфеты \"Ferrero Rocher\" (24 штуки)" if items['ferrero-b'].have:
            $ __give = 'ferrero-b'
            jump .good
        "Шоколад \"Ritter Sport\" mini (9 штук)" if items['ritter-m'].have:
            $ __give = 'ritter-m'
            jump .middle
        "Шоколад \"Ritter Sport\" (4 штуки)" if items['ritter-b'].have:
            $ __give = 'ritter-b'
            jump .middle

    label .kick_ears:
        $ poss['risk'].OpenStage(5)
        if current_room == house[1]:
            scene BG char Alice newdress
            if '09:00' <= tm < '20:00':
                $ renpy.show("Alice hugging aliceroom 01"+alice.dress+mgg.dress)
            else:
                $ renpy.show("Alice hugging aliceroom 01"+alice.dress+mgg.dress+'e')
        elif current_room == house[5]:
            scene BG char Alice hugging terrace-01
            $ renpy.show("Alice hugging terrace 01"+alice.dress+mgg.dress)
        elif current_room == house[6]:
            scene BG char Alice hugging yard-01
            $ renpy.show("Alice hugging yard 01"+alice.dress+mgg.dress)
        return

    label .kindred_hugs:
        $ poss['risk'].OpenStage(6)
        if current_room == house[1]:
            scene BG char Alice newdress
            if '09:00' <= tm < '20:00':
                $ renpy.show("Alice hugging aliceroom 02"+alice.dress+mgg.dress)
            else:
                $ renpy.show("Alice hugging aliceroom 02"+alice.dress+mgg.dress+'e')
        elif current_room == house[5]:
            scene BG char Alice hugging terrace-01
            $ renpy.show("Alice hugging terrace 02"+alice.dress+mgg.dress)
        elif current_room == house[6]:
            scene BG char Alice hugging yard-01
            $ renpy.show("Alice hugging yard 02"+alice.dress+mgg.dress)
        return

    label .middle_again:
        Alice_13 "Вот значит как! Снова купил эти шоколадки... Спасибо, конечно, но не очень-то тебе хочется избежать наказания, как я вижу."
        Max_08 "Просто так уж вышло... Может, ты всё же не будешь рассказывать маме про то, что было утром?"
        Alice_05 "Может и не буду, только сперва сделаю вот что... А ну-ка иди сюда..."   #спрайт с ушами
        call alice_sorry_gifts.kick_ears from _call_alice_sorry_gifts_kick_ears_3
        Max_12 "А-а-ай! Мне же больно, Алиса!"
        menu:
            Alice_16 "Ещё подглядывать за мной будешь, подлиза ты эдакий?"
            "Да я же случайно оказался около душа... {color=[_ch1.col]}(Убеждение. Шанс: [_ch1.vis]){/color}":
                if RandomChance(_ch1.ch):
                    $ Skill('social', 0.2)
                    Alice_05 "[succes!t]Пожалуй, на этот раз, я поверю и ничего не расскажу маме. Но, на всякий случай, за подглядывание, нужно сильнее потянуть..."
                    Max_14 "Ой! Я понял... Больше не буду!"
                    Alice_02 "Вот и молодец! Гуляй..."
                    $ flags['alice_hugs'] = 2
                else:
                    $ Skill('social', 0.1)
                    Alice_12 "[failed!t]Ты всерьёз думаешь, что меня можно в этом убедить?! Нет уж, я очень хочу посмотреть, как мама тебя отшлёпает!"
                    Max_14 "Но, Алиса, я же купил вкусняшку... Ой, отпусти!"
                    Alice_05 "Не считается, если она мне неинтересна! Так что - не повезло тебе..."
                    Max_14 "Ой! Я понял... Больше не буду!"
                    Alice_02 "Вот и молодец! Гуляй..."
                    $ flags['alice_hugs'] = 1
                    $ punreason[1] = 1
        return

    label .bad_again:
        Alice_17 "Макс, ты что, тупой?! Я тебе уже говорила, что не люблю эти конфеты! Ты меня, что, совсем не слушаешь, или у тебя помяти нет?!"
        Max_08 "Просто так уж вышло... Извини. Не смог достать другие."
        Alice_12 "Я тебе сейчас дам, не смог... А ну-ка иди сюда..."   #спрайт с ушами
        call alice_sorry_gifts.kick_ears from _call_alice_sorry_gifts_kick_ears_4
        Max_12 "А-а-ай! Мне же больно, Алиса!"
        Alice_16 "Ещё подглядывать за мной будешь, подлиза ты эдакий?"
        Max_10 "Да я же случайно оказался около душа..."
        Alice_12 "Ты всерьёз думаешь, что меня можно в этом убедить?! Нет уж, я очень хочу посмотреть, как мама тебя отшлёпает!"
        Max_14 "Но, Алиса, я же купил вкусняшку... Ой, отпусти!"
        Alice_05 "Не считается, если она мне не нравится! Так что - не повезло тебе..."
        Max_14 "Ой! Я понял... Больше не буду!"
        Alice_02 "Вот и молодец! Гуляй..."
        $ punreason[1] = 1
        $ flags['alice_hugs'] = 1
        return

    label .apology_accepted:
        Alice_05 "Ладно, Макс, считай твои извинения приняты... Ого, а что это у тебя здесь..."   #спрайт с ушами
        call alice_sorry_gifts.kick_ears from _call_alice_sorry_gifts_kick_ears_5
        Max_12 "А-а-ай! Мне же больно, Алиса!"
        Alice_16 "Ещё подглядывать за мной будешь, подлиза ты эдакий?"
        Max_10 "Да я же случайно оказался около душа..."
        Alice_05 "Видимо, ты хочешь, чтобы я ещё сильнее тебе ухо выкрутила... Я только с радостью!"
        Max_14 "Ой! Я понял... Больше не буду!"
        Alice_02 "Вот и молодец! Гуляй..."
        $ flags['alice_hugs'] = 2
        return

    label .you_deserve:
        $ Skill('social', 0.2)
        Alice_04 "[succes!t]Ладно, Макс, пожалуй ты заслужил это своими подарками..."   #спрайт с обнимашками
        call alice_sorry_gifts.kindred_hugs from _call_alice_sorry_gifts_kindred_hugs_1
        Max_03 "Вау! Это как-то очень непривычно... обнимать тебя без ущерба своему здоровью!"
        Alice_07 "Я вижу, что ты не просто хочешь избежать наказания, а ещё и мне приятно сделать стремишься. Вот я и не вредничаю..."
        Max_05 "Да, надо бы почаще так делать."
        Alice_02 "Подглядывать за мной или дарить мне сладости?!"
        Max_02 "Второе, конечно!"
        Alice_05 "Ну да, конечно... Иди давай."
        $ flags['alice_hugs'] = 4
        return

    label .what_bummer:
        $ Skill('social', 0.1)
        Alice_05 "[failed!t]Ах, а так хотелось! Какой облом..."
        Max_09 "Обнять меня или придушить?"
        Alice_07 "Зачем останавливаться на чём-то одном, Макс? Хи-хи..."
        Max_01 "Я тогда лучше пойду... погуляю."
        Alice_02 "Ну как хочешь..."
        $ flags['alice_hugs'] = 3
        return

    label .im_in_pain:
        Alice_05 "[failed!t]Ладно, Макс, считай твои извинения приняты... Ого, а что это у тебя здесь..."
        call alice_sorry_gifts.kick_ears from _call_alice_sorry_gifts_kick_ears_6
        Max_12 "А-а-ай! Мне же больно, Алиса!"
        Alice_16 "Ещё подглядывать за мной будешь, подлиза ты эдакий?"
        Max_10 "Да я же случайно оказался около душа..."
        Alice_05 "Видимо, ты хочешь, чтобы я ещё сильнее тебе ухо выкрутила... Я только с радостью!"
        Max_14 "Ой! Я понял... Больше не буду!"
        $ flags['alice_hugs'] = 2
        return

    label .what_disgusting:
        Alice_12 "Ой! Какая же гадость этот кокос, не люблю его, фу-у-у! Это большая ошибка, Макс!"
        Max_10 "Я же не знал! Если ты так их не любишь, то можно было и предупредить..."
        Alice_05 "Надо было, но теперь у меня есть повод сделать вот так... А ну-ка иди сюда..."   #спрайт с ушами
        call alice_sorry_gifts.kick_ears from _call_alice_sorry_gifts_kick_ears_7
        Max_12 "А-а-ай! Мне же больно, Алиса!"
        Alice_16 "Ещё подглядывать за мной будешь, подлиза ты эдакий?"
        Max_10 "Да я же случайно оказался около душа..."
        $ flags['alice_hugs'] = 2
        return

    label .bad: ## ненавистное
        $ items[__give].have = False
        $ poss['risk'].OpenStage(4)
        if len(sorry_gifts['alice'].give) == 0:  # ненавистное в первый раз
            Alice_12 "Ой! Какая же гадость этот кокос, не люблю его, фу-у-у! Это большая ошибка, Макс!"
            Max_10 "Я же не знал! Если ты так их не любишь, то можно было и предупредить..."
            Alice_05 "Надо было, но теперь у меня есть повод сделать вот так... А ну-ка иди сюда..."   #спрайт с ушами
            call alice_sorry_gifts.kick_ears from _call_alice_sorry_gifts_kick_ears_8
            Max_12 "А-а-ай! Алиса! Больно ведь!"
            menu:
                Alice_16 "Будешь ещё, извращенец лохматый, за мной подглядывать?"
                "Да я же случайно оказался около душа... {color=[_ch1.col]}(Убеждение. Шанс: [_ch1.vis]){/color}":
                    if RandomChance(_ch1.ch):
                        $ Skill('social', 0.2)
                        Alice_05 "[succes!t]Пожалуй, на этот раз, я поверю и ничего не расскажу маме. Но, на всякий случай, за подглядывание, нужно сильнее потянуть..."
                        Max_14 "Ой! Понял-понял, не буду! Больше не буду..."
                        Alice_04 "Ну и просто на будущее, знай, в следующий раз ты так легко не отделаешься! Если только это не будет моя любимая сладость..."
                        Max_08 "И какая у тебя любимая?"
                        Alice_03 "Так я тебе и сказала! Но её дольше всех других нужно разворачивать..."
                        Max_11 "Ладно! Я учту, только отпусти..."
                        Alice_02 "Вот и правильно! Гуляй..."
                    else:
                        $ Skill('social', 0.1)
                        Alice_12 "[failed!t]Ты всерьёз думаешь, что меня можно в этом убедить?! Нет уж, я очень хочу посмотреть, как мама тебя отшлёпает!"
                        Max_14 "Но, Алиса, я же купил вкусняшку... Ой, отпусти!"
                        Alice_05 "Не считается, если она мне не нравится! Так что - не повезло тебе..."
                        Max_08 "И какая у тебя любимая?"
                        Alice_03 "Так я тебе и сказала! Но её дольше всех других нужно разворачивать..."
                        Max_11 "Ладно! Я учту, только отпусти..."
                        Alice_02 "Вот и правильно! Гуляй..."
                        $ punreason[1] = 1

        elif len(sorry_gifts['alice'].give) == 1:  ## второе вручение
            if sorry_gifts['alice'].give[0] == 1:  ## ненавистное, ненавистное
                Alice_17 "Макс, ты что, тупой?! Я тебе уже говорила, что не люблю эти конфеты! Ты меня, что, совсем не слушаешь, или у тебя помяти нет?!"
                Max_08 "Просто так уж вышло... Извини. Не смог достать другие."
                Alice_12 "Я тебе сейчас дам, не смог... А ну-ка иди сюда..."   #спрайт с ушами
                call alice_sorry_gifts.kick_ears from _call_alice_sorry_gifts_kick_ears_9
                Max_12 "А-а-ай! Мне же больно, Алиса!"
                Alice_16 "Ещё подглядывать за мной будешь, подлиза ты эдакий?"
                Max_10 "Да я же случайно оказался около душа..."
                Alice_12 "Ты всерьёз думаешь, что меня можно в этом убедить?! Нет уж, я очень хочу посмотреть, как мама тебя отшлёпает!"
                Max_14 "Но, Алиса, я же купил вкусняшку... Ой, отпусти!"
                Alice_05 "Не считается, если она мне не нравится! Так что - не повезло тебе..."
                Max_08 "И какая у тебя любимая?"
                Alice_03 "Так я тебе и сказала! Но её дольше всех других нужно разворачивать..."
                Max_11 "Я обязательно подарю тебе любимую, обещаю! Отпусти уже..."
                Alice_02 "Вот и молодец! Гуляй..."
                $ punreason[1] = 1

            elif sorry_gifts['alice'].give[0] == 2: ## преемлемое, ненавистное
                call alice_sorry_gifts.what_disgusting from _call_alice_sorry_gifts_what_disgusting
                Alice_12 "Ты всерьёз думаешь, что меня можно в этом убедить?! Нет уж, я очень хочу посмотреть, как мама тебя отшлёпает!"
                Max_14 "Но, Алиса, я же купил вкусняшку... Ой, отпусти!"
                Alice_05 "Не считается, если она мне не нравится! Так что - не повезло тебе..."
                Max_08 "И какая у тебя любимая?"
                Alice_03 "Так я тебе и сказала! Но её дольше всех других нужно разворачивать..."
                Max_11 "Я обязательно подарю тебе любимую, обещаю! Отпусти уже..."
                Alice_02 "Вот и молодец! Гуляй..."
                $ punreason[1] = 1

            elif sorry_gifts['alice'].give[0] == 3: ## любимое, ненавистное
                Alice_12 "Ой! Какая же гадость этот кокос, не люблю его, фу-у-у! Это большая ошибка, Макс!"
                Max_10 "Я же не знал! Если ты так их не любишь, то можно было и предупредить..."
                Alice_05 "Надо было, но теперь у меня есть повод сделать вот так... А ну-ка иди сюда..."   #спрайт с ушами
                call alice_sorry_gifts.kick_ears from _call_alice_sorry_gifts_kick_ears_10
                Max_12 "А-а-ай! Мне же больно, Алиса!"
                menu:
                    Alice_16 "Ещё подглядывать за мной будешь, подлиза ты эдакий?"
                    "Да я же случайно оказался около душа... {color=[_ch1.col]}(Убеждение. Шанс: [_ch1.vis]){/color}":
                        if RandomChance(_ch1.ch):
                            $ Skill('social', 0.2)
                            Alice_05 "[succes!t]Пожалуй, на этот раз, я поверю и ничего не расскажу маме. Но, на всякий случай, за подглядывание, нужно сильнее потянуть..."
                            Max_14 "Ой! Я понял... Больше не буду!"
                            Alice_04 "Ну и просто на будущее, знай, в следующий раз ты так легко не отделаешься! Если только это не будет моя любимая сладость..."
                            Max_11 "Взято на заметку, Алиса! Отпусти уже..."
                            Alice_02 "Вот и молодец! Гуляй..."
                        else:
                            $ Skill('social', 0.1)
                            Alice_12 "[failed!t]Ты всерьёз думаешь, что меня можно в этом убедить?! Нет уж, я очень хочу посмотреть, как мама тебя отшлёпает!"
                            Max_14 "Но, Алиса, я же купил вкусняшку... Ой, отпусти!"
                            Alice_05 "Не считается, если она мне не нравится! Так что - не повезло тебе..."
                            Max_11 "Я обязательно подарю тебе любимую, обещаю! Отпусти уже..."
                            Alice_02 "Вот и молодец! Гуляй..."
                            $ punreason[1] = 1

        elif len(sorry_gifts['alice'].give) == 2:  ### третье вручение
            if sorry_gifts['alice'].give == [1, 1]:  ## ненависное, ненавистное, ненавистное
                Alice_17 "Макс, ты что, тупой, я тебе, уже дважды говорила, что не люблю эти конфеты?! Ты меня, что, совсем не слушаешь, или у тебя мозгов нет?!"
                Max_08 "Просто так уж вышло... Извини. Не смог достать другие."
                Alice_12 "Я тебе сейчас дам, не смог... А ну-ка иди сюда..."   #спрайт с ушами
                call alice_sorry_gifts.kick_ears from _call_alice_sorry_gifts_kick_ears_11
                Max_12 "А-а-ай! Мне же больно, Алиса!"
                Alice_16 "Ещё подглядывать за мной будешь, подлиза ты эдакий?"
                Max_10 "Да я же случайно оказался около душа..."
                Alice_12 "Ты всерьёз думаешь, что меня можно в этом убедить?! Нет уж, я очень хочу посмотреть, как мама тебя отшлёпает!"
                Max_14 "Но, Алиса, я же купил вкусняшку... Ой, отпусти!"
                Alice_16 "Да тебе просто наплевать на всё то, что я тебе говорю! Так что - сам виноват..."
                Max_14 "Ой! Я понял... Больше не буду!"
                Alice_17 "Понял он... Катись отсюда!"
                $ flags['alice_hugs'] = 1
                $ punreason[1] = 1

            elif sorry_gifts['alice'].give == [1, 2]:  ### ненавистное, преемлемое, ненавистное
                call alice_sorry_gifts.bad_again from _call_alice_sorry_gifts_bad_again

            elif sorry_gifts['alice'].give == [1, 3]:  ### ненавистное, любимое, ненавистное
                Alice_17 "Макс, ты что, тупой?! Я тебе уже говорила, что не люблю эти конфеты! Ты меня, что, совсем не слушаешь, или у тебя помяти нет?!"
                Max_08 "Просто так уж вышло... Извини. Не смог достать другие."
                Alice_12 "Я тебе сейчас дам, не смог... А ну-ка иди сюда..."   #спрайт с ушами
                call alice_sorry_gifts.kick_ears from _call_alice_sorry_gifts_kick_ears_12
                Max_12 "А-а-ай! Мне же больно, Алиса!"
                menu:
                    Alice_16 "Ещё подглядывать за мной будешь, подлиза ты эдакий?"
                    "Да я же случайно оказался около душа... {color=[_ch1.col]}(Убеждение. Шанс: [_ch1.vis]){/color}":
                        if RandomChance(_ch1.ch):
                            $ Skill('social', 0.2)
                            Alice_05 "[succes!t]Пожалуй, на этот раз, я поверю и ничего не расскажу маме. Но, на всякий случай, за подглядывание, нужно сильнее потянуть..."
                            Max_14 "Ой! Я понял... Больше не буду!"
                            Alice_02 "Вот и молодец! Гуляй..."
                            $ flags['alice_hugs'] = 2
                        else:
                            $ Skill('social', 0.1)
                            Alice_12 "[failed!t]Ты всерьёз думаешь, что меня можно в этом убедить?! Нет уж, я очень хочу посмотреть, как мама тебя отшлёпает!"
                            Max_14 "Но, Алиса, я же купил вкусняшку... Ой, отпусти!"
                            Alice_05 "Не считается, если она мне не нравится! Так что - не повезло тебе..."
                            Max_14 "Ой! Я понял... Больше не буду!"
                            Alice_02 "Вот и молодец! Гуляй..."
                            $ flags['alice_hugs'] = 1
                            $ punreason[1] = 1

            elif sorry_gifts['alice'].give == [2, 1]:  ### преемлемое, ненавистное, ненавистное
                call alice_sorry_gifts.bad_again from _call_alice_sorry_gifts_bad_again_1

            elif sorry_gifts['alice'].give == [2, 2]:  ### преемлемое, преемлемое, ненавистное
                Alice_12 "Ой! Какая же гадость этот кокос, не люблю его, фу-у-у! Это большая ошибка, Макс!"
                Max_10 "Я же не знал! Если ты так их не любишь, то можно было и предупредить..."
                Alice_05 "Надо было, но теперь у меня есть повод сделать вот так... А ну-ка иди сюда..."   #спрайт с ушами
                call alice_sorry_gifts.kick_ears from _call_alice_sorry_gifts_kick_ears_13
                Max_12 "А-а-ай! Мне же больно, Алиса!"
                menu:
                    Alice_16 "Ещё подглядывать за мной будешь, подлиза ты эдакий?"
                    "Да я же случайно оказался около душа... {color=[_ch1.col]}(Убеждение. Шанс: [_ch1.vis]){/color}":
                        if RandomChance(_ch1.ch):
                            $ Skill('social', 0.2)
                            Alice_05 "[succes!t]Пожалуй, на этот раз, я поверю и ничего не расскажу маме. Но, на всякий случай, за подглядывание, нужно сильнее потянуть..."
                            Max_14 "Ой! Я понял... Больше не буду!"
                            Alice_02 "Вот и молодец! Гуляй..."
                            $ flags['alice_hugs'] = 2
                        else:
                            $ Skill('social', 0.1)
                            Alice_12 "[failed!t]Ты всерьёз думаешь, что меня можно в этом убедить?! Нет уж, я очень хочу посмотреть, как мама тебя отшлёпает!"
                            Max_14 "Но, Алиса, я же купил вкусняшку... Ой, отпусти!"
                            Alice_05 "Не считается, если она мне не нравится! Так что - не повезло тебе..."
                            Max_14 "Ой! Я понял... Больше не буду!"
                            Alice_02 "Вот и молодец! Гуляй..."
                            $ flags['alice_hugs'] = 1
                            $ punreason[1] = 1

            elif sorry_gifts['alice'].give == [2, 3]:  ### преемлемое, любимое, ненавистное
                call alice_sorry_gifts.what_disgusting from _call_alice_sorry_gifts_what_disgusting_1
                Alice_05 "Пожалуй, на этот раз, я поверю и ничего не расскажу маме. Но, на всякий случай, за подглядывание, нужно сильнее потянуть..."
                Max_14 "Ой! Я понял... Больше не буду!"
                Alice_02 "Вот и молодец! Гуляй..."

            elif sorry_gifts['alice'].give == [3, 1]:  ### любимое, ненавистное, ненавистное
                call alice_sorry_gifts.bad_again from _call_alice_sorry_gifts_bad_again_2

            elif sorry_gifts['alice'].give == [3, 2]:  ### любимое, преемлемое, ненавистное
                Alice_12 "Ой! Какая же гадость этот кокос, не люблю его, фу-у-у! Это большая ошибка, Макс!"
                Max_10 "Я же не знал! Если ты так их не любишь, то можно было и предупредить..."
                Alice_05 "Надо было, но теперь у меня есть повод сделать вот так... А ну-ка иди сюда..."   #спрайт с ушами
                call alice_sorry_gifts.kick_ears from _call_alice_sorry_gifts_kick_ears_14
                Max_12 "А-а-ай! Мне же больно, Алиса!"
                menu:
                    Alice_16 "Ещё подглядывать за мной будешь, подлиза ты эдакий?"
                    "Да я же случайно оказался около душа... {color=[_ch1.col]}(Убеждение. Шанс: [_ch1.vis]){/color}":
                        if RandomChance(_ch1.ch):
                            $ Skill('social', 0.2)
                            Alice_05 "[succes!t]Пожалуй, на этот раз, я поверю и ничего не расскажу маме. Но, на всякий случай, за подглядывание, нужно сильнее потянуть..."
                            Max_14 "Ой! Я понял... Больше не буду!"
                            Alice_02 "Вот и молодец! Гуляй..."
                            $ flags['alice_hugs'] = 2
                        else:
                            $ Skill('social', 0.1)
                            Alice_12 "[failed!t]Ты всерьёз думаешь, что меня можно в этом убедить?! Нет уж, я очень хочу посмотреть, как мама тебя отшлёпает!"
                            Max_14 "Но, Алиса, я же купил вкусняшку... Ой, отпусти!"
                            Alice_05 "Не считается, если она мне не нравится! Так что - не повезло тебе..."
                            Max_14 "Ой! Я понял... Больше не буду!"
                            Alice_02 "Вот и молодец! Гуляй..."
                            $ flags['alice_hugs'] = 1
                            $ punreason[1] = 1

            else:  ### любимое, любимое, ненавистное
                call alice_sorry_gifts.what_disgusting from _call_alice_sorry_gifts_what_disgusting_2
                Alice_05 "Пожалуй, на этот раз, я поверю и ничего не расскажу маме. Но, на всякий случай, за подглядывание, нужно сильнее потянуть..."
                Max_14 "Ой! Я понял... Больше не буду!"
                Alice_02 "Вот и молодец! Гуляй..."

        $ sorry_gifts['alice'].give.append(1)
        jump .end

    label .middle: ## преемлемое
        $ items[__give].have = False
        $ poss['risk'].OpenStage(3)
        if len(sorry_gifts['alice'].give) == 0:  # преемлемой в первый раз
            Alice_03 "Неплохо... Не то, чтобы он мне нравился, не люблю многие начинки, но сойдёт. Спасибо!"
            Max_07 "Так значит, ты ничего не расскажешь маме об утреннем инцеденте?"
            Alice_05 "Конечно, Макс, считай твои извинения приняты... А ну-ка иди сюда..."   #спрайт с ушами
            call alice_sorry_gifts.kick_ears from _call_alice_sorry_gifts_kick_ears_15
            Max_12 "А-а-ай! Алиса! Больно ведь!"
            Alice_16 "Будешь ещё, извращенец лохматый, за мной подглядывать?"
            Max_10 "Да я же случайно оказался около душа..."
            Alice_05 "Ответ неправильный! Наверно, нужно сильнее потянуть..."
            Max_14 "Ой! Понял-понял, не буду! Больше не буду..."
            Alice_04 "Ну и просто на будущее, знай, в следующий раз ты так легко не отделаешься! Если только это не будет моя любимая сладость..."
            Max_08 "И какая у тебя любимая?"
            Alice_03 "Так я тебе и сказала! Но её дольше всех других нужно разворачивать..."
            Max_11 "Ладно! Я учту, только отпусти..."
            Alice_02 "Вот и правильно! Гуляй..."

        elif len(sorry_gifts['alice'].give) == 1:  ## второе вручение
            if sorry_gifts['alice'].give[0] == 1:  ## ненавистное, преемлемое
                Alice_03 "Неплохо... Не то, чтобы он мне нравился, не люблю многие начинки, но сойдёт. Спасибо!"
                Max_07 "Так значит, ты ничего не расскажешь маме об утреннем инцеденте?"
                Alice_05 "Может и не буду, только сперва сделаю вот что... А ну-ка иди сюда..."   #спрайт с ушами
                call alice_sorry_gifts.kick_ears from _call_alice_sorry_gifts_kick_ears_16
                Max_12 "А-а-ай! Мне же больно, Алиса!"
                menu:
                    Alice_16 "Ещё подглядывать за мной будешь, подлиза ты эдакий?"
                    "Да я же случайно оказался около душа... {color=[_ch1.col]}(Убеждение. Шанс: [_ch1.vis]){/color}":
                        if RandomChance(_ch1.ch):
                            $ Skill('social', 0.2)
                            Alice_05 "[succes!t]Пожалуй, на этот раз, я поверю и ничего не расскажу маме. Но, на всякий случай, за подглядывание, нужно сильнее потянуть..."
                            Max_14 "Ой! Я понял... Больше не буду!"
                            Alice_04 "Ну и просто на будущее, знай, в следующий раз ты так легко не отделаешься! Если только это не будет моя любимая сладость..."
                            Max_08 "И какая у тебя любимая?"
                            Alice_03 "Так я тебе и сказала! Но её дольше всех других нужно разворачивать..."
                            Max_11 "Взято на заметку, Алиса! Отпусти уже..."
                            Alice_02 "Вот и молодец! Гуляй..."
                        else:
                            $ Skill('social', 0.1)
                            Alice_12 "[failed!t]Ты всерьёз думаешь, что меня можно в этом убедить?! Нет уж, я очень хочу посмотреть, как мама тебя отшлёпает!"
                            Max_14 "Но, Алиса, я же купил вкусняшку... Ой, отпусти!"
                            Alice_05 "Не считается, если она мне неинтересна! Так что - не повезло тебе..."
                            Max_08 "И какая у тебя любимая?"
                            Alice_03 "Так я тебе и сказала! Но её дольше всех других нужно разворачивать..."
                            Max_11 "Я обязательно подарю тебе любимую, обещаю! Отпусти уже..."
                            Alice_02 "Вот и молодец! Гуляй..."
                            $ punreason[1] = 1

            elif sorry_gifts['alice'].give[0] == 2:  ## преемлемое, преемлемое
                Alice_13 "Вот значит как! Снова купил эти шоколадки... Спасибо, конечно, но не очень-то тебе хочется избежать наказания, как я вижу."
                Max_08 "Просто так уж вышло... Может, ты всё же не будешь рассказывать маме про то, что было утром?"
                Alice_05 "Может и не буду, только сперва сделаю вот что... А ну-ка иди сюда..."   #спрайт с ушами
                call alice_sorry_gifts.kick_ears from _call_alice_sorry_gifts_kick_ears_17
                Max_12 "А-а-ай! Мне же больно, Алиса!"
                menu:
                    Alice_16 "Ещё подглядывать за мной будешь, подлиза ты эдакий?"
                    "Да я же случайно оказался около душа... {color=[_ch1.col]}(Убеждение. Шанс: [_ch1.vis]){/color}":
                        if RandomChance(_ch1.ch):
                            $ Skill('social', 0.2)
                            Alice_05 "[succes!t]Пожалуй, на этот раз, я поверю и ничего не расскажу маме. Но, на всякий случай, за подглядывание, нужно сильнее потянуть..."
                            Max_14 "Ой! Я понял... Больше не буду!"
                            Alice_04 "Ну и просто на будущее, знай, в следующий раз ты так легко не отделаешься! Если только это не будет моя любимая сладость..."
                            Max_08 "И какая у тебя любимая?"
                            Alice_03 "Так я тебе и сказала! Но её дольше всех других нужно разворачивать..."
                            Max_11 "Взято на заметку, Алиса! Отпусти уже..."
                            Alice_02 "Вот и молодец! Гуляй..."
                        else:
                            $ Skill('social', 0.1)
                            Alice_12 "[failed!t]Ты всерьёз думаешь, что меня можно в этом убедить?! Нет уж, я очень хочу посмотреть, как мама тебя отшлёпает!"
                            Max_14 "Но, Алиса, я же купил вкусняшку... Ой, отпусти!"
                            Alice_05 "Не считается, если она мне неинтересна! Так что - не повезло тебе..."
                            Max_08 "И какая у тебя любимая?"
                            Alice_03 "Так я тебе и сказала! Но её дольше всех других нужно разворачивать..."
                            Max_11 "Я обязательно подарю тебе любимую, обещаю! Отпусти уже..."
                            Alice_02 "Вот и молодец! Гуляй..."
                            $ punreason[1] = 1

            else:  ## любимое, преемлемое
                Alice_03 "Неплохо... Не то, чтобы он мне нравился, не люблю многие начинки, но сойдёт. Спасибо!"
                Max_07 "Так значит, ты ничего не расскажешь маме об утреннем инцеденте?"
                Alice_05 "Ладно, Макс, считай твои извинения приняты... Ого, а что это у тебя здесь..."   #спрайт с ушами
                call alice_sorry_gifts.kick_ears from _call_alice_sorry_gifts_kick_ears_18
                Max_12 "А-а-ай! Мне же больно, Алиса!"
                Alice_16 "Ещё подглядывать за мной будешь, подлиза ты эдакий?"
                Max_10 "Да я же случайно оказался около душа..."
                Alice_05 "Видимо, ты хочешь, чтобы я ещё сильнее тебе ухо выкрутила... Я только с радостью!"
                Max_14 "Ой! Я понял... Больше не буду!"
                Alice_04 "Ну и просто на будущее, знай, в следующий раз ты так легко не отделаешься! Если только это не будет моя любимая сладость..."
                Max_11 "Взято на заметку, Алиса! Отпусти уже..."
                Alice_02 "Вот и молодец! Гуляй..."

        elif len(sorry_gifts['alice'].give) == 2:  ###  третье вручение
            if sorry_gifts['alice'].give == [1, 1]:  ### ненависное, ненавистное, преемлемое
                Alice_03 "Неплохо... Не то, чтобы он мне нравился, не люблю многие начинки, но сойдёт. Спасибо!"
                Max_07 "Так значит, ты ничего не расскажешь маме об утреннем инцеденте?"
                Alice_12 "Я тебе сейчас дам, ничего не расскажу... А ну-ка иди сюда..."   #спрайт с ушами
                call alice_sorry_gifts.kick_ears from _call_alice_sorry_gifts_kick_ears_19
                Max_12 "А-а-ай! Мне же больно, Алиса!"
                Alice_16 "Ещё подглядывать за мной будешь, подлиза ты эдакий?"
                Max_10 "Да я же случайно оказался около душа..."
                Alice_12 "Ты всерьёз думаешь, что меня можно в этом убедить?! Нет уж, я очень хочу посмотреть, как мама тебя отшлёпает!"
                Max_14 "Но, Алиса, я же купил вкусняшку... Ой, отпусти!"
                Alice_05 "Не считается, если она мне неинтересна! Ты так и не подарил самую мою любимую сладость! Так что - не повезло тебе..."
                Max_14 "Ой! Я понял... Больше не буду!"
                Alice_02 "Вот и молодец! Гуляй..."
                $ flags['alice_hugs'] = 1
                $ punreason[1] = 1

            elif sorry_gifts['alice'].give == [1, 2]:  ### ненавистное, преемлемое, преемлемое
                call alice_sorry_gifts.middle_again from _call_alice_sorry_gifts_middle_again

            elif sorry_gifts['alice'].give == [1, 3]:  ### ненавистное, любимое, преемлемое
                Alice_03 "Неплохо... Не то, чтобы он мне нравился, не люблю многие начинки, но сойдёт. Спасибо!"
                Max_07 "Так значит, ты ничего не расскажешь маме об утреннем инцеденте?"
                call alice_sorry_gifts.apology_accepted from _call_alice_sorry_gifts_apology_accepted

            elif sorry_gifts['alice'].give == [2, 1]:  ### преемлемое, ненавистное, преемлемое
                call alice_sorry_gifts.middle_again from _call_alice_sorry_gifts_middle_again_1

            elif sorry_gifts['alice'].give == [2, 2]:  ### преемлемое третий раз
                Alice_13 "Вот значит как! Снова купил эти шоколадки... А ты рисковый! Спасибо, конечно, но не очень-то тебе хочется избежать наказания, как я вижу."
                Max_08 "Просто так уж вышло... Может, ты всё же не будешь рассказывать маме про то, что было утром?"
                call alice_sorry_gifts.apology_accepted from _call_alice_sorry_gifts_apology_accepted_1

            elif sorry_gifts['alice'].give == [2, 3]:  ### преемлемое, любимое, преемлемое
                Alice_13 "Вот значит как! Снова купил эти шоколадки... Спасибо, конечно, но не очень-то тебе хочется избежать наказания, как я вижу."
                Max_08 "Просто так уж вышло... Может, ты всё же не будешь рассказывать маме про то, что было утром?"
                menu:
                    Alice_04 "Видимо, я должна представить, что ничего такого утром не было, а значит и маме нечего рассказывать, так?"
                    "Хочется надеяться, что так и будет... {color=[_ch1.col]}(Убеждение. Шанс: [_ch1.vis]){/color}":
                        if RandomChance(_ch1.ch):
                            $ Skill('social', 0.2)
                            Alice_03 "[succes!t]Ладно, так тому и быть, считай твои извинения приняты... Мама ничего не узнает, так что можешь не напрягаться."
                            Max_07 "Что, вот так вот просто?!"
                            Alice_05 "Ну, ты обещал мне вкусняшку и сдержал слово. А я сейчас более-менее добрая... Так что не искушай судьбу!"
                            Max_01 "Понял, сестрёнка! Не буду тебе мешать..."
                            $ flags['alice_hugs'] = 3
                        else:
                            $ Skill('social', 0.1)
                            Alice_05 "[failed!t]Ладно, Макс, считай твои извинения приняты... Ого, а что это у тебя здесь..."   #спрайт с ушами
                            call alice_sorry_gifts.kick_ears from _call_alice_sorry_gifts_kick_ears_20
                            Max_12 "А-а-ай! Мне же больно, Алиса!"
                            Alice_16 "Ещё подглядывать за мной будешь, подлиза ты эдакий?"
                            Max_10 "Да я же случайно оказался около душа..."
                            Alice_05 "Видимо, ты хочешь, чтобы я ещё сильнее тебе ухо выкрутила... Я только с радостью!"
                            Max_14 "Ой! Я понял... Больше не буду!"
                            Alice_02 "Вот и молодец! Гуляй..."
                            $ flags['alice_hugs'] = 2

            elif sorry_gifts['alice'].give == [3, 1]:  ### любимое, ненавистное, преемлемое
                Alice_03 "Неплохо... Не то, чтобы он мне нравился, не люблю многие начинки, но сойдёт. Спасибо!"
                Max_07 "Так значит, ты ничего не расскажешь маме об утреннем инцеденте?"
                Alice_05 "Может и не буду, только сперва сделаю вот что... А ну-ка иди сюда..."   #спрайт с ушами
                call alice_sorry_gifts.kick_ears from _call_alice_sorry_gifts_kick_ears_21
                Max_12 "А-а-ай! Мне же больно, Алиса!"
                menu:
                    Alice_16 "Ещё подглядывать за мной будешь, подлиза ты эдакий?"
                    "Да я же случайно оказался около душа... {color=[_ch1.col]}(Убеждение. Шанс: [_ch1.vis]){/color}":
                        if RandomChance(_ch1.ch):
                            $ Skill('social', 0.2)
                            Alice_05 "[succes!t]Пожалуй, на этот раз, я поверю и ничего не расскажу маме. Но, на всякий случай, за подглядывание, нужно сильнее потянуть..."
                            Max_14 "Ой! Я понял... Больше не буду!"
                            Alice_02 "Вот и молодец! Гуляй..."
                            $ flags['alice_hugs'] = 2
                        else:
                            $ Skill('social', 0.1)
                            Alice_12 "[failed!t]Ты всерьёз думаешь, что меня можно в этом убедить?! Нет уж, я очень хочу посмотреть, как мама тебя отшлёпает!"
                            Max_14 "Но, Алиса, я же купил вкусняшку... Ой, отпусти!"
                            Alice_05 "Не считается, если она мне неинтересна! Так что - не повезло тебе..."
                            Max_14 "Ой! Я понял... Больше не буду!"
                            Alice_02 "Вот и молодец! Гуляй..."
                            $ punreason[1] = 1
                            $ flags['alice_hugs'] = 1

            elif sorry_gifts['alice'].give == [3, 2]:  ### любимое, преемлемое, преемлемое
                Alice_13 "Вот значит как! Снова купил эти шоколадки... Спасибо, конечно, но не очень-то тебе хочется избежать наказания, как я вижу."
                Max_08 "Просто так уж вышло... Может, ты всё же не будешь рассказывать маме про то, что было утром?"
                call alice_sorry_gifts.apology_accepted from _call_alice_sorry_gifts_apology_accepted_2

            else:  ### любимое, любимое, преемлемое
                Alice_03 "Неплохо... Не то, чтобы он мне нравился, не люблю многие начинки, но сойдёт. Спасибо!"
                Max_07 "Так значит, ты ничего не расскажешь маме об утреннем инцеденте?"
                menu:
                    Alice_04 "Видимо, я должна представить, что ничего такого утром не было, а значит и маме нечего рассказывать, так?"
                    "Хочется надеяться, что так и будет... {color=[_ch1.col]}(Убеждение. Шанс: [_ch1.vis]){/color}":
                        if RandomChance(_ch1.ch):
                            $ Skill('social', 0.2)
                            Alice_03 "[succes!t]Ладно, так тому и быть, считай твои извинения приняты... Мама ничего не узнает, так что можешь не напрягаться."
                            Max_07 "Что, вот так вот просто?!"
                            Alice_05 "Ну, ты обещал мне вкусняшку и сдержал слово. А я сейчас более-менее добрая... Так что не искушай судьбу!"
                            Max_01 "Понял, сестрёнка! Не буду тебе мешать..."
                            $ flags['alice_hugs'] = 3
                        else:
                            $ Skill('social', 0.1)
                            Alice_05 "[failed!t]Ладно, Макс, считай твои извинения приняты... Ого, а что это у тебя здесь..."   #спрайт с ушами
                            call alice_sorry_gifts.kick_ears from _call_alice_sorry_gifts_kick_ears_22
                            Max_12 "А-а-ай! Мне же больно, Алиса!"
                            Alice_16 "Ещё подглядывать за мной будешь, подлиза ты эдакий?"
                            Max_10 "Да я же случайно оказался около душа..."
                            Alice_05 "Видимо, ты хочешь, чтобы я ещё сильнее тебе ухо выкрутила... Я только с радостью!"
                            Max_14 "Ой! Я понял... Больше не буду!"
                            Alice_02 "Вот и молодец! Гуляй..."
                            $ flags['alice_hugs'] = 2

        $ sorry_gifts['alice'].give.append(2)
        jump .end

    label .good: ## любимое
        $ items[__give].have = False
        $ poss['risk'].OpenStage(2)
        $ items['ferrero-b'].InShop = True
        $ sorry_gifts['alice'].valid.add('ferrero-b')
        if len(sorry_gifts['alice'].give) == 0:  # любимое, самый первый раз
            Alice_07 "Ничего себе! Ты даже умудрился купить мои любимые конфеты! Большое спасибо! И кто об этом проболтался?"
            Max_03 "Никто! Просто угадал..."
            Alice_05 "Хм... Похоже, что ты, Макс, большой везунчик! Поглазел на меня голую в душе, да ещё и с конфетами угадал... Не слишком ли?"
            Max_04 "Просто благоприятное стечение обстоятельств! И я за тобой не подглядывал, просто случайность..."
            menu:
                Alice_04 "В таком случае, видимо, я должна представить, что ничего такого утром не было, а значит и маме нечего рассказывать, так?"
                "Именно на это я и надеюсь... {color=[_ch1.col]}(Убеждение. Шанс: [_ch1.vis]){/color}":
                    if RandomChance(_ch1.ch):
                        $ Skill('social', 0.2)
                        Alice_03 "[succes!t]Ладно, Макс, считай твои извинения приняты... Мама ничего не узнает, так что можешь дышать спокойно."
                        Max_07 "И даже без подвоха?!"
                        Alice_05 "Ну, ты обещал мне вкусняшку и сдержал слово. А я добрая, если настроение хорошее. Более-менее добрая... Так что не искушай судьбу!"
                        Max_01 "О, я понял, сестрёнка! Не буду мешать..."
                    else:
                        $ Skill('social', 0.1)
                        Alice_05 "[failed!t]Ладно, Макс, считай твои извинения приняты... А ну-ка иди сюда..."   #спрайт с ушами
                        call alice_sorry_gifts.kick_ears from _call_alice_sorry_gifts_kick_ears_23
                        Max_12 "А-а-ай! Алиса! Больно ведь!"
                        Alice_16 "Будешь ещё, извращенец лохматый, за мной подглядывать?"
                        Max_10 "Да я же случайно оказался около душа..."
                        Alice_05 "Ответ неправильный! Наверно, нужно сильнее потянуть..."
                        Max_14 "Ой! Понял-понял, не буду! Больше не буду..."
                        Alice_02 "Вот и правильно! Гуляй..."
            $ AddRelMood('alice', 0, 50)

        elif len(sorry_gifts['alice'].give) == 1:  ## дарим во второй раз
            if sorry_gifts['alice'].give[0] == 1:  ## ненавистное, любимое
                Alice_07 "Ничего себе! Ты даже умудрился купить мои любимые конфеты! Большое спасибо! И кто об этом проболтался?"
                Max_03 "Никто! Просто повезло, а может твоя подсказа помогла."
                Alice_05 "Хм... Похоже, что ты, Макс, большой везунчик! Поглазел на меня голую в душе, да ещё и с конфетами угадал... Не слишком ли?"
                Max_04 "Просто благоприятное стечение обстоятельств! И я за тобой не подглядывал, просто случайность..."
                Alice_04 "В таком случае, видимо, я должна представить, что ничего такого утром не было, а значит и маме нечего рассказывать, так?"
                Max_01 "Хочется надеяться, что так и будет..."
                Alice_05 "Ладно, Макс, считай твои извинения приняты... Ого, а что это у тебя здесь..."   #спрайт с ушами
                call alice_sorry_gifts.kick_ears from _call_alice_sorry_gifts_kick_ears_24
                Max_12 "А-а-ай! Мне же больно, Алиса!"
                Alice_16 "Ещё подглядывать за мной будешь, подлиза ты эдакий?"
                Max_10 "Да я же случайно оказался около душа..."
                Alice_05 "Видимо, ты хочешь, чтобы я ещё сильнее тебе ухо выкрутила... Я только с радостью!"
                Max_14 "Ой! Я понял... Больше не буду!"
                Alice_04 "Ну и просто на будущее, знай, в следующий раз ты так легко не отделаешься! Разве только это не будет большая коробка моих любимых конфет..."
                Max_11 "Взято на заметку, Алиса! Отпусти уже..."
                Alice_02 "Вот и молодец! Гуляй..."
                $ AddRelMood('alice', 0, 50)

            elif sorry_gifts['alice'].give[0] == 2:  ## преемлемое, любимое
                Alice_07 "Ничего себе! Ты даже умудрился купить мои любимые конфеты! Большое спасибо! И кто об этом проболтался?"
                Max_03 "Никто! Просто повезло, а может твоя подсказа помогла."
                Alice_05 "Хм... Похоже, что ты, Макс, большой везунчик! Поглазел на меня голую в душе, да ещё и с конфетами угадал... Не слишком ли?"
                Max_04 "Просто благоприятное стечение обстоятельств! И я за тобой не подглядывал, просто случайность..."
                menu:
                    Alice_04 "В таком случае, видимо, я должна представить, что ничего такого утром не было, а значит и маме нечего рассказывать, так?"
                    "Хочется надеяться, что так и будет... {color=[_ch1.col]}(Убеждение. Шанс: [_ch1.vis]){/color}":
                        if RandomChance(_ch1.ch):
                            $ Skill('social', 0.2)
                            Alice_03 "[succes!t]Ладно, так тому и быть, считай твои извинения приняты... Мама ничего не узнает, так что можешь не напрягаться."
                            Max_07 "Что, вот так вот просто?!"
                            Alice_05 "Ну, ты обещал мне вкусняшку и сдержал слово. А я добрая, если настроение хорошее. Более-менее добрая... Так что не искушай судьбу!"
                            Max_01 "Понял, сестрёнка! Не буду тебе мешать..."
                        else:
                            $ Skill('social', 0.1)
                            Alice_05 "Ладно, Макс, считай твои извинения приняты... Ого, а что это у тебя здесь..."   #спрайт с ушами
                            call alice_sorry_gifts.kick_ears from _call_alice_sorry_gifts_kick_ears_25
                            Max_12 "А-а-ай! Мне же больно, Алиса!"
                            Alice_16 "Ещё подглядывать за мной будешь, подлиза ты эдакий?"
                            Max_10 "Да я же случайно оказался около душа..."
                            Alice_05 "Видимо, ты хочешь, чтобы я ещё сильнее тебе ухо выкрутила... Я только с радостью!"
                            Max_14 "Ой! Я понял... Больше не буду!"
                            Alice_02 "Вот и молодец! Гуляй..."
                $ AddRelMood('alice', 0, 50)

            else:  ## любимое, любимое
                Alice_07 "Ага! Снова купил мои любимые конфеты! Большое тебе спасибо, Макс! Я удивлена, они ведь дорогие..."
                Max_03 "Почему бы не порадовать старшую сестрёнку её любимыми конфетами, если уж возможность подворачивается."
                if __give[-1:] == 'm':  #если сладость маленькая
                    menu:
                        Alice_04 "Видимо, теперь я должна представить, что никто утром за мной в душе не подглядывал, да?"
                        "Хочется надеяться, что так и будет... {color=[_ch1.col]}(Убеждение. Шанс: [_ch1.vis]){/color}":
                            if RandomChance(_ch1.ch):
                                $ Skill('social', 0.2)
                                Alice_03 "[succes!t]Ладно, так тому и быть, считай твои извинения приняты... Мама ничего не узнает, так что можешь не напрягаться."
                                Max_07 "Что, вот так вот просто?!"
                                Alice_05 "Ну, ты обещал мне вкусняшку и сдержал слово. А я добрая, если настроение хорошее. Но, Макс, просто на будущее, знай, в следующий раз ты так легко не отделаешься! Разве только это не будет большая коробка моих любимых конфет..."
                                Max_01 "Понял, сестрёнка! Не буду тебе мешать..."
                            else:
                                $ Skill('social', 0.1)
                                call alice_sorry_gifts.im_in_pain from _call_alice_sorry_gifts_im_in_pain
                                Alice_04 "Ну и просто на будущее, знай, в следующий раз ты так легко не отделаешься! Разве только это не будет большая коробка моих любимых конфет..."
                                Max_11 "Взято на заметку, Алиса! Отпусти уже..."
                                Alice_02 "Вот и молодец! Гуляй..."
                    $ AddRelMood('alice', 0, 100)
                else:    #если сладость большая
                    Alice_05 "В этот раз конфет даже больше, так что я не припоминаю, чтобы утром за мной кто-то подглядывал! Всё было в порядке..."
                    Max_01 "Ну да, меня и рядом тогда не было!"
                    menu:
                        Alice_03 "Я даже подумываю, а не обнять ли тебя, Макс? Ну так... по семейному..."
                        "Только если без последующего насилия... {color=[_ch1.col]}(Убеждение. Шанс: [_ch1.vis]){/color}":
                            if RandomChance(_ch1.ch):
                                call alice_sorry_gifts.you_deserve from _call_alice_sorry_gifts_you_deserve
                            else:
                                call alice_sorry_gifts.what_bummer from _call_alice_sorry_gifts_what_bummer
                    $ AddRelMood('alice', 5, 150)

        elif len(sorry_gifts['alice'].give) == 2:  ### дарим в третий раз
            if sorry_gifts['alice'].give == [1, 1]:  ### ненависное, ненавистное, любимое
                Alice_07 "Ничего себе! Ты даже умудрился купить мои любимые конфеты! Большое спасибо! И кто об этом проболтался?"
                Max_03 "Никто! Просто повезло, а может твоя подсказа помогла."
                Alice_05 "Лучше поздно, чем никогда! А ну-ка иди сюда..."   #спрайт с ушами
                call alice_sorry_gifts.kick_ears from _call_alice_sorry_gifts_kick_ears_26
                Max_12 "А-а-ай! Мне же больно, Алиса!"
                menu:
                    Alice_16 "Ещё подглядывать за мной будешь, подлиза ты эдакий?"
                    "Да я же случайно оказался около душа... {color=[_ch1.col]}(Убеждение. Шанс: [_ch1.vis]){/color}":
                        if RandomChance(_ch1.ch):
                            $ Skill('social', 0.2)
                            Alice_05 "[succes!t]Пожалуй, на этот раз, я поверю и ничего не расскажу маме. Но, на всякий случай, за подглядывание, нужно сильнее потянуть..."
                            Max_14 "Ой! Я понял... Больше не буду!"
                            Alice_02 "Вот и молодец! Гуляй..."
                            $ flags['alice_hugs'] = 2
                        else:
                            $ Skill('social', 0.1)
                            Alice_12 "[failed!t]Ты всерьёз думаешь, что меня можно в этом убедить?! Нет уж, я очень хочу посмотреть, как мама тебя отшлёпает!"
                            Max_14 "Но, Алиса, я же купил вкусняшку... Ой, отпусти!"
                            Alice_05 "Слишком уж долго до тебя доходило, что я больше всего люблю... Так что - не повезло тебе..."
                            Max_14 "Ой! Я понял... Больше не буду!"
                            Alice_02 "Вот и молодец! Гуляй..."
                            $ flags['alice_hugs'] = 1
                            $ punreason[1] = 1

            elif sorry_gifts['alice'].give == [1, 2]:  ### ненавистное, преемлемое, любимое
                Alice_07 "Ничего себе! Ты даже умудрился купить мои любимые конфеты! Большое спасибо! И кто об этом проболтался?"
                Max_03 "Никто! Просто повезло, а может твоя подсказа помогла."
                Alice_04 "Видимо, теперь я должна представить, что никто утром за мной в душе не подглядывал, да?"
                Max_01 "Хочется надеяться, что так и будет..."
                call alice_sorry_gifts.apology_accepted from _call_alice_sorry_gifts_apology_accepted_3
                $ AddRelMood('alice', 0, 100)

            elif sorry_gifts['alice'].give == [1, 3]:  ### ненавистное, любимое, любимое
                Alice_07 "Ага! Снова купил мои любимые конфеты! Как здорово... Большое тебе спасибо, Макс!"
                Max_03 "Я люблю радовать старшую сестрёнку её любимыми конфетами."
                menu:
                    Alice_04 "Видимо, теперь я должна представить, что никто утром за мной в душе не подглядывал, да?"
                    "Хочется надеяться, что так и будет... {color=[_ch1.col]}(Убеждение. Шанс: [_ch1.vis]){/color}":
                        if RandomChance(_ch1.ch):
                            $ Skill('social', 0.2)
                            Alice_03 "[succes!t]Ладно, так тому и быть, считай твои извинения приняты... Мама ничего не узнает, так что можешь не напрягаться."
                            Max_07 "Что, вот так вот просто?!"
                            menu:
                                Alice_05 "Ну, ты обещал мне вкусняшку и сдержал слово. Я даже подумываю, а не обнять ли тебя, Макс? Ну так... совсем немного..."
                                "Только если без последующего насилия... {color=[_ch1.col]}(Убеждение. Шанс: [_ch1.vis]){/color}":
                                    if RandomChance(_ch1.ch):
                                        $ Skill('social', 0.2)
                                        Alice_04 "[succes!t]Пожалуй ты заслужил это своими подарками..."   #спрайт с обнимашками
                                        call alice_sorry_gifts.kindred_hugs from _call_alice_sorry_gifts_kindred_hugs_2
                                        Max_03 "Вау! Это как-то очень непривычно... обнимать тебя без ущерба своему здоровью!"
                                        Alice_07 "Я вижу, что ты не просто хочешь избежать наказания, а ещё и мне приятно сделать стремишься. Вот я и не вредничаю..."
                                        Max_05 "Да, надо бы почаще так делать."
                                        Alice_02 "Подглядывать за мной или дарить мне сладости?!"
                                        Max_02 "Второе, конечно!"
                                        Alice_05 "Ну да, конечно... Иди давай."
                                        $ flags['alice_hugs'] = 4
                                    else:
                                        $ Skill('social', 0.1)
                                        Alice_05 "[failed!t]Ах, а так хотелось! Какой облом..."
                                        Max_09 "Обнять меня или придушить?"
                                        Alice_07 "Зачем останавливаться на чём-то одном, Макс? Хи-хи..."
                                        Max_01 "Я тогда лучше пойду... погуляю."
                                        Alice_02 "Ну как хочешь..."
                                        $ flags['alice_hugs'] = 3
                        else:
                            $ Skill('social', 0.1)
                            call alice_sorry_gifts.im_in_pain from _call_alice_sorry_gifts_im_in_pain_1
                            Alice_02 "Вот и молодец! Гуляй..."
                $ AddRelMood('alice', 0, 100)

            elif sorry_gifts['alice'].give == [2, 1]:  ### преемлемое, ненавистное, любимое
                Alice_07 "Ничего себе! Ты даже умудрился купить мои любимые конфеты! Большое спасибо! И кто об этом проболтался?"
                Max_03 "Никто! Просто повезло, а может твоя подсказа помогла."
                Alice_04 "Видимо, теперь я должна представить, что никто утром за мной в душе не подглядывал, да?"
                Max_01 "Хочется надеяться, что так и будет..."
                call alice_sorry_gifts.apology_accepted from _call_alice_sorry_gifts_apology_accepted_4
                $ AddRelMood('alice', 0, 50)

            elif sorry_gifts['alice'].give == [2, 2]:  ### преемлемое, преемлемое, любимое
                Alice_07 "Ничего себе! Ты даже умудрился купить мои любимые конфеты! Большое спасибо! И кто об этом проболтался?"
                Max_03 "Никто! Просто повезло, а может твоя подсказа помогла."
                menu:
                    Alice_04 "Видимо, теперь я должна представить, что никто утром за мной в душе не подглядывал, да?"
                    "Хочется надеяться, что так и будет... {color=[_ch1.col]}(Убеждение. Шанс: [_ch1.vis]){/color}":
                        if RandomChance(_ch1.ch):
                            $ Skill('social', 0.2)
                            Alice_03 "[succes!t]Ладно, так тому и быть, считай твои извинения приняты... Мама ничего не узнает, так что можешь не напрягаться."
                            Max_07 "Что, вот так вот просто?!"
                            Alice_05 "Ну, ты обещал мне вкусняшку и сдержал слово. А я добрая, если настроение хорошее. Более-менее добрая... Так что не искушай судьбу!"
                            Max_01 "Понял, сестрёнка! Не буду тебе мешать..."
                            $ flags['alice_hugs'] = 3
                        else:
                            $ Skill('social', 0.1)
                            call alice_sorry_gifts.im_in_pain from _call_alice_sorry_gifts_im_in_pain_2
                            Alice_02 "Вот и молодец! Гуляй..."
                $ AddRelMood('alice', 0, 50)

            elif sorry_gifts['alice'].give == [2, 3]:  ### преемлемое, любимое, любимое
                Alice_07 "Ага! Снова купил мои любимые конфеты! Как здорово... Большое тебе спасибо, Макс!"
                Max_03 "Я люблю радовать старшую сестрёнку её любимыми конфетами."
                Alice_04 "Видимо, теперь я должна представить, что никто утром за мной в душе не подглядывал, да?"
                Max_01 "Хочется надеяться, что так и будет..."
                menu:
                    Alice_03 "Я даже подумываю, а не обнять ли тебя, Макс? Ну так... совсем немного..."
                    "Только если без последующего насилия... {color=[_ch1.col]}(Убеждение. Шанс: [_ch1.vis]){/color}":
                        if RandomChance(_ch1.ch):
                            call alice_sorry_gifts.you_deserve from _call_alice_sorry_gifts_you_deserve_1
                        else:
                            call alice_sorry_gifts.what_bummer from _call_alice_sorry_gifts_what_bummer_1
                $ AddRelMood('alice', 0, 100)

            elif sorry_gifts['alice'].give == [3, 1]:  ### любимое, ненавистное, любимое
                Alice_07 "Ага! Снова купил мои любимые конфеты! Как здорово... Большое тебе спасибо, Макс!"
                Max_03 "Я люблю радовать старшую сестрёнку её любимыми конфетами."
                Alice_04 "Видимо, теперь я должна представить, что никто утром за мной в душе не подглядывал, да?"
                Max_01 "Хочется надеяться, что так и будет..."
                call alice_sorry_gifts.apology_accepted from _call_alice_sorry_gifts_apology_accepted_5
                $ AddRelMood('alice', 0, 100)

            elif sorry_gifts['alice'].give == [3, 2]:  ### любимое, преемлемое, любимое
                Alice_07 "Ага! Снова купил мои любимые конфеты! Как здорово... Большое тебе спасибо, Макс!"
                Max_03 "Я люблю радовать старшую сестрёнку её любимыми конфетами."
                menu:
                    Alice_04 "Видимо, теперь я должна представить, что никто утром за мной в душе не подглядывал, да?"
                    "Хочется надеяться, что так и будет... {color=[_ch1.col]}(Убеждение. Шанс: [_ch1.vis]){/color}":
                        if RandomChance(_ch1.ch):
                            $ Skill('social', 0.2)
                            Alice_03 "[succes!t]Ладно, так тому и быть, считай твои извинения приняты... Мама ничего не узнает, так что можешь не напрягаться."
                            Max_07 "Что, вот так вот просто?!"
                            Alice_05 "Ну, ты обещал мне вкусняшку и сдержал слово. А я добрая, если настроение хорошее. Более-менее добрая... Так что не искушай судьбу!"
                            Max_01 "Понял, сестрёнка! Не буду тебе мешать..."
                            $ flags['alice_hugs'] = 3
                        else:
                            $ Skill('social', 0.1)
                            call alice_sorry_gifts.im_in_pain from _call_alice_sorry_gifts_im_in_pain_3
                            Alice_02 "Вот и молодец! Гуляй..."
                $ AddRelMood('alice', 0, 100)

            else:  ### любимое три раза
                Alice_07 "Ага! Снова купил мои любимые конфеты! Как здорово... Большое тебе спасибо, Макс!"
                Max_03 "Я люблю радовать старшую сестрёнку её любимыми конфетами."
                if __give[-1:] == 'm':  #если сладость маленькая
                    Alice_04 "Видимо, теперь я должна представить, что никто утром за мной в душе не подглядывал, да?"   #если сладость маленькая
                    Max_01 "Хочется надеяться, что так и будет..."
                    menu:
                        Alice_03 "Я даже подумываю, а не обнять ли тебя, Макс? Ну так... совсем немного..."
                        "Только если без последующего насилия... {color=[_ch1.col]}(Убеждение. Шанс: [_ch1.vis]){/color}":
                            if RandomChance(_ch1.ch):
                                call alice_sorry_gifts.you_deserve from _call_alice_sorry_gifts_you_deserve_2
                            else:
                                call alice_sorry_gifts.what_bummer from _call_alice_sorry_gifts_what_bummer_2
                    $ AddRelMood('alice', 0, 100)
                else:    #если сладость большая
                    Alice_05 "Конфет так много, что я не припоминаю, чтобы утром за мной кто-то подглядывал! Всё было в порядке..."
                    Max_01 "Ну да, меня и рядом тогда не было!"
                    Alice_04 "Я даже обниму тебя за это! Ну так... совсем немного... Иди ко мне."   #спрайт с обнимашками
                    call alice_sorry_gifts.kindred_hugs from _call_alice_sorry_gifts_kindred_hugs_3
                    Max_03 "Вау! Это как-то очень непривычно... обнимать тебя без ущерба своему здоровью!"
                    Alice_07 "Я вижу, что ты не просто хочешь избежать наказания, а ещё и мне приятно сделать стремишься. Вот я и не вредничаю..."
                    Max_05 "Да, надо бы почаще так делать."
                    Alice_02 "Подглядывать за мной или дарить мне сладости?!"
                    Max_02 "Второе, конечно!"
                    Alice_05 "Ну да, конечно... Иди давай."
                    $ flags['alice_hugs'] = 4
                    $ AddRelMood('alice', 5, 150)

        $ sorry_gifts['alice'].give.append(3)
        jump .end

    label .end:
        $ spent_time += 10
        jump Waiting


label alice_about_bath:
    $ flags['talkaboutbath'] = 2
    Alice_12 "Ты о чём, Макс?"
    Max_01 "Ну, ты вернулась ночью из клуба и мы разговаривали в ванной..."
    menu:
        Alice_13 "Я не помню такого... Тебе приснилось!"
        "Ты мне кое-что показала...":
            Alice_05 "Что?! Всё ты врёшь, Макс. Не было такого!"
            Max_02 "Да? Ну, думай так..."
            Alice_13 "Макс. Повторяю, ничего не было. И даже если и было, ты об этом забудешь, если хочешь жить. Ты меня понял?"
            Max_03 "Конечно..."
            Alice_16 "Я серьёзно! А теперь вали отсюда..."
            Max_01 "Хорошо..."
            $ AddRelMood('alice', 0, -50)
            jump .end
        "Ты мне кое-что сделала...":
            pass
        "Мы делали кое-что...":
            pass
        "Да, ну извини...":
            Alice_03 "Надо же, даже извинился... Вот только я правда мало что помню. Будем считать, что ничего и не было. И не напоминай мне больше об этом. Понял?"
            Max_03 "Ага..."
            $ AddRelMood('alice', 0, 50)
            jump .end

    Alice_15 "Что?! Макс! Ты всё врёшь! Если это правда, я тебя убью, обещаю! А если нет, то тоже! Быстро свалил отсюда!"
    Max_01 "Хорошо..."
    $ AddRelMood('alice', 0, -100)
    jump .end

    label .end:
        $ spent_time += 10
        jump Waiting


label alice_about_kiss:
    $ renpy.block_rollback()
    Alice_02 "Прости, Макс, что?"
    Max_01 "Да вот спрашиваю, умеешь ты целоваться или нет?"
    Alice_05 "Да, не показалось... Тебе заняться больше нечем, Макс?"
    Max_08 "Мне срочно нужно научиться целоваться, и я не знаю кто может помочь..."
    Alice_07 "Срочно?! Бедняжка... Ты знаешь, я в каком-то фильме смотрела, там учились целоваться на помидорах. Попробуй, может получится хотя бы у тебя..."
    Max_07 "Алиса, я серьёзно же!"
    Alice_12 "Макс, отвали. Я не буду целоваться с тобой, даже не мечтай. И придумай другой способ клеиться, а то этот на уровне детского сада, серьёзно."
    Max_09 "Да я не клеился!"

    $ talk_var['ask.teachkiss'].append('alice')
    $ spent_time += 10
    return
